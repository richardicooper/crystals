%SCRIPT PLATON                                                                  
%  VARIABLE CHARACTER CDIR CFILE                                                
%  VARIABLE LOGICAL VALID11 T                                                   
%%                                                                              
%    COPY "#BONDCALC FORCE"                                                     
%    COPY "END"                                                                 
%    COPY "#STORE CSYS CIF 'crysplat.cif'"                                        
%    COPY '#APPEND PUNCH '                                                      
%% Check list 11 is up to date                                                  
%    COPY '#SCRIPT VALID11'                                                     
%%                                                                              
%%                                                                              
%    IF ( VALID11 .EQ. TRUE ) THEN                                              
%     COPY '#CIF '                                                              
%     COPY 'END'                                                                
%     COPY '#PARAMETERS'                                                        
%     COPY 'LAYOUT INSET = 1 NCHAR = 120 ESD=EXCLRH'                            
%     COPY 'COORD SELECT=ALL MONITOR=LOW PRINT=YES PUNCH=CIF NCHAR=14'          
%     COPY "U'S MONITOR=OFF, PRINT=NO, PUNCH=NO, NCHAR=14"                      
%     COPY 'END'                                                                
%     COPY '#DIST'                                                              
%     COPY 'E.S.D YES YES'                                                      
%     COPY 'SELECT RANGE=L41'                                                   
%     COPY 'OUTPUT MON=DIST PUNCH = CIF HESD=NONFIXED'                          
%     COPY 'END'                                                                
%    ELSE 
%     COPY '#CIF '                                                              
%     COPY 'END'                                                                
%     COPY '#PARAMETERS'                                                        
%     COPY 'LAYOUT INSET = 1 NCHAR = 120 ESD=NO'                                
%     COPY 'COORD SELECT=ALL MONITOR=LOW PRINT=YES PUNCH=CIF NCHAR=14'          
%     COPY "U'S MONITOR=OFF, PRINT=NO, PUNCH=NO, NCHAR=14"                      
%     COPY 'END'                                                                
%     COPY '#DIST'                                                              
%     COPY 'SELECT RANGE=L41'                                                   
%     COPY 'OUTPUT MON=DIST PUNCH=CIF'                                          
%     COPY 'END'                                                                
%    END IF                                                                     
%% 
%      COPY '#OPEN FRN1 crysplat.fcf'                                            
%      COPY '#PUNCH 6 E'                                                      
%      COPY 'END'                                                               
  Reflections saved in {&publish.fcf{&                                          
                                                                             
%  COPY '#RELEASE PUNCH bfile.pch'                                              
%  COPY '#RELEASE FRN1 BFILE.FRN'                                           
%  EVALUATE T = FILEDELETE ( 'publish.chk' )                                    
%%                                                                              
^^CO GETKEY PLATONDIR                                                           
%%                                                                              
%%                                                                              
%  GET SILENT NOSTORE FINAL TEXT '1' '1'                                        
%  EVALUATE CFILE = CVALUE                                                      
%  IF CFILE .EQ. '1' .OR. FILEEXISTS ( CFILE ) .EQ. FALSE THEN                  
                                                                                
  Where is the Platon executable?                                               
                                                                                
^^CO SYSOPENFILE 'Platon*.exe' 'Platon executable'                              
                                                                                
%    CLEAR                                                                      
%    INSERT '^^CO SETKEY PLATONDIR "'                                           
%    GET SILENT TEXT 'Platon location:'                                         
%    IF CVALUE .NE. 'CANCEL' THEN                                               
%      EVALUATE CFILE = CVALUE                                                  
%      INSERT '"'                                                               
%      OUTPUT                                                                   
%    ELSE                                                                       
%      FINISH                                                                   
%    END IF                                                                     
%  END IF                                                                       
%%                                                                              
%%                                                                              
%  EVALUATE CDIR = GETPATH ( CFILE )                                            
%  IF FILEEXISTS ( CDIR // 'check.def' ) .EQ. FALSE THEN                        
#ifdef CRY_OSWIN32
%   TRANSFER '#spawn +copy "' // CDIR // 'check.def" .' TO DISPLAY              
%   TRANSFER '#spawn +copy "' // CDIR // 'check.def" .' TO CRYSTALS             
#else
%   TRANSFER '#spawn +cp "' // CDIR // 'check.def" .' TO DISPLAY              
%   TRANSFER '#spawn +cp "' // CDIR // 'check.def" .' TO CRYSTALS             
#endif
%  END IF      
%%                                                                 
%% Run Platon
%%
%  TRANSFER '#SPAWN + "' // CFILE // '" crysplat.cif' TO DISPLAY                  
%  TRANSFER '#SPAWN + "' // CFILE // '" crysplat.cif' TO CRYSTALS                 
%%
%% Show results   
#ifdef __GID__                                                                         
%  COPY '#SPAWN notepad.exe crysplat.lis'                              
#else
%  VARIABLE CHARACTER READER_FILE
%  EVALUATE READER_FILE = 'crysplat.lis'
%  COPY '#SCRIPT XREADER'
#endif
%%                                                                              
%END SCRIPT                                                                     
