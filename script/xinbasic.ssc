%SCRIPT XINBASIC
%% Input of basic data needed for data reduction
% VARIABLE LOGICAL SCRIPTEXT LCANCEL INITSETUP
% VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2
% VARIABLE LOGICAL ANSWER
% IF SCRIPTEXT .EQ. TRUE THEN
 
      Before you start data reduction, you should have loaded the basic
      data lists for your structure. Your DATA-BASE should contain data
      on the following :-
      LIST  1  The crystal's unit cell parameters
      LIST  2  The crystal's symmetry
      LIST  3  The scattering factors for elements in the structure
      LIST 13  The diffraction conditions used in the experiment
      LIST 23  The refinement conditions
      LIST 29  The elemental properties and composition
      LIST 30  The general experimantal conditions
      LIST 31  The cell parameter e.s.d.s
 
%   COPY '#PAUSE 10'
%% ELSE
%%    Checking the basic data, LISTS 1, 2, 3, 13, 23, 29, 30  and 31
% END IF
%  VARIABLE INTEGER INBASICS
%  EVALUATE INBASICS = 0
%% note that LISTS 30 & 31 are not mandatory.
%  IF ( EXISTS 1 + EXISTS 2 + EXISTS 3 + EXISTS 13 + EXISTS 23 -
     + EXISTS 29 + EXISTS 30 + EXISTS 31 .EQ. 8 ) THEN
%%   Mandatory LISTs are present.
%%
%% Only ask next question if the user hasn't just input all this!
%% INITSETUP indicates a call via XSHELXIN.
%%
%    IF ( .NOT. INITSETUP ) THEN
%%
%      ON ERROR REPEAT
%      EVALUATE QTITLE = 'Before reading in data...'
%      EVALUATE QLINE1 = 'Do you want to modify any existing data?'
%      EVALUATE QLINE2 = 'e.g. cell, spacegroup, etc.'
%      EVALUATE BUTTOK = '&No'
%      EVALUATE BUTTXX = '&Yes'
%      COPY '#SCRIPT XQUESTIO'
%      IF ANSWER .EQ. TRUE THEN
%        EVALUATE INBASICS = 1
%        FINISH
%      ELSE
%        COPY '#SCRIPT XINPUT'
%      END IF
%    ELSE
%      EVALUATE INBASICS = 1
%      FINISH
%    END IF
%  END IF
%%
%  CLEAR
%  BLOCK
%   LOOP
%    IF ( EXISTS 30 .NE. 1 ) THEN
     General Information  (LIST 30) is missing
%     COPY '#SCRIPT LIST30'
%    END IF
%%
%    IF ( EXISTS 1 .NE. 1 ) THEN
     Cell parameters (LIST 1)  are missing
%     COPY '#SCRIPT XCELLIN'
%     IF ( LCANCEL .EQ. TRUE ) THEN
%        FINISH
%     END IF
%    END IF
%%
%% XINLIST3 requires space group, and will call XSPACE if
%% necessary. XINLIST3 also gets LIST13 info.
%%
%%
%    IF ( EXISTS 3 .NE. 1 ) THEN
     Scattering Factors (LIST 3)  are missing
%     COPY '#SCRIPT XINLIST3'
%     IF ( LCANCEL .EQ. TRUE ) THEN
%        FINISH
%     END IF
%    END IF
%%
%    IF ( EXISTS 2 .NE. 1 ) THEN
     Symmetry information  (LIST 2)  is missing
%     COPY '#SCRIPT XSPACE'
%     IF ( LCANCEL .EQ. TRUE ) THEN
%        FINISH
%     END IF
%    END IF
%%
%    IF ( EXISTS 13 .NE. 1 ) THEN
     Experimental details (LIST 13) are missing
%     COPY '#SCRIPT XINLIST3'
%    END IF
%%
%% If running from inlist6, this question can be asked later to
%% reduce F/F2 confusion.
%%
%    IF ( EXISTS 23 .NE. 1 ) THEN
     Structure Factor control list (LIST 23)  is missing
%      IF ( INITSETUP ) THEN
     but we will get that later.
%       ELSE
%%       Put in a default LIST 23
%        COPY '#SCRIPT XQCK23'
%      END IF
%    END IF
%%
%    IF ( EXISTS 29 .NE. 1 ) THEN
     Elemental properties (LIST 29)  is missing
%     COPY '#SCRIPT INCOMP'
%    END IF
%%
%    IF ( EXISTS 31 .NE. 1 ) THEN
     Cell parameter esds (LIST 31)  is missing
%     COPY '#SCRIPT LIST31'
%    END IF
%    CLEAR
%% note that LISTS 30 & 31 are not mandatory.
%    IF ( EXISTS 1 + EXISTS 2 + EXISTS 3 + EXISTS 13 -
     + EXISTS 29 .NE. 5 ) THEN
  Something has gone wrong with the initial data input. Try again.
%      EVALUATE INBASICS = 0
%      EVALUATE  LOOPEND = LOOPCOUNTER
%    ELSE
%      EVALUATE INBASICS = 1
%      EVALUATE  LOOPEND = LOOPCOUNTER
%    END IF
%  END LOOP
%  END BLOCK
%END SCRIPT
