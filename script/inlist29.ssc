%SCRIPT INLIST29
%% CRYSTALS script for input of LIST 29 from library file
%  VARIABLE INTEGER ELEMENTCOUNT RADIATION
%  VARIABLE REAL WAVE
%  EVALUATE ELEMENTCOUNT = 0
 Input details of the elements in the asymmetric unit
 ----- ------- -- --- -------- -- --- ---------- ----
{I   If you are going to use SHELXS to solve the structure, or publish
{I   in a journal, remember to give C and H first, if present in your
{I   compound.
%
%    IF ( EXISTS 13 .NE. 0 ) THEN
%        COPY '#GENERALEDIT 13'
%        COPY 'LOCATE RECORDTYPE=103'
%        COPY 'TRANSFER FROM OFFSET=0 TO WAVE'
%        IF ( WAVE .GT. 1. ) THEN
%         EVALUATE RADIATION = 1
%        ELSE
%         EVALUATE RADIATION = 2
%        END IF
%        COPY 'END'
%    END IF
%  IF RADIATION .LE. 0 THEN
%    VERIFY CU MO
%    GET FINAL VERIFIED 'Radiation' 'MO'
%    EVALUATE RADIATION = VALUE
%  END IF
%  EVALUATE ELEMENTCOUNT = 0
%    COPY '#OPEN SCPDATA CRYSDIR:SCRIPT\PROPWIN.DAT'
%
%  MESSAGE USER1 'An invalid element name was given ( ignored )'
%
%  QUEUE REWIND
%  LOOP
%    ON END TERMINATE
%    ON ERROR REPEAT USER1
%
%    CLEAR
%    INSERT '%EXTRACT FIND STRING "'
%    GET APPEND KEYWORD -
      'Element name ( Use END, on a line by itself, to end input )'
%    INSERT '"'
%    EXECUTE SEND
%
%    CLEAR
%    INSERT 'ELEM '
%    EXTRACT TRANSFER COMMAND
%    QUEUE SEND
%
%    EXTRACT NEXT
%    IF RADIATION .EQ. 2 THEN
%      EXTRACT NEXT
%    END IF
%    CLEAR
%    EXTRACT TRANSFER COMMAND
%    QUEUE SEND
%
%    BLOCK
%      ON ERROR REPEAT
%      CLEAR
%      INSERT 'CONTINUE NUMBER = '
%      GET REAL 'Number of this element in the asymmetric unit'
%      QUEUE SEND
%    END BLOCK
%
%    EVALUATE ELEMENTCOUNT = ELEMENTCOUNT + 1
%
%  END LOOP
%
%  IF ELEMENTCOUNT .GT. 0 THEN
%    COPY '#LIST 29'
%    CLEAR
%    INSERT 'READ NELEMENT = '
%    STORE INTEGER ELEMENTCOUNT
%    SEND
%    QUEUE PROCESS
%    COPY 'END'
%    COPY '#SUM L 29'
%    COPY 'END'
%  ELSE
{E You have not specified any elements. No data has been stored
%  END IF
%
%  FINISH
%END SCRIPT
