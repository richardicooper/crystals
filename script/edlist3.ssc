%SCRIPT EDLIST3
%%
%  VARIABLE LOGICAL EDLIST3 LXIN3CANCEL NOUPDATE CHANG3
%  VARIABLE CHARACTER CLTYPE
%  VARIABLE REAL FPRIME FDOUBL WAVE
%%
%% The variables needed by XQUESTIO
%  VARIABLE CHARACTER QTITLE QLINE1 QLINE2 BUTTOK BUTTXX
%  VARIABLE LOGICAL   ANSWER
%%
%  EVALUATE EDLIST3 = TRUE
%  EVALUATE NOUPDATE = FALSE
%  EVALUATE CHANG3 = FALSE
%%
%  IF EXISTS 3 .NE. 1 THEN
{I No atoms, please give unit cell contents first.
%         EVALUATE QTITLE = 'No scattering factors found'
%         EVALUATE QLINE1 = 'Do you want to input a list of'
%         EVALUATE QLINE2 = 'the cell contents now?'
%         EVALUATE BUTTOK = '&Yes'
%         EVALUATE BUTTXX = '&No'
%         COPY '#SCRIPT XQUESTIO'
%         IF ANSWER .EQ. TRUE THEN
%           COPY '#SCRIPT XINLIST3'
%           IF ( LXIN3CANCEL ) THEN
%             FINISH
%           END IF
%         ELSE
%           FINISH
%         END IF
%  END IF
%%
%  IF ( EXISTS 13 .EQ. 1 ) THEN
%    COPY '#GENERALEDIT 13'
%    COPY 'LOCATE RECORDTYPE=103'
%    COPY 'TRANSFER FROM OFFSET=0 TO WAVE'
%    COPY 'END'
%  END IF
%%
%%
^^WI WINDOW EDLIST3 'Edit scattering factors' MODAL
^^WI COMMIT='ED3OK' CANCEL='ED3XX'
^^WI GRID GRIDM NROWS=7 NCOLS=3
^^WI {
^^WI  @ 6,2 GRID GRIDB NROWS=1 NCOLS=5
^^WI  {
^^WI    @ 1,1 BUTTON ED3OK '&Apply' DEFAULT
^^WI    @ 1,3 BUTTON ED3XX '&Cancel'
^^WI    @ 1,5 BUTTON ED3HELP '&Help'
^^WI  }
^^WI  @ 2,2 GRID GRIDT NROWS=1 NCOLS=4
^^WI  {
^^WI    @ 1,1 STATIC T1 'Wavelength: '
% TRANSFER "^^WI @ 1,2 STATIC WAVE " // CHARACTER WAVE TO DISPLAY
^^WI    @ 1,4 BUTTON INL3 'Change wavelength or scatterers'
^^WI  }
^^WI  @ 4,2 GRID GRIDB NROWS=1 NCOLS=9
^^WI  {
^^WI    @ 1,1 DROPDOWN XL3T INFORM=YES
^^WI          ADDTOLIST
%  COPY '#GENERALEDIT 3'
%  COPY 'LOCATE RECORDTYPE=101'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  LOOP
%    ON EDITERROR TERMINATE
%    COPY 'TRANSFER FROM OFFSET=0 TO CLTYPE'
%    TRANSFER '^^WI ' // CLTYPE TO DISPLAY
%    COPY 'NEXT'
%  END LOOP
%  COPY 'END'
^^WI         'NULL'
^^WI    @ 1,3 STATIC T2 "F':"
^^WI    @ 1,5 EDITBOX FPRIME ' ' NCOLS=10 REAL INFORM=YES
^^WI    @ 1,7 STATIC T3 'F":'
^^WI    @ 1,9 EDITBOX FDOUBL ' ' NCOLS=10 REAL INFORM=YES
^^WI  }
^^WI }
^^WI SHOW
^^CR
%%
%%
%  COPY '#GENERALEDIT 3'
%  COPY 'LOCATE RECORDTYPE=101'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%%
%  LOOP
%    IF  ( NOUPDATE ) THEN
%      EVALUATE NOUPDATE = FALSE
%    ELSE
%% Find current selected in L3.
%      COPY 'TOP'
^^??   XL3T SELECTED
%      GET NOSTORE SILENT INTEGER ' ' '1'
%      LOOP ( VALUE - 1 ) TIMES
%        COPY 'NEXT'
%      END LOOP
%      COPY 'TRANSFER FROM OFFSET=1 TO FPRIME'
%      COPY 'TRANSFER FROM OFFSET=2 TO FDOUBL'
%      TRANSFER '^^CO SET FPRIME TEXT ' // CHARACTER FPRIME TO DISPLAY
%      TRANSFER '^^CO SET FDOUBL TEXT ' // CHARACTER FDOUBL TO DISPLAY
%    END IF
%%
%    VERIFY ED3OK ED3XX ED3HELP XL3T FPRIME FDOUBL INL3
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%    CASE VALUE
%      BLOCK          %OK%
%        COPY 'WRITE'
%        COPY 'END'
^^CO     DISPOSE EDLIST3
%        FINISH
%      END BLOCK
%      BLOCK          %CANCEL%
^^CO     DISPOSE EDLIST3
%        COPY 'END'
%        FINISH
%      END BLOCK
%      BLOCK          %HELP%
%        COPY '#SCRIPT XHELP1'
 Use this dialog to set values of F' and F" for non-standard
 wavelengths, (e.g. synchrotron radiation), for each type
 of atom present in your structure.
%        COPY '#SCRIPT XHELP2'
%        EVALUATE NOUPDATE = TRUE
%      END BLOCK
%      BLOCK          %LIST CHANGED%
%       GET SILENT NOSTORE INTEGER 'New value' '0'
%      END BLOCK
%      BLOCK          %FPRIME CHANGED%
%        GET SILENT NOSTORE REAL 'fprime' '0.0'
%        EVALUATE FPRIME = VALUE
%        COPY 'TRANSFER TO OFFSET=1 FROM FPRIME'
%        EVALUATE NOUPDATE = TRUE
%        EVALUATE CHANG3 = TRUE
%      END BLOCK
%      BLOCK          %FDOUBLEPRIME CHANGED%
%        GET SILENT NOSTORE REAL 'fdoubleprime' '0.0'
%        EVALUATE FDOUBL = VALUE
%        COPY 'TRANSFER TO OFFSET=2 FROM FDOUBL'
%        EVALUATE NOUPDATE = TRUE
%        EVALUATE CHANG3 = TRUE
%      END BLOCK
%      BLOCK
%         IF ( CHANG3 ) THEN
%          EVALUATE QTITLE = 'Warning'
%          EVALUATE QLINE1 = "Any changes you have made to anomalous"
%          EVALUATE QLINE2 = 'scattering factors will be lost. Continue?'
%          EVALUATE BUTTOK = '&Yes'
%          EVALUATE BUTTXX = '&No'
%          COPY '#SCRIPT XQUESTIO'
%         ELSE
%          EVALUATE ANSWER = TRUE
%         END IF
%         IF ANSWER .EQ. TRUE THEN
%           COPY 'END'
%           COPY '#SCRIPT XINLIST3'
%% Re-do wavelength
%           IF ( EXISTS 13 .EQ. 1 ) THEN
%             COPY '#GENERALEDIT 13'
%             COPY 'LOCATE RECORDTYPE=103'
%             COPY 'TRANSFER FROM OFFSET=0 TO WAVE'
%             COPY 'END'
%           END IF
%           TRANSFER "^^CO SET WAVE TEXT " // CHARACTER WAVE TO DISPLAY
%% Re-do list of elements.
^^CO        SET XL3T REMOVE 0
^^WI        SET XL3T ADDTOLIST
%           COPY '#GENERALEDIT 3'
%           COPY 'LOCATE RECORDTYPE=101'
%           COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%           LOOP
%             ON EDITERROR TERMINATE
%             COPY 'TRANSFER FROM OFFSET=0 TO CLTYPE'
%             TRANSFER '^^WI ' // CLTYPE TO DISPLAY
%             COPY 'NEXT'
%           END LOOP
^^WI        'NULL'
^^CR
%         END IF
%      END BLOCK
%    END CASE
%  END LOOP
%END SCRIPT
