%SCRIPT XINLIST6
%% CRYSTALS reflection input script
%  VARIABLE INTEGER INBASICS
%  VARIABLE INTEGER SHELX FS NCOEF FMT
%  VARIABLE CHARACTER SWITCH
Input reflection data from a file containing h,k,l Fo, sigma (HKLI file)
----- ---------- ---- ----------- ---------- ----- --- ----- -----------
%%
%% Gather information before acting.
%%
^^WI WINDOW XINLIST6 'Input reflection data' MODAL
^^WI        COMMIT='BOK' CANCEL='BXX'
^^WI GRID GRIDM NROWS=9 NCOLS=3
^^WI {
^^WI  @ 2,2 GRID GF NROWS=1 NCOLS=3 OUTLINE='File'
^^WI  {
^^WI    @ 1,1 EDITBOX L6FILE ' ' CHARS=48 INFORM=NO
^^WI    @ 1,3 BUTTON BROWSE 'Browse...'
^^WI  }
^^WI  @ 4,2 GRID GD NROWS=2 NCOLS=1 OUTLINE='Data type'
^^WI  {
^^WI    @ 1,1 GRID GT NROWS=2 NCOLS=5
^^WI    {
^^WI      @ 1,1 RADIOBUTTON RFS1 'F' IGNORE
^^WI      @ 1,3 RADIOBUTTON RFS2 'F-squared' STATE=ON IGNORE
^^WI      @ 1,5 RADIOBUTTON RFS3 'I' IGNORE
^^WI    }
^^WI    @ 2,1 CHECKBOX CS 'Data contains sigmas' STATE=ON IGNORE
^^WI  }
^^WI  @ 6,2 GRID GL NROWS=1 NCOLS=2
^^WI  {
^^WI   @ 1,1 GRID GD NROWS=3 NCOLS=2 OUTLINE='Format'
^^WI   {
^^WI    @ 1,1 RADIOBUTTON RDF1 'Free format' INFORM
^^WI    @ 2,1 RADIOBUTTON RDF2 'Shelx (fixed format)' 
^^WI                               INFORM STATE=ON
^^WI    @ 3,1 RADIOBUTTON RDF3 'Other (fixed format):'
^^WI                               INFORM 
^^WI    @ 3,2 EDITBOX L6FORM ' (3F4.0, 2F8.0) ' CHARS=24
^^WI                                         DISABLED=YES
^^WI   }                                  
^^WI   @ 1,2 GRID GB NROWS=6 NCOLS=2
^^WI   {
^^WI    @ 2,2 BUTTON BOK '&Ok' DEFAULT
^^WI    @ 4,2 BUTTON BXX '&Cancel'
^^WI    @ 6,2 BUTTON HELP1 'Help...' 
^^WI   }
^^WI  }
^^WI }
^^WI SHOW
^^CR
%%
%% Now the window is displayed, we wait for any of the following
%% things to happen:
%% "BOK" - the OK button is pressed.
%% "BXX" - the Cancel button is pressed.    
%% "BROWSE" - the browse button is pressed.
%% "HELP1" - the help button is pressed.
%% "RDF1" - free format is selected. (DISable L6FORM)
%% "RDF2" - shelx format is selected.(DISable L6FORM)
%% "RDF3" - other format is selected.(ENable L6FORM)
%%
% LOOP
%   VERIFY BOK BXX BROWSE HELP1 RDF1 RDF2 RDF3
%   GET NOSTORE FINAL ABBREVIATED 'Opt' ' '
%   CASE VALUE
%     BLOCK                         %BOK%
^^WI    DISPOSE XINLIST6
^^CR
%     END BLOCK
%     BLOCK                         %BXX%
^^WI    DISPOSE XINLIST6
^^CR
%       FINISH
%     END BLOCK
%     BLOCK                         %BROWSE%
^^WI    SYSOPENFILE '*.hkl' 'HKL file (*.hkl)'
^^CR
%       GET KEYWORD 'New filename:'
%       IF CVALUE .NE. 'CANCEL' THEN
%        CLEAR
%        TRANSFER "^^WI SET L6FILE TEXT " // CVALUE TO DISPLAY
^^CR
%       END IF
%     END BLOCK
%     BLOCK                         %HELP1%
%       COPY '#SCRIPT XHELP1'
X-ray observation data can be stored in several different formats:

I's --               Intensities. The "raw" reflection data as
                     recorded by the diffractometer.
F's and F-squared -- Structure factor amplitudes (or their square).
                     Data reduction and absorption corrections on
                     I's produce F-squared or F.
e.g. a file processed by RC93 will contain intensities.
     a file processed by DIPIN will contain F-squared.
%       COPY '#SCRIPT XHELP2'
%     END BLOCK
%     BLOCK                         %RDF1%
^^WI SET L6FORM DISABLED=YES
^^CR
%     END BLOCK
%     BLOCK                         %RDF2%
^^WI SET L6FORM DISABLED=YES
^^CR
%     END BLOCK
%     BLOCK                         %RDF3%
^^WI SET L6FORM DISABLED=NO
^^CR
%     END BLOCK
%   END CASE
% END LOOP
%END SCRIPT





%  CLEAR
%  ON ERROR CONTINUE
%  COPY  '#CLOSE HKLI'
%  BLOCK
%  ON END TERMINATE
%    BLOCK
%      ON ERROR REPEAT
%      QUEUE REWIND
%      CLEAR
%      INSERT '#OPEN HKLI'
%      GET FINAL FILENAME -
      'Filename of file containing reflections (HKLI file) '
%      QUEUE SEND
%    END BLOCK
%    ON ERROR REPEAT
%    QUEUE PROCESS
%  END BLOCK
%%
%  VERIFY F  FSQ  I
%  GET NOSTORE FINAL VERIFIED   'Is the data F, FSQ, I' 'FSQ'
%  EVALUATE FS = VALUE
%
%%
%%   WE NEED BASIC LISTS FOR DATA REDUCTION
%    CLEAR
%    COPY '#SCRIPT INBASICS'
%    IF INBASICS .NE. 1 THEN
  Basic Cell, Space Group or composition information is missing.
  This SCRIPT will be abandoned without reading the reflections.
%      FINISH
%    END IF
%%
%  VERIFY YES NO
%  GET NOSTORE FINAL ABBREVIATED  -
   'Is the data in free format?' 'NO'
%    EVALUATE FMT = VALUE
%  IF VALUE .EQ. 2 THEN
%    GET NOSTORE FINAL ABBREVIATED  -
     'Is the data in a standard SHELX format' 'YES'
%    EVALUATE SHELX = VALUE
%    EVALUATE NCOEF = 5
%  END IF
%%
%  IF (  SHELX = 2 .OR.  FMT = 2 ) THEN
%    GET NOSTORE FINAL ABBREVIATED  -
     'Do you have sigma values? ' 'YES'
%    EVALUATE NCOEF = 6 - VALUE
%  END IF
%%
%  BLOCK
%  QUEUE REWIND
%  ON ERROR RESTART
%    CLEAR
%    INSERT "READ "
%    IF FS = 1 THEN
%     INSERT " F'S=FO"
%    ELSE
%      INSERT " F'S=FSQ"
%    END IF
%    IF NCOEF .EQ. 4 THEN
%      INSERT ' NCOEF = 4'
%    ELSE
%      INSERT ' NCOEF = 5'
%    END IF
%    IF FMT .EQ. 1 THEN
%      INSERT ' TYPE = FREE CHECK = YES'
%    ELSE
%      INSERT ' TYPE = FIXED CHECK = NO'
%    END IF
%    QUEUE SEND
%%
%    CLEAR
%    IF NCOEF .EQ. 5 THEN
%      INSERT 'INPUT H K L /FO/ SIGMA(/FO/) '
%    ELSE
%      INSERT 'INPUT H K L /FO/ '
%    END IF
%    QUEUE SEND
%%
%    CLEAR
%    IF (  SHELX = 2 .OR.  FMT = 2 ) THEN
%      BLOCK
   Input the data format e.g. (3F4.0, 2F8.0)
%      CLEAR
%      INSERT 'FORMAT '
%      GET APPEND TEXT 'FORMAT' ' (3F4.0, 2F8.0) '
%      QUEUE SEND
%      END BLOCK
%    END IF
%%
%      CLEAR
%      INSERT 'STORE NCOEF=6'
%      QUEUE SEND
%      CLEAR
%      INSERT 'OUTPUT INDICES /FO/ SIGMA(/FO/) RATIO/JCODE CORRECTIONS SERIAL '
%      QUEUE SEND
%      CLEAR
%  END BLOCK
%  COPY '#HKLI'
%  QUEUE PROCESS
%  COPY 'END'
%  COPY '#CLOSE HKLI'
%%
%  IF FS .EQ. 3 THEN
%    COPY '#LP'
%    COPY 'END'
%  END IF
%  MESSAGE USER1 ' Error during reflection processing'
%    ON ERROR TERMINATE USER1
            * Removing systematically absent reflections
%    COPY '#SYSTEMATIC'
%    COPY 'END'
%    CLEAR
            * Sorting reflections
%      COPY '#SORT'
%      COPY 'END'
%    CLEAR
%      GET NOSTORE FINAL ABBREVIATED  -
  'Do you wish to merge reflections?'  'YES'
%    IF VALUE .EQ. 1 THEN
%
            * Merging equivalent reflections
%      CLEAR
%      COPY '#MERGE'
%      COPY 'END'
%    END IF
%
            * Final copy to DSC file
%      COPY '#LIST 6'
%      COPY 'READ TYPE = COPY'
%      COPY 'END'
%  COPY '#LIST 4'
%  COPY 'END'
%%
%END SCRIPT
