%SCRIPT XINLIST6
%% CRYSTALS reflection input script
%  VARIABLE CHARACTER INSTITLE HKLTITLE
%  VARIABLE INTEGER INBASICS
%  VARIABLE INTEGER FS NCOEF FMT
%  VARIABLE LOGICAL LCANCEL
%  VARIABLE LOGICAL XQMERGE
%  EVALUATE LCANCEL = FALSE
 Input reflection data from  file containing h,k,l Fo, sigma (HKLI file)
 ----- ---------- ---- ---------- ---------- ----- --- ----- -----------
%%
%% Initial checks. Is there any point proceeding.
%  COPY '#SCRIPT XINBASIC'
%  IF INBASICS .NE. 1 THEN
 
{0,3   Basic Cell, Space Group or composition information is missing.
{0,3   This SCRIPT will be abandoned without reading the reflections.
   EITHER:
{0,2          a - choose CELL, SPACE GROUP & ATOMIC PROPERTIES off
{0,2              the X-ray data menu,
   OR:
{0,2          b - choose the Shelx (INS of RES) option off 
{0,2              the X-ray data menu,
 
%    FINISH
%  END IF
%% XInBasic returns LCANCEL as TRUE if the user cancels the dialog.
%  IF LCANCEL .EQ. TRUE THEN
 
{0,3   Basic Cell, Space Group or composition information is missing.
{0,3   This SCRIPT will be abandoned without reading the reflections.
   EITHER:
{0,2          a - choose CELL, SPACE GROUP & ATOMIC PROPERTIES off
{0,2              the X-ray data menu,
   OR:
{0,2          b - choose the Shelx (INS of RES) option off 
{0,2              the X-ray data menu,
 
%    FINISH
%  END IF
%%
%% Gather information before acting.
%%
^^WI WINDOW XINLIST6 'Input reflection data' MODAL
^^WI        COMMIT='BOK' CANCEL='BXX'
^^WI GRID GRIDM NROWS=9 NCOLS=3
^^WI {
^^WI  @ 2,2 GRID GF NROWS=1 NCOLS=3 OUTLINE='File'
^^WI  {
^^WI    @ 1,1 EDITBOX L6FILE ' ' CHARS=48 INFORM=NO
^^WI    @ 1,3 BUTTON BROWSE 'Browse...'
^^WI  }
^^WI  @ 4,2 GRID GD NROWS=4 NCOLS=1 OUTLINE='Data type'
^^WI  {
^^WI    @ 1,1 GRID GT NROWS=1 NCOLS=4
^^WI    {
^^WI      @ 1,1 ICON IM INFO
^^WI      @ 1,4 GRID GT NROWS=3 NCOLS=1
^^WI      {
^^WI @ 1,1 STATIC G 'Please give the type of the data in the file above.'
^^WI @ 2,1 STATIC G 'This is NOT related to whether you want to refine'
^^WI @ 3,1 STATIC G 'against F or F squared! Files usually contain F-squared.'
^^WI      }
^^WI    }
^^WI    @ 3,1 GRID GT NROWS=1 NCOLS=5
^^WI    {
^^WI      @ 1,1 RADIOBUTTON RFS1 'F' IGNORE
^^WI      @ 1,3 RADIOBUTTON RFS2 'F-squared' STATE=ON IGNORE
^^WI      @ 1,5 RADIOBUTTON RFS3 'I' IGNORE
^^WI    }
^^WI    @ 4,1 CHECKBOX CS 'Data contains sigmas' STATE=ON INFORM=NO
^^WI  }
^^WI  @ 6,2 GRID GL NROWS=1 NCOLS=2
^^WI  {
^^WI   @ 1,1 GRID GD NROWS=3 NCOLS=2 OUTLINE='Format'
^^WI   {
^^WI    @ 1,1 RADIOBUTTON RDF1 'Free format' INFORM
^^WI    @ 2,1 RADIOBUTTON RDF2 'Shelx (fixed format)'
^^WI                               INFORM STATE=ON
^^WI    @ 3,1 RADIOBUTTON RDF3 'Other (fixed format):'
^^WI                               INFORM
^^WI    @ 3,2 EDITBOX L6FORM ' (3F4.0, 2F8.0) ' CHARS=24
^^WI                                         DISABLED=YES
^^WI   }
^^WI   @ 1,2 GRID GB NROWS=6 NCOLS=2
^^WI   {
^^WI    @ 2,2 BUTTON BOK '&Ok' DEFAULT
^^WI    @ 4,2 BUTTON BXX '&Cancel'
^^WI    @ 6,2 BUTTON HELP1 'Help...'
^^WI   }
^^WI  }
^^WI }
^^WI SHOW
^^CR
% EVALUATE HKLTITLE = INSTITLE // '.hkl'
% IF ( FILEEXISTS ( HKLTITLE ) ) THEN
%   TRANSFER "^^CO SET L6FILE TEXT '" // HKLTITLE // "'" TO DISPLAY
% END IF
%%
%% Now the window is displayed, we wait for any of the following
%% things to happen:
%% "BOK" - the OK button is pressed.
%% "BXX" - the Cancel button is pressed.
%% "BROWSE" - the browse button is pressed.
%% "HELP1" - the help button is pressed.
%% "RDF1" - free format is selected. (DISable L6FORM)
%% "RDF2" - shelx format is selected.(DISable L6FORM)
%% "RDF3" - other format is selected.(ENable L6FORM)
%%
% LOOP
%   VERIFY BOK BXX BROWSE HELP1 RDF1 RDF2 RDF3
%   GET SILENT NOSTORE FINAL ABBREVIATED 'Opt' ' '
%   CASE VALUE
%     BLOCK                         %BOK%
%%
%% First get the filename.
%       CLEAR
%       QUEUE REWIND
%       INSERT '#CLOSE HKLI'
%       QUEUE SEND
%       CLEAR
%       INSERT '#OPEN HKLI'
^^??    L6FILE TEXT
%       GET NOSTORE SILENT TEXT 'Reflection File (HKLI file) '
%       IF ( .NOT. ( FILEEXISTS CVALUE ) ) THEN
 Error: Filename given does not exist.
%       ELSE
%         TRANSFER '"' // CVALUE // '"' TO BUFFER
%         QUEUE SEND
%         QUEUE PROCESS
%         BLOCK   %REFLECTION TYPE%
%           VERIFY ON OFF
^^??        RFS1 STATE
%           GET SILENT NOSTORE FINAL ABBREVIATED 'Fs?' 'OFF'
%           IF VALUE .EQ. 1 THEN
%             EVALUATE FS = 1
%           ELSE
^^??          RFS2 STATE
%             GET SILENT NOSTORE FINAL ABBREVIATED 'Fsq?' 'ON'
%             IF VALUE .EQ. 1 THEN
%               EVALUATE FS = 2
%             ELSE
%               EVALUATE FS = 3
%             END IF
%           END IF
^^??        RDF1 STATE
%           GET SILENT NOSTORE FINAL ABBREVIATED 'Free format?' 'OFF'
%%
%% FMT : 1 = free format, 2 = fixed, 3 = fixed shelx
%%
%           EVALUATE FMT = VALUE
%%
%           IF ( FMT .EQ. 2 ) THEN
^^??          RDF2 STATE
%             GET SILENT NOSTORE FINAL ABBREVIATED 'Shelx format?' 'ON'
%             IF ( VALUE .EQ. 1 ) THEN
%                EVALUATE FMT = 3
%             END IF
%             EVALUATE NCOEF = 5
%           END IF
^^??        CS STATE
%           GET SILENT NOSTORE FINAL ABBREVIATED 'sigmas? ' 'ON'
%           EVALUATE NCOEF = 6 - VALUE
%         END BLOCK
%         BLOCK
%           QUEUE REWIND
%           ON ERROR RESTART
%           CLEAR
%           INSERT "READ "
%           IF FS = 1 THEN
%             INSERT " F'S=FO"
%           ELSE
%             INSERT " F'S=FSQ"
%           END IF
%           IF NCOEF .EQ. 4 THEN
%             INSERT ' NCOEF = 4'
%           ELSE
%             INSERT ' NCOEF = 5'
%           END IF
%           IF FMT .EQ. 1 THEN
%             INSERT ' TYPE = FREE CHECK = NO'
%           ELSE
%             INSERT ' TYPE = FIXED CHECK = NO'
%           END IF
%           QUEUE SEND
%%
%           CLEAR
%           IF NCOEF .EQ. 5 THEN
%             INSERT 'INPUT H K L /FO/ SIGMA(/FO/) '
%           ELSE
%             INSERT 'INPUT H K L /FO/ '
%           END IF
%           QUEUE SEND
%%
%           CLEAR
%           IF ( FMT .EQ. 2 ) THEN
%             INSERT 'FORMAT '
^^?? L6FORM TEXT
%             GET SILENT APPEND TEXT 'FORMAT' ' (3F4.0, 2F8.0) '
%             QUEUE SEND
%           ELSE IF ( FMT .EQ. 3 ) THEN
%             INSERT 'FORMAT (3F4.0, 2F8.0)'
%             QUEUE SEND
%           END IF
%%
%           CLEAR
%           INSERT 'STORE NCOEF=6'
%           QUEUE SEND
%           CLEAR
%  INSERT 'OUTPUT INDICES /FO/ SIGMA(/FO/) RATIO/JCODE CORRECTIONS SERIAL '
%           QUEUE SEND
%           CLEAR
%         END BLOCK
^^WI      DISPOSE XINLIST6
^^CR     
%         COPY '#HKLI'
%         QUEUE PROCESS
%         COPY 'END'
%         COPY '#CLOSE HKLI'
%%
%         IF FS .EQ. 3 THEN
%           COPY '#LP'
%           COPY 'END'
%         END IF
%         COPY '#SCRIPT XFRIEDEL'
%         MESSAGE USER1 ' Error during reflection processing'
%         ON ERROR TERMINATE USER1
{S * Removing systematically absent reflections
%         COPY '#SYSTEMATIC'
%         COPY 'END'
%         CLEAR
{S * Sorting reflections
%         COPY '#SORT'
%         COPY 'END'
%         CLEAR
%% Ask user if they want to merge reflections:
%         COPY '#SCRIPT XQMERGE'
%         IF ( XQMERGE ) THEN
{S * Merging equivalent reflections
%           CLEAR
%           COPY '#MERGE'
%           COPY 'END'
%         END IF
%
{S * Final copy to DSC file
%         COPY '#LIST 6'
%         COPY 'READ TYPE = COPY'
%         COPY 'END'
%         COPY '#LIST 4'
%         COPY 'END'
%         COPY '#WEIGHT'
%         COPY 'END'

{I Give the minimum I/sigma(i) and [sin(theta)/lambda]^2 to be 
{I used in refinement. They can be changed at any time later
{I using the menu item "X-ray Data" -> "Filter Reflections".

{R Recommended settings for F refinement:
{R   Fo/sigma(Fo)              is at least 3.0    eliminates noise
{R   [Sin theta / lambda] ** 2 is at least 0.01   eliminates beam stop

%         COPY '#SCRIPT XLIST28'
%%
%         IF ( EXISTS 23 .NE. 1 ) THEN
%           COPY '#LIST 23'
%           COPY 'END'
%         END IF
%%
%%
{R * Done. Reflection data input complete.

%         FINISH
%       END IF
%     END BLOCK
%     BLOCK                         %BXX%
^^WI    DISPOSE XINLIST6
^^CR
%       FINISH
%     END BLOCK
%     BLOCK                         %BROWSE%
^^WI    SYSOPENFILE '*.hkl;*.hkp' 'HKL file (*.hkl,*.hkp)'
^^CR
%       GET SILENT TEXT 'New filename:'
%       IF CVALUE .NE. 'CANCEL' THEN
%        CLEAR
^^WI SET L6FILE TEXT 
%        TRANSFER "^^WI '" // CVALUE // "'" TO DISPLAY
^^CR
%       END IF
%     END BLOCK
%     BLOCK                         %HELP1%
%       COPY '#SCRIPT XHELP1'
X-ray observation data can be stored in several different formats:
 
I's --               Intensities. The "raw" reflection data as
                     recorded by the diffractometer.
F's and F-squared -- Structure factor amplitudes (or their square).
                     Data reduction and absorption corrections on
                     I's produce F-squared or F.
e.g. a file processed by RC93 will contain intensities.
     a file processed by DIPIN will contain F-squared.
%       COPY '#SCRIPT XHELP2'
%     END BLOCK
%     BLOCK                         %RDF1%
^^WI SET L6FORM DISABLED=YES
^^CR
%     END BLOCK
%     BLOCK                         %RDF2%
^^WI SET L6FORM DISABLED=YES
^^CR
%     END BLOCK
%     BLOCK                         %RDF3%
^^WI SET L6FORM DISABLED=NO
^^CR
%     END BLOCK
%   END CASE
% END LOOP
%END SCRIPT
%%
%% $Log: not supported by cvs2svn $
%% Revision 1.20  2002/03/12 17:57:18  ckp2
%% Warn about the meaning of F and F2 in this context.
%%
%% Revision 1.19  2002/02/27 19:52:35  ckp2
%% RIC: Whenever SPAWN command is used with unexpanded environment variable as arg,
%% ensure that arg is in double quotes in case expanded value contains spaces.
%% RIC: Make consistent use of \ after CRYSDIR:
%%
%% Revision 1.18  2001/11/23 09:15:01  ckp2
%% Call the xlist28 script for setting up a list 28 when importing hkl file.
%%
%% Revision 1.17  2001/11/13 09:51:29  ckp2
%% Fix free format reads of reflections.
%% Fix bug when no file name given.
%% Fix default format when reading shelx files.
%%
%% Revision 1.16  2001/10/31 13:04:06  ckp2
%% Option to read in reflection files with HKP extension, as well as normal HKL's.
%% (HKP's are output by PLATON).
%%
%% Revision 1.15  2001/09/11 11:18:07  ckp2
%% Option not to merge friedel pairs.
%%
%% Revision 1.14  2001/03/27 15:06:23  richard
%% Use XQMERGE to ask the MERGE question.
%%
%% Revision 1.13  2001/02/06 10:18:33  richard
%% Changed "IGNORE" token after CHECKBOX to "INFORM=NO"
%%
%% Revision 1.12  2000/10/31 15:18:16  ckp2
%% New text colour method
%%
