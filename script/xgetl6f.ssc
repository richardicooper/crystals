%SCRIPT XGETL6
% VARIABLE REAL OSCALE DIFF
% COPY '#GENERALEDIT 5'
% COPY 'LOCATE RECORDTYPE = 102'
% COPY 'TRANSFER FROM OFFSET = 0 TO OSCALE'
% COPY 'END'
% BLOCK
^^WI WINDOW LIST1 'Reflections' MODAL SIZE
^^WI STARTGRID                GRID01 'MainGrid' NROWS=3 NCOLS=3
^^WI    STARTGRID   ROW=2 COL=3 GRDTR1 'N' NROWS=3 NCOLS=3
^^WI       BUTTON      ROW=1 COL=2  BUTT01 'Ok' DEFAULT COMMIT
^^WI       BUTTON      ROW=3 COL=2  BUTT02 'Cancel' CANCEL
^^WI    ENDGRID
^^WI    LISTCTRL ROW=2 COL=1  LIST01 VISLINES=10 NCOLS=7 
^^WI    'H' 'K' 'L' '|FO|' 'sigma|FO|' '|FC|' '||FO|-|FC||'
^^WI    ADDTOLIST
%  COPY '#RELEASE PUNCH SCPLST06.DAT'
%  COPY '#PUNCH 6 C'
%  COPY 'END'
%  COPY '#RELEASE PUNCH CRFILE.PCH'
%  COPY '#OPEN SCPDATA SCPLST06.DAT'
Skipping first 8 lines...
%  LOOP
%  CLEAR
%  ON ERROR TERMINATE
%  EVALUATE LOOPEND = 8
%  EXTRACT NEXT
%  END LOOP
Skipped.
%  LOOP
%  ON ERROR TERMINATE
%  EXTRACT NEXT
%  EXTRACT TRANSFER INPUT
%  CLEAR
%  INSERT '^^WI '
%  GET INTEGER ' '
%  IF ( VALUE .NE. - 512 ) THEN 
%  GET INTEGER ' '
%  GET INTEGER ' '
%  GET REAL ' '
%  EVALUATE DIFF = VALUE
%  GET REAL ' '
%  GET REAL ' '
%  EVALUATE DIFF = DIFF - ( VALUE * OSCALE )
%  IF ( DIFF .LT. 0.0 ) THEN
%  EVALUATE DIFF = 0.0 - DIFF
%  END IF
%  STORE FORMAT /(F8.3)/ LENGTH 8 REAL DIFF
%  OUTPUT
%  ELSE
%  EVALUATE LOOPEND = LOOPCOUNTER
%  EXTRACT CLOSE
%  END IF
%  END LOOP
^^WI    NULL
^^WI ENDGRID
^^WI SHOW
^^CR
% END BLOCK
%
% VERIFY BUTT01 BUTT02
% GET FINAL FILL NOSTORE ABBREVIATED ' ' ' '
% CASE VALUE
%   BLOCK 
You selected Ok.
^^WI DISPOSE LIST1
^^CR
%   END BLOCK
%   BLOCK
You selected Cancel.
^^WI DISPOSE LIST1
^^CR
%   END BLOCK
% END CASE
%END SCRIPT
