%SCRIPT CHECKDENS
%%
% VARIABLE REAL NELE  
% VARIABLE CHARACTER CELE3
% VARIABLE REAL VOLPRATM NNONHATM CELLVOL SGMULT NALLOWREF ESTNPARA ESTOTP
% 
%%
%%
%    EVALUATE NNONHATM = 0.0
%    COPY '#GENERALEDIT 29'
%    COPY 'LOCATE RECORDTYPE=101'
%    COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%    LOOP
%       ON EDITERROR TERMINATE
%       COPY 'TRANSFER FROM OFFSET=0 TO CELE3'
%       IF CELE3 .NE. 'H' THEN
%          COPY 'TRANSFER FROM OFFSET=4 TO NELE'
%          EVALUATE NNONHATM = NNONHATM + NELE
%       END IF
%       COPY 'NEXT'
%    END LOOP
%    COPY 'END'
%%
%    CLEAR
%    INSERT '{I Number of non-h atoms in asymmetric unit: '
%    STORE FORMAT /(F7.2)/ LENGTH 7 REAL NNONHATM
%    OUTPUT
%%
%    BLOCK
%      ON ERROR TERMINATE
%      ON EDITERROR TERMINATE
%      COPY '#GENERALEDIT 1'
%      COPY 'LOCATE RECORDTYPE=101'
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%      COPY 'TRANSFER FROM OFFSET=6 TO CELLVOL'
%      COPY 'END'
%    END BLOCK
%%
%    CLEAR
%    INSERT '{I Cell volume:                              '
%    STORE FORMAT /(F9.3)/ LENGTH 9 REAL CELLVOL
%    INSERT 'A**3'
%    OUTPUT
%%
%    BLOCK
%      ON ERROR TERMINATE
%      ON EDITERROR TERMINATE
%      COPY '#GENERALEDIT 2'
%      COPY 'LOCATE RECORDTYPE=101'
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%      COPY 'TRANSFER FROM OFFSET=3 TO SGMULT'
%      COPY 'END'
%    END BLOCK
%%
%    CLEAR
%    INSERT '{I Multiplicity of space group:              '
%    STORE FORMAT /(I4)/ LENGTH 4 INTEGER INTEGER SGMULT
%    OUTPUT
%%
%    BLOCK
%      ON ERROR TERMINATE
%      ON EDITERROR TERMINATE
%      COPY '#GENERALEDIT 30'
%      COPY 'LOCATE RECORDTYPE=106'
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%      COPY 'TRANSFER FROM OFFSET=9 TO NALLOWREF'
%      COPY 'END'
%    END BLOCK
%%
%    CLEAR
%    INSERT '{I Number of allowed reflections:            '
%    STORE FORMAT /(I7)/ LENGTH 7 INTEGER INTEGER NALLOWREF
%    OUTPUT
%%
%    EVALUATE VOLPRATM  = CELLVOL / ( NNONHATM * SGMULT )
%%
%    CLEAR
%    INSERT '{R Volume per non-h atom (ideal ~= 18.0):    '
%    STORE FORMAT /(F6.2)/ LENGTH 6 REAL VOLPRATM
%    INSERT 'A**3'
%    OUTPUT
%%
%    EVALUATE ESTNPARA = 1.0 + NNONHATM * 9.0
%%
%    CLEAR
%    INSERT '{R Estimated final number of lsq parameters: '
%    STORE FORMAT /(I6)/ LENGTH 6 INTEGER INTEGER ESTNPARA
%    OUTPUT
%%
%    EVALUATE ESTOTP = NALLOWREF / ESTNPARA 
%    CLEAR
%    INSERT '{R Estimated final obs/parameter ratio:      '
%    STORE FORMAT /(F6.2)/ LENGTH 6 REAL ESTOTP
%    INSERT ': 1'
%    OUTPUT
%%
%END SCRIPT

