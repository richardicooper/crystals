%SCRIPT XINRC93  %Script for quick input of CAD4 data%
%  VARIABLE CHARACTER FILENAME SCFNAME HKLNAME ABSNAME
%  VARIABLE INTEGER INBASICS SOLVEMETHOD
%% AUTOMATIC INPUT OF CAD4 DATA
%% -------   ----- -- ---- ----
%%
%  VARIABLE CHARACTER QTITLE QLINE1 QLINE2 BUTTOK BUTTXX
%  VARIABLE LOGICAL ANSWER
%  VARIABLE CHARACTER CNULL CDLINE
%%%% Definition %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
^^WI WINDOW XINRC93 'Input RC93 data' MODAL
^^WI COMMIT='BTNOK' CANCEL='BTNXX'
^^WI GRID MAIN NROWS=2 NCOLS=1
^^WI {
^^WI  @ 1,1 GRID TOP NROWS=1 NCOLS=2
^^WI  {
^^WI   @ 1,1 GRID LEFT NROWS=6 NCOLS=2
^^WI   {
^^WI    @ 2,2 STATIC T1 'Please enter the title of the hkl file'
^^WI    @ 3,2 STATIC T2 'generated by RC93 (without the extension).'
^^WI   }
^^WI   @ 1,2 GRID RIGHT NROWS=8 NCOLS=10
^^WI   {
^^WI    @ 2,9 BUTTON BTNOK '&Ok' DISABLED=YES DEFAULT COMMIT
^^WI    @ 4,9 BUTTON BTNXX '&Cancel'
^^WI    @ 6,9 BUTTON BTNRR '&Run RC93 now'
^^WI   }
^^WI  }
^^WI  @ 2,1 GRID BOTTOM NROWS=2 NCOLS=5
^^WI  {
^^WI   @ 1,2 EDITBOX E1 '' CHARS=50 INFORM=YES
^^WI   @ 1,4 BUTTON BROWSE '&Browse'
^^WI  }
^^WI }
^^WI SHOW
^^CR
^^CO FOCUS E1
%%%% Processing %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% LOOP
%  VERIFY BTNOK BTNXX BTNRR BROWSE E1
%  GET SILENT NOSTORE FINAL ABBREVIATED 'Option:' ' '
%  CASE VALUE
%    BLOCK              %OK%
^^??    E1 TEXT
%       GET SILENT NOSTORE TEXT 'Get E1 filename:'
%       IF CVALUE .NE. CNULL THEN
%         EVALUATE LOOPEND = LOOPCOUNTER
%         EVALUATE FILENAME = CVALUE
^^WI      DISPOSE XINRC93
^^CR
%       END IF
%    END BLOCK
%    BLOCK             %CANCEL%
^^WI      DISPOSE XINRC93
^^CR
%         FINISH
%    END BLOCK
%    BLOCK             %RUN RC93%
{S * Running RC93.
%      COPY '#SPAWN % "CRYSDIR:rc93.exe" "CRYSDIR:"'
{R   Done.
% copy '#type rc93.err'
%    END BLOCK
%    BLOCK      %BROWSE%
^^WI SYSOPENFILE '*.hkl' 'RC93 generated file (*.hkl)' TITLEONLY
^^CR
%      GET SILENT TEXT 'New filename:'
%      IF CVALUE .NE. 'CANCEL' THEN
%        TRANSFER "^^WI SET E1 TEXT '" // CVALUE // "'" TO DISPLAY
^^WI       SET BTNOK DISABLED=NO
^^CR
%      END IF
%    END BLOCK
%    BLOCK      %E1%
%       GET SILENT NOSTORE TEXT 'Changed text' '_NOTHING'
%       IF CVALUE .NE. '_NOTHING' THEN
^^WI       SET BTNOK DISABLED=NO
^^CR
%       ELSE
^^WI       SET BTNOK DISABLED=YES
^^CR
%       END IF
%     END BLOCK
%  END CASE
% END LOOP
%%%% Execution %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%  BLOCK
%    COPY '#DISK'
%    COPY 'EXTEND SIZE = 10 TRIES  = 1000'
%    COPY 'END'
Files INITIAL.* will be created in your directory. They may be important.
%    COPY '#RELEASE PRINT initial.lis'
%    COPY '#RELEASE LOG INITIAL.DAT'
%  END BLOCK
%%
%  BLOCK
%    ON END TERMINATE
%    ON ERROR REPEAT
%    CLEAR
  You must know the name of your files, e.g.
      XTL11ZAP.SCF, XTL11ZAP.HKL,
               Give ONLY the specific part, e.g. XTL11ZAP or [CAD4]XTL11ZAP
%    EVALUATE SCFNAME = FILENAME // '.SCF'
%    EVALUATE HKLNAME = FILENAME // '.HKL'
%    EVALUATE ABSNAME = FILENAME // '.ABS'
%    TRANSFER '#USE "' //  SCFNAME // '" ' TO CRYSTALS
%    TRANSFER '#OPEN HKLI "' //  HKLNAME // '" ' TO CRYSTALS
%%     COPY '#OPEN FRN2 ARCHIVE-HKL.CIF'
%    TRANSFER '#USE "' //  ABSNAME // '" ' TO CRYSTALS
%%     COPY '#CLOSE FRN2'
%    BLOCK
%      ON ERROR REPEAT
%%     CHECK THAT THE BASIC DATA IS READ IN OK
 -------------------------------------------------------------------------
       NB RC93 creates both LIST30 and LIST31 automatically.
        DO NOT MODIFY THEM or this information will be lost
 -------------------------------------------------------------------------
%      COPY '#SCRIPT XINBASIC'
%      IF INBASICS .NE. 1 THEN
%        FINISH
%      END IF
%    END BLOCK
%%
%    BLOCK
%      ON ERROR REPEAT
%      IF ( EXISTS 14 .NE. 1 ) THEN
%        VERIFY YES NO
%      GET FINAL ABBREVIATED  -
  'Do you know the asymmetric part of the unit cell?' 'YES'
%        IF ( VALUE .EQ. 1 ) THEN
%          COPY '#SCRIPT INLIST14'
%        ELSE
  You will need this information later when you do Fourier maps. Look in
  International Tables before then.
%        END IF
%      END IF
%    END BLOCK
%%
%
%%
%  END BLOCK
%%

{S * Applying Lp corection
%COPY '#LP'
%COPY 'END'
%%
%  COPY '#SCRIPT XFRIEDEL'
%%
{S * Checking systematic absences
%COPY '#SYSTEMATIC'
%COPY 'END'
{S * Sorting data
%COPY '#SORT'
%COPY 'END'
{S * Merging equivalent reflections
%    COPY '#MERGE'
%    COPY 'WEIGHT SCHEME=2 NPARAM=6'
%    COPY 'PARAM 0.5 3.0 1.0 2.0 0.01 0.00001'
%    COPY 'REFLECTIONS NJCODE = 3'
%    COPY 'JCODE 3 VALUE = 0.0'
%    COPY 'JCODE 4 VALUE = 0.0'
%    COPY 'JCODE 8 VALUE = 0.0'
%    COPY 'END'
%    IF ( EXISTS 28 .NE. 1 ) THEN
%     COPY '#SCRIPT XLIST28'
%    END IF
%    COPY '#LIST 6'
%    COPY 'READ TYPE = COPY'
%    COPY 'END'
%    COPY '#SCRIPT XFRESH6'
%    COPY '#RELEASE PRINT bfile.lis'
%    IF FILEEXISTS ( 'rc93.err' ) THEN
%       COPY '#TYPE RC93.ERR'

%    ELSE
{I    There is no RC93 "error file", which suggests that you may be using
{I    data from a very old version of RC93. Crystallographic results will
{I    not be affected, but if you still have the original CAD4 data, you
{I    may like to reprocess it with the current version.

%    END IF
{R   Done. RC93 input complete.
%%
%END SCRIPT

