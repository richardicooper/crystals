%SCRIPT XFILE_ANY
%%
%% Called from XFILE A/R/N/S/W if the extension is unexpected. In these case
%% C_FILE will hold the path and CEXTN the extension (uppercased)
%
%% FTYPE CARG CLASS
%% 0           Unknown
%% 1     S4   SHEL  hklf4
%% 2     S5   SHEL  hklf5
%% 3     SI   SHEL  ins/res
%% 4     A    DIFF  Rigaku Oxdiff (ROD cif)
%% 5     N    DIFF  Nonius (NO Kccd)
%% 6     R    DIFF  Rigaku (RI cif)
%% 7     W    DIFF  WinGX (General GD cif)
%% 8     FA   CIFS  Atoms etc cif
%% 8     FR   CIFS  Reflections cif
%% 9     FM   CIFS  Multi-cif
%% 10    CD   CRYS  CRYSTALS USE file
%% 11    PP   DIFF  Pre-processed DIFFIN output
%% 12    FS   CIFS  Shelx from Cif (use file to be followed by import checks)
%%
%% Guess the type from the extension
%% 0 - unknown
%% 1 - hkl
%% 2 - hklf5
%% 3 - ins,res
%% 4 - cif (rigakuod)
%% 5 - cif (nonius)
%% 6-  cif (rigaku)
%% 7 - cif (general instrument)
%% 8 - cif (general structure or reflections)
%% 9 - cif (general multiple structures)
%% 10 - CRYSTALS use file
%% 11 - diffin output (use file to be followed by import checks)
%%
%%
%  VARIABLE LOGICAL T
%  VARIABLE INTEGER FTYPE
%  VARIABLE CHARACTER HKLTITLE INSTITLE FILE
%  VARIABLE CHARACTER C_FILE C_UPP CEXTN CROOT CARG CLASS CLOC
%  EVALUATE FTYPE = 0
%%
%  IF ( .NOT. ( FILEEXISTS C_FILE ) ) THEN
%% If called incorrectly or without argument, then get a file.
^^WI SYSOPENFILE 
^^WI [ '*.ins;*.res;*.hkl;*.hklf5;*.cif;*.cmf;*.fcf;*.cif_od;*.pcf;'
^^WI   'Crystallographic files'
^^WI   '*.*;' 'All files (*.*)'
^^WI   '*.ins;*.res;*.hkl*;*.hkl;' 'SHELX files (*.ins,*.res,*.hkl*)'
^^WI   '*.dat;*.cry;*.12;*.use;*.l12;*.l16' 'CRYSTALS files (*.dat, etc)'
^^WI   '*.cif*;*.cmf;*.fcf;*.pcf;' 'CIF files (*.cif*,*.cmf,*.fcf,*.pcf)' ]
^^CR                                                                            
%    GET SILENT NOSTORE TEXT 'New filename:' 'CANCEL'                                                                              
%    IF CVALUE .NE. 'CANCEL' THEN                                            
%      EVALUATE C_FILE = CVALUE                                                 
%      IF ( .NOT. ( FILEEXISTS C_FILE ) ) THEN                                  
{E Error: Filename given does not exist.
%        FINISH
%      END IF 
%%
%      EVALUATE CEXTN = GETEXTN ( C_FILE )
%      EVALUATE CEXTN = UPPERCASE ( CEXTN )
%    ELSE
%      FINISH
%    END IF
%  END IF
%%
%  EVALUATE C_UPP = UPPERCASE ( C_FILE )
%  EVALUATE CEXTN = GETEXTN ( C_FILE )
%  EVALUATE CEXTN = UPPERCASE ( CEXTN )
%  EVALUATE CROOT = GETTITLE ( C_FILE )
%  EVALUATE CROOT = UPPERCASE ( CROOT )
%  EVALUATE CLOC = CROOT // '.' // CEXTN
%%
%% CLOC is just local part of filename.extension
%% C_UPP is whole filename includig path and extension 
%%
%  IF ( CLOC .EQ. 'FROM-CIF.CRY' ) THEN
%    EVALUATE FTYPE = 11
%  ELSE IF ( CLOC .EQ. 'IMPORT.CIF' ) THEN
%    EVALUATE FTYPE = 5
%  ELSE IF ( CEXTN .EQ. 'CRY' ) .OR.  ( CEXTN .EQ. 'DAT' ) THEN
%    EVALUATE FTYPE = 10
%  ELSE IF ( CEXTN .EQ. 'USE' ) .OR.  ( CEXTN .EQ. 'L12' ) THEN
%    EVALUATE FTYPE = 10
%  ELSE IF ( CEXTN .EQ. 'L16' ) .OR.  ( CEXTN .EQ. 'L5' ) THEN
%    EVALUATE FTYPE = 10
%  ELSE IF ( CEXTN .EQ. '16' ) .OR.  ( CEXTN .EQ. '12' ) THEN
%    EVALUATE FTYPE = 10
%  ELSE IF  ( CEXTN .EQ. 'CMF' ) THEN                        % What is this?
%    EVALUATE FTYPE = 9
%  ELSE IF  ( CEXTN .EQ. 'INS' ) .OR.  ( CEXTN .EQ. 'RES' ) THEN
%    EVALUATE FTYPE = 3
%  ELSE IF  ( CEXTN .EQ. 'HKL' ) THEN
%    EVALUATE FTYPE = 1
%  ELSE IF  ( CEXTN .EQ. 'HKLF5' ) THEN
%    EVALUATE FTYPE = 2
%  ELSE IF  ( CEXTN .EQ. 'CIF_OD' ) THEN
%    EVALUATE FTYPE = 4
%  ELSE IF  ( CROOT .EQ. 'CRYSTALCLEAR' ) THEN
%    EVALUATE FTYPE = 6
%  ELSE IF  ( CEXTN .EQ. 'HKL' ) THEN
%    EVALUATE FTYPE = 1
%  ELSE IF  ( CEXTN .EQ. 'CIF' ) THEN
%    EVALUATE FTYPE = 7
%  ELSE IF  ( CEXTN .EQ. 'PCF' ) THEN                        %Bruker format file
%    EVALUATE FTYPE = 7
%  ELSE IF  ( CEXTN .EQ. 'FCF' ) THEN                        %IUCr format reflections
%    EVALUATE FTYPE = 8
%  END IF
%
%%
%%
%  IF ( FTYPE .EQ. 3 ) THEN
%%  No ambiguity, just go for it.
%    EVALUATE CARG = 'SI'                                           
%    EVALUATE CLASS = 'SHEL'
%  ELSE
^^WI WINDOW GETARG 'Import Data' 
^^WI MODAL COMMIT='CFBOK' CANCEL='CFBXX'               
^^WI GRID GRIDM NROWS=7 NCOLS=3
^^WI {                                                                          
^^WI  @ 2,2 GRID CFG NROWS=3 NCOLS=1 OUTLINE='Get file:'                  
^^WI  {                                                                         
^^WI    @ 1,1 STATIC CIFILE
%   TRANSFER "^^WI '" // C_FILE // "'" TO DISPLAY
%   CASE FTYPE + 1
% BLOCK                                                                   %%0
^^WI    @ 3,1 STATIC M 'Please select type of file below, then click OK.'
% END BLOCK
% BLOCK                                                                   %%1
^^WI    @ 3,1 STATIC M 'HKL file detected. Confirm and click OK.'
% END BLOCK
% BLOCK                                                                   %%2
^^WI    @ 3,1 STATIC M 'HKLF5 file detected. Confirm and click OK.'
% END BLOCK
% BLOCK                                                                   %%3
^^WI    @ 3,1 STATIC M 'INs or RES file detected. Confirm and click OK.'
% END BLOCK
% BLOCK                                                                   %%4
^^WI    @ 3,1 STATIC M 'RigakuOD CIF detected. Confirm and click OK.'
% END BLOCK
% BLOCK                                                                   %%5
^^WI    @ 3,1 STATIC M 'Nonius CIF file detected. Confirm and click OK.'
% END BLOCK
% BLOCK                                                                   %%6
^^WI    @ 3,1 STATIC M 'Rigaku CIF type. Confirm and click OK.'
% END BLOCK
% BLOCK                                                                   %%7
^^WI    @ 3,1 STATIC M 'General data CIF type. Confirm and click OK.'
% END BLOCK
% BLOCK                                                                   %%8
^^WI    @ 3,1 STATIC M 'Structure or data CIF. Confirm and click OK.'
% END BLOCK
% BLOCK                                                                   %%9
^^WI    @ 3,1 STATIC M 'Multiple structure CIF. Confirm and click OK.'
% END BLOCK
% BLOCK                                                                   %%10
^^WI    @ 3,1 STATIC M 'CRYSTALS instruction file. Confirm and click OK.'
% END BLOCK
% BLOCK                                                                   %%11
^^WI    @ 3,1 STATIC M 'Output from CRYSTALS preprocessing detected.'
% END BLOCK
% BLOCK                                                                   %%12
^^WI    @ 3,1 STATIC M 'Multi RES in cif, selected by user.'
% END BLOCK
%  END CASE
^^WI  }
%%
^^WI  @ 4,2 GRID ZZZ NROWS=14 NCOLS=7 OUTLINE='Data Files'  
^^WI  {                       
%%
%%
^^WI     @ 2,2 STATIC T1                         'Basic data cifs:'                                          
^^WI     @ 4,2 RADIOBUTTON NO 'Nonius Kccd' 
% IF FTYPE .EQ. 5 THEN
^^WI       STATE=ON
% END IF
^^WI     @ 5,2 RADIOBUTTON ROD 'Rigaku OD' 
% IF FTYPE .EQ. 4 THEN
^^WI       STATE=ON
% END IF
^^WI     @ 6,2 RADIOBUTTON RI 'Rigaku'         
% IF FTYPE .EQ. 6 THEN
^^WI       STATE=ON
% END IF
^^WI     @ 7,2 RADIOBUTTON GD 'Generalised cif'  
% IF FTYPE .EQ. 7 THEN
^^WI       STATE=ON
% END IF
^^WI     @ 7,4 RADIOBUTTON PP 'Pre-processed' 
% IF FTYPE .EQ. 11 THEN
^^WI       STATE=ON
% ELSE
^^WI       DISABLED=YES
% END IF
^^WI     @ 8,2 RADIOBUTTON FR 'Reflection cif files' 
% IF FTYPE .EQ. 8 THEN
^^WI       STATE=ON
% END IF
%%
%%
^^WI     @ 2,6 STATIC T2                         'SHELX files:'                                          
^^WI     @ 4,6 RADIOBUTTON SI 'SHELX ins or res files' 
% IF FTYPE .EQ. 3 THEN
^^WI       STATE=ON
% END IF
^^WI     @ 6,6 RADIOBUTTON S4 'SHELX hklf4 files' 
% IF FTYPE .EQ. 1 THEN
^^WI       STATE=ON
% END IF
^^WI     @ 7,6 RADIOBUTTON S5 'SHELX hklf5 files' 
% IF FTYPE .EQ. 2 THEN
^^WI       STATE=ON
% END IF
%%                                                                       
%%
^^WI     @ 10,2 STATIC T3                         'Other cifs:'                                          
^^WI     @ 11,2 RADIOBUTTON FS 'Cif containing SHELX files'
% IF FTYPE .EQ. 12 THEN
^^WI       STATE=ON
% END IF
^^WI     @ 12,2 RADIOBUTTON FA 'Atom (CSD) etc cif files' 
^^WI     @ 13,2 RADIOBUTTON FM 'Multi-cif files' 
% IF FTYPE .EQ. 9 THEN
^^WI       STATE=ON
% END IF
%%
%%
^^WI     @ 9,6 STATIC T4                         'CRYSTALS files:'                                          
^^WI     @ 11,6 RADIOBUTTON CD 'CRYSTALS instruction file' 
% IF FTYPE .EQ. 10 .OR. FTYPE .EQ. 0 THEN
^^WI       STATE=ON
% END IF
^^WI  }
%%
^^WI  @ 6,2 GRID GB NROWS=1 NCOLS=4                                             
^^WI  {                                                                         
^^WI    @ 1,2 BUTTON CFBOK '&Ok' DEFAULT                                        
^^WI    @ 1,4 BUTTON CFBXX '&Cancel'                                            
^^WI  }                                                                         
%%
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR
%%
%   VERIFY CFBXX CFBOK
%   GET SILENT NOSTORE FINAL ABBREVIATED 'BXX?' 'BXX'                           
%   CASE VALUE                                                                  
%%
%      BLOCK                         %CFBXX%                                     
%%
^^CO       DISPOSE GETARG                                                         
%         FINISH                                                                  
%      END BLOCK                                                                 
%%
%      BLOCK                         %CFBOK%                                     
%%
^^??      NO STATE
%         VERIFY ON OFF                                                         
%         GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%         IF VALUE .EQ. 1  THEN
%           EVALUATE CARG = 'N'                                    %NONIUS
%           EVALUATE CLASS = 'DIFF'
%         ELSE
%%
^^??       ROD STATE                                                              
%          VERIFY ON OFF                                                         
%          GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%          IF ( VALUE .EQ. 1 ) THEN                                              
%            EVALUATE CARG = 'A'                                   %RIGAKU OD                                      
%            EVALUATE CLASS = 'DIFF'
%          ELSE
%%
^^??        RI STATE                                                              
%           VERIFY ON OFF                                                         
%           GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%           IF ( VALUE .EQ. 1 ) THEN                                              
%             EVALUATE CARG = 'R'                                  %RIGAKU                               
%             EVALUATE CLASS = 'DIFF'
%           ELSE
%%
^^??         GD STATE                                                              
%            VERIFY ON OFF                                                         
%            GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%            IF ( VALUE .EQ. 1 ) THEN                                              
%%             WinGX
%              EVALUATE CARG = 'W'                                  %WINGX                                       
%              EVALUATE CLASS = 'DIFF'
%            ELSE
%%
^^??          PP STATE                                                              
%             VERIFY ON OFF                                                         
%             GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%             IF ( VALUE .EQ. 1 ) THEN                                              
%               EVALUATE CARG = 'PP'                               %DIFFIN output            
%               EVALUATE CLASS = 'DIFF'
%             ELSE
%%   
^^??           SI STATE                                                              
%              VERIFY ON OFF                                                         
%              GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%              IF ( VALUE .EQ. 1 ) THEN                                              
%                EVALUATE CARG = 'SI'                              %SHELX ins/res             
%                EVALUATE CLASS = 'SHEL'
%              ELSE
%%
^^??            S4 STATE                                                              
%               VERIFY ON OFF                                                         
%               GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%               IF ( VALUE .EQ. 1 ) THEN                                              
%                 EVALUATE CARG = 'S4'                             %SHELX hklf4              
%                 EVALUATE CLASS = 'SHEL'
%               ELSE                                                                
%%
^^??             S5 STATE                                                              
%                VERIFY ON OFF                                                         
%                GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%                IF ( VALUE .EQ. 1 ) THEN                                              
%                  EVALUATE CARG = 'S5'                            %SHELX hklf5               
%                  EVALUATE CLASS = 'SHEL'
%                ELSE                                                                
%%
^^??              CD STATE                                                              
%                 VERIFY ON OFF                                                         
%                 GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%                 IF ( VALUE .EQ. 1 ) THEN                                              
%                   EVALUATE CARG = 'CD'                           %CRYSTALS USE file                
%                   EVALUATE CLASS = 'CRYS'
%                 ELSE
%%
^^??               FA STATE                                                              
%                  VERIFY ON OFF                                                         
%                  GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%                  IF ( VALUE .EQ. 1 ) THEN                                              
%                    EVALUATE CARG = 'FA'                          %Atom cif                 
%                    EVALUATE CLASS = 'CIFS'
%                  ELSE                                                                
%%        
^^??                FR STATE                                                              
%                   VERIFY ON OFF                                                         
%                   GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%                   IF ( VALUE .EQ. 1 ) THEN                                              
%                     EVALUATE CARG = 'FR'                         %Reflection cif                  
%                     EVALUATE CLASS = 'CIFS'
%                   ELSE                                                                
%%
^^??                 FM STATE                                                              
%                    VERIFY ON OFF                                                         
%                    GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%                    IF ( VALUE .EQ. 1 ) THEN                                              
%                      EVALUATE CARG = 'FM'                        %Multi-cif without SHELX                   
%                      EVALUATE CLASS = 'CIFS'
%                    ELSE                                                                
^^??                  FS STATE                                                              
%                     VERIFY ON OFF                                                         
%                     GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%                     IF ( VALUE .EQ. 1 ) THEN                                             
%                       EVALUATE CARG = 'FS'                       %Multi SHELX in cif
%                       EVALUATE CLASS = 'SHEL'
%                     END IF                                                                
%                    END IF
%                   END IF                                                                
%                  END IF                                                                
%                 END IF                                                                
%                END IF                                                                
%               END IF                                                                
%              END IF                                                                
%             END IF                                                                
%            END IF                                                                
%           END IF                                                                
%          END IF                                                                
%         END IF                                                               
^^CO      DISPOSE GETARG                                                       
%      END BLOCK                                                                 
%   END CASE                                                                    
%  END IF
%%
%%show ftype carg class
%%
%  IF CLASS .EQ. 'DIFF' THEN
%   IF CARG = 'PP' THEN
%     COPY '#SCRIPT DIFFEXEC'
%     FINISH
%   ELSE
 {I Diffractometer input cifs - Launch DIFFIN followed by DIFFEXEC                                                               
%     if      ( carg .eq. 'A' ) then
%      copy '#script xfilea'
%     else if ( carg .eq. 'N' ) then 
%      copy '#script xfilen'
%     else if ( carg .eq. 'R' ) then 
%      copy '#script xfiler'
%     else if ( carg .eq. 'W' ) then 
%      copy '#script xfilew'
%     end if
%   END IF
%%
%  ELSE IF CLASS .EQ. 'SHEL' THEN
%%
 Import SHELX data
%      IF CARG .EQ. 'S4' THEN
%        IF C_FILE .NE. ' ' THEN
%         EVALUATE HKLTITLE =  C_FILE 
%        END IF
%        COPY '#SCRIPT XINLIST6' 
%      ELSE IF CARG .EQ. 'S5' THEN
%        COPY '#SCRIPT HKLF52CRY'
%%                                        SHELX files or a cif with embedded SHELX FILES 
%      ELSE IF  ( CARG .EQ. 'FS' ) .OR. (  CARG .EQ. 'SI' ) THEN             
%        IF C_FILE .NE. ' ' THEN
%         EVALUATE INSTITLE =  C_FILE 
%%        Run Pascals program directly.
%         IF FILEEXISTS ( 'crystalsinput.dat' ) THEN
%           EVALUATE T =  FILEDELETE ( 'crystalsinput.cry' )
%         END IF
%% show c_file
% TRANSFER '#SPAWN % "CRYSDIR:shelx2cry.exe" ' // '"' // C_FILE // '"' TO CRYSTALS
%% If there are several .res in the .cif, Pascal creates crystalsinput.dat
%% This contains the SX data plus a call to try to read the first of the .hkl files.
%% The user may not want the first data set.
%% Use the existence if crystalsinput.dat to indicate that file processing has been run
%          IF FILEEXISTS ( 'crystalsinput.cry' ) THEN
%%           don't just #USE it because we need to check other things
%            COPY '#SCRIPT DIFFEXEC' 
%          ELSE


{I  The cif contains more than one .RES file.
{I  Go to DATA input again and select the file you want. 


%          END IF
%        END IF
%      END IF
%%
%  ELSE IF CLASS .EQ. 'CRYS' THEN
%%
{I Reading CRYSTALS instruction file
%      TRANSFER '#USE ' // C_FILE // ' '  TO CRYSTALS
{I CRYSTALS instruction file closed
%%
%  ELSE IF CLASS .EQ. 'CIFS' THEN
%%
DO CIF STUFF
%      IF CARG .EQ. 'FM' THEN
%%        MULTIPLE CIFS IN ONE FILE
%         EVALUATE FILE = C_FILE
%         COPY '#SCRIPT CIF2CRY'
%      else if carg .eq. 'FR' then
%         copy '#script xfilew'    %Generalised reflections
%      ELSE
%        VARIABLE CHARACTER CTITLE CPATH COUT
%        EVALUATE CTITLE = GETTITLE ( C_FILE )
%        EVALUATE CPATH = GETPATH ( C_FILE )
%        EVALUATE COUT = CTITLE // '.cry'
%        EVALUATE CARG = ' -f -d CSD -o '
%% 'SPAWN +' Starts in DOS box
cif2cry
% TRANSFER '#SPAWN + "CRYSDIR:diffin.exe" ' // CARG // '"' // COUT -           
  // '" "' // C_FILE // '"' TO CRYSTALS                                            
%%                                                                              
%        TRANSFER '#USE ' // COUT  TO CRYSTALS    
%        TRANSFER '#USE ' // COUT  TO DISPLAY
%%                                   
%      END IF 
%%
%%
%  ELSE
%%
Something wrong
%  END IF
% END SCRIPT                                                                     
