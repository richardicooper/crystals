%SCRIPT TIDYUP                                                                  
%%                                                                              
%  VARIABLE LOGICAL TWINNED VALID11
%  EVALUATE TWINNED = FALSE                                                      
%  BLOCK                                                                        
%    VARIABLE INTEGER ITMP                                                      
%    IF ( EXISTS 13 .GT. 0 ) THEN                                               
%        COPY '#GENERALEDIT 13'                                                 
%        COPY 'LOCATE RECORDTYPE=101'                                           
%        COPY 'TRANSFER FROM OFFSET=1 TO ITMP'                                  
%        IF ( ITMP .GE. 0 ) THEN                                                
%         EVALUATE TWINNED = TRUE                                               
%        ELSE                                                                   
%         EVALUATE TWINNED = FALSE                                              
%        END IF                                                                 
%        COPY 'END'                                                             
%    END IF                                                                     
%  END BLOCK
%  COPY '#SCRIPT VALID11'
%  IF ( .NOT. VALID11 ) THEN
{I     Forming and inverting normal matrix in order to 
{I      recalculate parameter standard uncertainties.
%    COPY '#SFLS'                                                               
%    COPY 'REFINE'                                                              
%    COPY 'SHIFT GENERAL=0.0'                                                   
%    COPY 'END'                                                                 
%  END IF
%%                                                                              
%% A penultimate difference map                                                 
%  COPY '#SCRIPT PREFOUR'
%  COPY 'MAP TYPE=DIFF'
%  COPY 'PEAK HEIGHT=1 '
%  COPY 'END'
%  COPY '#SCRIPT POSTFOUR'
{I Difference Fourier complete.
%%                                                                              
%  VARIABLE REAL NREF NPARAM RHOMIN RHOMAX SOESD WORK1 ZMAX
%% The variables needed by XQUESTIO                                             
%  VARIABLE CHARACTER QTITLE QLINE1 QLINE2 BUTTOK BUTTXX                        
%  VARIABLE LOGICAL   ANSWER                                                    
%%                                                                              
%  IF ( EXISTS 30 .NE. 1 ) THEN                                                 
{E There in no LIST 30 with the general details available                       
{I Type #SCRIPT ED30 to input general details.                                  
%    COPY '#SCRIPT DIRECT'                                                      
%  END IF                                                                       
%  COPY '#GENERALEDIT 30'
%  COPY 'LOCATE RECORDTYPE = 103'
%  COPY 'TRANSFER FROM OFFSET =  2 TO NPARAM'
%  COPY 'TRANSFER FROM OFFSET =  5 TO RHOMIN'
%  COPY 'TRANSFER FROM OFFSET =  6 TO RHOMAX'
%  COPY 'TRANSFER FROM OFFSET =  7 TO SOESD'
%  COPY 'TRANSFER FROM OFFSET =  8 TO NREF '
%  COPY 'END'                                                                   
%%
%  IF ( NREF / NPARAM )  .LE. 5.0 THEN                                          
{E You have {RFAR{E too few X-ray observations for the number of variables      
{E you are trying to refine. Think again or seek help.                          
%    COPY '#SCRIPT DIRECT'                                                      
%  END IF                                                                       
%%                                                                              
%  EVALUATE ZMAX = 4.0
%  COPY '#GENERALEDIT 29'
%  COPY 'LOCATE RECORDTYPE=101'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  LOOP
%    ON EDITERROR TERMINATE
%    COPY 'TRANSFER FROM OFFSET=6 TO WORK1'
%    IF ( WORK1 .GT. ZMAX ) THEN
%       EVALUATE ZMAX = WORK1
%    END IF
%    COPY 'NEXT'
%  END LOOP
%  COPY 'END'
%  EVALUATE ZMAX = ZMAX / 24.84
%%
%  IF ( RHOMIN .LT. ( - ZMAX * 0.75 ) ) .OR.  -
      ( RHOMAX .GT. (   ZMAX * 0.75 ) ) THEN
%%
{R The residual density is rather high. Could you have disorder,                
{R missing solvent or an absorption problem?
%%                                                                              
%    EVALUATE QTITLE = 'Residual density high'                                                
%    EVALUATE QLINE1 = 'Rho max=' // CHARACTER ( RHOMAX ) // ' min=' // -
                                     CHARACTER ( RHOMIN )
%    EVALUATE QLINE2 = 'Do you want to view the residual peaks?'                
%    EVALUATE BUTTOK = '&Yes'                                                   
%    EVALUATE BUTTXX = '&No'                                                    
%    COPY '#SCRIPT XQUESTIO'                                                    
%    IF ANSWER .EQ. TRUE THEN                                                   
%      COPY '#COLL 10 5'                                                        
%      COPY 'SEL TYPE=PEAK'                                                     
%      COPY 'END'                                                               
%%
%      EVALUATE QTITLE = 'Tidy up'                                                
%      EVALUATE QLINE1 = 'Do you want to ignore these peaks? (you must'           
%      EVALUATE QLINE2 = 'be sure that they are not important)'                   
%      EVALUATE BUTTOK = '&Ignore'                                                
%      EVALUATE BUTTXX = '&Back'                                                  
%      COPY '#SCRIPT XQUESTIO'                                                    
%      IF ANSWER .EQ. FALSE THEN                                                  
{R The 'ROUTINE' script cannot handle this problem.                             
{R Consult an experienced user or the User Guide.                               
%        COPY '#SCRIPT DIRECT'                                                    
%      ELSE                                                                       
%        COPY '#SCRIPT XDELQ'                                                             
%      END IF                                                                     
%    END IF                                                                     
%  END IF                                                                       
%%                                                                              
%  EVALUATE ANSWER = FALSE
%  IF ( SOESD .GT. 0.01 ) .OR. ( .NOT. VALID11 ) THEN
%    EVALUATE QTITLE = 'Tidy up - unconverged parameters'
%    EVALUATE QLINE1 = 'Carry out 6 final cycles of refinement?'
%    EVALUATE QLINE2 = '(Grind the structure into a minimum!)'
%    EVALUATE BUTTOK = '&Yes'
%    EVALUATE BUTTXX = '&No'
%    COPY '#SCRIPT XQUESTIO'
%    IF ANSWER .EQ. TRUE THEN
%      COPY '#SCRIPT SIXCYCLE'
%    END IF
%  END IF
%% A final difference map.                                                      
%  COPY '#SCRIPT PREFOUR'
%  COPY 'MAP TYPE=DIFF'
%  COPY 'PEAK HEIGHT=1 '
%  COPY 'END'
%  COPY '#SCRIPT POSTFOUR'
{I Final difference Fourier complete.
%%                                                                              
%END SCRIPT                                                                     
