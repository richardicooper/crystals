%SCRIPT XQCK23
%%
% VARIABLE INTEGER BPRESS
%%
%% Check for already stored user preference about FSq
^^?? GETKEY FSQORF
%    GET SILENT NOSTORE FINAL TEXT 'NONE' 'NONE'
%    IF CVALUE .EQ. 'F' THEN
%      COPY '#LIST 23'                                                          
%      COPY 'MINIMISE F-SQ=NO'                                                  
%      COPY 'END'                                                               
%      FINISH
%    ELSE IF CVALUE .EQ. 'FSQ' THEN
%      COPY '#LIST 23'                                                          
%      COPY 'MINIMISE F-SQ=YES'                                                  
%      COPY 'END'
%      FINISH
%    END IF
%%
^^WI WINDOW XQCK23 'Minimisation function' MODAL COMMIT='MF_F2' CANCEL='MF_FO'  
^^WI GRID MAIN NROWS=7 NCOLS=1                                                  
^^WI {                                                                          
^^WI   @ 2,1 GRID EXPLN NROWS=12 NCOLS=3                                        
^^WI   {                                                                        
^^WI     @ 1,2 STATIC E1 'During refinement do you prefer to minimise:'         
^^WI     @ 2,2 STATIC E2 'SUM[ w*(Fo-Fc)**2 ] or'                               
^^WI     @ 3,2 STATIC E2 'SUM[ w*(Fo*Fo-Fc*Fc)**2 ] ?'                          
^^WI     @ 5,2 STATIC E2                                                        
^^WI 'The former, `F refinement`, was traditionally favoured until recent'      
^^WI     @ 6,2 STATIC E2                                                        
^^WI 'times. Now the latter function, `F squared refinement` is more popular.'  
^^WI     @ 7,2 STATIC E2                                                        
^^WI 'They both have the same minimum. The only difference likely to effect'    
^^WI     @ 8,2 STATIC E2                                                        
^^WI 'routine structure analysis is that Fsq refinement reduces the leverage'   
^^WI     @ 9,2 STATIC E2                                                        
^^WI 'of weak unreliable reflections to the extent that they can be left'       
^^WI     @ 10,2 STATIC E2                                                       
^^WI 'in the refinement without detrimentally influencing the results.'         
^^WI     @ 12,2 STATIC E2                                                       
^^WI 'Journals should accept good structures refined using either method.'      
^^WI   }                                                                        
^^WI   @ 4,1 GRID BTNS NROWS=1 NCOLS=7                                          
^^WI   {                                                                        
^^WI     @ 1,1 STRETCH MF_ST HORIZONTAL                                         
^^WI     @ 1,2 BUTTON MF_F2 'F &squared' DEFAULT COMMIT                         
^^WI     @ 1,4 BUTTON MF_FO '&F' LENGTH='F sqauared' CANCEL                     
^^WI     @ 1,5 STRETCH MF_ST HORIZONTAL                                         
^^WI     @ 1,6 BUTTON MF_HELP '&Help'                                           
^^WI   }
^^WI   @ 5,1 CHECKBOX FFSQ '&Remember this decision'                            
^^WI           INFORM=NO STATE=OFF
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
%%Nov 09      Remove LIST 4 work from here and set it later in FRESH.SSC
%  LOOP                                                                         
%   VERIFY MF_FO MF_F2 MF_HELP                                                  
%   GET SILENT NOSTORE FINAL ABBREVIATED ' ' 'MF_F2'
%   EVALUATE BPRESS = VALUE
%   IF BPRESS .EQ. 3 THEN
%      COPY '#SPAWN "CRYSDIR:manual/crystalsmanual-sfls.html"'
%   ELSE
%      VERIFY ON OFF                                                             
^^??   FFSQ STATE                                                                
%      GET SILENT NOSTORE FINAL ABBREVIATED ' '                                  
%      IF VALUE .EQ. 1 THEN
%        IF BPRESS .EQ. 1 THEN
^^CO       SETKEY FSQORF F
%        ELSE
^^CO       SETKEY FSQORF FSQ
%        END IF                                                                    
%      END IF                                                                    
%      CASE BPRESS                                                     
%       BLOCK                                                                        
%        COPY '#LIST 23'                                                          
%        COPY 'MINIMISE F-SQ=NO'                                                  
%        COPY 'END'                                                               
^^CO     DISPOSE XQCK23                                                           
%        FINISH                                                                   
%       END BLOCK                                                                   
%       BLOCK                                                                       
%        COPY '#LIST 23'                                                          
%        COPY 'MINIMISE F-SQ=YES'                                                 
%        COPY 'END'                                                               
^^CO     DISPOSE XQCK23                                                           
%        FINISH                                                                   
%       END BLOCK                                                                   
%      END CASE
%   END IF
%  END LOOP                                                                     
%END SCRIPT                                                                    

