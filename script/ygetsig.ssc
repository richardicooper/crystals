%SCRIPT YGETSIG
%%    read internal and external sigmas from the file SIGMAS.DAT 
%%    called from XVARIANCE.SSC
%     VARIABLE INTEGER H1 H2 H3 H4 H5 H6 H7 H8 H9 H10
%     VARIABLE INTEGER B1 B2 B3 B4 B5 B6 B7 B8 B9 B10
%     VARIABLE REAL INTERNAL EXTERNAL SXX SXY GRADIENT  NREFMES 
%     VARIABLE INTEGER NSIGMAS NITEMS
%     VARIABLE REAL ZERO
%     EVALUATE ZERO = 0.
%     COPY '#OPEN SCPDATA sigmas.dat' 
%%   Skip first line:                                                      
%     EXTRACT NEXT
%     LOOP
%      TRANSFER '^^CO SET _MW_SOFAR TEXT ' // CHARACTER LOOPCOUNTER TO DISPLAY                                                                         
%      ON ERROR TERMINATE                                                           
%      ON END TERMINATE
%      EXTRACT NEXT                                                             
%      CLEAR                                                                        
%      EXTRACT TRANSFER INPUT                                                       
%      CLEAR
%%     get  fifth, sixth and seventh items
%      GET NOSTORE NOPROMPT REAL ' ' '-1.0' 
%      GET NOSTORE NOPROMPT REAL ' ' '-1.0' 
%      GET NOSTORE NOPROMPT REAL ' ' '-1.0'                                         
%      GET NOSTORE NOPROMPT REAL ' ' '-1.0'                                         
%      GET NOSTORE NOPROMPT REAL ' ' '-1.0'                                         
%      EVALUATE EXTERNAL = VALUE                                        
%      GET NOSTORE NOPROMPT REAL ' ' '-1.0' 
%      EVALUATE INTERNAL = VALUE                                        
%      GET NOSTORE NOPROMPT INTEGER ' ' '1' 
%      EVALUATE NITEMS = VALUE  
%%
%      CLEAR
%      INSERT !^^PL LABEL '!   
%      STORE FORMAT /(I5)/ LENGTH 5 INTEGER NITEMS
%      INSERT !' DATA !
%      IF NITEMS .LT. 6 THEN
%       STORE FORMAT /(F10.2)/ LENGTH 10 REAL EXTERNAL
%       STORE FORMAT /(F10.2)/ LENGTH 10 REAL INTERNAL
%       STORE FORMAT /(F10.2)/ LENGTH 10 REAL ZERO
%       STORE FORMAT /(F10.2)/ LENGTH 10 REAL ZERO
%      ELSE
%       STORE FORMAT /(F10.2)/ LENGTH 10 REAL ZERO
%       STORE FORMAT /(F10.2)/ LENGTH 10 REAL ZERO
%       STORE FORMAT /(F10.2)/ LENGTH 10 REAL EXTERNAL
%       STORE FORMAT /(F10.2)/ LENGTH 10 REAL INTERNAL
%      END IF
%      IF NITEMS .GE. 3 THEN
%        OUTPUT
%        EVALUATE SXX = SXX + EXTERNAL * EXTERNAL * REAL ( NITEMS )
%        EVALUATE SXY = SXY + EXTERNAL * INTERNAL * REAL ( NITEMS )
%        EVALUATE NSIGMAS = NSIGMAS + 1
%      END IF
%% BUILD HISTOGRAM
%  IF NITEMS .LE. B4 THEN
%      IF NITEMS .LE. B1 THEN
%       EVALUATE H1 = H1 + 1
%      ELSE IF NITEMS .LE. B2 THEN
%       EVALUATE H2 = H2 + 1
%      ELSE IF NITEMS .LE. B3 THEN
%       EVALUATE H3 = H3 + 1
%      ELSE 
%       EVALUATE H4 = H4 + 1
%      END IF
%  ELSE
%      IF NITEMS .LE. B5 THEN
%       EVALUATE H5 = H5 + 1
%      ELSE IF NITEMS .LE. B6 THEN
%       EVALUATE H6 = H6 + 1
%      ELSE IF NITEMS .LE. B7 THEN
%       EVALUATE H7 = H7 + 1
%      ELSE IF NITEMS .LE. B8 THEN
%       EVALUATE H8 = H8 + 1
%      ELSE
%       EVALUATE H9 = H9 + 1
%      END IF
%  END IF
%%
%     END LOOP
%%
%     TRANSFER '^^CO SET _MW_USED TEXT ' // CHARACTER NSIGMAS TO DISPLAY                             
%     IF ( NSIGMAS .GT. 1 ) THEN
%      EVALUATE GRADIENT = SXY / SXX    
%      transfer '^^CO SET _MW_GRAD TEXT ' // CHARACTER GRADIENT TO DISPLAY                             
%     ELSE
{E Insufficient equivalent reflections for variance plot in "sigmas.dat" 
%      TRANSFER '{E   ' //  CHARACTER NSIGMAS // ' equivalents found' TO DISPLAY
%     END IF                                                                          
%%
%     COPY '#CLOSE SCPDATA'
%END SCRIPT
