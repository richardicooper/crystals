%SCRIPT XREFINE
%%
%  VARIABLE INTEGER LSQ:FLAG 
%  EVALUATE LSQ:FLAG = 3
%  VARIABLE REAL SHIFT  FREESHIFT
%  EVALUATE SHIFT = 1.0
%%
%  VARIABLE LOGICAL EDONE PDONE PMASK SERR XREFINE CONVRG 
%  VARIABLE INTEGER NCYCLE NFORCE
%  VARIABLE REAL RMIN R OSCALE
%  EVALUATE SERR = TRUE
%  EVALUATE CONVRG = FALSE
%  VARIABLE REAL L5:OK
%  VARIABLE REAL CONVERGE
%  VARIABLE INTEGER INVERTSTATUS
%%
%%
%% make sure SFLS record exists in LIST 39
%    COPY '#SCRIPT SFLS39'
%%
%  COPY '#SCRIPT RFACTOR'
%%
%% If xrefine is set to true before calling, then ncycle and nforce should
%% also be set - otherwise if xrefine is false (not set) then ncycle and
%% nforce are set here:
%%
%%
%  IF XREFINE .EQ. FALSE THEN
%% Is LIST 3 OK?
%    COPY '#PROPERTIES'
%    COPY 'END'
%    IF L5:OK .LT. 0.0 THEN
 Checking scattering factors
%      COPY '#SCRIPT CHECK3'
%    END IF
%%
^^CO GETKEY XREFINE_CONVERGE
%    GET SILENT NOSTORE FINAL TEXT '1' '1'
%    IF CVALUE .EQ. '1' THEN
%       EVALUATE CONVRG = TRUE
%    END IF
%%
%%
^^CO GETKEY XREFINE_CYCLES
%    GET SILENT NOSTORE INTEGER '5' '5'
%    EVALUATE NCYCLE = VALUE
%%
%%
^^WI WINDOW XREFINE 'Least Squares Refinement' MODAL
^^WI COMMIT='RF_BOK' CANCEL='RF_BXX'
^^WI GRID MAIN NROWS=4 NCOLS=7
^^WI {
^^WI  @ 2,2 GRID L NROWS=7 NCOLS=1
^^WI  {
^^WI   @ 1,1 GRID NCYC NROWS=3 NCOLS=3
^^WI   {
^^WI    @ 1,1 STATIC T1 'Maximum number of cycles of refinement'
^^WI    @ 1,3 EDITBOX NC
%        TRANSFER "^^WI '  " // CHARACTER ( NCYCLE )  // "'" TO DISPLAY
^^WI     CHARS=5 INTEGER
^^WI    @ 3,1 STATIC T1 'Partial Shift Factor'
^^WI    @ 3,3 EDITBOX NS
%        TRANSFER "^^WI '  " // CHARACTER ( SHIFT )  // "'" TO DISPLAY
^^WI     CHARS=5 REAL
^^WI   }
^^WI   @ 3,1 CHECKBOX NF
^^WI   'Stop cycling if convergence is detected       '
%    IF CONVRG THEN
^^WI     STATE=ON
%    ELSE
^^WI     STATE=OFF
%    END IF
%%
^^WI    @ 5,1  CHECKBOX ND
^^WI         'Minimise disk useage' STATE=ON
^^WI    @ 7,1 GRID LED NROWS=1 NCOLS=5
^^WI   {
^^WI    @ 1,1 BUTTON RF_B12 'Edit directives'
^^WI    @ 1,3 BUTTON RF_B16 'Edit restraints'
^^WI    @ 1,5 BUTTON RF_INFO 'Info'
^^WI   }
^^WI  }
^^WI  @ 2,6 GRID R NROWS=3 NCOLS=1
^^WI  {
^^WI   @ 1,1 BUTTON RF_BOK '&OK' DEFAULT
^^WI   @ 3,1 BUTTON RF_BXX '&Cancel'
^^WI  }
^^WI }
^^WI SHOW
^^CR
%%
%%
%%
%    LOOP
%      ON ERROR TERMINATE
%      VERIFY RF_BXX RF_BOK RF_B12 RF_B16 RF_INFO
%      GET NOSTORE SILENT FINAL ABBREVIATED ' ' ' '
%      CASE VALUE
%       BLOCK
^^CO      DISPOSE XREFINE
%         FINISH
%       END BLOCK
%       BLOCK
%         EVALUATE SERR = FALSE
%         EVALUATE LOOPEND = LOOPCOUNTER
%       END BLOCK
%       BLOCK         % EDIT 12
%         COPY '#SCRIPT EDLIST12'
%       END BLOCK
%       BLOCK         % EDIT 16
%         COPY '#SCRIPT EDLIST16'
%       END BLOCK
%       BLOCK
{1,11 Partial Shift Factor:
{12,11      Least Squares shifts are multiplied by this value.
{12,11      Try 0.5 if the maximum shift keeps changing sign
{12,11
%        END BLOCK
%      END CASE
%    END LOOP
%    IF SERR .EQ. TRUE THEN
^^CO      DISPOSE XREFINE
{E An error of some type occured. The xrefine script has stopped just
{E in case it is serious. A possible cause is an invalid List 12 - maybe
{E it refers to non-existant atoms.
%         FINISH
%    END IF
%%

^^??   ND STATE
%      VERIFY ON OFF
%      GET NOSTORE SILENT FINAL ABBREVIATED 'ONorOFF' ' '
^^CR
%      IF VALUE .EQ. 1 THEN
%%       COPY '#SUM LIST 12'
%%       COPY 'END'
%       IF EXISTS 11 .EQ. 1 THEN
%         COPY '#DISK'
%         COPY 'DELETE 11'
%         COPY 'END'
%       END IF
%% Purge everything until bug is fixed.
%       COPY '#PURGE LIST=11 MON=LOW'
%%      COPY '#PURGE '
%       COPY 'END'
%       COPY '#LIST 22'
%       COPY 'END'
%      END IF
%%
^^?? NC TEXT
%    GET NOSTORE SILENT FINAL INTEGER 'How many cycles' '5'
%    EVALUATE NCYCLE = VALUE
% TRANSFER "^^CO SETKEY XREFINE_CYCLES " // CHARACTER ( NCYCLE ) TO DISPLAY
^^?? NS TEXT
%    GET NOSTORE SILENT FINAL REAL 'Partial Shift Factor' '1.0'
%    EVALUATE SHIFT = VALUE
^^?? NF STATE
%    VERIFY ON OFF
%    GET NOSTORE SILENT FINAL ABBREVIATED 'ONorOFF' ' '
%    EVALUATE NFORCE = VALUE
^^CO DISPOSE XREFINE
%%
%  END IF
%%
%%
%%
%%
%  EVALUATE RMIN = R
% if ( oscale - 1.000 .ge. - .0001 ) .and. ( oscale - 1.000 .le. .0001 ) then
%      copy '#sfls'
%      copy 'scale'
%      copy 'end'
%      end if
%%
%  COPY '#SCRIPT XGETINFO'
%  IF ( EDONE .EQ. TRUE ) .AND. ( PDONE .EQ. FALSE ) THEN
%    IF EXISTS 11 .EQ. 1 THEN
%      COPY '#DISK'
%      COPY 'DELETE 11'
%      COPY 'END'
%    END IF
%    EVALUATE PMASK = TRUE
%    EVALUATE PDONE = TRUE
%    COPY '#SCRIPT XPUTINFO'
%    COPY '#PURGE LIST=11 MON=LOW'
%    COPY 'END'
%  END IF
%%
%  COPY '#SCRIPT XDELQ'
%%
%  COPY '#LIST 22'
%  COPY 'END'
%%
%  IF NCYCLE .GT. 0 THEN
%    CASE NFORCE
%%
%      BLOCK                                        % NORMAL (not forced)
%%
%  IF XREFINE .EQ. FALSE THEN
^^CO     SETKEY XREFINE_CONVERGE 1
%  END IF
^^WI WINDOW XABORT 'Refining' MODAL GRID MAIN NROWS=3 NCOLS=5
^^WI { @ 2,2 STATIC T1 'Click to abort (after next cycle).'
^^WI   @ 2,4 BUTTON RF_BAB '&Abort' INFORM=NO DEFAULT DISABLED=NO } SHOW
^^CR
%          LOOP NCYCLE TIMES
%            COPY '#SFLS'
%            COPY 'REFINE'
%              IF SHIFT .LT. 1.0 THEN
%               clear
%               insert 'SHIFT GENERAL ='
%               store real  shift
%               insert ' OVERALL = '
%               store real  shift
%               send
%              END IF
%            COPY 'END'
%%         TEST IF INVERSION WENT OK
%          COPY '#GENERALEDIT 11'
%          COPY 'LOCATE RECORDTYPE=103'
%          COPY 'TRANSFER FROM OFFSET=30 TO INVERTSTATUS'
%          COPY 'END'
%          IF INVERTSTATUS .LT. 0 THEN
{E Inversion is approximate please check your model and restraints. 
{E Cycles of refinements have been stopped
%              EVALUATE LOOPEND = LOOPCOUNTER
%          END IF
%%         TEST IF CONVERGED 
%          COPY '#SCRIPT SFLSCONVERGE'
%            IF CONVERGE .GT. 1. THEN
%              EVALUATE LOOPEND = LOOPCOUNTER
{I Refinement has converged
%            END IF
%  TRANSFER '{I Finished Cycle ' // character loopcounter //  ' of ' //  character ncycle TO DISPLAY 
^^??         RF_BAB STATE
%            VERIFY ON OFF
%            GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%            IF VALUE .EQ. 1  THEN
%              EVALUATE LOOPEND = LOOPCOUNTER
{I Further refinement cycles abandoned at users request.
%            END IF
%          END LOOP
%      END BLOCK
%%
%      BLOCK                                        % FORCE
%%
%% n forced cycles.
%%
%  IF XREFINE .EQ. FALSE THEN
^^CO SETKEY XREFINE_CONVERGE 0
%  END IF 
^^WI WINDOW XABORT 'Refining' MODAL GRID MAIN NROWS=3 NCOLS=5
^^WI { @ 2,2 STATIC T1 'Click to abort (after next cycle).'
^^WI   @ 2,4 BUTTON RF_BAB '&Abort' INFORM=NO DEFAULT DISABLED=NO } SHOW
^^CR
%        LOOP NCYCLE TIMES
%          COPY '#SFLS'
%          COPY 'REFINE'
%              IF SHIFT .LT. 1.0 THEN
%                clear
%                insert 'SHIFT GENERAL ='
%                store real  shift
%                insert ' OVERALL = '
%                store real  shift
%                send
%              END IF
%          COPY 'END'
%  TRANSFER '{I Finished Cycle ' // character loopcounter //  ' of ' //  character ncycle TO DISPLAY 
^^??       RF_BAB STATE
%          VERIFY ON OFF
%          GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%          IF VALUE .EQ. 1 THEN
{I Further refinement cycles abandoned at users request.
%              EVALUATE LOOPEND = LOOPCOUNTER
%          END IF
%%         TEST IF INVERSION WENT OK
%          COPY '#GENERALEDIT 11'
%          COPY 'LOCATE RECORDTYPE=103'
%          COPY 'TRANSFER FROM OFFSET=30 TO INVERTSTATUS'
%          COPY 'END'
%          IF INVERTSTATUS .LT. 0 THEN
{E Inversion is approximate please check your model and restraints. 
{E Cycles of refinements have been stopped
%              EVALUATE LOOPEND = LOOPCOUNTER
%          END IF
%        END LOOP
%      END BLOCK
%    END CASE
%  END IF
^^CO DISPOSE XABORT
%%
%  COPY '#SCRIPT RFACTOR'
%  CLEAR
%  INSERT '{RThe old R-factor was'
%  STORE FORMAT /(F8.3)/ LENGTH 8 REAL RMIN
%  INSERT '%'
%%
%  IF ( RMIN .GT. 0.0 ) THEN
%   IF R .GT. RMIN * 1.1 THEN
%    INSERT ', the R-factor is worse.'
%   ELSE IF R .GE. RMIN * 0.99 THEN
%    INSERT ', the refinement has probably converged.'
%   END IF
%  END IF
%  OUTPUT
%%
%  EVALUATE RMIN = R
%%
%END SCRIPT
