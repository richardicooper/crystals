%SCRIPT TON                                                                     
%%                                                                              
%%                                                                              
%  VARIABLE LOGICAL SERR                                                        
%  VARIABLE REAL CRITER                                                         
%  EVALUATE SERR = TRUE                                                         
%  EVALUATE CRITER = 99999.                                                      
%  VARIABLE INTEGER IPLOT IPUNCH L5SER L5NEW 
%  VARIABLE CHARACTER CPLOT CPUNCH                                                
%  EVALUATE IPLOT = 0                                                           
%  EVALUATE IPUNCH = 0                                                           
%%                                                                              
%%                                                                              
^^WI WINDOW XHOOFT 'Absolute Configuration' MODAL                               
^^WI COMMIT='RF_BOK' CANCEL='RF_BXX'                                            
^^WI GRID MAIN NROWS=4 NCOLS=7                                                  
^^WI {                                                                          
^^WI  @ 2,2 GRID L NROWS=9 NCOLS=1                                              
^^WI  {                                                                         
^^WI   @ 1,1 STATIC T2 'Relections are omitted if delta(Fo) '                   
^^WI   @ 2,1 STATIC T3 'is greater than CRITERION*delta(Fc) '                   
^^WI   @ 3,1 STATIC T4 'i.e. F+ or F- is probably in error  '                   
^^WI   @ 5,1 GRID NCYC NROWS=3 NCOLS=3                                          
^^WI   {                                                                        
^^WI    @ 1,1 STATIC T1 'Criterion'                                             
^^WI    @ 1,3 EDITBOX NC                                                        
%        TRANSFER "^^WI '  " // CHARACTER ( CRITER )  // "'" TO DISPLAY         
^^WI     CHARS=12 REAL                                                          
^^WI   }                                                                        
^^WI    @ 6,1 GRID AC NROWS=1 NCOLS=3 OUTLINE='Plots'                           
^^WI    {                                                                       
^^WI     @ 1,1 RADIOBUTTON IPLOTA 'None'                                        
^^WI            STATE=ON                                                       
^^WI     @ 1,2 RADIOBUTTON IPLOTB 'Fo/Fc'                                       
^^WI            STATE=OFF                                                        
^^WI     @ 1,3 RADIOBUTTON IPLOTC 'NPP'                                         
^^WI            STATE=OFF    
^^WI    }                                                                       
^^WI    @ 8,1 GRID AC NROWS=1 NCOLS=3 OUTLINE='Output'                           
^^WI    {                                                                       
^^WI     @ 1,1 RADIOBUTTON IPUNCHA 'None'                                        
^^WI            STATE=ON                                                       
^^WI     @ 1,2 RADIOBUTTON IPUNCHB 'Punch'                                       
^^WI            STATE=OFF                                                        
^^WI     @ 1,3 RADIOBUTTON IPUNCHC 'Restrain'                                         
^^WI            STATE=OFF    
^^WI    }                                                                       
^^WI  }                                                                         
^^WI  @ 2,6 GRID R NROWS=3 NCOLS=1                                              
^^WI  {                                                                         
^^WI   @ 1,1 BUTTON RF_BOK '&OK' DEFAULT                                        
^^WI   @ 3,1 BUTTON RF_BXX '&Cancel'                                            
^^WI  }                                                                         
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
%%                                                                              
%%                                                                              
%    LOOP                                                                       
%      ON ERROR TERMINATE                                                       
%      VERIFY RF_BXX RF_BOK                                                     
%      GET NOSTORE SILENT FINAL ABBREVIATED ' ' ' '                             
%      CASE VALUE                                                               
%       BLOCK                                                                   
^^CO      DISPOSE XHOOFT                                                        
%         FINISH                                                                
%       END BLOCK                                                               
%       BLOCK                                                                   
%         EVALUATE SERR = FALSE                                                 
%         EVALUATE LOOPEND = LOOPCOUNTER                                        
%       END BLOCK                                                               
%      END CASE                                                                 
%    END LOOP                                                                   
%    IF SERR .EQ. TRUE THEN                                                     
^^CO      DISPOSE XHOOFT                                                        
{E An error of some type occured.                                               
{E The TON script has stopped just in case it is serious.                       
%         FINISH                                                                
%    END IF                                                                     
%%                                                                              
^^?? NC TEXT                                                                    
%    GET NOSTORE SILENT FINAL REAL 'Criterion' '100.0'                          
%    EVALUATE CRITER = VALUE                                                    
^^??  IPLOTA STATE                                                              
%     VERIFY ON OFF                                                             
%     GET SILENT NOSTORE FINAL ABBREVIATED ' '                                  
%     IF VALUE .EQ. 1 THEN                                                      
%       EVALUATE IPLOT = 0
%       EVALUATE CPLOT = 'NO'                                                      
%     END IF                                                                    
^^??  IPLOTB STATE                                                              
%     VERIFY ON OFF                                                             
%     GET SILENT NOSTORE FINAL ABBREVIATED ' '                                  
%     IF VALUE .EQ. 1 THEN                                                      
%       EVALUATE IPLOT = 1                                                      
%       EVALUATE CPLOT = 'FO'                                                      
%     END IF                                                                    
^^??  IPLOTC STATE                                                              
%     VERIFY ON OFF                                                             
%     GET SILENT NOSTORE FINAL ABBREVIATED ' '                                  
%     IF VALUE .EQ. 1 THEN                                                      
%       EVALUATE IPLOT = 2                                                      
%       EVALUATE CPLOT = 'NPP'                                                      
%     END IF                                                                    
^^??  IPUNCHA STATE                                                              
%     VERIFY ON OFF                                                             
%     GET SILENT NOSTORE FINAL ABBREVIATED ' '                                  
%     IF VALUE .EQ. 1 THEN                                                      
%       EVALUATE IPUNCH = 0                                                      
%       EVALUATE CPUNCH = 'NO'                                                      
%     END IF                                                                    
^^??  IPUNCHB STATE                                                              
%     VERIFY ON OFF                                                             
%     GET SILENT NOSTORE FINAL ABBREVIATED ' '                                  
%     IF VALUE .EQ. 1 THEN                                                      
%       EVALUATE IPUNCH = 1                                                      
%       EVALUATE CPUNCH = 'YES'                                                      
%     END IF                                                                    
^^??  IPUNCHC STATE                                                              
%     VERIFY ON OFF                                                             
%     GET SILENT NOSTORE FINAL ABBREVIATED ' '                                  
%     IF VALUE .EQ. 1 THEN                                                      
%       EVALUATE IPUNCH = 2   
%       EVALUATE CPUNCH = 'REST'                                                      
%     END IF                                                                    
^^CO DISPOSE XHOOFT                                                             
%%                                                                              
%% Create special LIST 7 with F+ and F- in adjacent slots                       
%  BLOCK                                                                        
%    IF ( EXISTS 6 .NE. 1 ) THEN                                                
{E **** FATAL: There are no reflections to work with                            
%      FINISH                                                                   
%    END IF                                                                     
%  END BLOCK                                                                    
%  IF ( EXISTS 7 .EQ. 1 ) THEN                                                  
%    COPY '#DISK'                                                               
%    COPY 'DELETE 7'                                                            
%    COPY 'END'                                                                 
%  END IF                                                                       
%COPY '#SCRIPT copy67'                                                          
%  BLOCK                                                                        
%    IF ( EXISTS 7 .NE. 1 ) THEN                                                
{E **** FATAL: The reflection convertion failed                                 
%      FINISH                                                                   
%    END IF                                                                     
%  END BLOCK                                                                    
%copy '#disk'                                                                   
%copy 'retain 5'                                                                
%copy 'end'                                                                     
%copy '#generaledit 5'                                                          
%copy 'getserial l5ser'                                                         
%copy 'end'                                                                     
% if l5ser .lt. 0 then                                                          
%  evaluate l5ser = - l5ser                                                     
% end if                                                                        
%copy '#edit'                                                                   
%copy 'change enantio 0'                                                        
%copy 'end'                                                                     
%copy '#sfls in=7'                                                              
%copy 'calc'                                                                    
%copy 'end'                                                                     
%%
%copy '#generaledit 5'                                                          
%copy 'getserial l5new'                                                         
%copy 'end'                                                                     
%%
%copy '#disk'                                                                   
%clear                                                                          
%insert 'reset 5 '                                                              
%store integer l5ser                                                            
%send                                                                           
%%
%clear                                                                          
%insert 'delete 5 '                                                              
%store integer l5new                                                            
%send                                                                           
%%
%copy 'retain 5 action=no'                                                      
%copy 'end'                                                                     
%copy '#syst in=7 fried=yes'                                                    
%copy 'end'                                                                     
%copy '#sort in=7'                                                              
%copy 'end'                                                                     
%copy '#purge  list=7'                                                          
%copy 'end'                                                                     
%%        
%%
%% open the restraint file if required
% IF IPUNCH = 2 THEN                                                                   
%   COPY '#RELEASE PUNCH flack-restraint.pch'                                          
% END IF
% IF IPLOT .EQ. 0 THEN                                                          
%   TRANSFER '#TON CRITER= ' // CHARACTER ( CRITER ) // '  -
PUNCH = ' //   CPUNCH TO CRYSTALS        
%   TRANSFER "END" TO CRYSTALS                                                  
%       IF IPUNCH = 2 THEN                                                                   
%         COPY '#RELEASE PUNCH bfile.pch'                                          
%       END IF
% ELSE IF IPLOT .EQ. 1 THEN                                                     
^^WI WINDOW xnormpp 'Do/s vs Dc/s plot' KEEP SIZE MODAL                             
^^WI COMMIT='NPP_BOK' CANCEL='NPP_BOK'                                          
^^WI GRID MAIN NROWS=2 NCOLS=1                                                  
^^WI {                                                                          
^^WI  @ 1,1 GRID PRIME NROWS=7 NCOLS=3                                          
^^WI  {                                                                         
^^WI   @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''                               
^^WI   {                                                                        
^^WI    @ 1,3 GRID EXP2 NROWS=4 NCOLS=1                                         
^^WI    {                                                                       
^^WI     @ 1,1 STATIC XFW1                                                      
^^WI "The (observed anomalous differences)/sigma(Do) plotted against "
^^WI     @ 2,1 STATIC XFW2                                                      
^^WI 'the (calculated anomalous differences)/sigma(Do). '
^^WI     @ 3,1 STATIC XFW3                                                      
^^WI ' The plot should belinear and pass through the origin'
^^WI     @ 4,1 STATIC XFW4
^^WI ' if the weights are good.'              
^^WI    }                                                                       
^^WI    @ 1,1 ICON IM INFO                                                      
^^WI   }                                                                        
^^WI   @ 4,2 PLOTWINDOW _VFOFC NROWS=20 NCOLS=64                                
^^WI   @ 6,2 GRID EXBT NROWS=1 NCOLS=1                                          
^^WI   {                                                                        
^^WI    @ 1,1 BUTTON NPP_BOK 'Close' DEFAULT                                    
^^WI   }                                                                        
^^WI  }                                                                         
^^WI  @ 2,1 PROGRESS _FOFCPR 'Please wait'                                      
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
^^CO SENDPROGRESSTO _FOFCPR                                                     
%  LOOP                                                                         
% TRANSFER '#TON CRITER= ' // CHARACTER ( CRITER ) // -
' PLOT=FO PUNCH = ' // CPUNCH TO CRYSTALS   
% TRANSFER "END" TO CRYSTALS                                                    
%    VERIFY NPP_BOK OMIT                                                        
%    GET SILENT NOSTORE FINAL ABBREVIATED '?' 'EXT_BOK'                         
%    IF VALUE .EQ. 1 THEN                                                       
^^CO    DISPOSE xnormpp                                                         
%       IF IPUNCH = 2 THEN                                                                   
%         COPY '#RELEASE PUNCH bfile.pch'                                          
%       END IF
%       FINISH                                                                  
%    ELSE IF VALUE .EQ. 2 THEN                                                  
"test"                                                                          
%    END IF                                                                     
%  END LOOP                                                                     
%%                                                                              
% ELSE IF IPLOT .EQ. 2 THEN                                                     
^^WI WINDOW xnormpp 'Normal probabability plot' KEEP SIZE MODAL                 
^^WI COMMIT='NPP_BOK' CANCEL='NPP_BOK'                                          
^^WI GRID MAIN NROWS=2 NCOLS=1                                                  
^^WI {                                                                          
^^WI  @ 1,1 GRID PRIME NROWS=7 NCOLS=3                                          
^^WI  {                                                                         
^^WI   @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''                               
^^WI   {                                                                        
^^WI    @ 1,3 GRID EXP2 NROWS=3 NCOLS=1                                         
^^WI    {                                                                       
^^WI     @ 1,1 STATIC XFW1                                                      
^^WI "The resudual is plotted against the 'normal score' - the "                
^^WI     @ 2,1 STATIC XFW2                                                      
^^WI 'expected distribution of errors based on the normal pdf. It should be'    
^^WI     @ 3,1 STATIC XFW3                                                      
^^WI 'linear and pass through the origin if the weights are good.'              
^^WI    }                                                                       
^^WI    @ 1,1 ICON IM INFO                                                      
^^WI   }                                                                        
^^WI   @ 4,2 PLOTWINDOW _VNORMPP NROWS=20 NCOLS=64                              
^^WI   @ 6,2 GRID EXBT NROWS=1 NCOLS=1                                          
^^WI   {                                                                        
^^WI    @ 1,1 BUTTON NPP_BOK 'Close' DEFAULT                                    
^^WI   }                                                                        
^^WI  }                                                                         
^^WI  @ 2,1 PROGRESS _FOFCPR 'Please wait'                                      
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
^^CO SENDPROGRESSTO _FOFCPR                                                     
%  LOOP                                                                         
% TRANSFER '#TON CRITER= ' // CHARACTER ( CRITER ) // -
' PLOT=NPP PUNCH = ' // CPUNCH  TO CRYSTALS  
% TRANSFER "END" TO CRYSTALS                                                    
%    VERIFY NPP_BOK OMIT                                                        
%    GET SILENT NOSTORE FINAL ABBREVIATED '?' 'EXT_BOK'                         
%    IF VALUE .EQ. 1 THEN                                                       
^^CO    DISPOSE xnormpp                                                         
%       IF IPUNCH = 2 THEN                                                                   
%         COPY '#RELEASE PUNCH bfile.pch'                                          
%       END IF
%       FINISH                                                                  
%    ELSE IF VALUE .EQ. 2 THEN                                                  
"test"                                                                          
%    END IF                                                                     
%  END LOOP                                                                     
%%                                                                              
% END IF    
