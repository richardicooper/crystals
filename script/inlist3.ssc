%SCRIPT INLIST3
%% CRYSTALS scattering factor input script
%  VARIABLE INTEGER ELEMENTCOUNT RADIATION
 Input scattering factors
 ----- ---------- -------
%  QUEUE REWIND
%  EVALUATE ELEMENTCOUNT = 0
%  IF RADIATION .LE. 0 THEN
%    VERIFY CU MO
%    GET FINAL VERIFIED 'Radiation' 'MO'
%    EVALUATE RADIATION = VALUE
%  END IF
%
%%  CASE RADIATION
%    COPY '#OPEN SCPDATA CRYSDIR:script/scatt.dat'
%%  END CASE
%
%  MESSAGE USER1 'An invalid element name was given ( ignored )'
%
%  LOOP
%    ON END TERMINATE
%    ON ERROR REPEAT USER1
%
%    CLEAR
%    INSERT '%EXTRACT FIND STRING "'
%    GET APPEND KEYWORD -
      'Element name ( Use END, on a line by itself, to end input )'
%    INSERT ' "'
%    EXECUTE SEND
%
%    CLEAR
%    INSERT 'SCATT '
% CASE RADIATION
%  BLOCK
%    EXTRACT NEXT
%    EXTRACT TRANSFER COMMAND
%  END BLOCK
%  BLOCK
%    EXTRACT TRANSFER COMMAND
%    EXTRACT NEXT
%  END BLOCK
% END CASE
%    QUEUE SEND
%
%    CLEAR
%    EXTRACT NEXT
%    EXTRACT TRANSFER COMMAND
%    QUEUE SEND
%
%    EVALUATE ELEMENTCOUNT = ELEMENTCOUNT + 1
%
%  END LOOP
%
%  IF ELEMENTCOUNT .GT. 0 THEN
%    COPY '#LIST 3'
%    CLEAR
%    INSERT 'READ NSCATTER = '
%    STORE INTEGER ELEMENTCOUNT
%    SEND
%    QUEUE PROCESS
%    COPY 'END'
%    COPY '#SUMMARY LIST 3'
%    COPY 'END'
%  ELSE
      You have not specified any elements. No data has been stored
%  END IF
%
%  FINISH
%END SCRIPT
