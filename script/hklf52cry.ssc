%SCRIPT HKLF52CRY   
%%  
%% Run hklf52cry with common options.  
%%  
%  VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2
%  VARIABLE LOGICAL ANSWER
%  VARIABLE CHARACTER CIN COUT CARG  
%  VARIABLE LOGICAL NEW25 RUNNOW
%  VARIABLE REAL VOLF  
%  VARIABLE INTEGER N25 NEL  
%  VARIABLE LOGICAL EDITOR_OK
%  VARIABLE CHARACTER EDITOR_FILE
%  EVALUATE N25 = 0
%  EVALUATE RUNNOW = FALSE  
%  EVALUATE NEW25 = FALSE  
^^WI WINDOW HKLF52CRY 'hklf5cry' MODAL COMMIT='CFBOK' CANCEL='CFBXX'  
^^WI GRID GRIDM NROWS=6 NCOLS=3  
^^WI {  
^^WI @ 2,2 GRID CFG NROWS=1 NCOLS=3 OUTLINE='hklf5 to convert:'  
^^WI {  
^^WI @ 1,1 EDITBOX CINILE ' ' CHARS=48 INFORM=NO INPUT  
^^WI @ 1,3 BUTTON BROWSE 'Browse...'  
^^WI }  
^^WI @ 4,2 GRID GB NROWS=3 NCOLS=5  
^^WI {  
^^WI @ 1,1 STATIC T1 'Output file:'  
^^WI @ 1,3 EDITBOX OFNAM 'hklf52cry.cry'  
^^WI }  
^^WI @ 5,2 GRID GB NROWS=1 NCOLS=5  
^^WI {  
^^WI @ 1,1 BUTTON CFBOK '&Ok' DEFAULT  
^^WI @ 1,3 BUTTON CFBXX '&Cancel' 
^^WI @ 1,5 BUTTON CFHELP 'Help' 
^^WI }  
^^WI }  
^^WI SHOW  
^^CR  
%  LOOP  
%    VERIFY BROWSE CFBXX CFBOK  CFHELP
%    GET SILENT NOSTORE FINAL ABBREVIATED 'BXX?' 'BXX'
%    CASE VALUE  
%      BLOCK %BROWSE%  
^^WI SYSOPENFILE '*.hkl;*.dat;' 'HKL format (*.hkl,*.dat)'  
^^CR  
%        GET SILENT TEXT 'New filename:'  
%        IF CVALUE .NE. 'CANCEL' THEN  
%          CLEAR  
^^WI SET CINILE TEXT  
%          TRANSFER "^^WI '" // CVALUE // "'" TO DISPLAY  
^^CR  
%          EVALUATE CIN = CVALUE  
%          IF ( .NOT. ( FILEEXISTS CIN ) ) THEN  
{E Error: Filename given does not exist.  
%          END IF  
%        END IF  
%      END BLOCK  
%      BLOCK %CFBXX%  
^^CO DISPOSE HKLF52CRY  
%        FINISH  
%      END BLOCK  
%      BLOCK %CFBOK%  
^^?? CINILE TEXT  
%        GET SILENT TEXT 'CIN name '  
%        EVALUATE CIN = CVALUE  
%        IF ( .NOT. ( FILEEXISTS CIN ) ) THEN  
{E Error: Filename given does not exist.  
%          ELSE  
^^?? OFNAM TEXT  
%          GET SILENT TEXT 'Output file '  
%          EVALUATE COUT = CVALUE  
%%  
^^CO DISPOSE HKLF52CRY  
%          EVALUATE LOOPEND = LOOPCOUNTER  
%        END IF  
%      END BLOCK  
%      BLOCK %CFHELP%
%       COPY '#SPAWN CRYSDIR:manual/Xray-data/table.html'
%      END BLOCK
%    END CASE  
%  END LOOP  
%%  
%% Launch HKLF5 TO CRY  
%%  
%% Remove any previous files
%  VARIABLE LOGICAL T
%  EVALUATE T = FILEDELETE ( 'l25.cry' )
%  EVALUATE T = FILEDELETE ( cout )
%%
%  variable character ccommand  
%  evaluate ccommand = '#SPAWN + "CRYSDIR:hklf52cry.exe" "' -  
// cin // '" -o ' // cout  
%  TRANSFER ccommand TO display  
%  TRANSFER ccommand TO CRYSTALS  
%%  
%%  
%%  START PROCESSING THE OUTPUT
%%  
%  if ( fileexists 'l25.cry' ) then
%    EVALUATE RUNNOW = TRUE
%%  Show the matrix


{I CRYSTALS thinks that the twin laws are:
%    copy '#type l25.cry'
%  else
%    EVALUATE RUNNOW = FALSE

{E  CRYSTALS is having problems understanding the hkl5 file.
{E  The most common reason is that the HKLF5 file
{E  contains merged reflections.
{E  Create an unmerged file which will be merged in CRYSTALS.
{E  Look in the LOG file (TLAW_OUT.LIS) for details.

%  end if
%%  
^^WI WINDOW A1 'Process Twin output' SIZE LARGE KEEP MODAL
^^WI CANCEL='RF_BXX'
^^WI GRID MAIN NROWS=2 NCOLS=3
^^WI {
^^WI   @ 1,2 GRID TEXT NROWS=4  NCOLS=1
^^WI   {
^^WI    @ 1,1 STATIC TEXT1 
^^WI    'If CRYSTALS has found the correct twin law, choose Input Matrix'
^^WI    @ 2,1 STATIC TEXT1 
^^WI    'If you have other ideas, you can edit the Matrix File'
^^WI    @ 3,1 STATIC TEXT1 
^^WI    'If you know the matrix from the instrument, Manually Input a matrix'
^^WI    @ 4,1 STATIC TEXT1 
^^WI    'If you need more facts. look at the log file'
^^WI   }
^^WI   @ 2,2 GRID BTNS NROWS=4 NCOLS=3 OUTLINE=' '
^^WI   {
^^WI       @ 1,1 RADIOBUTTON ITYP11 'Input Matrix file'    STATE=OFF INFORM
^^WI       @ 1,2 RADIOBUTTON IPUN12 'Look at LOG file'     STATE=OFF INFORM
^^WI       @ 2,1 RADIOBUTTON ITYP21 'Edit Matrix File'     STATE=OFF INFORM
^^WI       @ 2,2 RADIOBUTTON ITYP22 'Manual Matrix Input'   STATE=OFF INFORM
^^WI       @ 4,1 BUTTON RF_BXX 'Quit' 
^^WI       @ 4,2 BUTTON RF_CONT 'Continue' 
^^WI       @ 4,3 BUTTON RF_HELP 'Help'        
^^WI   }     
^^WI }
^^WI SHOW
^^CR
% VARIABLE INTEGER JLOOP IDJW 
% VARIABLE LOGICAL USE6
% EVALUATE USE6 = FALSE
% LOOP
%  EVALUATE JLOOP = LOOPCOUNTER
%  VERIFY  RF_BXX ITYP11 IPUN12 ITYP21 ITYP22 RF_HELP RF_CONT
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  EVALUATE IDJW = VALUE
%  CASE VALUE
%   BLOCK                                           %RF_BXX - QUIT%
%       EVALUATE LOOPCOUNTER = LOOPEND
%   END BLOCK
%%
%   BLOCK                                           %READ MATRIX%
%    IF FILEEXISTS ( 'l25.cry' ) THEN
%     COPY '#USE l25.cry'
%     EVALUATE NEW25 = TRUE
%     EVALUATE USE6 = TRUE
%     EVALUATE LOOPCOUNTER = LOOPEND
%    ELSE
{E  A matrix has not been computed
%    END IF   
%   END BLOCK 
%%
%   BLOCK                                           %LOOK AT LOG%
%    IF FILEEXISTS ( 'TLAW_OUT.LIS' ) THEN
#ifdef __GID__
%      COPY '#SPAWN +notepad.exe TLAW_OUT.LIS'
#else
%      EVALUATE EDITOR_FILE = 'TLAW_OUT.LIS'
%      COPY '#SCRIPT XEDITOR'
#endif
%    ELSE
{E  CRYSTALS failed to process the HKLF 5 file
%    END IF
%   END BLOCK
%%
%   BLOCK                                           %EDIT MATRIX%
%    IF FILEEXISTS ( 'l25.cry' ) THEN
#ifdef __GID__
%      COPY '#SPAWN +notepad.exe l25.cry'
#else
%      EVALUATE EDITOR_FILE = 'l25.cry'
%      COPY '#SCRIPT XEDITOR'
%      IF ( .NOT. EDITOR_OK ) THEN
 Editing abandoned
%      ELSE
%        EVALUATE USE6 = TRUE
%        EVALUATE LOOPCOUNTER = LOOPEND
%      END IF                                                                             
#endif
%    ELSE
{E  There is no matrix file (l25.cry) to edit
%    END IF   
%   END BLOCK
%%
%   BLOCK                                           %MANUAL MATRIX%
%    copy '#script IN25'
%    EVALUATE NEW25 = TRUE
%    EVALUATE USE6 = TRUE
%    EVALUATE LOOPCOUNTER = LOOPEND
%%
%   END BLOCK
%   BLOCK                                           %HELP%
%    COPY '#SPAWN CRYSDIR:manual/analyse/hklf52cry.html'
%   END BLOCK
%   BLOCK                                           %CONTINUE%
{I   Choose an option
%   END BLOCK
%  END CASE
% END LOOP
^^CO    DISPOSE A1
%%
%  IF USE6 .EQ. FALSE THEN
{E The reflections will not be processed
%   FINISH
%  END IF

{I The reflections will be processed after some
{I intenal checks have been made




%% MODIFY LISTS 5, 13 AND 25
%  IF ( EXISTS 5 .NE. 1 ) THEN  
%   COPY '#LIST 5'
%   COPY 'READ NATOM=0'
%   COPY 'END'
%  END IF
%%
%  IF ( EXISTS 25 .EQ. 1 ) THEN  
Found List 25 (twin laws), enabling twin flag  
%    VARIABLE INTEGER ITMP  
%    COPY '#GENERALEDIT 13'  
%    COPY 'LOCATE RECORDTYPE=101'  
%% Check for twin laws. If present, restore L13 flag.  
%    EVALUATE ITMP = 0  
%    COPY 'TRANSFER TO OFFSET=1 FROM ITMP'  
%    COPY 'WRITE'  
%    COPY 'END'  
%%  
%    EVALUATE VOLF = 1.  
%    COPY '#GENERALEDIT 25'  
%    COPY 'TRANSHEAD FROM OFFSET=3 TO N25'  
%    COPY 'GETSERIAL ORIGSER'  
%    COPY 'END'  
%    EVALUATE VOLF = 1. / REAL N25  
%  END IF  
%%  
%    IF N25 .GE. 2 THEN  
Resetting element scale factors  
%      COPY '#GENERALEDIT 5'  
%      COPY 'TRANSHEAD FROM OFFSET=22 TO NEL'  
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'  
%      COPY 'LOCATE RECORDTYPE=105'  
%% Insert new record.  
%      COPY 'TRANSHEAD TO OFFSET=22 FROM N25'  
%      COPY 'INSERT'  
%      IF NEL .GT. 0 THEN  
%% Remove old record  
%        COPY 'DELETE'  
%      END IF  
%      COPY 'TRANSHEAD TO OFFSET=22 FROM N25'  
%      COPY 'TOP'  
%      LOOP N25 TIMES  
%        CASE LOOPCOUNTER  
%          COPY 'TRANSFER TO OFFSET = 0 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 1 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 2 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 3 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 4 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 5 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 6 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 7 FROM VOLF'  
%          COPY 'TRANSFER TO OFFSET = 8 FROM VOLF'  
%        END CASE  
%      END LOOP  
%      COPY 'WRITE'  
%      COPY 'END'  
%    END IF  
%%  
%%  
%% Only import reflections if LIST 25 exists
%  IF ( .NOT. RUNNOW ) THEN

{E  HKLF 5 Reflection processing was unsuccessful.
{E  The most common reason is that the HKLF5 file
{E  contains merged reflections.
{E  Create an unmerged file which will be merged in CRYSTALS.
{E  Look in TLAW_OUT.LIS for details
%  ELSE
%%  
%   IF ( ( EXISTS 25 ) .GE. 1 ) THEN  
%     EVALUATE QTITLE = 'Import reflections'
%     EVALUATE QLINE1 = 'Do you want to import the reflections?'
%     EVALUATE QLINE2 = ' '
%     EVALUATE BUTTOK = '&Yes'
%     EVALUATE BUTTXX = '&No'
%     COPY '#SCRIPT XQUESTIO'
%     IF ANSWER .EQ. FALSE THEN
%       FINISH
%     ELSE
%       TRANSFER '#USE ' // COUT TO CRYSTALS  
%       COPY '#SYST'
%       COPY 'END'
%       COPY '#SORT'
%       COPY 'END'
%       COPY '#MERGE'
%       COPY 'END'
%     END IF
%   ELSE
{E  CRYSTALS must have twin laws (LIST 25) before twinned
{E  reflections can be processed
%   END IF
%  END IF
%%  
% END SCRIPT  
