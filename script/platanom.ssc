% SCRIPT PLATANOM
%% variables used by the question box below                                     
%  VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2   
%  VARIABLE CHARACTER BUTINFO BUTFILE                     
%  VARIABLE LOGICAL ANSWER   T                                                   
%%
%  IF .not. FILEEXISTS ( 'LIST6.ABC' ) THEN
{0,4 You must run a normal SQUEEZE job before you can run this utility
%    FINISH
%  END IF

{1,8
{1,8 This utility modifies the conventional output from SQUEEZE
{1,8 to account for anomalous scattering by atoms assumed to be
{1,8 somewhere in the void region.
{1,8 You will have to make an intelligent guess at the formula
{1,8 of what ever is in the void.
{1,8 
{0,4 
{0,4 If you apply Anomalous Scattering to SQUEEZE, the dsc file
{0,4 WILL NOT BE COMPATIBLE with previous versions of CRYSTALS
{0,4 

%%
%  EVALUATE QTITLE = 'Anomalous Modifications '                              
%  EVALUATE QLINE1 = 'Do you want to modify the SQUEEZE data to'                           
%  EVALUATE QLINE2 = 'include anomalous scatering from the void?'                         
%  EVALUATE BUTTOK = '&Yes'                                                 
%  EVALUATE BUTTXX = '&No'           
%  EVALUATE BUTINFO = 'Info'
%  EVALUATE BUTFILE = 'platanom.dat'                                       
%  COPY '#SCRIPT XHELPQUEST'                                        
%  IF ANSWER .EQ. TRUE THEN 
%% use the results
%    IF FILEEXISTS ( 'LIST6.DEF' ) .EQ. TRUE THEN
Trying to delete old modified Squeeze files
%     EVALUATE T = FILEDELETE ( 'LIST6.DEF' )
Deleted old modified Squeeze files
%    END IF
%    COPY '#SPAWN + "CRYSDIR:PLATANOM.EXE"'
%    IF .not. FILEEXISTS ( 'LIST6.DEF' ) THEN
The PLATANOM.exe process failed to write an output file.
%      FINISH
%    END IF
%    COPY '#USE LIST6.DEF'
%  ELSE
{1,8 Restoring normal SQUEEZE reflections
%    COPY '#USE LIST6.ABC'
%  END IF
Update LIST 23 to show the A & B parts are stored, and inhibit
them being overwritten
%    COPY '#GENERALEDIT 23'
%    COPY 'LOCATE RECORDTYPE=101'
%%   set modify partial=YES
%    COPY 'CHANGE OFFSET=4 MODE=INTEGER INTEGER=0'
%    COPY 'WRITE'
%%   set modify update=NO
%    COPY 'CHANGE OFFSET=5 MODE=INTEGER INTEGER=-1'
%    COPY 'WRITE'
%    COPY 'END'
%%
% COPY '#SFLS'
% COPY 'CALC'
% COPY 'END'
% COPY '#WEIGHT'
% COPY 'END'
% COPY '#SFLS'
% COPY 'CALC'
% COPY 'END'
%  END SCRIPT 
