%SCRIPT xnormpp
^^WI WINDOW xnormpp 'Normal probability plot' KEEP SIZE MODAL
^^WI COMMIT='NPP_BOK' CANCEL='NPP_BOK'
^^WI GRID MAIN NROWS=2 NCOLS=1
^^WI {
^^WI  @ 1,1 GRID PRIME NROWS=7 NCOLS=3
^^WI  {
^^WI   @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI   {
^^WI    @ 1,3 GRID EXP2 NROWS=4 NCOLS=1
^^WI    {
^^WI     @ 1,1 STATIC XFW1
^^WI "The weighted residual (delta/sigma) is plotted against the 'normal"
^^WI     @ 2,1 STATIC XFW2
^^WI "score' the expected distribution based on the normal pdf. It should"
^^WI     @ 3,1 STATIC XFW3
^^WI 'be linear and pass through the origin if the weights are good.'
^^WI     @ 4,1 STATIC XFW4
^^WI 'Note that sigma is estimated using the observation weight^(-0.5).'
^^WI    }
^^WI    @ 1,1 ICON IM INFO
^^WI   }
^^WI   @ 4,2 PLOTWINDOW _VNORMPP NROWS=20 NCOLS=64
^^WI     DEFINEPOPUPMENU POPUP 'Selection'
^^WI       ITEM _SDELA '&Omit _L' 'OMIT_N_L'
^^WI     ENDDEFINEMENU
^^WI   @ 6,2 GRID EXBT NROWS=1 NCOLS=6
^^WI   {
^^WI    @ 1,1 STRETCH VB HORIZONTAL
^^WI    @ 1,2 BUTTON SAVENPP 'Save Image'
^^WI    @ 1,4 BUTTON EXPNPP 'Export Data'
^^WI    @ 1,6 BUTTON NPP_BOK 'Close' DEFAULT COMMIT CANCEL
^^WI   }
^^WI  }
^^WI  @ 2,1 PROGRESS _FOFCPR 'Please wait'
^^WI }
^^WI SHOW
^^CR
^^CO SENDPROGRESSTO _FOFCPR
%  COPY '#sum list 6 level=normpp'
%  COPY 'END'
%  LOOP
%    VERIFY NPP_BOK SAVENPP EXPNPP OMIT
%    GET SILENT NOSTORE FINAL ABBREVIATED '?' 'EXT_BOK'
%    IF VALUE .EQ. 1 THEN
^^CO    DISPOSE xnormpp
%       FINISH
%    ELSE IF VALUE .EQ. 2 THEN
^^CO    SET _VNORMPP SAVE 800 600 NPP
%    ELSE IF VALUE .EQ. 3 THEN
^^CO    SET _VNORMPP EXPORT NPP.csv
%    ELSE IF VALUE .EQ. 4 THEN
%      ON EDITERROR TERMINATE
%      COPY '#GENERALEDIT 28'
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%      COPY 'LOCATE RECORDTYPE = 104'
%      COPY 'INSERT'
%      CLEAR
%      INSERT 'CHANGE 0 REAL REAL ='
%      GET INTEGER ' ' ' '
%      SEND
%      OUTPUT
%      CLEAR
%      INSERT 'CHANGE 1 REAL REAL ='
%      GET INTEGER ' ' ' '
%      SEND
%      OUTPUT
%      CLEAR
%      INSERT 'CHANGE 2 REAL REAL ='
%      GET INTEGER ' ' ' '
%      SEND
%      OUTPUT
%      CLEAR
%      COPY 'WRITE'
%      COPY 'END'
%%     update plot
%      COPY '#sum list 6 level=normpp'
%      COPY 'END'
%    END IF
%  END LOOP
%END SCRIPT

