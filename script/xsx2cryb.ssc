%SCRIPT XSX2CRYB         
%%
%% This script reads Pascal's transform.cry file in order to set
%% HKLTnn, the transformation matrix                                                     
%%                                                                              
% VARIABLE CHARACTER CFILE INSTITLE HKLTITLE
% VARIABLE REAL HKLT11 HKLT12 HKLT13 HKLT21 HKLT22 HKLT23
% VARIABLE REAL HKLT31 HKLT32 HKLT33
% VARIABLE INTEGER HKLFN 
%%
% TRANSFER '#OPEN SCPDATA '// 'crystalsinput-extra.cry' TO CRYSTALS                                                                  
%% skip one line
%      EXTRACT NEXT
%% skip omit line
%      EXTRACT NEXT
%% skip (for now) the HKL line
%      EXTRACT NEXT
%% skip next line too (scale)
%      EXTRACT NEXT
%% get first line of matrix
%      EXTRACT NEXT
%      CLEAR
%      EXTRACT TRANSFER INPUT
%      CLEAR
%      GET NOSTORE NOPROMPT KEYWORD ' ' ' '
%     GET NOSTORE NOPROMPT REAL 'HKLT11' '2'
%     EVALUATE HKLT11 = VALUE
%     GET NOSTORE NOPROMPT REAL 'HKLT12' '0'
%     EVALUATE HKLT12 = VALUE
%     GET NOSTORE NOPROMPT REAL 'HKLT13' '0'
%     EVALUATE HKLT13 = VALUE
%      EXTRACT NEXT
%      EXTRACT TRANSFER INPUT
%      CLEAR
%      GET NOSTORE NOPROMPT KEYWORD ' ' ' '
%     GET NOSTORE NOPROMPT REAL 'HKLT21' '0'
%     EVALUATE HKLT21 = VALUE
%     GET NOSTORE NOPROMPT REAL 'HKLT22' '2'
%     EVALUATE HKLT22 = VALUE
%     GET NOSTORE NOPROMPT REAL 'HKLT23' '0'
%     EVALUATE HKLT23 = VALUE
%      EXTRACT NEXT
%      EXTRACT TRANSFER INPUT
%      CLEAR
%      GET NOSTORE NOPROMPT KEYWORD ' ' ' '
%     GET NOSTORE NOPROMPT REAL 'HKLT31' '0'
%     EVALUATE HKLT31 = VALUE
%     GET NOSTORE NOPROMPT REAL 'HKLT32' '0'
%     EVALUATE HKLT32 = VALUE
%     GET NOSTORE NOPROMPT REAL 'HKLT33' '2'
%     EVALUATE HKLT33 = VALUE
%     GET NOSTORE NOPROMPT TEXT NULLSTRING ' '
%     COPY '#CLOSE SCPDATA'
EXTRACTING matrix FROM .CRY FILE
%SHOW HKLT11 HKLT12 HKLT13
%SHOW HKLT21 HKLT22 HKLT23
%SHOW HKLT31 HKLT32 HKLT33
%%
% IF ( INSTITLE .EQ. ' ' ) .OR. ( .NOT. ( FILEEXISTS INSTITLE ) ) THEN
%%
^^WI WINDOW XINSHELX 'Input RES or INS data' MODAL
^^WI        COMMIT='BOK' CANCEL='BXX'
^^WI GRID GRIDM NROWS=5 NCOLS=3
^^WI {
^^WI  @ 2,2 GRID TP NROWS=1 NCOLS=2
^^WI  {
^^WI   @ 1,1 GRID TX NROWS=4 NCOLS=1
^^WI   {
^^WI    @ 1,1 STATIC T1 ' '
^^WI    @ 2,1 STATIC T2 ' '
^^WI    @ 3,1 STATIC T3 'Existing reflections will be overwritten '
^^WI    @ 4,1 STATIC T4 'by the new ones.'
^^WI   }
^^WI   @ 1,2 GRID GB NROWS=5 NCOLS=5
^^WI   {
^^WI    @ 2,5 BUTTON BOK '&Ok' DEFAULT
^^WI    @ 4,5 BUTTON BXX '&Cancel'
^^WI   }
^^WI  }
^^WI  @ 4,2 GRID GF NROWS=1 NCOLS=3 OUTLINE='Shelx File to input:'
^^WI  {
% TRANSFER "^^WI @ 1,1 EDITBOX L6FILE '" // INSTITLE // -
  "' CHARS=48 INFORM=NO INPUT " TO DISPLAY
^^WI    @ 1,3 BUTTON BROWSE 'Browse...'
^^WI  }
^^WI }
^^WI SHOW
^^CR
%  LOOP
%   VERIFY BROWSE BXX BOK
%   GET SILENT NOSTORE FINAL ABBREVIATED 'BXX?' 'BXX'
%   CASE VALUE
%     BLOCK                         %BROWSE%
^^WI    SYSOPENFILE '*.hkl' 'Shelx file (*.hkl)'
^^CR
%       GET SILENT TEXT 'New filename:'
%       IF CVALUE .NE. 'CANCEL' THEN
%         CLEAR
^^WI      SET L6FILE TEXT
%         TRANSFER "^^WI '" // CVALUE // "'" TO DISPLAY
^^CR
%         EVALUATE CFILE = CVALUE
%         IF ( .NOT. ( FILEEXISTS CFILE ) ) THEN
{E Error: Filename given does not exist.
%         ELSE
%           EVALUATE INSTITLE = CFILE
^^CO        DISPOSE XINSHELX
%           EVALUATE LOOPEND = LOOPCOUNTER
%         END IF
%       END IF
%     END BLOCK
%     BLOCK                         %BXX%
^^CO    DISPOSE XINSHELX
%       FINISH
%     END BLOCK
%     BLOCK                         %BOK%
^^??    L6FILE TEXT
%       GET SILENT TEXT 'Shelx File (RES or INS file) '
%       EVALUATE CFILE = CVALUE
%       IF ( .NOT. ( FILEEXISTS CFILE ) ) THEN
{E Error: Filename given does not exist.
%       ELSE
%         EVALUATE INSTITLE = CFILE
^^CO      DISPOSE XINSHELX
%         EVALUATE LOOPEND = LOOPCOUNTER
%       END IF
%     END BLOCK
%   END CASE
%  END LOOP
% ELSE
%   EVALUATE CFILE = INSTITLE
%%   EVALUATE INSTITLE = GETTITLE ( CFILE )
% END IF
%%
%%
%% See if an hkl is available and offer to open it.
%%
%        EVALUATE HKLTITLE = GETPATH ( INSTITLE ) // -
                             GETTITLE ( INSTITLE ) // '.hkl'
%        IF ( FILEEXISTS ( HKLTITLE ) ) THEN
%          VARIABLE LOGICAL INITSETUP
%          EVALUATE INITSETUP = TRUE
%          COPY '#SCRIPT XINLIST6'
%        END IF
%%                                                                              
%END SCRIPT                                                                     
