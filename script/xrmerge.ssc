%SCRIPT XRMERGE                                                                 
% VARIABLE REAL MRGR                                       
% COPY '#OPEN SCPDATA mergingr.dat'                                             
^^WI WINDOW XRMERGE 'Merging R' MODAL KEEP SIZE                                 
^^WI COMMIT='RMR_BOK' CANCEL='^^CO DISPOSE XRMERGE'                             
^^WI GRID MAIN NROWS=2 NCOLS=1                                                  
^^WI {                                                                          
^^WI  @ 1,1 GRID PRIME NROWS=7 NCOLS=3                                          
^^WI  {                                                                         
^^WI   @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''                               
^^WI   {                                                                        
^^WI    @ 1,3 GRID EXP2 NROWS=3 NCOLS=1                                         
^^WI    {                                                                       
^^WI     @ 1,1 STATIC XFW1                                                      
^^WI 'You are looking at merging R statistics.'                                 
^^WI     @ 2,1 STATIC XFW2 ' '                                                  
^^WI     @ 3,1 STATIC XFW3 ' '                                                  
^^WI    }                                                                       
^^WI    @ 1,1 ICON IM INFO                                                      
^^WI   }                                                                        
^^WI   @ 4,2 PLOTWINDOW _VRMERGE NROWS=20 NCOLS=64                              
^^WI  }                                                                         
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
{I About to get all the data.                                                   
%  EXTRACT REWIND                                                               
%% Skip first three lines:                                                      
%  EXTRACT NEXT                                                                 
%  EXTRACT NEXT                                                                 
%  EXTRACT NEXT                                                                 
%%                                                                              
^^PL PLOTDATA _CLASS BARGRAPH ATTACH _VRMERGE KEY                               
^^PL XAXIS TITLE 'I/sigma(I)'                                         
^^PL YAXISRIGHT TITLE 'Number Of Reflections'                                   
^^PL NSERIES=2 LENGTH=100 YAXIS ZOOM 0 50 TITLE 'R(merge), %'              
^^PL SERIES 1 SERIESNAME 'Merging R factor'                                     
^^PL SERIES 2 SERIESNAME 'Number of observations'                               
^^PL SERIES 2 TYPE LINE USERIGHTAXIS                                            
%%                                                                              
% LOOP                                                                          
%  ON ERROR TERMINATE                                                           
%  ON END TERMINATE                                                             
%%                                                                              
%% Get the data for each result                                                 
%%                                                                              
%  EXTRACT NEXT                                                                 
%  CLEAR                                                                        
%  EXTRACT TRANSFER INPUT                                                       
%  CLEAR                                                                        
%  INSERT !^^PL LABEL !                                                         
%  GET NOPROMPT REAL ' ' '-1.0'                                                 
%  INSERT ! DATA !                                                              
%  GET NOSTORE NOPROMPT REAL ' ' '-1.0'                                         
%  EVALUATE MRGR = VALUE                                                        
%  IF MRGR .LT. 0.0 THEN                                                        
%   EVALUATE MRGR = - MRGR                                                      
%  END IF                                                                       
%% avoid problems with Rmerge exactly zero
%  IF MRGR .LT. 0.1 THEN                                                        
%   EVALUATE MRGR = 0.1                                                      
%  END IF                                                                       
%  STORE REAL MRGR                                                              
%  GET NOSTORE NOPROMPT INTEGER ' ' '-1'                                        
%  GET NOPROMPT INTEGER ' ' '-1'                                                
%  OUTPUT                                                                       
% END LOOP                                                                      
^^PL SHOW                                                                       
^^CR                                                                            
%END SCRIPT                                                                     
                                                                                
