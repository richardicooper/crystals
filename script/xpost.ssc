%SCRIPT XPOST  %Script for outputting a Cameron postscript file%
% VARIABLE INTEGER IFILET ITYPE
% VARIABLE CHARACTER FILENAME FILETYPE
^^WI WINDOW XPOST 'Save Image' MODAL
^^WI GRID G1 NROWS=3 NCOLS=5
^^WI {
^^WI  @ 2,2 GRID GL NROWS=6 NCOLS=1
^^WI  {
^^WI   @ 1,1 CHECKBOX    XCOLOUR 'Colour' STATE=ON INFORM=NO
^^WI   @ 3,1 RADIOBUTTON RPOST 'Postscript' STATE=ON IGNORE
^^WI   @ 4,1 RADIOBUTTON RENCA 'Encapsulated Postscript' IGNORE
^^WI   @ 6,1 GRID GB NROWS=1 NCOLS=3
^^WI   {
^^WI    @ 1,1 STATIC  T1 'Filename: '
^^WI    @ 1,3 EDITBOX E1 'picture1.ps' CHARS=15 INFORM=NO
^^WI   }
^^WI  }
^^WI  @ 2,4 GRID   GR NROWS=3 NCOLS=3
^^WI  {
^^WI   @ 1,2 BUTTON      BTOK 'Ok' DEFAULT
^^WI   @ 3,2 BUTTON      BTXX 'Cancel'
^^WI  }
^^WI }
^^WI SHOW
^^CR
% VERIFY BTOK BTXX
% GET NOSTORE FINAL ABBREVIATED ' '
% CASE VALUE
%   BLOCK        %BTNOK%
^^WI   GETVALUE RPOST
^^CR
%      VERIFY ON OFF
%      GET NOSTORE FINAL ABBREVIATED ' '
%      EVALUATE IFILET = VALUE
^^??   XCOLOUR STATE
^^CR
%      VERIFY ON OFF
%      GET NOSTORE FINAL ABBREVIATED ' '
%%If not colour add 2 to the value of ifilet to give itype
%      EVALUATE ITYPE = ( ( VALUE  - 1 ) * 2 ) + IFILET
^^??   E1 TEXT
%      GET KEYWORD 'New filename: '
%      EVALUATE FILENAME = CVALUE
^^WI   DISPOSE XPOST
^^CR   
%      CASE ITYPE
%          EVALUATE FILETYPE = 'CPOST'
%          EVALUATE FILETYPE = 'CENCAP'
%          EVALUATE FILETYPE = 'POST'
%          EVALUATE FILETYPE = 'ENCAP'
%      END CASE
%      TRANSFER "HARDCOPY " // FILETYPE TO CRYSTALS
%      TRANSFER "COPY " // FILENAME TO CRYSTALS
%      TRANSFER "   " TO CRYSTALS
%      FINISH
%   END BLOCK
%   BLOCK
^^WI   DISPOSE XPOST
^^CR   
%      FINISH
%   END BLOCK
% END CASE
%END SCRIPT
