%SCRIPT XGUIDES
%%
%% Decide on a course of action.
%% Display non-modal window with available options. 
%% Exit. - The window will callback XGUIDESC.SCP with events.
%%
%  VARIABLE INTEGER KTDEFA
%% KTDEFA  = 
%%  1 Run SIR92
%%  2 Run SHELXS
%%  3 Run SIR97
%%  4 Import Sir92 data.
%%  5 Import Sir97 data.
%%  6 Shelx file.
%%  7 Cameron file.
%%%
%%
%% Check for windows already open
%%
^^?? XGUIDER EXISTS
%  VERIFY TRUE FALSE
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  IF VALUE .EQ. 1  THEN
^^CO DISPOSE XGUIDER
%  END IF
^^?? XGUIDEI EXISTS
%  VERIFY TRUE FALSE
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  IF VALUE .EQ. 1  THEN
^^CO DISPOSE XGUIDEI
%  END IF
^^?? XGUIDES EXISTS
%  VERIFY TRUE FALSE
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  IF VALUE .EQ. 2  THEN
%    CLEAR
%    ON ERROR TERMINATE
%%
^^WI WINDOW XGUIDES 'Supervisor' POSITION CENTRED 'MODEL01'
^^WI COMMIT='GS_BOK' CANCEL='GS_BXX'
^^WI COMMAND='#SCRIPT XGUIDESC' DISABLEIF='IN'
^^WI GRID HO NROWS=5 NCOLS=3
^^WI {
^^WI  @ 2,2 GRID OUTER NROWS=1 NCOLS=5
^^WI  {
^^WI   @ 1,1 BITMAP GUIDEB FILE 'guide2.bmp'
^^WI   @ 1,3 LISTBOX AUTOLIST VISLINES=11 ADDTOLIST
^^WI     'Run Sir92'
^^WI     'Run ShelxS'
^^WI     'Run Sir97'
^^WI     'Use Sir92 output'
^^WI     'Use Sir97 output'
^^WI     'Use Shelx file'
^^WI     'Use a Cameron file'
^^WI     NULL
^^WI   @ 1,5 GRID BTNS NROWS=6 NCOLS=1
^^WI   {
^^WI     @ 1,1 BUTTON GS_BOK '&Ok' DEFAULT COMMIT
^^WI     @ 4,1 BUTTON GS_BXX '&Hide' CANCEL
^^WI     @ 6,1 BUTTON GS_BHELP '&Help'
^^WI   }
^^WI  }
^^WI  @ 4,2 STATIC SUPT
^^WI 'You can continuing working while this window is open.'
^^WI }
^^WI SHOW
^^CR
%  END IF

{I   Choose a method of structure solution, or a source to import
{I   an existing trial model.

% END SCRIPT
%%
%%  $Log: not supported by cvs2svn $
