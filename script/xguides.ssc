%SCRIPT XGUIDES
%%
%% Decide on a course of action.
%% Display non-modal window with available options. 
%% Exit. - The window will callback XGUIDESC.SCP with events.
%%
%  VARIABLE INTEGER KTDEFA
%  VARIABLE LOGICAL I6DONE
%% KTDEFA  = 
%%  1 Run SIR92
%%  2 Run SHELXS
%%  3 Run SIR97
%%  4 Import Sir92 data.
%%  5 Import Sir97 data.
%%  6 Shelx file.
%%  7 Cameron file.
%%  8 Initial analysis.
%%%
%%
%% Check for windows already open
%%
^^?? XGUIDER EXISTS
%  VERIFY TRUE FALSE
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  IF VALUE .EQ. 1  THEN
^^CO DISPOSE XGUIDER
%  END IF
^^?? XGUIDEI EXISTS
%  VERIFY TRUE FALSE
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  IF VALUE .EQ. 1  THEN
^^CO DISPOSE XGUIDEI
%  END IF
^^?? XGUIDES EXISTS
%  VERIFY TRUE FALSE
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  IF VALUE .EQ. 2  THEN
%    CLEAR
%    ON ERROR TERMINATE
%%
^^WI WINDOW XGUIDES 'The GUIDE' POSITION CASCADE '_MAINTEXTOUTPUT'
^^WI COMMAND='#SCRIPT XGUIDESC' DISABLEIF='IN' CANCEL='GS_BXX'
^^WI GRID HO NROWS=3 NCOLS=5
^^WI {
^^WI  @ 2,2 BITMAP GUIDEB TRANS FILE 'guide2.bmp'
^^WI  @ 2,4 GRID MID NROWS=3 NCOLS=3
^^WI  {
^^WI   @ 1,1 DROPDOWN _XG_LIST ADDTOLIST
^^WI     'Run Sir92'
^^WI     'Run ShelxS'
^^WI     'Run Sir97'
^^WI     'Use Sir92 output'
^^WI     'Use Sir97 output'
^^WI     'Use Shelx file'
^^WI     'Use a Cameron file'
^^WI     'Initial analysis'
^^WI     NULL
^^WI   @ 3,1 GRID BTNS NROWS=1 NCOLS=4
^^WI   {
^^WI     @ 1,1 STRETCH _H HORIZONTAL
^^WI     @ 1,4 BUTTON GS_BHELP '&Help' DISABLEIF='IN'
^^WI   }
^^WI   @ 1,3 BUTTON GS_BOK '&Ok' DISABLEIF='IN'
^^WI   @ 3,3 BUTTON GS_BXX '&Hide' DISABLEIF='IN'
^^WI  }
^^WI }
^^WI SHOW
^^CR
%  END IF

{I   Carry out an initial inspection of the reflection data.
{I   then choose a method of structure solution, or a source to import
{I   an existing trial model.

%  COPY '#SCRIPT XGETINFO'
%  IF ( I6DONE ) THEN
%    EVALUATE KTDEFA = 1
%  ELSE
%    EVALUATE KTDEFA = 8
%  END IF
%%
%%
%  CLEAR
%  INSERT '^^CO SET _XG_LIST SELECTION='
%  STORE FORMAT /(I2)/ LENGTH 2 INTEGER KTDEFA
%  OUTPUT
%END SCRIPT
%%
%%  $Log: not supported by cvs2svn $
%%  Revision 1.5  2001/04/03 09:52:17  richard
%%  Changed name from 'Supervisor' to 'The GUIDE' for consistency.
%%
%%  Revision 1.4  2001/03/21 17:07:17  richard
%%  Changed window layout and bitmap.
%%
%%  Revision 1.3  2001/03/13 09:36:47  richard
%%  Re-instated cancel action. Made cancel action the default in the xguide?c scripts.
%%
%%  Revision 1.2  2001/03/09 18:01:26  richard
%%  Removed default actions for buttons, as these are now non-modal windows.
%%  They were interfering with the user pressing RETURN in other scripts, while
%%  the windows were open.
%%
%%  Revision 1.1  2001/03/08 14:00:23  richard
%%  New GUIDE scripts. The main script, xguide, decides on the overall stage
%%  and calls one of xguidei - for data import, xguides - for solving or
%%  xguider - for refinement. These display a window recommending a course of
%%  action and exit - thus allowing the user access to the full range of CRYSTALS
%%  commands even while being guided. The window knows to call one of xguideic, xguidesc,
%%  or xguiderc respectively when the user does something to it e.g. pushes a button.
%%
