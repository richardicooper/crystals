%SCRIPT XFILEW
%% 
%    VARIABLE LOGICAL T
%    VARIABLE CHARACTER C_FILE CEXTN CARG
%    VARIABLE INTEGER EXITSTATUS                                                    
%%                               
%  IF ( .NOT. ( FILEEXISTS C_FILE ) ) THEN
%% If called incorrectly or without argument, then get a file.
^^WI SYSOPENFILE 
^^WI [ '*.ins;*.res;*.hkl;*.hklf5;*.cif;*.cmf;*.fcf;*.cif_od;*.pcf;'
^^WI   'Crystallographic files'
^^WI   '*.cif*;*.cmf;*.fcf;*.pcf;' 'CIF files (*.cif*,*.cmf,*.fcf,*.pcf)' ]
^^CR                                                                            
%    GET SILENT NOSTORE TEXT 'New filename:' 'CANCEL'                                                                              
%    IF CVALUE .NE. 'CANCEL' THEN                                            
%      EVALUATE C_FILE = CVALUE                                                 
%      IF ( .NOT. ( FILEEXISTS C_FILE ) ) THEN                                  
{E Error: Filename given does not exist.
%        FINISH
%      END IF 
%%
%      EVALUATE CEXTN = GETEXTN ( C_FILE )
%      EVALUATE CEXTN = UPPERCASE ( CEXTN )
%    ELSE
%      FINISH
%    END IF
%  END IF
%%
%   IF ( .NOT. ( FILEEXISTS C_FILE ) ) THEN
{E Error: Filename given does not exist.
%     FINISH
%   END IF 
%%   
%   EVALUATE CEXTN = GETEXTN ( C_FILE )
%   EVALUATE CEXTN = UPPERCASE ( CEXTN )
%   IF ( CEXTN .EQ. 'CIF' ) .OR. ( CEXTN .EQ. 'PCF' ) THEN
%     IF FILEEXISTS ( 'from-cif.cry' ) THEN
%       EVALUATE T =  FILEDELETE ( 'from-cif.cry' )
%     END IF
%     IF FILEEXISTS ( 'cif-reflections.hkl' ) THEN
%       EVALUATE T =  FILEDELETE ( 'cif-reflections.hkl' )
%     END IF
%     TRANSFER '#SPAWN % "CRYSDIR:diffin.exe" -f -d W -o from-cif.cry "' - 
      // C_FILE //   '" ' TO CRYSTALS
%     TRANSFER 'EXIT CODE: ' // CHARACTER ( EXITSTATUS ) TO DISPLAY      
%%
%     IF FILEEXISTS ( 'from-cif.cry' ) THEN
%       COPY '#SCRIPT DIFFEXEC'
%     ELSE
 {E The diffractometer files have not been processed          
%     END IF
%     FINISH
%   ELSE IF ( CEXTN .EQ. 'FCF' )  .OR. ( CEXTN .EQ. 'HKL' ) THEN
%     IF FILEEXISTS ( 'cif-reflections.hkl' ) THEN
%       EVALUATE T =  FILEDELETE ( 'cif-reflections.hkl' )
%     END IF
%     IF FILEEXISTS ( 'cif-reflections.cry' ) THEN
%       EVALUATE T =  FILEDELETE ( 'cif-reflections.cry' )
%     END IF
%     VARIABLE CHARACTER CTITLE CPATH COUT
%%     EVALUATE CTITLE = GETTITLE ( C_FILE )
%%     EVALUATE CPATH = GETPATH ( C_FILE )
%     EVALUATE COUT = 'cif-reflections' // '.cry'
%     EVALUATE CARG = ' -f -d W -o '
% TRANSFER '#SPAWN % "CRYSDIR:diffin.exe" ' // CARG // '"' // COUT -           
  // '" "' // C_FILE // '"' TO CRYSTALS                                            
%     TRANSFER 'EXIT CODE: ' // CHARACTER ( EXITSTATUS ) TO DISPLAY      

%     IF FILEEXISTS ( 'cif-reflections.cry' ) .AND. -
         FILEEXISTS ( 'cif-reflections.hkl' ) THEN
%       COPY '#SCRIPT DIFFEXEC'
%     ELSE
 {E The diffractometer files have not been processed properly         
%     END IF
%     FINISH
%   END IF
%%

{E Your file has not been processed

%
%END SCRIPT
