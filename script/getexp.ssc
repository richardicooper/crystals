%SCRIPT GETEXP
%
%    VARIABLE INTEGER TNATM EXPECTED EXPECTALL
%    VARIABLE REAL OCCUR
%    VARIABLE CHARACTER ELEMT
%    EVALUATE TNATM = 0
%    EVALUATE EXPECTED = 0
%    EVALUATE EXPECTALL = 0
%%
%    IF EXISTS 29 .NE. 1 THEN
{E No list 29 present. CRYSTALS cannot determine how many atoms to keep.
%      EVALUATE EXPECTED = 10000
%      EVALUATE EXPECTALL = 10000
%      FINISH
%    END IF
%%
%%  Look at the list 29 to find the largest atom type present and the total
%%  number of atoms to find
%%
%    COPY '#GENERALEDIT 29'
%    COPY 'ERROR MESSAGE=NO SIGNAL=NONE ACTION=CONTING NAME=NOMORE'
%    COPY 'LOCATE RECORDTYPE = 101'
%    LOOP 40 TIMES
%       ON NOMORE TERMINATE
%       COPY 'TRANSFER FROM OFFSET=0 TO ELEMT'
%       COPY 'TRANSFER FROM OFFSET=4 TO OCCUR'
%       IF ELEMT .NE. 'H' THEN
%         EVALUATE TNATM = TNATM + INTEGER OCCUR
%       END IF
%       EVALUATE EXPECTALL = EXPECTALL + INTEGER OCCUR
%       COPY 'NEXT'
%    END LOOP
%%
%    CLEAR
%    INSERT 'CRYSTALS is looking for '
%    STORE INTEGER EXPECTALL
%    INSERT ' atoms'
%    OUTPUT
%    CLEAR
%    INSERT '              including '
%    STORE INTEGER TNATM
%    INSERT ' non-H atoms'
%    OUTPUT
%    CLEAR
%    EVALUATE EXPECTED = TNATM
%    IF EXPECTED .EQ. 0 THEN
%       EVALUATE EXPECTED = 10000
%    END IF
%    IF EXPECTALL .EQ. 0 THEN
%       EVALUATE EXPECTALL = 10000
%    END IF
%%
%    COPY 'END'
%END SCRIPT
