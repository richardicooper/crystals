%SCRIPT ORTHOATOMS
%%
%% Variable to give back to calling script
%%
% VARIABLE INTEGER IATOMCOUNT
%%
% VARIABLE CHARACTER CATM CELE CCHK
% VARIABLE INTEGER ISER
% VARIABLE REAL ATX ATY ATZ ATOX ATOY ATOZ RCHK
% VARIABLE REAL OM1 OM2 OM3 OM4 OM5 OM6 OM7 OM8 OM9
%%
%% Get orthogonalisation matrix
%%
%  COPY '#GENERALEDIT 1'
%  COPY 'LOCATE RECORDTYPE=105'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  COPY 'TRANSFER FROM OFFSET=0 TO OM1'
%  COPY 'TRANSFER FROM OFFSET=1 TO OM2'
%  COPY 'TRANSFER FROM OFFSET=2 TO OM3'
%  COPY 'TRANSFER FROM OFFSET=3 TO OM4'
%  COPY 'TRANSFER FROM OFFSET=4 TO OM5'
%  COPY 'TRANSFER FROM OFFSET=5 TO OM6'
%  COPY 'TRANSFER FROM OFFSET=6 TO OM7'
%  COPY 'TRANSFER FROM OFFSET=7 TO OM8'
%  COPY 'TRANSFER FROM OFFSET=8 TO OM9'
%  COPY 'END'
%%
%  EVALUATE IATOMCOUNT = 0
%%
%  COPY '#RELEASE PUNCH AtomsOrtho.dat'
%  COPY '#OPEN SCPDATA AtomsToWorkOn.dat'
%  LOOP
%    ON ERROR TERMINATE
%    ON END TERMINATE
%    EXTRACT NEXT
%    EXTRACT TRANSFER INPUT
%    CLEAR
%    GET TEXT 'Atom'
%    EVALUATE CATM = CVALUE
%    IF CVALUE .EQ. 'END' THEN
%      EVALUATE LOOPEND = LOOPCOUNTER
%    ELSE
%      EVALUATE IATOMCOUNT = IATOMCOUNT + 1
%      EVALUATE CELE = FIRSTSTR ( CATM )
%      EVALUATE ISER = FIRSTINT ( CATM )
%      ON EDITERROR TERMINATE
%      COPY '#GENERALEDIT 5'
%      COPY 'LOCATE RECORDTYPE=101'
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%      LOOP
%        ON EDITERROR TERMINATE
%        COPY 'TRANSFER FROM OFFSET=0 TO CCHK'
%        COPY 'TRANSFER FROM OFFSET=1 TO RCHK'
%        IF ( ( CELE .EQ. CCHK ) .AND. ( ISER .EQ. INTEGER ( RCHK ) ) ) THEN
%          COPY 'TRANSFER FROM OFFSET=4 TO ATX'
%          COPY 'TRANSFER FROM OFFSET=5 TO ATY'
%          COPY 'TRANSFER FROM OFFSET=6 TO ATZ'
%          COPY 'END'
%% Multiply the coordinates by the ortho matrix...
%          EVALUATE ATOX = ATX * OM1 + ATY * OM2 + ATZ * OM3
%          EVALUATE ATOY = ATX * OM4 + ATY * OM5 + ATZ * OM6
%          EVALUATE ATOZ = ATX * OM7 + ATY * OM8 + ATZ * OM9
%% and write them to file
%%         TRANSFER CATM // " " // CHARACTER ( ATOX ) // " " // CHARACTER ( ATOY ) // " " // CHARACTER ( ATOZ ) TO DISPLAY
%          TRANSFER CATM // " " // CHARACTER ( ATOX ) // " " // CHARACTER ( ATOY ) // " " // CHARACTER ( ATOZ ) TO PUNCH
%          EVALUATE LOOPEND = LOOPCOUNTER
%%
%        END IF
%        COPY 'NEXT'
%      END LOOP
%    END IF
%  END LOOP
%  COPY '#CLOSE SCPDATA'
%  COPY '#RELEASE PUNCH bfile.pch'
%% EVALUATE T = FILEDELETE ( 'AtomsToWorkOn.dat' )
%%
%% TRANSFER "Number of atoms transformed to orthogonal system is " // CHARACTER ( IATOMCOUNT ) // "." TO DISPLAY
%%
%END SCRIPT