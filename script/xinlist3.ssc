%SCRIPT XINLIST3
% VARIABLE REAL MULT NUMBER WAVE 
% VARIABLE CHARACTER CWAVE                                               
% VARIABLE CHARACTER CNUMB CELEM CTEM CTEST                                     
% VARIABLE INTEGER ECOUNT RADIATION ORIENTTYPE SWITCH ITMP XORN
% VARIABLE LOGICAL LCANCEL LNEW29 EDLIST3 LXIN3CANCEL                           
% VARIABLE CHARACTER SPACEHOOK                                                  
% VARIABLE CHARACTER CTYPE                                                      
% VARIABLE LOGICAL FRIEDEL NEUTRONS
%% The variables needed by XQUESTIO                                             
% VARIABLE CHARACTER QTITLE QLINE1 QLINE2 BUTTOK BUTTXX                         
% VARIABLE LOGICAL   ANSWER                                                     
%%                                                                              
%% Ensure we have a LIST 13
% IF ( EXISTS 13 .NE. 1 ) THEN                                                  
%   copy '#script inlist13'                                                     
% END IF                                                                        
%   COPY '#GENERALEDIT 13'                                                      
%   COPY 'LOCATE RECORDTYPE=103'                                                
%   COPY 'TRANSFER FROM OFFSET=0 TO WAVE'                                       
%   COPY 'LOCATE RECORDTYPE=101'                                           
%   COPY 'TRANSFER FROM OFFSET=0 TO ITMP'                                  
%   COPY 'LOCATE RECORDTYPE=102'                                           
%   COPY 'TRANSFER FROM OFFSET=1 TO XORN'                                  
%   IF ( ITMP .GE. 0 ) THEN                                                
%         EVALUATE FRIEDEL = TRUE                                               
%   ELSE                                                                   
%         EVALUATE FRIEDEL = FALSE                                              
%   END IF
%%   TRANSFER CHARACTER ( XORN ) TO DISPLAY
%   IF ( XORN .EQ. 0 ) THEN
%         EVALUATE NEUTRONS = TRUE
%   ELSE
%         EVALUATE NEUTRONS = FALSE
%   END IF
%   COPY 'END'                                                                  
%%
%   IF ( NEUTRONS .EQ. TRUE ) THEN                 
%     EVALUATE CWAVE = 'NE'
%   ELSE IF ( WAVE .GT. 1.5417 ) .AND. ( WAVE .LT. 1.5419 ) THEN                     
%     EVALUATE CWAVE = 'CU'                                       
%   ELSE IF ( WAVE .GT. 0.7107 ) .AND. ( WAVE .LT. 0.7108 ) THEN                 
%     EVALUATE CWAVE = 'MO'                                       
%   ELSE
%     EVALUATE CWAVE = 'XX'
%   END IF
%%  See also below
%%  
%%
%%
% EVALUATE LCANCEL = FALSE                                                      
% EVALUATE LXIN3CANCEL = FALSE                                                  
% CLEAR                                                                         
^^WI WINDOW INLIST3 'InList3' MODAL SIZE                                        
^^WI COMMIT=BOKI3 CANCEL=BXXI3                                                  
^^WI GRID             G1 NROWS=9 NCOLS=5                                        
^^WI {                                                                          
^^WI  @ 2,2 GRID G2 NROWS=4 NCOLS=2 OUTLINE='Diffraction Experiment'            
^^WI  {                                                                         
^^WI   @ 1,1 RADIOBUTTON COP 'Copper' 
%      IF CWAVE .EQ. 'CU' THEN
^^WI     STATE=ON INFORM
%      ELSE
^^WI     STATE=OFF INFORM
%      END IF
^^WI   @ 2,1 RADIOBUTTON MOL 'Molybdenum' 
%      IF CWAVE .EQ. 'MO' THEN
^^WI     STATE=ON INFORM
%      ELSE
^^WI     STATE=OFF INFORM
%      END IF
^^WI   @ 3,1 RADIOBUTTON NEU 'Neutrons' 
%      IF CWAVE .EQ. 'NE' THEN
^^WI     STATE=ON INFORM
%      ELSE
^^WI     STATE=OFF INFORM
%      END IF
^^WI   @ 4,1 RADIOBUTTON OTH 'Other X-ray:'                           
%      IF CWAVE .EQ. 'XX' THEN
^^WI     STATE=ON INFORM
%      ELSE
^^WI     STATE=OFF INFORM
%      END IF
%%
%%
^^WI   @ 1,2 CHECKBOX FRIE 'Apply Friedels Law during merge' 
%    IF ( FRIEDEL )  THEN                         
^^WI      STATE=ON                                                              
%    ELSE                                                                       
^^WI      STATE=OFF                                                             
%    END IF                                                                     
^^WI   @ 4,2 EDITBOX WAVEL '0.71073' NCOLS=10 LIMIT=20 REAL 
%      IF CWAVE .EQ. 'XX' THEN
^^WI         DISABLED=NO        
%      ELSE
^^WI         DISABLED=YES        
%      END IF
^^WI  }                                                                         
^^WI  @ 4,2 STATIC T1 'Enter or edit the cell contents here:'                           
^^WI  @ 5,2 EDITBOX LE ' ' NCOLS=80 LIMIT=80                                    
^^WI  @ 7,2 STATIC T2 'Enter element symbol followed by number in cell,'        
^^WI  @ 8,2 STATIC T3 'e.g. C 8 H 13 N 1 O 4 - use spaces.'                     
^^WI  @ 2,4 GRID B2 NROWS=3 NCOLS=1                                             
^^WI  {                                                                         
^^WI   @ 1,1 BUTTON    BOKI3 'Done' DEFAULT COMMIT                                
^^WI   @ 3,1 BUTTON    BXXI3 'Cancel' CANCEL                                    
^^WI  }                                                                         
^^WI }                                                                          
^^CR                                                                            
^^WI SHOW                                                                       
^^CR                                                                            
%   TRANSFER "^^CO SET WAVEL TEXT " // CHARACTER WAVE TO DISPLAY                
%   IF ( NEUTRONS .EQ. TRUE ) THEN
^^CO   SET NEU STATE=ON SET WAVEL DISABLED=NO                 
%   ELSE IF ( WAVE .GT. 1.5417 ) .AND. ( WAVE .LT. 1.5419 ) THEN                     
^^CO   SET COP STATE=ON                                        
%   ELSE IF ( WAVE .GT. 0.7107 ) .AND. ( WAVE .LT. 0.7108 ) THEN                 
^^CO   SET MOL STATE=ON  
%   ELSE
^^CO   SET OTH STATE=ON SET WAVEL DISABLED=NO                 
%% OTHER greyed out inexplicably, sometimes
%%      IF ( .NOT. EDLIST3 ) THEN                                                
%%       EVALUATE QTITLE = 'Warning - non-standard wavelength'                   
%%       EVALUATE QLINE1 = 'Any anomalous scattering values that you have'       
%%       EVALUATE QLINE2 = 'already entered will be overwritten. Continue?'      
%%       EVALUATE BUTTOK = '&Yes'                                                
%%       EVALUATE BUTTXX = '&No'                                                 
%%       COPY '#SCRIPT XQUESTIO'                                                 
%%       IF ANSWER .EQ. FALSE THEN                                               
%%^^CO     DISPOSE INLIST3                                                        
%%        FINISH                                                                 
%%       END IF                                                                  
%%      END IF                                                                   
%   END IF                                                                      
%%                                                                              
% IF ( EXISTS 2 .NE. 1 ) THEN                                                   
%    EVALUATE SPACEHOOK = 'Space group required before Cell Contents:'          
%    COPY '#SCRIPT XSPACE'                                                      
%    IF ( LCANCEL .EQ. TRUE ) THEN                                              
^^WI    DISPOSE INLIST3                                                         
^^CR                                                                            
%       EVALUATE LXIN3CANCEL = TRUE                                             
%       FINISH                                                                  
%    END IF                                                                     
% END IF                                                                        
% BLOCK                                                                         
%   ON ERROR TERMINATE                                                          
%   ON EDITERROR TERMINATE                                                      
%   COPY '#GENERALEDIT 2'                                                       
%   COPY 'LOCATE RECORDTYPE=101'                                                
%   COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'                     
%   COPY 'TRANSFER FROM OFFSET=3 TO MULT'                                       
%   COPY 'END'                                                                  
% END BLOCK                                                                     
% IF ( EXISTS 29 .NE. 1 ) THEN                                                  
^^CO SET LE TEXT='C 1.0 H 1.0 O 1.0 N 1.0'                                      
% ELSE                                                                          
^^CO SET LE TEXT=''                                                             
%   COPY '#GENERALEDIT 29'                                                      
%   COPY 'LOCATE RECORDTYPE=101'                                                
%   COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'                     
%   EVALUATE ECOUNT = 0                                                         
%   LOOP                                                                        
%       ON EDITERROR TERMINATE                                                  
%       COPY 'TRANSFER FROM OFFSET=0 TO CTYPE'                                  
%       COPY 'TRANSFER FROM OFFSET=4 TO NUMBER'                                 
%       EVALUATE NUMBER = NUMBER * MULT                                         
%       EVALUATE ECOUNT = ECOUNT + 1                                            
%       IF ( NUMBER - REAL ( INTEGER ( NUMBER ) ) .LT. 0.01 ) THEN              
%         EVALUATE CTEST = COMPRESS ( CTEST // ' ' // CTYPE // ' ' // -         
                            CHARACTER ( INTEGER ( NUMBER ) ) )                  
%       ELSE                                                                    
%         EVALUATE CTEST = COMPRESS ( CTEST // ' ' // CTYPE // ' ' // -         
                            CHARACTER ( NUMBER ) )                              
%       END IF                                                                  
%       COPY 'NEXT'                                                             
%   END LOOP                                                                    
%   TRANSFER '^^CO SET LE TEXT="' // CTEST // '"' TO DISPLAY                    
%   IF ECOUNT .EQ. 0 THEN                                                       
^^CO   SET LE TEXT='C 1.0 H 1.0 O 1.0 N 1.0'                                    
%   END IF                                                                      
%   COPY 'END'                                                                  
% END IF                                                                        
%%                                                                              
% LOOP                                                                          
%  IF ( LNEW29 ) THEN                                                           
%   EVALUATE SWITCH = 2                                                         
%  ELSE                                                                         
%   VERIFY BXXI3 BOKI3 MOL COP OTH NEU
%   GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                                
%   EVALUATE SWITCH = VALUE                                                     
%  END IF                                                                       
%  CASE SWITCH                                                                  
%    BLOCK        %BXXI3%                                                       
^^WI   DISPOSE INLIST3                                                          
^^CR                                                                            
%      EVALUATE LCANCEL = FALSE                                                 
%      EVALUATE LXIN3CANCEL = TRUE                                              
%      FINISH                                                                   
%    END BLOCK                                                                  
%    BLOCK        %BOKI3%                                                       
%    IF ( EXISTS 13 .NE. 1 ) THEN                                               
                                                                                
{I * Writing a default LIST 13 - should never happen.                           
                                                                                
%      copy '#list 13' 
%      copy 'END'                                                         
%    END IF                                                                     
                                                                                
{I * Getting composition contents info                                          
                                                                                
%      CLEAR                                                                    
%      QUEUE REWIND                                                             
%      INSERT 'CONTENTS'                                                        
^^??   LE TEXT                                                                  
%      GET SILENT KEYWORD 'Element' 'fini'                                      
%      EVALUATE CELEM = CVALUE                                                  
%      LOOP 18 TIMES                                                            
%        GET NOPROMPT KEYWORD 'Number' 'fini'                                   
%        EVALUATE CNUMB = CVALUE                                                
%        IF ( CELEM .EQ. CNUMB ) .OR. -                                         
            ( CELEM .EQ. 'fini' ) .OR. ( CNUMB .EQ. 'fini' ) THEN               
%          EVALUATE LOOPEND = LOOPCOUNTER                                       
%        ELSE                                                                   
%          QUEUE SEND                                                           
%          OUTPUT                                                               
%          CLEAR                                                                
%          INSERT 'CONTINUE '                                                   
%          GET NOPROMPT KEYWORD 'Element' 'fini'                                
%          EVALUATE CELEM = CVALUE                                              
%        END IF                                                                 
%      END LOOP                                                                 
%%                                                                              
                                                                                
{I * Updating List 13 info                                                      
                                                                                
% VARIABLE INTEGER IDJW
%      COPY '#GENERALEDIT 13'
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
^^??   COP STATE                                                                
%      VERIFY ON OFF                                                            
%      GET SILENT NOSTORE VERIFIED 'ON or Off' ' '                              
%      IF VALUE .EQ. 1 THEN                                                     
%        EVALUATE RADIATION = 1                                                 
%      ELSE                                                                     
^^??     MOL STATE                                                              
%        VERIFY ON OFF                                                          
%        GET SILENT NOSTORE VERIFIED 'ON or Off' ' '                            
%        IF VALUE .EQ. 1 THEN                                                   
%          EVALUATE RADIATION = 2                                               
%        ELSE                                                                   
^^??       OTH STATE                                                              
%          VERIFY ON OFF                                                          
%          GET SILENT NOSTORE VERIFIED 'ON or Off' ' '                            
%          IF VALUE .EQ. 1 THEN                                                   
%            EVALUATE RADIATION = 3
%          ELSE                                                                   
%            EVALUATE RADIATION = 4                                             
%          END IF                                                                 
%        END IF                                                                 
%      END IF                                                                   
^^??   FRIE STATE                                                               
%      VERIFY ON OFF                                                            
%      GET SILENT NOSTORE VERIFIED 'ON or Off' ' '                              
%      IF VALUE .EQ. 1 THEN                                                     
%        EVALUATE IDJW = 0                                                      
%      ELSE                                                                     
%        EVALUATE IDJW = - 1                                                    
%      END IF                                                                   
%      COPY 'LOCATE RECORD=101'                                                 
%      COPY 'TRANSFER TO OFFSET=0 FROM IDJW'                                    
%      COPY 'WRITE'                                                             
%%                                                                              
% VARIABLE REAL ADJW                                                            
%      CASE RADIATION                                                           
%       EVALUATE ADJW = 1.5418  %CU%                                            
%       EVALUATE ADJW = 0.71073 %MO%                                            
%       BLOCK                   %OTHER%                                         
^^??      WAVEL TEXT                                                            
%         GET SILENT REAL 'Wave' '0.0123'                                       
%         EVALUATE CTEST = CHARACTER ( VALUE )                                  
%         EVALUATE ADJW = VALUE                                                 
%       END BLOCK                                                               
%       BLOCK                   %NEU%                                         
^^??      WAVEL TEXT                                                            
%         GET SILENT REAL 'Wave' '0.0123'                                       
%         EVALUATE CTEST = CHARACTER ( VALUE )                                  
%         EVALUATE ADJW = VALUE                                                 
%       END BLOCK                                                               
%      END CASE                                                                 
%      COPY 'LOCATE RECORD=103'                                                 
%      COPY 'TRANSFER TO OFFSET=0 FROM ADJW'                                    
%      COPY 'WRITE'
%      EVALUATE XORN = - 1
%      IF RADIATION .EQ. 4 THEN
%        EVALUATE XORN = 0
%      END IF
%      COPY 'LOCATE RECORD=102'
%      COPY 'TRANSFER TO OFFSET=1 FROM XORN'                                    
%      COPY 'WRITE'                                                             
%      COPY 'END'                                                               
%%                                                                              
%      IF ( RADIATION .EQ. 4 ) THEN
%        QUEUE COPY SCATT CRYSDIR:script/nscatt.dat
%      ELSE
%        QUEUE COPY SCATT CRYSDIR:script/scatt.dat
%      END IF
%      QUEUE COPY PROPERTIES CRYSDIR:script/propwin.dat                         
%%                                                                              
{I * Updating composition info                                                  
%      COPY '#COMPOSITION'                                                      
%      QUEUE PROCESS                                                            
%      COPY 'END'                                                               
                                                                                
%      COPY '#SUM L 29'                                                         
%      COPY 'END'                                                               
%%                                                                              
%%                                                                              
%%      IF ( ( RADIATION .EQ. 3 ) .AND. ( .NOT. EDLIST3 ) ) THEN                 
%%         EVALUATE QTITLE = 'Odd wavelength: ' // CTEST                         
%%         EVALUATE QLINE1 = 'Do you want to input anomalous scattering'         
%%         EVALUATE QLINE2 = 'factors for these elements?'                       
%%         EVALUATE BUTTOK = '&Yes'                                              
%%         EVALUATE BUTTXX = '&No'                                               
%%         COPY '#SCRIPT XQUESTIO'                                               
%%         IF ANSWER .EQ. TRUE THEN                                              
%%           COPY '#SCRIPT EDLIST3'                                              
%%         END IF                                                                
%%      END IF                                                                   
%%                                                                              
%      EVALUATE LXIN3CANCEL = FALSE                                             
^^CO   DISPOSE INLIST3                                                          
%      FINISH                                                                   
%                                                                               
%    END BLOCK                                                                  
%    BLOCK    %MOL                                                              
^^CO SET WAVEL DISABLED=YES TEXT='0.71073'                                      
%    END BLOCK                                                                  
%    BLOCK    %COP                                                              
^^CO SET WAVEL DISABLED=YES TEXT='1.5418'                                       
%    END BLOCK                                                                  
%    BLOCK    %OTH                                                              
^^CO SET WAVEL DISABLED=NO                                                      
%    END BLOCK                                                                  
%    BLOCK    %NEU                                                              
^^CO SET WAVEL DISABLED=NO
%    END BLOCK                                                                  
%  END CASE                                                                     
% END LOOP                                                                      
%END SCRIPT                                                                     
