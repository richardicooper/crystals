%SCRIPT XFLIPINLIST6
%% CRYSTALS reflection input script                                             
%  VARIABLE CHARACTER INSTITLE HKLTITLE
%  VARIABLE INTEGER INBASICS FTYPE FS NCOEF FMT
%  VARIABLE LOGICAL LCANCEL INITSETUP XQMERGE LOKOK
%  EVALUATE LCANCEL = FALSE
%  EVALUATE LOKOK = FALSE
%% variables used by the question box
%  VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2
%  VARIABLE LOGICAL ANSWER
%
%        EVALUATE QTITLE = 'Re-import of reflection data'
% EVALUATE QLINE1 = 'You are strongly advised to re-import the reflection data.'
%        EVALUATE QLINE2 = 'because the space group/point group has changed.'
%        EVALUATE BUTTOK = 'OK'
%        COPY '#SCRIPT XMESSAGE'
%%
%%
%% Gather information before acting.
%%
^^WI WINDOW XINLIST6 'Input reflection data' MODAL                              
^^WI        COMMIT='BOK' CANCEL='BXX'
^^WI GRID GRIDM NROWS=2 NCOLS=1
^^WI {                                                                          
^^WI  @ 1,1 TABCTRL IN6TABS
^^WI  {                                                                         
^^WI   TAB IN6TAB0 'HKL data' GRID XIN6T NROWS=9 NCOLS=3                        
^^WI   {                                                                        
^^WI    @ 2,2 GRID GF NROWS=1 NCOLS=3 OUTLINE='File'                            
^^WI    {                                                                       
^^WI     @ 1,1 EDITBOX L6FILE ' ' CHARS=48 INFORM=YES                           
^^WI     @ 1,3 BUTTON BROWSE 'Browse...'                                        
^^WI    }                                                                       
^^WI    @ 4,2 GRID GD NROWS=4 NCOLS=1 OUTLINE='Data type'                       
^^WI    {                                                                       
^^WI     @ 1,1 GRID GT NROWS=1 NCOLS=4                                          
^^WI     {                                                                      
^^WI      @ 1,1 ICON IM INFO                                                    
^^WI      @ 1,4 GRID GT NROWS=3 NCOLS=1                                         
^^WI      {                                                                     
^^WI @ 1,1 STATIC G 'Please give the type of the data in the file above.'
^^WI @ 2,1 STATIC G 'This is NOT related to whether you want to refine'         
^^WI @ 3,1 STATIC G 'against F or F squared! Files usually contain F-squared.'  
^^WI      }                                                                     
^^WI     }                                                                      
^^WI     @ 3,1 GRID GT NROWS=1 NCOLS=5
^^WI     {                                                                      
^^WI      @ 1,1 RADIOBUTTON RFS1 'F' IGNORE                                     
^^WI      @ 1,3 RADIOBUTTON RFS2 'F-squared' STATE=ON IGNORE                    
^^WI      @ 1,5 RADIOBUTTON RFS3 'I' IGNORE                                     
^^WI     }                                                                      
^^WI     @ 4,1 CHECKBOX CS 'Data contains sigmas' STATE=ON INFORM=NO            
^^WI    }                                                                       
^^WI    @ 6,2 GRID GD NROWS=3 NCOLS=2 OUTLINE='Format'                          
^^WI    {                                                                       
^^WI     @ 1,1 RADIOBUTTON RDF1 'Free format' INFORM                            
^^WI     @ 2,1 RADIOBUTTON RDF2 'Shelx (fixed format)'
^^WI                              INFORM STATE=ON                               
^^WI     @ 3,1 RADIOBUTTON RDF3 'Other (fixed format):'
^^WI                              INFORM                                        
^^WI     @ 3,2 EDITBOX L6FORM ' (3F4.0, 2F8.0) ' CHARS=24                       
^^WI                                        DISABLED=YES                        
^^WI    }                                                                       
^^WI   }                                                                        
^^WI   TAB IN6TAB0 'Advanced' GRID XIN6A NROWS=5 NCOLS=3                        
^^WI   {                                                                        
^^WI    @ 2,2 GRID GAB NROWS=2 NCOLS=1 OUTLINE ='Transform'                     
^^WI    {                                                                       
^^WI     @ 1,1 CHECKBOX MAT                                                     
^^WI 'Apply this transformation matrix to the hkl data.' STATE=OFF              
^^WI     @ 2,1 GRID GBB NROWS=5 NCOLS=5                                         
^^WI     {                                                                      
^^WI      @ 1,1 EDITBOX M11 '1.000' CHARS=6 REAL                                
^^WI      @ 1,3 EDITBOX M12 '0.000' CHARS=6 REAL                                
^^WI      @ 1,5 EDITBOX M13 '0.000' CHARS=6 REAL                                
^^WI      @ 3,1 EDITBOX M21 '0.000' CHARS=6 REAL                                
^^WI      @ 3,3 EDITBOX M22 '1.000' CHARS=6 REAL                                
^^WI      @ 3,5 EDITBOX M23 '0.000' CHARS=6 REAL                                
^^WI      @ 5,1 EDITBOX M31 '0.000' CHARS=6 REAL                                
^^WI      @ 5,3 EDITBOX M32 '0.000' CHARS=6 REAL                                
^^WI      @ 5,5 EDITBOX M33 '1.000' CHARS=6 REAL                                
^^WI     }                                                                      
^^WI    }                                                                       
^^WI   }                                                                        
^^WI  }                                                                         
^^WI  @ 2,1 GRID GB NROWS=3 NCOLS=7
^^WI  {                                                                         
^^WI   @ 2,2 BUTTON BOK '&Ok' DEFAULT DISABLED=YES                              
^^WI   @ 2,4 BUTTON BXX '&Cancel'                                               
^^WI   @ 2,5 STRETCH ACROS HORIZONTAL                                           
^^WI   @ 2,6 BUTTON HELP1 'Help...'                                             
^^WI  }                                                                         
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
% IF ( FILEEXISTS ( HKLTITLE ) ) THEN                                           
%   TRANSFER "^^CO SET L6FILE TEXT '" // HKLTITLE // "'" TO DISPLAY             
^^CO SET L6FILE INFORM=NO SET BOK DISABLED=NO                                   
%       EVALUATE LOKOK = TRUE                                                   
% END IF                                                                        
%%                                                                              
%% Now the window is displayed, we wait for any of the following                
%% things to happen:                                                            
%% "BOK" - the OK button is pressed.                                            
%% "BXX" - the Cancel button is pressed.                                        
%% "BROWSE" - the browse button is pressed.                                     
%% "HELP1" - the help button is pressed.                                        
%% "RDF1" - free format is selected. (DISable L6FORM)                           
%% "RDF2" - shelx format is selected.(DISable L6FORM)                           
%% "RDF3" - other format is selected.(ENable L6FORM)                            
%%
% LOOP                                                                          
%   VERIFY L6FILE BOK BXX BROWSE HELP1 RDF1 RDF2 RDF3                           
%   GET SILENT NOSTORE FINAL ABBREVIATED 'Opt' ' '                              
%   CASE VALUE                                                                  
%     BLOCK                         %L6FILE%                                    
%       GET SILENT NOSTORE TEXT ' ' ' '                                         
^^CO SET L6FILE INFORM=NO SET BOK DISABLED=NO                                   
%       EVALUATE LOKOK = TRUE                                                   
%     END BLOCK                                                                 
%     BLOCK                         %BOK%                                       
%%                                                                              
%      IF LOKOK THEN                                                            
%% First get the filename.                                                      
%       CLEAR                                                                   
%       QUEUE REWIND                                                            
%       INSERT '#CLOSE HKLI'                                                    
%       QUEUE SEND                                                              
%       CLEAR                                                                   
%       INSERT '#OPEN HKLI'                                                     
^^??    L6FILE TEXT                                                             
%       GET NOSTORE SILENT TEXT 'Reflection File (HKLI file) '                  
%       IF ( .NOT. ( FILEEXISTS CVALUE ) ) THEN                                 
^^CO SET L6FILE INFORM=YES SET BOK DISABLED=YES                                 
%       EVALUATE LOKOK = FALSE                                                  
 Error: Filename given does not exist.                                          
%       ELSE                                                                    
%         TRANSFER '"' // CVALUE // '"' TO BUFFER                               
%         QUEUE SEND                                                            
%         QUEUE PROCESS                                                         
%         BLOCK   %REFLECTION TYPE%                                             
%           VERIFY ON OFF                                                       
^^??        RFS1 STATE                                                          
%           GET SILENT NOSTORE FINAL ABBREVIATED 'Fs?' 'OFF'                    
%           IF VALUE .EQ. 1 THEN                                                
%             EVALUATE FS = 1                                                   
%             EVALUATE QTITLE = 'Format warning'                                
% EVALUATE QLINE1 = 'You have stated that your hkl file contains F values.'     
% EVALUATE QLINE2 = 'This is unlikely to be the case in modern times...'        
%             EVALUATE BUTTOK = 'I meant F squared'                             
%             EVALUATE BUTTXX = 'I really did mean F values'                    
%             COPY '#SCRIPT XQUESTIO'                                           
%             IF ANSWER .EQ. TRUE THEN                                          
%               EVALUATE FS = 2                                                 
%             END IF                                                            
%           ELSE                                                                
^^??          RFS2 STATE                                                        
%             VERIFY ON OFF                                                     
%             GET SILENT NOSTORE FINAL ABBREVIATED 'Fsq?' 'ON'                  
%             IF VALUE .EQ. 1 THEN                                              
%               EVALUATE FS = 2                                                 
%             ELSE                                                              
%               EVALUATE FS = 3                                                 
%             END IF
%           END IF                                                              
%           VERIFY ON OFF                                                       
^^??        RDF1 STATE                                                          
%           GET SILENT NOSTORE FINAL ABBREVIATED 'Free format?' 'OFF'           
%%                                                                              
%% FMT : 1 = free format, 2 = fixed, 3 = fixed shelx                            
%%                                                                              
%           EVALUATE FMT = VALUE                                                
%%                                                                              
%           IF ( FMT .EQ. 2 ) THEN                                              
^^??          RDF2 STATE                                                        
%             GET SILENT NOSTORE FINAL ABBREVIATED 'Shelx format?' 'ON'         
%             IF ( VALUE .EQ. 1 ) THEN                                          
%                EVALUATE FMT = 3                                               
%             END IF                                                            
%             EVALUATE NCOEF = 5                                                
%           END IF                                                              
^^??        CS STATE                                                            
%           GET SILENT NOSTORE FINAL ABBREVIATED 'sigmas? ' 'ON'                
%           EVALUATE NCOEF = 6 - VALUE                                          
%         END BLOCK                                                             
%         BLOCK                                                                 
%           QUEUE REWIND                                                        
%           ON ERROR RESTART                                                    
%           CLEAR                                                               
%           INSERT "READ "                                                      
%           IF FS = 1 THEN                                                      
%             INSERT " F'S=FO"                                                  
%           ELSE                                                                
%             INSERT " F'S=FSQ"                                                 
%           END IF                                                              
%           IF NCOEF .EQ. 4 THEN                                                
%             INSERT ' NCOEF=4'                                               
%           ELSE                                                                
%             INSERT ' NCOEF=5'                                               
%           END IF  
%% don't archive since this is a re-input of data                                                            
%           IF FMT .EQ. 1 THEN                                                  
%             INSERT ' TYPE=FREE CHECK=NO ARCH=NO'                                  
%           ELSE                                                                
%             INSERT ' TYPE=FIXED CHECK=NO ARCH=NO'                                 
%           END IF                                                              
%           QUEUE SEND                                                          
%%                                                                              
%           CLEAR                                                               
%           IF NCOEF .EQ. 5 THEN                                                
%             INSERT 'INPUT H K L /FO/ SIGMA(/FO/) '                            
%           ELSE                                                                
%             INSERT 'INPUT H K L /FO/ '                                        
%           END IF                                                              
%           QUEUE SEND                                                          
%%                                                                              
%           CLEAR                                                               
%           IF ( FMT .EQ. 2 ) THEN
%             INSERT 'FORMAT '                                                  
^^?? L6FORM TEXT                                                                
%             GET SILENT APPEND TEXT 'FORMAT' ' (3F4.0, 2F8.0) '                
%             QUEUE SEND                                                        
%           ELSE IF ( FMT .EQ. 3 ) THEN                                         
%             INSERT 'FORMAT (3F4.0, 2F8.0)'                                    
%             QUEUE SEND                                                        
%           END IF                                                              
%%                                                                              
%           CLEAR                                                               
%           INSERT 'STORE NCOEF=6'                                              
%           QUEUE SEND                                                          
%           CLEAR                                                               
%  INSERT 'OUTPUT INDICES /FO/ SIGMA(/FO/) RATIO/JCODE CORRECTIONS SERIAL '     
%           QUEUE SEND                                                          
%           CLEAR                                                               
^^?? MAT STATE                                                                  
%           VERIFY ON OFF                                                       
%           GET SILENT NOSTORE FINAL ABBREVIATED 'Transform?' 'ON'              
%           IF ( VALUE .EQ. 1 ) THEN                                            
%             INSERT 'MATRIX'                                                   
^^??          M11 TEXT                                                          
%             GET SILENT REAL ' ' '1.0'                                         
^^??          M21 TEXT                                                          
%             GET SILENT REAL ' ' '0.0'                                         
^^??          M31 TEXT                                                          
%             GET SILENT REAL ' ' '0.0'                                         
^^??          M12 TEXT                                                          
%             GET SILENT REAL ' ' '0.0'                                         
%             QUEUE SEND                                                        
%             CLEAR                                                             
%             INSERT 'CONT'                                                     
^^??          M22 TEXT                                                          
%             GET SILENT REAL ' ' '1.0'                                         
^^??          M32 TEXT                                                          
%             GET SILENT REAL ' ' '0.0'                                         
^^??          M13 TEXT                                                          
%             GET SILENT REAL ' ' '0.0'                                         
^^??          M23 TEXT                                                          
%             GET SILENT REAL ' ' '0.0'                                         
^^??          M33 TEXT                                                          
%             GET SILENT REAL ' ' '1.0'                                         
%             QUEUE SEND                                                        
%           END IF                                                              
%         END BLOCK                                                             
^^WI      DISPOSE XINLIST6                                                      
^^CR                                                                            
%         COPY '#HKLI'                                                          
%         QUEUE PROCESS                                                         
%         COPY 'END'                                                            
%         COPY '#CLOSE HKLI'                                                    
%%                                                                              
%         IF FS .EQ. 3 THEN
%           COPY '#LP'                                                          
%           COPY 'END'                                                          
%         END IF                                                                
%         COPY '#SCRIPT XFRIEDEL'                                               
%         MESSAGE USER1 ' Error during reflection processing'                   
%         ON ERROR TERMINATE USER1                                              
{S * Removing systematically absent reflections                                 
%         COPY '#SYSTEMATIC'                                                    
%         COPY 'END'                                                            
%         CLEAR                                                                 
{S * Sorting reflections                                                        
%         COPY '#SORT'                                                          
%         COPY 'END'                                                            
%         CLEAR                                                                 
%% This is now done in XFRIEDEL, using same XQMERGE variable                    
%% Ask user if they want to merge reflections:                                  
%%         COPY '#SCRIPT XQMERGE'                                               
%         IF ( XQMERGE ) THEN                                                   
{S * Merging equivalent reflections                                             
%           CLEAR                                                               
%           COPY '#MERGE'                                                       
%           COPY 'END'                                                          
%         END IF                                                                
%                                                                               
{S * Final copy to DSC file                                                     
%         COPY '#LIST 6'                                                        
%         COPY 'READ TYPE = COPY'                                               
%         COPY 'END'                                                            
%%                                                                              
%%                                                                              
%         COPY '#SCRIPT XFRESH6'
%%                                                                              
%%                                                                              
{R * Done. Reflection data input complete.                                      
                                                                                
%         FINISH                                                                
%       END IF                                                                  
%      END IF                                                                   
%     END BLOCK                                                                 
%     BLOCK                         %BXX%                                       
^^WI    DISPOSE XINLIST6                                                        
^^CR                                                                            
%       FINISH                                                                  
%     END BLOCK                                                                 
%     BLOCK                         %BROWSE%                                    
^^WI    SYSOPENFILE '*.hkl;*.hkp' 'HKL file (*.hkl,*.hkp)'                      
^^CR                                                                            
%       GET SILENT TEXT 'New filename:'                                         
%       IF CVALUE .NE. 'CANCEL' THEN                                            
%        CLEAR                                                                  
^^WI     SET L6FILE TEXT                                                        
%        TRANSFER "^^WI '" // CVALUE // "'" TO DISPLAY                          
^^CR                                                                            
^^CO      SET L6FILE INFORM=NO SET BOK DISABLED=NO                              
%         EVALUATE LOKOK = TRUE                                                 
%% For from-cif.hkl files set correct FORMAT
%         IF ( UPPERCASE GETFILE ( CVALUE ) .EQ. 'FROM-CIF.HKL' ) THEN
^^CO        SET L6FORM DISABLED=NO TEXT='(3F4.0,2F10.0)' 
^^CO        SET RDF3 STATE=ON
%         ELSE
^^CO        SET L6FORM DISABLED=YES  
^^CO        SET RDF2 STATE=ON
%         END IF
%       END IF                                                                  
%     END BLOCK                                                                 
%     BLOCK                         %HELP1%                                     
%       COPY '#SCRIPT XHELP1'                                                   
X-ray observation data can be stored in several different formats:              
                                                                                
I's --               Intensities. The "raw" reflection data as                  
                     recorded by the diffractometer.                            
F's and F-squared -- Structure factor amplitudes (or their square).             
                     Data reduction and absorption corrections on               
                     I's produce F-squared or F.                                
e.g. a file processed by RC93 will contain intensities.                         
     a file processed by DIPIN will contain F-squared.                          
%       COPY '#SCRIPT XHELP2'                                                   
%     END BLOCK                                                                 
%     BLOCK                         %RDF1%                                      
^^WI SET L6FORM DISABLED=YES
^^CR                                                                            
%     END BLOCK                                                                 
%     BLOCK                         %RDF2%                                      
^^WI SET L6FORM DISABLED=YES                                                    
^^CR                                                                            
%     END BLOCK                                                                 
%     BLOCK                         %RDF3%                                      
^^WI SET L6FORM DISABLED=NO                                                     
^^CR                                                                            
%     END BLOCK                                                                 
%   END CASE                                                                    
% END LOOP                                                                      
%END SCRIPT                                                                     
%%                                                                              
%% $Log: not supported by cvs2svn $
%% Revision 1.5  2012/01/03 16:47:23  rich
%% Fix INSTITLE rubbish.
%%
%% Revision 1.4  2011/12/16 15:13:14  djw
%% Replace individual setting of LISTS 4, 23 and 28 by a call to XFRESH6
%%
%% Revision 1.3  2011/06/14 10:05:53  djw
%% List 28 is an auto-create list. Try to ensure it is created before any weigting is done. If LIST 23 exists, try to set defaults at -3 for F^2 and +3 for F. If LIST 23 does not exist, call script XLIST28 to enable user to set values.
%% Scripts changed are: difabs, inemap,newstart,xcifnosu, xflipinlist6, xinlist6 xinrc93
%%
%% Revision 1.2  2008/06/27 14:38:56  djw
%% Arie's superflip changes
%%                                                       
%% Revision 1.28  2005/01/23 10:00:25  rich                                     
%% Reinstate CVS history for scripts. History from Dec23 - Jan23 is lost, but   
%% changes are not!                                                             
%%                                                                              
%% Revision 1.1.1.1  2004/12/13 11:16:23  rich                                  
%% New CRYSTALS repository                                                      
%%                                                                              
%% Revision 1.27  2003/07/08 10:00:09  rich                                     
%%                                                                              
%% Col/row ordering was wrong for new                                           
%% transformation matrix on input of                                            
%% LIST 6. Fixed.                                                               
%%                                                                              
%% Revision 1.26  2003/07/07 14:24:29  rich                                     
%% If an hkl filename is passed in, then enable the OK button, or the hapless   
%% user will be stuck.                                                          
%%                                                                              
%% Revision 1.25  2003/06/27 14:44:13  rich                                     
%% Added option to apply a transformation matrix to HKL data as it is read in.  
%%                                                                              
%% Revision 1.24  2003/02/25 16:47:26  rich                                     
%%                                                                              
%% Streamline input of data. Merging question and Friedel question moved        
%% into one dialog (in XFRIEDEL).                                               
%%                                                                              
%% F or F^2 question moved to AFTER the reflection file format is given - this w
%% hopefully reduce some confusion.                                             
%%                                                                              
%% Revision 1.23  2002/09/27 14:40:28  rich                                     
%% Message about applying unit weights.                                         
%%                                                                              
%% Revision 1.22  2002/07/08 11:34:08  richard                                  
%% If user selects F as data format, warn and give option to revert to F^2.     
%%                                                                              
%% Revision 1.21  2002/05/15 08:02:56  richard                                  
%% Little update using new HIDDENSTRING GUI control - the stem of the .ins      
%% filename is remembered and used for the default .hkl filename if the file    
%% exists.
%%                                                                              
%% Revision 1.20  2002/03/12 17:57:18  ckp2                                     
%% Warn about the meaning of F and F2 in this context.                          
%%                                                                              
%% Revision 1.19  2002/02/27 19:52:35  ckp2                                     
%% RIC: Whenever SPAWN command is used with unexpanded environment variable as a
%% ensure that arg is in double quotes in case expanded value contains spaces.  
%% RIC: Make consistent use of \ after CRYSDIR:                                 
%%                                                                              
%% Revision 1.18  2001/11/23 09:15:01  ckp2                                     
%% Call the xlist28 script for setting up a list 28 when importing hkl file.    
%%                                                                              
%% Revision 1.17  2001/11/13 09:51:29  ckp2                                     
%% Fix free format reads of reflections.                                        
%% Fix bug when no file name given.                                             
%% Fix default format when reading shelx files.                                 
%%                                                                              
%% Revision 1.16  2001/10/31 13:04:06  ckp2                                     
%% Option to read in reflection files with HKP extension, as well as normal HKL'
%% (HKP's are output by PLATON).                                                
%%                                                                              
%% Revision 1.15  2001/09/11 11:18:07  ckp2                                     
%% Option not to merge friedel pairs.                                           
%%                                                                              
%% Revision 1.14  2001/03/27 15:06:23  richard                                  
%% Use XQMERGE to ask the MERGE question.                                       
%%                                                                              
%% Revision 1.13  2001/02/06 10:18:33  richard                                  
%% Changed "IGNORE" token after CHECKBOX to "INFORM=NO"                         
%%                                                                              
%% Revision 1.12  2000/10/31 15:18:16  ckp2                                     
%% New text colour method                                                       
%%                                                                              
