%SCRIPT GFINDH                                                                  
% VARIABLE CHARACTER CTYPE                                                      
% VARIABLE INTEGER HFOUND  DOCALC
% VARIABLE REAL SERIAL                                                          
%%                                                                              
% VARIABLE CHARACTER CATOM CEL CTYPE                                            
% VARIABLE REAL SX SY SZ SS SERI                                                
% VARIABLE LOGICAL HQEXEC L12SMASK T                                            
% VARIABLE LOGICAL HDONE HMASK FOUREF AHFOUND REGUL RENUMB                      
% VARIABLE INTEGER L12SER                                                       
%% The variables needed by XQUESTIO                                             
% VARIABLE CHARACTER QTITLE QLINE1 QLINE2 BUTTOK BUTTXX                         
% VARIABLE LOGICAL   ANSWER                                                     
%% Variable returned by assess5                                                 
% VARIABLE REAL HFIRST                                                          
%% Variable set by XGUIDERC if caller.
% VARIABLE LOGICAL XGRETURN
%%                                                                              
% VARIABLE INTEGER OLDSER                                                       
% COPY '#GENERALEDIT 5'                                                         
% COPY 'GETSERIAL OLDSER'                                                       
% COPY 'END'                                                                    
% VARIABLE LOGICAL IS41 
% VARIABLE INTEGER ORIG41 NEW41 
% EVALUATE IS41 =  ( ( EXISTS 41 ) .GE. 1 )                                     
% IF ( IS41 ) THEN                                                              
%   COPY '#GENERALEDIT 41'                                                      
%   COPY 'GETSERIAL ORIG41'                                                    
%   COPY 'END'                                                                  
% END IF
%%                                                                              
% VARIABLE LOGICAL TWINNED  MERGED NEUTRONS
% EVALUATE TWINNED = FALSE                                                      
% EVALUATE MERGED = FALSE                                                       
% COPY '#SCRIPT XGETWZ'
%%
^^WI WINDOW FINDH 'Add Hydrogen' MODAL KEEP SIZE LARGE COMMIT='FHBOK'           
^^WI CANCEL='FHBXX'                                                             
^^WI GRID MAIN NROWS=9 NCOLS=3                                                  
^^WI {                                                                          
^^WI   @ 2,2 GRID GRIDQ NROWS=1 NCOLS=3 OUTLINE=''                              
^^WI   {                                                                        
^^WI     @ 1,1 ICON IM INFO                                                     
^^WI     @ 1,3 GRID EXP2 NROWS=7 NCOLS=1                                        
^^WI     {                                                                      
^^WI       @ 1,1 STATIC T1                                                      
^^WI 'Key: H (white) - Geometric H positions, QH (pink) - Diff Fourier peaks.'  
^^WI       @ 2,1 STATIC T2                                                      
^^WI 'On exit, all white H atoms will be retained, and all pink atoms deleted.' 
^^WI       @ 3,1 STATIC T3                                                      
^^WI 'If necessary, change (pink) QH peaks to H by clicking them.'              
^^WI       @ 4,1 STATIC T4                                                      
^^WI 'Delete any computed (white) H atoms you do not want to keep.'             
^^WI       @ 5,1 STATIC T5                                                      
^^WI 'If the structure is complete, check "All H atoms have been found" below.' 
^^WI       @ 6,1 STATIC T6                                                      
^^WI 'Renumbering will create systematic numbers for changed atoms'             
^^WI       @ 7,1 STATIC T7                                                      
^^WI 'Regularisation will optimise the H positions using the X-ray data'        
^^WI     }                                                                      
^^WI   }                                                                        
^^WI   @ 4,2 GRID TEXT NROWS=1 NCOLS=3                                          
^^WI   {                                                                        
^^WI      @ 1,1 GRID OPTIONS NROWS=3 NCOLS=1                                    
^^WI      {                                                                     
^^WI        @ 1,1 GRID CLICK NROWS=2 NCOLS=3 OUTLINE='Click action'             
^^WI        {                                                                   
^^WI           @ 1,1 STATIC _ELT 'change to:'                                   
^^WI           @ 2,1 LISTBOX _ELS VISLINES=6 ADDTOLIST                          
^^WI             'H' 'QH' 'Delete' 'NULL'                                       
^^WI        }                                                                   
^^WI      }                                                                     
^^WI      @ 1,3 MODELWINDOW MODELRE 'N' NROWS=5 NCOLS=65                        
^^WI            ATTACH L5 RADSCALE=250 RADTYPE=COV                              
^^WI            MOUSEACTION=HEADERATOM                                          
^^WI   }                                                                        
^^WI   @ 6,2 GRID CHECKB NROWS=1 NCOLS=7                                        
^^WI     @ 1,1 CHECKBOX AHFOUND                                                 
^^WI       'All H atoms have been found' STATE=OFF                              
^^WI     @ 1,3 CHECKBOX RENUMB                                                  
^^WI       'Create systematic numbers' STATE=ON                                 
^^WI     @ 1,5 CHECKBOX REGUL                                                   
^^WI       'Regularise the H using restraints' STATE=ON                         
^^WI     @ 1,7 CHECKBOX FOUREF                                                  
^^WI       'Advanced: Fourier refinement using QH peaks' STATE=OFF              
^^WI   }                                                                        
^^WI   @ 8,2 GRID BTNS NROWS=1 NCOLS=7                                          
^^WI   {                                                                        
^^WI     @ 1,1 BUTTON FHBOK 'D&one' DEFAULT COMMIT                              
^^WI     @ 1,3 BUTTON FHBXX '&Cancel' CANCEL                                    
^^WI     @ 1,5 BUTTON FHBHELP '&Help'                                           
^^WI   }                                                                        
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
^^WI WINDOW XABORT 'Calculating:' MODAL GRID MAIN NROWS=3 NCOLS=3               
^^WI { @ 2,2 STATIC GFT1 'Please wait. Checking current model.' } SHOW          
^^CR                                                                            
                                                                                
 * Rename any QH peaks that you wish to keep to 'H'.                            
                                                                                
 {2,15 The WHITE atoms are geometrically placed on the carbon and nitrogen      
 {2,15 skeleton, and the PINK atoms have been found in a Fourier map.           
                                                                                
 {2,15 Where the atoms almost co-incide, you can get a qualitative idea         
 {2,15 of the quality of your data.                                             
                                                                                
 {2,15 Where hydrogens have not been geometrically placed, you can              
 {2,15 change the PINK atom to a WHITE hydrogen, by clicking on it              
 {2,15 with the RIGHT mouse button and choosing "Change Type" from              
 {2,15 the menu.                                                                
                                                                                
% COPY '#SCRIPT ASSESS5'                                                        
% IF ( HFIRST .GT. 0.0 ) THEN                                                   
%   EVALUATE QTITLE = 'Delete Hydrogen'                                         
%   EVALUATE QLINE1 = 'Do you want to delete'                                   
%   EVALUATE QLINE2 = 'existing Hydrogen atoms?'                                
%   EVALUATE BUTTOK = '&No'                                                     
%   EVALUATE BUTTXX = '&Yes'                                                    
%   COPY '#SCRIPT XQUESTIO'                                                     
%   IF ANSWER .EQ. FALSE THEN                                                   
%      COPY '#EDIT'                                                             
%      COPY 'SELECT TYPE NE H'                                                  
%      COPY 'END'                  
%      COPY '#RELEASE PUNCH gfindh12.pch'                                              
%      COPY '#PUNCH 12'                                                             
%      COPY 'END'                                                                   
%      COPY '#RELEASE PUNCH bfile.pch'                                                   
%      COPY '#LIST 12'                                                             
%      COPY 'FULL'                                                             
%      COPY 'END'                     
^^CO SET RENUMB STATE=OFF
%   END IF                                                                      
% END IF                                                                        
%%                                                                              
^^CO SET GFT1 TEXT 'Calculating phases from model.'                             
% EVALUATE DOCALC = 1
% COPY '#SCRIPT XDELQ'                                                          
% COPY '#SCRIPT VALID12'                                                        
%%                                                                              
^^CO SET GFT1 TEXT 'Difference Fourier map.'                                    
% COPY '#SCRIPT PREFOUR'
% IF ( NEUTRONS ) THEN
% COPY 'MAP TYPE=DIFF NMAP=YES'
% COPY 'PEAK HEIGHT=3'                                                          
% ELSE
% COPY 'MAP TYPE=DIFF'
% COPY 'PEAK HEIGHT=1'                                                          
% END IF
% COPY 'END'                                                                    
% COPY '#SCRIPT POSTFOUR'                                                       
^^CO SET GFT1 TEXT 'Rejecting coincident peaks.'                                
% COPY '#PEAKS 10 10'                                                           
% COPY 'SEL TYPE=PEAK'                                                          
% COPY 'END'                                                                    
^^CO SET GFT1 TEXT 'Collecting peaks (pink).'                                   
% COPY '#COLL 10 10'                                                            
% COPY 'SEL TYPE=PEAK'                                                          
% COPY 'END'                                                                    
^^CO SET GFT1 TEXT 'Adding geometric H (white).'                                
% COPY '#PERHYDRO 5 5'                                                          
% COPY 'ACTION BOTH'                                                            
% COPY 'END'                                                                    
%% Remove possible duplicate H atoms if pivot lies on a symmetry element        
%% removed djw Jan08 because it may eliminate split atoms                       
%% COPY '#PEAK 5 5'                                                             
%% COPY 'SELECT TYPE=AVERAGE'                                                   
%% COPY 'END'                                                                   
%% Copy peaks from L10 into L5                                                  
% QUEUE REWIND                                                                  
% COPY '#GENERALEDIT 10'                                                        
% COPY "LOCATE RECORDTYPE=101"                                                  
% COPY "ERROR MESSAGE=NO SIGNAL=NONE ACTION=CONTING NAME=EDITERROR"             
% COPY "TOP"                                                                    
% LOOP                                                                          
%   ON EDITERROR TERMINATE                                                      
%   COPY 'TRANSFER FROM OFFSET=0 TO CTYPE'                                      
%   IF ( CTYPE .EQ. 'Q' ) THEN                                                  
%     COPY 'TRANSFER FROM OFFSET=1 TO SERI'                                     
%     COPY 'TRANSFER FROM OFFSET=4 TO SX'                                       
%     COPY 'TRANSFER FROM OFFSET=5 TO SY'                                       
%     COPY 'TRANSFER FROM OFFSET=6 TO SZ'                                       
%     COPY 'TRANSFER FROM OFFSET=13 TO SS'                                      
%     CLEAR                                                                     
%     INSERT 'ATOM QH'                                                          
%     STORE INTEGER INTEGER SERI                                                
%     INSERT ' X='                                                              
%     STORE REAL SX                                                             
%     STORE REAL SY                                                             
%     STORE REAL SZ                                                             
%     INSERT ' SPAR='                                                           
%     STORE REAL SS                                                             
%     QUEUE SEND                                                                
%   END IF                                                                      
%   COPY 'NEXT'                                                                 
% END LOOP                                                                      
% COPY 'END'                                                                    
% COPY '#EDIT'                                                                  
% QUEUE PROCESS                                                                 
% COPY 'END'                                                                    
%%                                                                              
^^CO SET GFT1 TEXT 'Changing Q to QH.'                                          
% COPY '#EDIT'                                                                  
% COPY 'GUI HIGH'                                                               
% COPY 'TYPECHANGE TYPE EQ Q QH'                                                
% COPY 'CLASH FIXLATTER'                                                        
% COPY 'EXEC'                                                                   
^^CO DISPOSE XABORT                                                             
% LOOP                                                                          
%   VERIFY FHBOK FHBXX ATOM FHBHELP                                             
%   GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                                
%   CASE VALUE                                                                  
%    BLOCK      %FHBOK                                                          
%      COPY 'END'                                                               
^^??   FOUREF STATE                                                             
%      VERIFY ON OFF                                                            
%      GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                             
%      EVALUATE FOUREF = FALSE                                                  
%      IF ( VALUE .EQ. 1 ) THEN                                                 
%        EVALUATE FOUREF = TRUE                                                 
%      END IF                                                                   
^^??   REGUL STATE                                                              
%      VERIFY ON OFF                                                            
%      GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                             
%      EVALUATE REGUL = FALSE                                                   
%      IF ( VALUE .EQ. 1 ) THEN                                                 
%        EVALUATE REGUL = TRUE                                                  
%      END IF                                                                   
^^??   RENUMB STATE                                                             
%      VERIFY ON OFF                                                            
%      GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                             
%      EVALUATE RENUMB = FALSE                                                  
%      IF ( VALUE .EQ. 1 ) THEN                                                 
%        EVALUATE RENUMB = TRUE                                                 
%      END IF                                                                   
^^??   AHFOUND STATE                                                            
%      VERIFY ON OFF                                                            
%      GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                             
%      EVALUATE AHFOUND = FALSE                                                 
%      IF ( VALUE .EQ. 1 ) THEN                                                 
%        EVALUATE AHFOUND = TRUE                                                
%      END IF                                                                   
%      IF FOUREF .EQ. TRUE THEN                                                 
{I * Using QH atoms to adjust positions of existing model atoms.                
%        COPY '#EDIT'                                                           
%        COPY 'TYPECHANGE TYPE EQ QH Q'                                         
%        COPY 'END'                                                             
%        COPY '#PEAK 5 5'                                                       
%        COPY 'SEL REJECT = .0001 TYPE=PEAK'                                    
%        COPY 'REFINE DIST = .3'                                                
%        COPY 'END'                                                             
%        COPY '#SCRIPT PLOT'                                                    
%        COPY '#EDIT'                                                           
%        COPY 'SEL TYPE NE Q'                                                   
%        COPY 'END'                                                             
%      ELSE                                                                     
{I * Deleting remaining QH atoms.                                               
%        COPY '#PEAK 5 5'                                                       
%        COPY 'SEL TYPE=PEAK'                                                   
%        COPY 'END'                                                             
%        COPY '#EDIT'                                                           
%        COPY 'SEL TYPE NE QH'                                                  
%        COPY 'SEL TYPE NE QN'                                                  
%        COPY 'SEL TYPE NE Q'                                                   
%        COPY 'END'                                                             
%      END IF                                                                   
%      IF ( RENUMB .EQ. TRUE ) THEN                                             
{I * Renumbering found atoms.                                                   
%            COPY '#HNAME'                                                      
%            COPY 'END'                                                         
%      END IF                                                                   
%      IF ( REGUL .EQ. TRUE ) THEN                                              
%% remove old file if it exists
%        EVALUATE T = FILEDELETE ( 'sav1216.dat' )                              
%        COPY '#RELEASE PUNCH sav1216.dat'                                      
%        COPY '#PUNCH 12'                                                       
%        COPY 'END'                                                             
%        COPY '#PUNCH 16'                                                       
%        COPY 'END'                                                             
%        COPY '#RELEASE PUNCH bfile.pch'                                        
%%                                                                              
%        COPY '#SCRIPT XREGULH'                                                 
%%                                                                              
%        COPY '#USE sav1216.dat'                                                
%% Store the current L12 serial, so other scripts know.                         
%        COPY '#GENERALEDIT 12'                                                 
%        COPY 'GETSERIAL L12SER'                                                
%        COPY 'END'                                                             
%        EVALUATE L12SMASK = TRUE                                               
                                                                                
% VARIABLE LOGICAL HRMASK                                                       
% VARIABLE INTEGER HRTYP                                                        
% EVALUATE HRMASK = TRUE                                                        
% EVALUATE HRTYP = 10                                                           
                                                                                
%        COPY '#SCRIPT XPUTINFO'                                                
%      END IF                                                                   
 * Remember - the data may not be good enough to reveal all H atoms.            
%      IF AHFOUND .EQ. TRUE THEN                                                
%% Store the fact that all H's have been found.                                 
%        EVALUATE HDONE = TRUE                                                  
%        EVALUATE HMASK = TRUE                                                  
%        COPY '#SCRIPT XPUTINFO'                                                
%      END IF                                                                   
%      COPY '#SFLS'                                                             
%      COPY 'CALC'                                                              
%      COPY 'END'                                                               
%      VARIABLE LOGICAL REFREQD                                                 
%      EVALUATE REFREQD = TRUE                                                  
^^CO   DISPOSE FINDH                                                            
%%
%      EVALUATE IS41 =  ( ( EXISTS 41 ) .GE. 1 )                                     
%      IF ( IS41 ) THEN                                                              
%        COPY '#GENERALEDIT 41'                                                      
%        COPY 'GETSERIAL NEW41'                                                    
%        COPY 'END'                                                                  
%        IF ( NEW41 .GT. ORIG41 ) THEN
%          COPY '#PURGE LIST = 41'
%          COPY 'END'
%          COPY '#PURGE LIST = 22'
%          COPY 'END'
%          COPY '#PURGE LIST = 33'
%          COPY 'END'
%          COPY '#PURGE LIST = 36'
%          COPY 'END'
%        END IF
%      END IF
%%
%% If the guide is open but we were called manually. Update the guide.
%%
%      IF ( XGRETURN .EQ. FALSE ) THEN
^^??     XGUIDER EXISTS                                                             
%        VERIFY TRUE FALSE                                                            
%        GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                                 
%        IF VALUE .EQ. 1  THEN
%          COPY '#SCRIPT XGUIDE'
%        END IF
%      END IF
%%
%      FINISH                                                                   
%    END BLOCK                                                                  
%    BLOCK      %BXX                                                            
 * Dialog cancelled. No hydrogen added.                                         
%      COPY 'QUIT'                                                              
%      COPY 'END'                                                               
%      COPY '#DISK'                                                             
%      CLEAR                                                                    
%      INSERT 'RESET 5 '                                                        
%      STORE INTEGER OLDSER                                                     
%      SEND                                                                     
%      COPY '#SUM L 5'                                                          
%      COPY 'END'                                                               
^^CO   DISPOSE FINDH                                                            
%      FINISH                                                                   
%    END BLOCK                                                                  
%    BLOCK      %ATOM                                                           
%     GET SILENT NOSTORE TEXT ' ' ' '                                           
^^CW                                                                            
%     EVALUATE CATOM = CVALUE                                                   
%     EVALUATE HQEXEC = TRUE                                                    
%                                                                               
%     CLEAR                                                                     
%     INSERT '^^?? _ELS LISTTEXT '                                              
^^??     _ELS SELECTED                                                          
%     GET SILENT INTEGER ' '                                                    
%     OUTPUT                                                                    
%     GET SILENT NOSTORE KEYWORD ' ' 'ERROR'                                    
%     EVALUATE CEL = CVALUE                                                     
%%                                                                              
%     IF .NOT. ( CATOM STARTSWITH 'H' .OR. CATOM STARTSWITH 'QH' ) THEN         
%      EVALUATE QTITLE = 'Careful!'                                             
%      EVALUATE QLINE1 = 'Are you sure you want to change ' // CATOM // ' to'   
%      EVALUATE QLINE2 = CEL // '. You should be changing QH peaks to H.'       
%      EVALUATE BUTTOK = '&Sure'                                                
%      EVALUATE BUTTXX = '&Cancel'                                              
%      COPY '#SCRIPT XQUESTIO'                                                  
%      IF ANSWER .EQ. FALSE THEN                                                
%        EVALUATE HQEXEC = FALSE                                                
%      END IF                                                                   
%     END IF                                                                    
%%                                                                              
%     IF ( HQEXEC ) THEN                                                        
%      IF ( ( UPPERCASE CEL ) .EQ. 'DELETE' ) THEN                              
%        TRANSFER 'DELETE ' // CATOM TO CRYSTALS                                
%        COPY 'EXEC'                                                            
%      ELSE                                                                     
%        TRANSFER 'DRENAME ' // CATOM // ' ' // CEL // '(' // -                 
         CHARACTER ( FIRSTINT ( CATOM ) ) // ')' TO CRYSTALS                    
%        COPY 'EXEC'                                                            
%      END IF                                                                   
%     END IF                                                                    
%    END BLOCK                                                                  
%    BLOCK      %HELP                                                           
%      COPY '#SCRIPT XHELP1'                                                    
                                                                                
 * Rename any QH peaks that you wish to keep to 'H'.                            
                                                                                
 {2,15 The WHITE atoms are geometrically placed on the carbon and nitrogen      
 {2,15 skeleton, and the PINK atoms have been found in a Fourier map.           
                                                                                
 {2,15 Where the atoms almost co-incide, you can get a qualitative idea         
 {2,15 of the quality of your data.                                             
                                                                                
 {2,15 Where hydrogens have not been geometrically placed, you can              
 {2,15 change the PINK atom to a WHITE hydrogen, by clicking on it              
 {2,15 with the RIGHT mouse button and choosing "Change Type" from              
 {2,15 the menu.                                                                
                                                                                
 Click on the atoms in the molecule to either change their                      
 type to the current selection (e.g. H), or delete them.                        
                                                                                
 Click Done when complete.                                                      
                                                                                
%      COPY '#SCRIPT XHELP2'                                                    
%    END BLOCK                                                                  
%   END CASE                                                                    
% END LOOP                                                                      
%END SCRIPT                                                                     
