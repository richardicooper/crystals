%SCRIPT GFINDH
% VARIABLE LOGICAL XGRETURN
%% The variables needed by XQUESTIO
% VARIABLE CHARACTER QTITLE QLINE1 QLINE2 BUTTOK BUTTXX
% VARIABLE LOGICAL   ANSWER
%% Variable returned by assess5
% VARIABLE REAL HFIRST
%%
% VARIABLE LOGICAL TWINNED
% EVALUATE TWINNED = FALSE
%  BLOCK
%    VARIABLE INTEGER ITMP
%    IF ( EXISTS 13 .NE. 0 ) THEN
%        COPY '#GENERALEDIT 13'
%        COPY 'LOCATE RECORDTYPE=101'
%        COPY 'TRANSFER FROM OFFSET=1 TO ITMP'
%        IF ( ITMP .GE. 0 ) THEN
%         EVALUATE TWINNED = TRUE
%        ELSE
%         EVALUATE TWINNED = FALSE
%        END IF
%        COPY 'END'
%    END IF
%  END BLOCK
%%
 
 * This SCRIPT permits you to remove all existing hydrogen atoms before
   starting the placement.
 * It computes the positions of hydrogen atoms on carbon atoms but does
   not include them in the structure factor calculation.
 * It then searches to confirm them in a difference map. Calculated (H)
   and found (QH) atoms are displayed, and the model may be edited.
 * Finally, any peaks close to a calculated H atom are eliminated, and
   any remaining peaks deleted.

% COPY '#SCRIPT ASSESS5'
% IF ( HFIRST .GT. 0.0 ) THEN
%   EVALUATE QTITLE = 'Delete Hydrogen'
%   EVALUATE QLINE1 = 'Do you want to delete'
%   EVALUATE QLINE2 = 'existing hydrogen atoms?'
%   EVALUATE BUTTOK = '&No'
%   EVALUATE BUTTXX = '&Yes'
%   COPY '#SCRIPT XQUESTIO'
%   IF ANSWER .EQ. FALSE THEN
%      COPY '#EDIT'
%      COPY 'SELECT TYPE NE H'
%      COPY 'END'
%   END IF
% END IF
%%
% COPY '#SCRIPT XDELQ'
% COPY '#SCRIPT VALID12'
% COPY '#SFLS'
% IF TWINNED .EQ. TRUE THEN
%    COPY 'CALC /FO/=SCALED-/FOT/'
% ELSE
%    COPY 'CALC'
% END IF
% COPY 'END'
% COPY '#PERHYDRO'
% COPY 'ACTION BOTH'
% COPY 'END'
%%
% COPY '#FOUR'
% COPY 'MAP TYPE=DIFF'
% COPY 'PEAK HEIGHT=1'
% COPY 'END'
% COPY '#COLL 10 5'
% COPY 'SEL TYPE=PEAK'
% COPY 'END'
%%
% COPY '#EDIT'
% COPY 'TYPECHANGE TYPE EQ Q QH'
% COPY 'END'
^^CO SET _MODBAR ITEM _MBALL STATE ON ITEM _MADP STATE OFF
^^CO SET MODEL01 RADTYPE=COV RADSCALE=250
%%
% IF ( XGRETURN ) THEN
^^WI  WINDOW CONTINUE 'Continue' POSITION BELOW XGUIDER
% ELSE
^^WI  WINDOW CONTINUE 'Continue' POSITION CASCADE '_MAINTEXTOUTPUT'
% END IF
^^WI COMMIT='BOK' CANCEL='BXX' DISABLEIF='IN'
^^WI GRID MAIN NROWS=3 NCOLS=5
^^WI {
^^WI   @ 2,2 GRID L NROWS=5 NCOLS=1
^^WI   {
^^WI     @ 1,1 STATIC T0 'Rename QH peaks in good positions as'
^^WI     @ 2,1 STATIC T1 'H atoms. You may use CAMERON to do this'
^^WI     @ 3,1 STATIC T2 'if you prefer. Then click "Continue".'
^^WI     @ 4,1 CHECKBOX FOUREF
^^WI       'Advanced: Fourier refinement using peaks' STATE=OFF
^^WI     @ 5,1 CHECKBOX AHFOUND
^^WI       'All H atoms have been found' STATE=OFF
^^WI   }
^^WI   @ 2,4 GRID R NROWS=3 NCOLS=1
^^WI   {
% IF ( XGRETURN ) THEN
^^WI     @ 1,1 GRID R NROWS=3 NCOLS=1 COMMAND='#SCRIPT GFINH_NAUTO'
% ELSE
^^WI     @ 1,1 GRID RT NROWS=3 NCOLS=1 COMMAND='#SCRIPT GFINH_NFIND'
% END IF
^^WI     {
^^WI       @ 1,1 BUTTON BOK '&Continue'
^^WI       @ 3,1 BUTTON BXX '&Cancel'
^^WI     }
^^WI     @ 3,1 GRID RB NROWS=1 NCOLS=1 COMMAND='#SCRIPT XRETYPE'
^^WI     {
^^WI       @ 1,1 BUTTON NULL '&Rename'
^^WI     }
^^WI   }
^^WI }
^^WI SHOW
^^CR
 
 * Rename any QH peaks that you wish to keep to 'H'.

 {2,15 Move the floating window out of the way of the model.
 
 {2,15 The WHITE atoms are geometrically placed on the carbon and nitrogen
 {2,15 skeleton, and the PINK atoms have been found in a Fourier map.

 {2,15 Where the atoms almost co-incide, you can get a qualitative idea
 {2,15 of the quality of your data.

 {2,15 Where hydrogens have not been geometrically placed, you can
 {2,15 change the PINK atom to a WHITE hydrogen, by clicking on it
 {2,15 with the RIGHT mouse button and choosing "Change Type" from
 {2,15 the menu.

 {2,15 When you are finished, click "Continue" in the floating window.

% COPY '#SCRIPT DIRECT'
%END SCRIPT
