%SCRIPT XPUTINFO
%%
%% Store, in LIST 39, information about whether:
%%    H have all been found       (HDONE)
%%    Extinction has been checked (EDONE)
%%    Weights have been chosen    (WDONE)
%%
%  VARIABLE LOGICAL HMASK EMASK WMASK PMASK HRMASK L12SMASK
%  VARIABLE LOGICAL HDONE EDONE WDONE PDONE
%  VARIABLE INTEGER HRTYP L12SER
%  VARIABLE INTEGER IKEY
%%

 * Storing info in L39.
%  IF EXISTS 39 .LT. 1 THEN
 * Creating new L39.
%      COPY '#LIST 39'
%      COPY 'END'
%  END IF
%  VARIABLE INTEGER I1
%  VARIABLE LOGICAL DOINSERT
%  COPY '#GENERALEDIT 39'
%  COPY 'LOCATE RECORDTYPE = 101'
%  COPY 'KEY OFFSET = 0 LENGTH = 1 MODE = CHARACTER'
%  COPY 'GROUP MODE = INTEGER'
%  COPY 'RECORD MODE = INTEGER'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%%
%  EVALUATE DOINSERT = TRUE
%  BLOCK
%    ON EDITERROR TERMINATE
%    COPY 'CHECK'
%    COPY 'SEARCH CHAR = INFO'
%    EVALUATE DOINSERT = FALSE
%  END BLOCK
%  IF DOINSERT THEN
 * Inserting new key "INFO 0"
%     COPY 'INSERT'
%  END IF
%  COPY 'CHANGE 0 CHAR CHAR = INFO'
%  COPY 'CHANGE 1 INTEGER INTEGER = 0'
%  IF ( HMASK .EQ. TRUE ) THEN
%    IF ( HDONE .EQ. TRUE ) THEN
%      COPY 'CHANGE 2 INTEGER INTEGER = 1'
  Storing: All H placed.
%    ELSE
%      COPY 'CHANGE 2 INTEGER INTEGER = 0'
  Storing: All H not placed.
%    END IF
%  END IF
%  IF ( EMASK .EQ. TRUE ) THEN
%    IF ( EDONE .EQ. TRUE ) THEN
%      COPY 'CHANGE 3 INTEGER INTEGER = 1'
  Storing: Extinction checked.
%    ELSE
%      COPY 'CHANGE 3 INTEGER INTEGER = 0'
  Storing: Extinction not checked.
%    END IF
%  END IF
%  IF ( WMASK .EQ. TRUE ) THEN
%    IF ( WDONE .EQ. TRUE ) THEN
%      COPY 'CHANGE 4 INTEGER INTEGER = 1'
  Storing: Weights applied.
%    ELSE
%      COPY 'CHANGE 4 INTEGER INTEGER = 0'
  Storing: Weights not applied.
%    END IF
%  END IF
%  IF ( PMASK .EQ. TRUE ) THEN
%    IF ( PDONE .EQ. TRUE ) THEN
%      COPY 'CHANGE 5 INTEGER INTEGER = 1'
  Storing: Purged list 11.
%    ELSE
%      COPY 'CHANGE 5 INTEGER INTEGER = 0'
  Storing: 11 not purged.
%    END IF
%  END IF
%  IF ( HRMASK ) THEN
%    CLEAR
%    INSERT 'CHANGE 6 INTEGER INTEGER = '
%    STORE FORMAT /(I4)/ LENGTH 4 INTEGER HRTYP
%    SEND
  Storing: LS Hydrogen treatment preference.
%  END IF
%  IF ( L12SMASK ) THEN
%    CLEAR
%    INSERT 'CHANGE 7 INTEGER INTEGER = '
%    STORE FORMAT /(I6)/ LENGTH 6 INTEGER L12SER
%    SEND
  Storing: Refinement directive list serial number.
%  END IF
%%
%  COPY 'WRITE'
%  COPY 'END'
 * Done.
%END SCRIPT
