%SCRIPT XGUIDEI
%%
%% Decide on a course of action.
%% Display non-modal window with available options. 
%% Exit. - The window will callback XGUIDEIC.SCP with events.
%%
%  VARIABLE INTEGER KTDEFA
%% KTDEFA  = 
%%  1 Run KCCDIN
%%  2 Run RC93
%%  3 Run DIPIN
%%  4 Inport KCCDIN output.
%%  5 Import RC93 output
%%  6 Import DIPIN output.
%%  7 Import SHELX data.
%%  8 Input cell
%%  9 Input cell contents
%%  10 Input space group.
%%  11 Input reflection data.
%%  12 Edit CIF type data
%%  13 Read in CRYSTALS instructions.
%%%
%%
%% Check for windows already open
%%
^^?? XGUIDER EXISTS
%  VERIFY TRUE FALSE
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  IF VALUE .EQ. 1  THEN
^^CO DISPOSE XGUIDER
%  END IF
^^?? XGUIDES EXISTS
%  VERIFY TRUE FALSE
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  IF VALUE .EQ. 1  THEN
^^CO DISPOSE XGUIDES
%  END IF
^^?? XGUIDEI EXISTS
%  VERIFY TRUE FALSE
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  IF VALUE .EQ. 2  THEN
%    CLEAR
%    ON ERROR TERMINATE
%%
^^WI WINDOW XGUIDEI 'Supervisor' POSITION CASCADE '_MAINTEXTOUTPUT'
^^WI COMMAND='#SCRIPT XGUIDEIC' DISABLEIF='IN' CANCEL='GI_BXX'
^^WI GRID HO NROWS=5 NCOLS=3
^^WI {
^^WI  @ 2,2 GRID OUTER NROWS=1 NCOLS=5
^^WI  {
^^WI   @ 1,1 BITMAP GUIDEB FILE 'guide1.bmp'
^^WI   @ 1,3 LISTBOX AUTOLIST VISLINES=11 ADDTOLIST
^^WI     'Run KCCDIN'
^^WI     'Run RC93'
^^WI     'Run DIPIN'
^^WI     'Use KCCDIN output'
^^WI     'Use RC93 output'
^^WI     'Use DIPIN output'
^^WI     'Import SHELX data'
^^WI     'Input cell'
^^WI     'Input formula'
^^WI     'Input spacegroup'
^^WI     'Input reflections'
^^WI     'Edit extra info'
^^WI     'Read from file'
^^WI     NULL
^^WI   @ 1,5 GRID BTNS NROWS=6 NCOLS=1
^^WI   {
^^WI     @ 1,1 BUTTON GI_BOK '&Ok'
^^WI     @ 4,1 BUTTON GI_BXX '&Hide' 
^^WI     @ 6,1 BUTTON GI_BHELP '&Help'
^^WI   }
^^WI  }
^^WI  @ 4,2 STATIC SUPT
^^WI 'You can continuing working while this window is open.'
^^WI }
^^WI SHOW
^^CR
%  END IF

{I   Choose an option. The following data is still required:

%% List missing data reqd before leaving IMPORT phase of GUIDE.
% IF EXISTS ( 6 ) .LT. 1 THEN
{I   Reflection data is still required.
% ELSE IF EXISTS ( 1 ) .LT. 1 THEN
{I   Cell dimension data is still required.
% ELSE IF EXISTS ( 2 ) .LT. 1 THEN
{I   Spacegroup data is still required.
% ELSE IF EXISTS ( 3 ) .LT. 1 THEN
{I   Cell contents (molecular formula) data is still required.
% END IF
% END SCRIPT
%%
%%  $Log: not supported by cvs2svn $
%%  Revision 1.2  2001/03/09 18:01:26  richard
%%  Removed default actions for buttons, as these are now non-modal windows.
%%  They were interfering with the user pressing RETURN in other scripts, while
%%  the windows were open.
%%
%%  Revision 1.1  2001/03/08 14:00:22  richard
%%  New GUIDE scripts. The main script, xguide, decides on the overall stage
%%  and calls one of xguidei - for data import, xguides - for solving or
%%  xguider - for refinement. These display a window recommending a course of
%%  action and exit - thus allowing the user access to the full range of CRYSTALS
%%  commands even while being guided. The window knows to call one of xguideic, xguidesc,
%%  or xguiderc respectively when the user does something to it e.g. pushes a button.
%%
