%SCRIPT XGUIDEI
%%
%% Decide on a course of action.
%% Display non-modal window with available options. 
%% Exit. - The window will callback XGUIDEIC.SCP with events.
%%
%  VARIABLE CHARACTER INSTITLE
%  VARIABLE INTEGER KTDEFA
%% KTDEFA  = 
%%  1 Run KCCDIN
%%  2 Run RC93
%%  3 Run DIPIN
%%  4 Import KCCDIN output.
%%  5 Import RC93 output
%%  6 Import DIPIN output.
%%  7 Import SHELX data.
%%  8 Input reflection data.
%%  9 Input cell
%%  10 Input cell contents
%%  11 Input space group.
%%  12 Input CIF file
%%  13 Edit CIF type data
%%  14 Read in CRYSTALS instructions.
%% added 2011
%%  15 Read in generalised diffractometer cifs
%%%
%%
%% Check for windows already open
%%
^^?? XGUIDER EXISTS
%  VERIFY TRUE FALSE
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  IF VALUE .EQ. 1  THEN
^^CO DISPOSE XGUIDER
%  END IF
^^?? XGUIDES EXISTS
%  VERIFY TRUE FALSE
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  IF VALUE .EQ. 1  THEN
^^CO DISPOSE XGUIDES
%  END IF
^^?? XGUIDEI EXISTS
%  VERIFY TRUE FALSE
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  IF VALUE .EQ. 2  THEN
%    CLEAR
%    ON ERROR TERMINATE
%%
^^WI WINDOW XGUIDEI 'The GUIDE' POSITION CASCADE '_MAINTEXTOUTPUT'
^^WI COMMAND='#SCRIPT XGUIDEIC' DISABLEIF='IN' CANCEL='GI_BXX'
^^WI GRID HO NROWS=3 NCOLS=5
^^WI {
^^WI  @ 2,2 GRID BM NROWS=1 NCOLS=1
^^WI  {
^^WI    @ 1,1 BITMAP GUIDEB TRANS FILE 'guide1.bmp'
^^WI  }
^^WI  @ 2,4 GRID MID NROWS=3 NCOLS=3
^^WI  {
^^WI   @ 1,1 DROPDOWN _XG_LIST ADDTOLIST
^^WI     'Run KCCDIN'
^^WI     'Run RC93'
^^WI     'Run DIPIN'
^^WI     'Use KCCDIN output'
^^WI     'Use RC93 output'
^^WI     'Use DIPIN output'
^^WI     'Import SHELX data (ins,res)'
^^WI     'Import reflections (hkl)'
^^WI     'Input cell parameters'
^^WI     'Input molecular formula'
^^WI     'Input spacegroup'
^^WI     'Input CIF data'
^^WI     'Edit extra (CIF) info'
^^WI     'Read from file (crystals)'
^^WI     'Read Diffractometer cif'
^^WI     NULL
^^WI   @ 3,1 GRID BTNS NROWS=1 NCOLS=4
^^WI   {
^^WI     @ 1,1 STRETCH _H HORIZONTAL
^^WI     @ 1,4 BUTTON GI_BHELP '&Help' DISABLEIF='IN'
^^WI   }
^^WI   @ 1,3 BUTTON GI_BOK '&Ok' DISABLEIF='IN' DEFAULT COMMIT
^^WI   @ 3,3 BUTTON GI_BXX '&Hide' DISABLEIF='IN' CANCEL
^^WI  }
% TRANSFER "^^WI @ 3,5 HIDDENSTRING INSTTL '" // INSTITLE // "'" TO DISPLAY
^^WI }
^^WI SHOW
^^CR
%  END IF
%  TRANSFER "^^CO SET INSTTL TEXT '" // INSTITLE // "'" TO DISPLAY
%  EVALUATE KTDEFA = 1
%% List missing data reqd before leaving IMPORT phase of GUIDE.
% IF FILEEXISTS ( 'kccd.ins' ) THEN
{I   KCCDIN output file detected.
%    EVALUATE KTDEFA = 4
% ELSE IF FILEEXISTS ( 'import.cif' ) THEN
{I   KCCDIN input file detected.
%    EVALUATE KTDEFA = 1
% ELSE IF EXISTS ( 1 ) .LT. 1 THEN
{I   Cell dimension data is still required.
%    EVALUATE KTDEFA = 7
% ELSE IF EXISTS ( 6 ) .LT. 1 THEN
{I   Reflection data is still required.
%    EVALUATE KTDEFA = 8
% ELSE IF EXISTS ( 2 ) .LT. 1 THEN
%    EVALUATE KTDEFA = 11
{I   Spacegroup data is still required.
% ELSE IF EXISTS ( 3 ) .LT. 1 THEN
%    EVALUATE KTDEFA = 10
{I   Cell contents (molecular formula) data is still required.
% END IF
%%
%%
% CLEAR
% INSERT '^^CO SET _XG_LIST SELECTION='
% STORE FORMAT /(I2)/ LENGTH 2 INTEGER KTDEFA
% OUTPUT
^^CO FOCUS XGUIDEI
%END SCRIPT
%%
%%  $Log: not supported by cvs2svn $
%%  Revision 1.14  2011/03/22 14:41:54  rich
%%  Added default and focus.
%%
%%  Revision 1.13  2011/02/04 17:21:05  djw
%%  Add Agilent to know instruments
%%
%%  Revision 1.12  2009/12/07 10:59:45  djw
%%  Attempt to make cif2cry calable from the GUIDE.  The call to SCRIPT CIFPROC from inside cif2cry.fpp throws up errors which I cannot track down. Menu item just generates a message for the moment
%%
%%  Revision 1.11  2005/01/23 10:00:25  rich
%%  Reinstate CVS history for scripts. History from Dec23 - Jan23 is lost, but
%%  changes are not!
%%
%%  Revision 1.1.1.1  2004/12/13 11:16:23  rich
%%  New CRYSTALS repository
%%
%%  Revision 1.10  2004/10/19 15:53:57  rich
%%  Default guide to Run KCCDIN if import.cif detected.
%%
%%  Revision 1.9  2004/09/28 09:35:32  rich
%%  Removed redundant output.
%%
%%  Revision 1.8  2002/05/15 08:02:56  richard
%%  Little update using new HIDDENSTRING GUI control - the stem of the .ins
%%  filename is remembered and used for the default .hkl filename if the file
%%  exists.
%%
%%  Revision 1.7  2002/03/21 17:56:19  richard
%%  Enclose bitmaps in GRID object to stop them stretching.
%%
%%  Revision 1.6  2001/11/16 15:28:18  ckp2
%%  Add a bit more intelligence to the import guide to help poor old SHELX users.
%%
%%  Revision 1.5  2001/04/03 09:52:17  richard
%%  Changed name from 'Supervisor' to 'The GUIDE' for consistency.
%%
%%  Revision 1.4  2001/03/21 17:07:16  richard
%%  Changed window layout and bitmap.
%%
%%  Revision 1.3  2001/03/13 09:36:46  richard
%%  Re-instated cancel action. Made cancel action the default in the xguide?c scripts.
%%
%%  Revision 1.2  2001/03/09 18:01:26  richard
%%  Removed default actions for buttons, as these are now non-modal windows.
%%  They were interfering with the user pressing RETURN in other scripts, while
%%  the windows were open.
%%
%%  Revision 1.1  2001/03/08 14:00:22  richard
%%  New GUIDE scripts. The main script, xguide, decides on the overall stage
%%  and calls one of xguidei - for data import, xguides - for solving or
%%  xguider - for refinement. These display a window recommending a course of
%%  action and exit - thus allowing the user access to the full range of CRYSTALS
%%  commands even while being guided. The window knows to call one of xguideic, xguidesc,
%%  or xguiderc respectively when the user does something to it e.g. pushes a button.
%%
