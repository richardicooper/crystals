%SCRIPT CHANGEATOMTYPE
%%
%% The atom name passed by the calling script is ATOM
%%
%  VARIABLE CHARACTER ATOM
%%
%  VARIABLE CHARACTER CTITLE CPOSITION CLTYPE
%  VARIABLE INTEGER ISEL
%  VARIABLE LOGICAL CALLED LDEBUG
%%
%% Variables to call yretype.scp
%%
%  VARIABLE CHARACTER CATOM CTYPE
%%
%  EVALUATE CTITLE = "Change atom type"
%  EVALUATE CPOSITION = "=CENTERED '_MAINTEXTOUTPUT'"
%%
%  EVALUATE LDEBUG = TRUE
%  EVALUATE LDEBUG = FALSE
%%
%% Now the selection process starts
%%
^^WI WINDOW XCA
% TRANSFER "^^WI '" // CTITLE // "'" TO DISPLAY
^^WI MODAL
% TRANSFER "^^WI POSITION" // CPOSITION TO DISPLAY
^^WI GRID M NROWS=5 NCOLS=3 {
^^WI   @ 2,2 GRID B NROWS=1 NCOLS=3 {
^^WI     @ 1,1 STATIC T1 'Atom to change type:'
% TRANSFER "^^WI     @ 1,3 STATIC T2 '" // ATOM // " '" TO DISPLAY
^^WI   }
^^WI   @ 4,2 GRID B NROWS=1 NCOLS=3 {
^^WI     @ 1,1 DROPDOWN DDCA INFORM=YES ADDTOLIST
%%
%  COPY '#GENERALEDIT 3'
%  COPY 'LOCATE RECORDTYPE=101'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  LOOP
%    ON EDITERROR TERMINATE
%    COPY 'TRANSFER FROM OFFSET=0 TO CLTYPE'
%    TRANSFER '^^WI ' // CLTYPE TO DISPLAY
%    COPY 'NEXT'
%  END LOOP
%  COPY 'END'
%%
^^WI     'NULL'
^^WI     @ 1,2 STRETCH S1 HORIZONTAL
^^WI     @ 1,3 BUTTON BCL '&Cancel' DEFAULT COMMIT CANCEL
^^WI   }
^^WI }
^^WI SHOW
^^CR
%%
%  VERIFY DDCA BCL
%  GET SILENT FINAL NOSTORE ABBREVIATED ' ' ' '
%  CASE VALUE
%%
%    BLOCK        % DDCA - dropdown
%%
%%     TRANSFER "{I DD activated..." TO DISPLAY
%      GET SILENT NOSTORE INTEGER 'New value' '0'
%      EVALUATE ISEL = VALUE
%%
%      COPY '#GENERALEDIT 3'
%      COPY 'LOCATE RECORDTYPE=101'
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%      LOOP ISEL - 1 TIMES
%        ON EDITERROR TERMINATE
%        COPY 'NEXT'
%      END LOOP
%      COPY 'TRANSFER FROM OFFSET=0 TO CLTYPE'
%      COPY 'END'
%%
%      EVALUATE CALLED = TRUE
%      EVALUATE CATOM = ATOM
%      EVALUATE CTYPE = CLTYPE
%      QUEUE REWIND
%      COPY '#SCRIPT YRETYPE'
%      COPY '#EDIT'
%      COPY 'MONITOR OFF'
%      QUEUE PROCESS
%      COPY 'END'
%%
^^CO   DISPOSE XCA
%      FINISH
%%
%    END BLOCK
%%
%    BLOCK        % BCL - Cancel
%%
^^CO   DISPOSE XCA
%      FINISH
%%
%    END BLOCK
%%
%  END CASE
%%
%END SCRIPT