%SCRIPT INCAD4
%% CRYSTALS CAD4 reflection input script
%%
% VARIABLE INTEGER REDRAW OLDPROG SIMPLE
% EVALUATE REDRAW = 1
%  LOOP
%  ON END TERMINATE
%  IF REDRAW .EQ. 1 THEN
^^WI WINDOW INCAD4 'Input CAD4 data' MODAL
^^WI GRID MAIN NROWS=2 NCOLS=1
^^WI {
^^WI  @ 1,1 GRID UPPER NROWS=2 NCOLS=3
^^WI  {
^^WI   @ 2,2 STATIC TITLE 'Read in data from a CAD4 Diffractometer'
^^WI  }
^^WI  @ 2,1 GRID LOWER NROWS=2 NCOLS=2
^^WI  {
^^WI   @ 1,1 GRID LLEFT NROWS=6 NCOLS=2
^^WI   {
^^WI    @ 2,2 GRID BOX1 NROWS=2 NCOLS=3 OUTLINE='Input mode'
^^WI    {
^^WI     @ 1,2 RADIOBUTTON XSIMPLE 'Simple default' STATE=ON IGNORE
^^WI     @ 2,2 RADIOBUTTON XDETAIL 'Detailed questions' STATE=OFF IGNORE
^^WI    }
^^WI    @ 4,2 GRID BOX2 NROWS=2 NCOLS=2 OUTLINE='Program used' 
^^WI    {
^^WI     @ 1,2 RADIOBUTTON XRC85 'RC-85' STATE=OFF IGNORE
^^WI     @ 2,2 RADIOBUTTON XRC93 'RC-93' STATE=ON IGNORE
^^WI    }
^^WI   }
^^WI   @ 1,2 GRID LRIGH NROWS=6 NCOLS=3
^^WI   {
^^WI    @ 2,2 BUTTON BUTTONOK '&Ok' DEFAULT
^^WI    @ 4,2 BUTTON BUTTONXX '&Cancel'
^^WI   }
^^WI  }
^^WI }
^^WI SHOW
^^CR
%    EVALUATE REDRAW = 0
%  END IF
%  VERIFY BUTTONOK BUTTONXX
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  CASE VALUE
%       BLOCK     %BUTTONOK%
%% XSimple state
^^WI            GETVALUE XSIMPLE
^^CR
%       VERIFY ON OFF
%       GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%       CASE VALUE
%           BLOCK    %ON%
%               EVALUATE SIMPLE = 1
%           END BLOCK
%           BLOCK    %OFF%
%               EVALUATE SIMPLE = 0
%           END BLOCK
%       END CASE
%% XRC85 state
^^WI        GETVALUE XRC85
^^CR
%       VERIFY ON OFF
%       GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%       CASE VALUE
%           BLOCK    %ON%
%               EVALUATE OLDPROG = 1
%           END BLOCK
%           BLOCK    %OFF%
%               EVALUATE OLDPROG = 0
%           END BLOCK
%       END CASE
^^WI            DISPOSE INCAD4
^^CR
%%%%%%
%       IF SIMPLE .EQ. 0 THEN
%           COPY '#SCRIPT CADSLOW'
%       ELSE IF OLDPROG .EQ. 0 THEN
%           COPY '#SCRIPT XINRC93'
%       ELSE
%           COPY '#SCRIPT CADQUICK'
%       END IF
%       FINISH
%   END BLOCK
%   BLOCK     %BUTTONXX%
^^WI            DISPOSE INCAD4
^^CR
%       FINISH
%   END BLOCK
%  END CASE
%  END LOOP
%END SCRIPT
