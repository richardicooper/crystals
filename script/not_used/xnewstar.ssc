%SCRIPT NEWSTART
% VARIABLE LOGICAL SCRIPTEXT
% VARIABLE CHARACTER COMMAND
% IF SCRIPTEXT .EQ. TRUE THEN
 
  CRYSTALS has powerfull reflection processing instructions, able to use
  data in many formats, from more than one crystal, suffering from decay
  and from twinned crystals. To make use of these facilities, you must
  read the printed manual.
  This SCRIPT processes data prepared by the CAD4 data preprocessor RC85
  in the 'standard' SHELX format, and most other formats that contain h,k
  l and structure factor information. It handles F, F**2 and I data.
 
% ELSE
Script for starting on a new structure
% END IF
%LOOP
% VARIABLE CHARACTER QTITLE BUTTOK BUTTXX
% VARIABLE INTEGER QNLINES QNITEMS QVLINES
% VARIABLE LOGICAL QANSWER
% VARIABLE CHARACTER QLINE1 QLINE2 QLINE3
% VARIABLE CHARACTER QLIST1 QLIST2 QLIST3 QLIST4 QLIST5
% VARIABLE CHARACTER QLIST6 QLIST7 QLIST8 QLIST9 QLIST10
% VARIABLE CHARACTER QHELP1 QHELP2 QHELP3 QHELP4 QHELP5
% VARIABLE CHARACTER QHELP6 QHELP7 QHELP8 QHELP9 QHELP10
% VARIABLE INTEGER   QIANSWER
%%
%% The title of the script box
% EVALUATE QTITLE = 'New Start'
%%
%% The button text
% EVALUATE BUTTOK = '&Ok'
% EVALUATE BUTTXX = '&Cancel'
%%
%% The text to appear in the left pane of the box
% EVALUATE QLINE1 = 'Choose a source'
% EVALUATE QLINE2 = 'of data input.'
% EVALUATE QNLINES = 2
%%
%% The items to appear in the listbox
% EVALUATE QLIST1 = 'Cad 4'
% EVALUATE QLIST2 = 'DIP 2000'
% EVALUATE QLIST3 = 'Shelx file'
% EVALUATE QLIST4 = 'Y290'
% EVALUATE QLIST5 = 'Initial'
% EVALUATE QLIST6 = 'Data'
% EVALUATE QLIST7 = 'Modify'
% EVALUATE QLIST8 = 'Look'
%%
%% The help text for each item in the box
% EVALUATE QHELP1 = 'Input CAD4 data from RC93 program.       '
% EVALUATE QHELP2 = 'Input data from DIP 2000.'
% EVALUATE QHELP3 = 'Input data in SHELX format.'
% EVALUATE QHELP4 = 'Hilger and Watts Nottingham format.'
% EVALUATE QHELP5 = 'Generalised startup for non-RC93'
% EVALUATE QHELP6 = 'General data input script.'
% EVALUATE QHELP7 = 'Modify existing lists.'
% EVALUATE QHELP8 = 'Examine existing lists.'
%%
%% The number of visible lines in the box at one time
% EVALUATE QVLINES = 8
%%
%% The total number of items in the list
% EVALUATE QNITEMS = 8
%%
%% Run the script
% COPY '#SCRIPT XSELECT'
%%
%% On return QANSWER is TRUE if BUTTOK or return was pressed
%% QANSWER is FALSE if BUTTXX or ESC was pressed.
%% QIANSWER holds the index of the list item selected (NB starting at 1).
%% QCANSWER holds the text of the button that was selected.
% IF QANSWER .EQ. TRUE THEN
%       COPY '#DISK'
%       COPY 'EXTEND SIZE=10 TRIES=1000'
%       COPY 'END'
Files INITIAL.* will be created in your directory. They may be important.
%       COPY '#RELEASE PRINT initial.lis'
%       COPY '#RELEASE LOG INITIAL.DAT'
%       COPY '#LIST 28'
%       COPY 'END'
%       COPY '#LIST 4'
%       COPY 'END'
%   CASE QIANSWER
%       EVALUATE COMMAND = '#SCRIPT XINCAD4'
%       EVALUATE COMMAND = '#SCRIPT DIPIN'
%       EVALUATE COMMAND = '#SCRIPT QUICKST'
%       EVALUATE COMMAND = '#SCRIPT INY290'
%       EVALUATE COMMAND = '#SCRIPT INITIAL'
%       EVALUATE COMMAND = '#SCRIPT DATA'
%       EVALUATE COMMAND = '#SCRIPT MODIFY'
%       EVALUATE COMMAND = '#SCRIPT LOOK'
%   END CASE
% ELSE
%   FINISH
% END IF
%%
%% For look and modify, repeat this script.
%% For a proper input, end the script.
% IF ( QIANSWER .EQ. 7 ) .OR. ( QIANSWER .EQ. 8 ) THEN
%   TRANSFER COMMAND TO CRYSTALS
% ELSE
%       EVALUATE LOOPEND = LOOPCOUNTER
% END IF
%%
%END LOOP
%%
%% This has been moved out of the main loop to try to cut
%% down the number of stack frames in use at once.
%%
%  TRANSFER COMMAND TO CRYSTALS
%%
%END SCRIPT
