%SCRIPT AUTO12
% VARIABLE LOGICAL SCRIPTEXT
% IF SCRIPTEXT .EQ. TRUE THEN
 
 This SCRIPT leads you through least squares refinement.
 It assumes the structure is small enough for full matrix refinement
 and that there are no problems with disorder etc.
 It chooses refinement types only on the basis of R factor
 
% ELSE
            a SCRIPT to help set up refinement directives.
It may sort LIST 5 so that hydrogen atoms come after all other types of
                   atom in the atom list.
% END IF
%  VARIABLE LOGICAL EXTINCT PURGE
%  VARIABLE INTEGER ADP
%  EVALUATE EXTINCT = FALSE
%  EVALUATE PURGE  = FALSE
%  VARIABLE CHARACTER CLAST CPARAM CPARAM2
%
%  VARIABLE LOGICAL OPTIM HPOS
%  EVALUATE OPTIM = FALSE
%  EVALUATE HPOS = FALSE
%  VARIABLE INTEGER ISTAGE
%        EVALUATE ISTAGE = 0
%%
%% R-FACTOR THRESHOLDS FOR VARIOUS CASES
%  VARIABLE REAL R1 R2 R3 R4 RMIN
%% Fourier might be better
%  EVALUATE R1 = 40.0
%% Isotropic refinement
%  EVALUATE R2 = 20.0
%% Aniso
%  EVALUATE R3 = 10.0
%% Tidy up
%  EVALUATE R4 = 6.0
%%
%%
%% R, OSCALE, RATIO are set in SCRIPT RFACTOR
%  VARIABLE REAL R OSCALE RATIO
%  VARIABLE CHARACTER CRVAL
%  VARIABLE REAL ELECTRON
%% make sure we have a valid LIST 12 of some sort
%% and find the last non- hydrogen atom
%  COPY '#SCRIPT CHECK5'
%  COPY '#SCRIPT CHECK12'
%% try to guess what stage we are at
%% ISTAGE 1 - X'S
%% ISTAGE 2 - X'S, U[ISO]
%% ISTAGE 3 - X'S, U'S
%  EVALUATE ISTAGE = ADP + 1
% LOOP
%    COPY '#SCRIPT RFACTOR'
%    IF RATIO .GE. 1.3 THEN
%% we need the R factor on a good scale
       Recomputing over-all scale factor and phases
%      COPY '#SFLS'
%      COPY 'SCALE'
%      COPY 'END'
%      COPY '#SFLS'
%      COPY 'SCALE'
%      COPY 'END'
%      COPY '#SCRIPT RFACTOR'
%    END IF
%    EVALUATE RMIN = R
%    CLEAR
%    ON ERROR TERMINATE
%%
%    CLEAR
%    IF R .GT. R1 THEN
 Large R factor - It might be best to do Fourier refinement, otherwise
 only refine positions.
%      VERIFY YES NO
%      GET NOSTORE FINAL ABBREVIATED  -
  'Do you want to do Fourier refinement' 'YES'
%      IF VALUE .EQ. 1 THEN
%        COPY '#SCRIPT AFOUR'
%        EVALUATE CPARAM = " X'S  "
%      END IF
%      EVALUATE ISTAGE = 1
%    ELSE IF ( R .GT. R2 ) .OR. ( ISTAGE .LE. 1 ) THEN
     Medium R factor - Refine positions and U[iso]
%      COPY '#EDIT'
%      COPY 'MONITOR OFF'
%%     Just in case we get here from aniso
%      COPY 'UEQUIV FIRST UNTIL LAST'
%      COPY 'MONITOR MEDIUM'
%      COPY 'END'
%      EVALUATE CPARAM = " X'S, U[ISO] "
%      EVALUATE ISTAGE = 2
%%
%    ELSE IF ( R .GT. R3 ) .AND. ( ISTAGE .LE. 2 ) THEN
     Low R factor - Refine positions and Uaniso
%      EVALUATE CPARAM = " X'S, U'S "
%      EVALUATE ISTAGE = 3
%%
%    ELSE
%      IF ( R .GT. R3 ) THEN
      R factor is large after aniso refinement. Perhaps there is missing
      solvent, disorder or some other problem. CRYSTALS will try an
      extinction correction, but it may make matters worse.
%        EVALUATE CPARAM2 =  ' EXTPARAM'
%        EVALUATE CPARAM = " X'S, U'S "
%        EVALUATE EXTINCT = TRUE
%      ELSE
          Very low R factor - Begin to tidy up
%      END IF
%      IF HPOS .EQ. FALSE THEN
%        VERIFY YES NO
%        GET NOSTORE FINAL ABBREVIATED  -
  'Do you want to try to locate Hydrogen atoms?' 'YES'
%        IF VALUE .EQ. 1 THEN
%          COPY '#SCRIPT FINDH'
%          COPY '#SCRIPT CHECK12'
%          VERIFY YES NO
%          GET NOSTORE FINAL ABBREVIATED  -
 'Have all H atoms been located?' 'YES'
Remember - the data may not be good enough to reveal all H atoms
%          IF VALUE .EQ. 1 THEN
%            EVALUATE HPOS = TRUE
%          END IF
%          COPY '#SFLS'
%          COPY 'CALC'
%          COPY 'END'
%          EVALUATE CPARAM = " X'S, U'S "
%        END IF
%      ELSE
%        IF EXTINCT .EQ. FALSE THEN
 You will be shown an analysis of residuals.
 
 If the entries near the bottom of column 3 are generally lower than
    those in column 4, you need to refine an extinction parameter.
%          COPY '#PAUSE 5'
%          EVALUATE EXTINCT = TRUE
%          COPY '#ANALYSE'
%          COPY 'END'
%%        refine everything anyway
%          EVALUATE CPARAM = " X'S, U'S "
%          VERIFY YES NO
%          GET NOSTORE FINAL ABBREVIATED  -
          'Do you want to refine an extinction parameter?' 'YES'
%          IF VALUE .EQ. 1 THEN
%            EVALUATE CPARAM2 =  ' EXTPARAM'
%          END IF
%        END IF
%      END IF
%%
%    END IF
%    QUEUE REWIND
%    QUEUE COPY #LIST 12
%    CLEAR
%    STORE CHARACTER -
    'FULL ' // cparam2 // ' First(' // CPARAM // ') until '
%    QUEUE SEND
%    CLEAR
%    STORE CHARACTER "CONTINUE " // CLAST
%    QUEUE SEND
%    TRANSFER "END" TO CRYSTALS
%%
%    QUEUE COPY END
%    QUEUE PROCESS
%   COPY '#SUM LIST 12'
%   COPY 'END'
%   IF ( EXTINCT .EQ. TRUE ) .AND. ( PURGE .EQ. FALSE ) THEN
%      COPY '#DISK'
%      COPY 'DELETE 11'
%      COPY 'END'
%      COPY '#PURGE'
%      COPY 'END'
%     EVALUATE PURGE = TRUE
%   END IF
%   COPY '#LIST 22'
%   COPY 'END'
%%
%   IF ( OPTIM .EQ. FALSE  ) .AND. ( R .LE. R4 ) .AND. -
    ( EXTINCT .EQ. TRUE ) THEN
%     VERIFY YES NO
%     GET NOSTORE FINAL ABBREVIATED  -
      'Do you want to check the least squares weights?' 'YES'
%     IF VALUE .EQ. 1 THEN
%       COPY '#SCRIPT WEIGHTS'
%       EVALUATE OPTIM = TRUE
%     END IF
%   END IF
%   COPY '#SCRIPT REFINE'
%   COPY '#SCRIPT RFACTOR'
 Test for near convergence of the R factor
%   IF R .GE. RMIN * 0.99 THEN
%     VERIFY YES NO
%     GET NOSTORE FINAL ABBREVIATED  -
      'Do you want to continue refinement?' 'YES'
%     IF VALUE .EQ. 2 THEN
 You will be shown your structure again.  If you need to, edit it and
 continue refinement.
%       COPY '#SCRIPT PLOT'
 
%       VERIFY YES NO
%       GET NOSTORE FINAL ABBREVIATED  -
        'Do you want to continue refinement?' 'YES'
%       IF VALUE .EQ. 2 THEN
%         EVALUATE LOOPEND = LOOPCOUNTER
%       END IF
%     END IF
%   END IF
%   EVALUATE RMIN = R
% END LOOP
%END SCRIPT
