%SCRIPT XSHIFT                                                                  
%% Use the SG operators to try to centre each moiety separately                 
%%
%  VARIABLE LOGICAL TOOL-HELP
%  COPY '#SCRIPT TEST-TOOL-HELP'
%  VARIABLE CHARACTER BUTTOK BUTTXX 
%  VARIABLE LOGICAL RUNNOW
%  EVALUATE RUNNOW = FALSE  
% IF TOOL-HELP .EQ. TRUE THEN
^^WI WINDOW CENTRE 'Centre Structure' MODAL COMMIT='CFBOK' CANCEL='CFBXX'  
^^WI GRID GRIDM NROWS=3 NCOLS=3  
^^WI {  
^^WI   @ 2,2 GRID GB NROWS=1 NCOLS=5  
^^WI  {  
^^WI   @ 1,1 BUTTON CFBOK '&Continue' DEFAULT  
^^WI   @ 1,3 BUTTON CFBXX '&Cancel' 
^^WI   @ 1,5 BUTTON CFHELP 'Help' 
^^WI  }  
^^WI }  
^^WI SHOW  
^^CR  
%  LOOP 
%    VERIFY  CFBXX CFHELP CFBOK  
%    GET SILENT NOSTORE FINAL ABBREVIATED 'BXX?' 'BXX'
%    CASE VALUE  
%      BLOCK %CFBXX% 

{I Automatic centring cancelled
 
^^CO     DISPOSE CENTRE
%        FINISH  
%      END BLOCK  
%      BLOCK %CFHELP%
%       COPY '#SPAWN CRYSDIR:manual/structure/new-centre.html'
%      END BLOCK
%      BLOCK %CFBOK%  
%          EVALUATE RUNNOW = TRUE 
^^CO DISPOSE INVERT  
%          EVALUATE LOOPEND = LOOPCOUNTER  
%      END BLOCK  
%    END CASE  
%  END LOOP  
%%  
%  IF RUNNOW .EQ. FALSE THEN
%   FINISH
%  END IF
% END IF
%%
%  VARIABLE INTEGER RESIDUE OLDRESIDUE                                          
%  VARIABLE CHARACTER CTYPE CISER CLAST                                         
%       EVALUATE CLAST = " LAST"                                                
%       EVALUATE OLDRESIDUE = 0                                                 
%%                                                                              
%  IF EXISTS 5 .LE. 0 THEN                                                      
WARNING - You have no atoms                                                     
%    FINISH                                                                     
%  END IF
%% Do not continue if PARTS have been set
% BLOCK
%  VARIABLE INTEGER IPART
%  COPY '#GENERALEDIT 23'                                                        
%  COPY 'LOCATE RECORDTYPE = 101'                                                
%  COPY 'TRANSFER FROM OFFSET = 4 TO IPART'                                     
%  IF IPART .GE. 0 THEN

{I Remove Structure Factor A & B PARTS (LIST 23) before re-centring

%   FINISH
%  END IF
% END BLOCK
%                                                                               
%% MOVE TO MAKE FIRST ATOM CLOSE TO .5 .5 .5                                    
% COPY '#ORIGIN MODE=FIRST'                                                     
% COPY 'END'                                                                    
% copy '#script xdelq'                                                          
% copy '#edit'                                                                  
% copy 'monitor off'                                                            
% copy 'reset residue 0 all'                                                    
% copy 'insert residue'                                                         
% copy 'end'                                                                    
%%                                                                              
% TRANSFER "#GENERALEDIT 5" TO CRYSTALS                                         
% TRANSFER "LOCATE RECORDTYPE=101" TO CRYSTALS                                  
% TRANSFER "ERROR MESSAGE=NO SIGNAL=NONE ACTION=CONTING NAME=EDITERROR" -       
  TO CRYSTALS                                                                   
% TRANSFER "TOP" TO CRYSTALS                                                    
%% get residue                                                                  
% EVALUATE RESIDUE = 0                                                          
% LOOP                                                                          
%   ON EDITERROR TERMINATE                                                      
%   TRANSFER "NEXT" TO CRYSTALS                                                 
%%  get residue number                                                          
%   TRANSFER "TRANSFER FROM OFFSET=16 TO RESIDUE" TO CRYSTALS                   
%   IF ( RESIDUE  .GT. OLDRESIDUE ) THEN                                        
%    EVALUATE OLDRESIDUE = RESIDUE                                              
%   END IF                                                                      
% END LOOP                                                                      
% TRANSFER "END" TO CRYSTALS                                                    
%%                                                                              
% queue rewind                                                                  
% queue copy #edit                                                              
% queue copy monitor  OFF                                                       
% loop oldresidue times                                                         
%  evaluate residue = oldresidue + 1 - loopcounter                              
%   CLEAR                                                                       
%   INSERT 'cent'                                                               
%   STORE FORMAT "(i6)" integer residue                                         
%   INSERT ' residue( '                                                         
%   STORE FORMAT "(i6)" integer residue                                         
%   INSERT ' ) '                                                                
%   queue SEND                                                                  
% end loop                                                                      
% queue copy atom q 1 x=.5 .5 .5                                                
% queue copy END                                                                
% queue process                                                                 
% queue rewind                                                                  
% queue copy #edit                                                              
% queue copy monitor  OFF                                                       
% queue copy move q(1)                                                          
% loop oldresidue times                                                         
%   CLEAR                                                                       
%   insert 'move qc('                                                           
%   STORE FORMAT "(i6)" integer loopcounter                                     
%   insert ')'                                                                  
%   queue send                                                                  
% end loop                                                                      
% queue copy END                                                                
% queue process                                                                 
% copy '#collect'                                                               
% copy 'sel type = all tol=20 monitor = OFF'                                    
% copy 'end'                                                                    
% copy '#edit'                                                                  
% copy 'monitor OFF'                                                            
% copy 'del q(1)'                                                               
% copy 'end'                                                                    
% copy '#collect'                                                              
% copy 'sel type = all tol = 5. monitor = OFF'                                 
% copy 'end'                                                                   
% copy '#collect'                                                               
% copy 'sel type = all monitor = OFF'                                           
% copy 'end'                                                                    
% copy '#script xdelq'                                                          
% COPY '#PURGE LIST=41'                                                         
% COPY 'END'                                                                    
%% MOVE TO MAKE FIRST ATOM CLOSE TO .5 .5 .5                                    
% COPY '#ORIGIN MODE=FIRST'                                                     
% COPY 'END'                                                                    
% COPY '#SCRIPT INSIDE'                                                         
%%                                                                              
%END SCRIPT                                                                     
