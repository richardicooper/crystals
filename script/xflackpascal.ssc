%SCRIPT XFLACKPASCAL
% VARIABLE INTEGER IDJW ITYPE JTYPE
% VARIABLE LOGICAL SOMEERROR SERR LGREYED
% VARIABLE CHARACTER CPUNCH CTYPE CWEIGHT
% EVALUATE CPUNCH = 'NO'
% EVALUATE CTYPE = "PASCAL"
% EVALUATE LGREYED = TRUE
% EVALUATE ITYPE = 1
% VARIABLE INTEGER   IPUNCH
% EVALUATE SOMEERROR = TRUE
% EVALUATE SERR = FALSE
% VARIABLE REAL FILTER1 FILTER2 FILTER3 FILTER4 FILTER5
%% Dont forget to change COMMANDS.SRC if you change these
% EVALUATE FILTER1 = 0.0001                                                        
% EVALUATE FILTER2 = 1.0                                                      
% EVALUATE FILTER3 = 2.0 
% EVALUATE FILTER4 = 0.001
% EVALUATE FILTER5 = 2.5
% VARIABLE INTEGER _BADNPP 
%%
% VARIABLE INTEGER ICENTRIC
%      IF ( EXISTS 2 .GT. 0 ) THEN
%          COPY '#GENERALEDIT 2'
%          COPY 'LOCATE RECORDTYPE=101'
%          COPY 'TRANSFER FROM OFFSET =  0 TO ICENTRIC'
%          COPY 'END'
%          IF ICENTRIC .EQ. 1 THEN
{E The space group is cenrosymmetric.
{E There is no Absolute Structure
%          FINISH 
%      END IF
%%
% IF ( EXISTS 6 ) .NE. 1 THEN
{E  No reflections have been stored.
{E  Absolute configuration analysis is not possible.
%   FINISH
% ELSE
%   VARIABLE LOGICAL FLACKERROR
%   COPY '#SCRIPT TONSWORK'
%   IF FLACKERROR .EQ. TRUE THEN
%    FINISH
%   END IF
%   IF ( EXISTS 7 ) .NE. 1 THEN
{E   Reflections could not be re-formatted
%    FINISH
%   END IF
% END IF
^^WI WINDOW A1 'Absolute Configuration' SIZE LARGE KEEP MODAL
^^WI CANCEL='RF_BXX'
^^WI GRID MAIN NROWS=8 NCOLS=3
^^WI {
^^WI  @ 2,2 GRID TOP NROWS=1 NCOLS=1
^^WI  {
^^WI   @ 1,1 TABCTRL _MAINTABS
^^WI   {
^^WI    TAB _TAB1 'Histogram' GRID _MAINTG2 NROWS=3 NCOLS=3
^^WI    {
^^WI          @ 2,2 RESIZE _XARSZ2 VERTICAL
^^WI          {
^^WI            ITEM GRID GRAPH NROWS=2 NCOLS=1
^^WI            {
^^WI             @ 1,1 GRID PRIME NROWS=3 NCOLS=1                                          
^^WI             {                                                                         
^^WI               @ 1,1 GRID EXP2 NROWS=3 NCOLS=1 OUTLINE=' '
^^WI               {                                                                       
^^WI                @ 1,1 STATIC XFW1                                                      
^^WI                "Distribution of Flack(x) applying filters 1,2,3 and 5"                
^^WI                @ 2,1 STATIC XFW2                                                      
^^WI 'Both frequency and normalised weight should peak between'    
^^WI                @ 3,1 STATIC XFW3                                                      
^^WI 'zero and unity. The extreme bins also contain any outliers'  
^^WI               }                                                                       
^^WI              @ 2,1 PLOTWINDOW _VHIST NROWS=20 NCOLS=64                              
^^WI              @ 3,1 GRID EXBT NROWS=2 NCOLS=7
^^WI              {     
^^WI               @ 1,1 STATIC HPN 
^^WI               ' Number of filtered reflections '
^^WI               @ 1,2 STATIC _HIS_NREF '0      '
^^WI               @ 1,4 STRETCH VB HORIZONTAL
^^WI               @ 1,5 BUTTON SAVEVHIST 'Save Image'
^^WI               @ 1,7 BUTTON EXPOVHIST 'Export data'
^^WI              }
^^WI             }                                                                         
^^WI            }
^^WI            ITEM GRID GRAPH NROWS=2 NCOLS=1
^^WI            {
^^WI             @ 1,1 GRID PRIME NROWS=3 NCOLS=1                                          
^^WI             {                                                                         
^^WI              @ 1,1 GRID EXP2 NROWS=3 NCOLS=2 OUTLINE=' '
^^WI              {                                                                       
^^WI               @ 1,1 STATIC XFW1                                                      
^^WI "Distribution of D/sigma(Do). " 
^^WI               @ 2,1 STATIC XFW2                                                      
^^WI 'The pairs of curves (red/green and blue/blue) should be similar to each other'    
^^WI               @ 3,1 STATIC XFW3                                                      
^^WI 'The Ds/sigma(Do) (green or pale blue) curve should peak in the end bins'  
^^WI               }                                                                       
^^WI              @ 2,1 PLOTWINDOW _VDIST NROWS=20 NCOLS=64                              
^^WI              @ 3,1 GRID EXBT NROWS=2 NCOLS=7
^^WI              {     
^^WI               @ 1,1 STATIC HPN 
^^WI               ' Number of reflections used is  '
^^WI               @ 1,2 STATIC _DIS_NREF '0     '

^^WI               @ 2,1 STATIC HPN 
^^WI               ' Number of filtered reflections '
^^WI               @ 2,2 STATIC _DIS_NFIL '0     '


^^WI               @ 1,4 STRETCH VB HORIZONTAL
^^WI               @ 1,5 BUTTON SAVEVDIST 'Save Image'
^^WI               @ 1,7 BUTTON EXPOVDIST 'Export data'
^^WI              }
^^WI             }                                                                         
^^WI            }
^^WI          }
^^WI    }
^^WI    TAB _TAB2 'NPP and Do/2Ao' GRID _MAINTG2 NROWS=3 NCOLS=3
^^WI    {
^^WI          @ 2,2 RESIZE _XARSZ2 VERTICAL
^^WI          {
^^WI            ITEM GRID GRAPH NROWS=3 NCOLS=1
^^WI            {
^^WI              @ 1,1 GRID EXP2 NROWS=3 NCOLS=1 OUTLINE=' '
^^WI              {                                                                       
^^WI               @ 1,1 STATIC XFW1                                                      
^^WI               "Normal Probability Plot"                
^^WI               @ 2,1 STATIC XFW2                                                      
^^WI               'Plot should be a straight line with unit gradient'    
^^WI               @ 3,1 STATIC XFW3                                                      
^^WI               'Extreme ends may contain a few outliers'  
^^WI              }                                                                       
^^WI              @ 2,1 PLOTWINDOW _VNPP NROWS=20 NCOLS=48
^^WI              @ 3,1 GRID RB NROWS=1 NCOLS=4
^^WI              {
^^WI                 @ 1,1 STRETCH VB HORIZONTAL
^^WI                 @ 1,2 BUTTON SAVEVNPP 'Save Image'
^^WI                 @ 1,4 BUTTON EXPOVNPP 'Export data'
^^WI              }
^^WI            }
^^WI            ITEM GRID GRAPH NROWS=3 NCOLS=1
^^WI            {
^^WI              @ 1,1 GRID EXP2 NROWS=3 NCOLS=1 OUTLINE=' '
^^WI              {                                                                       
^^WI               @ 1,1 STATIC XFW1                                                      
^^WI               "Flacks 2Ao/As plot"                
^^WI               @ 2,1 STATIC XFW2                                                      
^^WI     'The green Do points should lie on the same line as the '    
^^WI               @ 3,1 STATIC XFW3                                                      
^^WI     'red 2Ao points for a correctly assigned enantiopure sample'  
^^WI              }                                                                       
^^WI              @ 2,1 PLOTWINDOW _VAO NROWS=20 NCOLS=64
^^WI              @ 3,1 GRID RB NROWS=1 NCOLS=4
^^WI              {
^^WI                 @ 1,1 STRETCH VB HORIZONTAL
^^WI                 @ 1,2 BUTTON SAVEVAO 'Save Image'
^^WI                 @ 1,4 BUTTON EXPOVAO 'Export Data'
^^WI              }
^^WI            }
^^WI          }
^^WI    }
^^WI    TAB _TAB3 'Signal' GRID _MAINTG2 NROWS=3 NCOLS=3
^^WI    {
^^WI              @ 1,1 GRID EXP2 NROWS=3 NCOLS=2 OUTLINE=' '
^^WI              {                                                                       
^^WI               @ 1,1 STATIC XFW1                                                      
^^WI              "<|Ds|/sigma(Do)> plot for shells of constant volume"
^^WI               @ 2,1 STATIC XFW2                                                      
^^WI                'The red line is the number of pairs in each shell'   
^^WI               @ 3,1 STATIC XFW3                                                      
^^WI               'The green curve is the average <Ds/sigma(Do)>'
^^WI               @ 3,2 STATIC XWF4
^^WI               'in each shell>'
^^WI              }                                                                       
^^WI              @ 2,1 PLOTWINDOW _VDO NROWS=20 NCOLS=48
^^WI              @ 3,1 GRID RB NROWS=1 NCOLS=4
^^WI              {
^^WI                 @ 1,1 STRETCH VB HORIZONTAL
^^WI                 @ 1,2 BUTTON SAVEVDO 'Save Image'
^^WI                 @ 1,4 BUTTON EXPOVDO 'Export data'
^^WI              }
^^WI    }
^^WI   }
^^WI  }
^^WI  @ 4,2 GRID BOX NROWS=1 NCOLS=13   OUTLINE='  '
^^WI  {
^^WI     @ 1,1 GRID BTNS NROWS=1 NCOLS=7 OUTLINE='Type'
^^WI     {
^^WI       @ 1,1 RADIOBUTTON ITYPEP 'Pascal'     STATE=ON  INFORM
^^WI       @ 1,3 RADIOBUTTON ITYPEA 'Difference' STATE=OFF INFORM
^^WI       @ 1,5 RADIOBUTTON ITYPEB 'Quotient'   STATE=OFF INFORM
^^WI     }
^^WI     @ 1,5 GRID BTNS NROWS=1 NCOLS=6 OUTLINE='Output'
^^WI     {
^^WI      @ 1,1 RADIOBUTTON IPUNCHA 'None'     STATE=ON  DISABLED=NO
^^WI      @ 1,2 RADIOBUTTON IPUNCHB 'Table'    STATE=OFF DISABLED=NO
^^WI      @ 1,3 RADIOBUTTON IPUNCHC 'Restrain' STATE=OFF DISABLED=YES
^^WI      @ 1,4 RADIOBUTTON IPUNCHE 'Summary'  STATE=OFF DISABLED=YES
^^WI     }
%%
^^WI     @ 1,8 GRID BTNS2 NROWS=1 NCOLS=4 OUTLINE='Weight'
^^WI     {
^^WI       @ 1,1 RADIOBUTTON ITYPEC 'Sigma' STATE=ON
^^WI       @ 1,3 RADIOBUTTON ITYPED 'Lsq' STATE=OFF
^^WI     }
^^WI     @ 1,9 STRETCH VB HORIZONTAL
^^WI     @ 1,10 BUTTON HELP   '&Help'
^^WI     @ 1,11 BUTTON RF_BXX '&Quit'
^^WI     @ 1,13 BUTTON CALC  '&Recalculate'
^^WI  }
^^WI  @ 6,2 GRID RESLT NROWS=2 NCOLS=7
^^WI  {
^^WI               @ 1,1 STATIC HPC 
^^WI               ' Percentage of data with Flack(x) >-0.5 and <1.5 is '
^^WI               @ 1,2 STATIC _HIS_PC '0000.0'
^^WI               @ 2,1 STATIC HPN 
^^WI               ' Number of reflections with significant weight is '
^^WI               @ 2,2 STATIC _NREF '0000.0'
^^WI               @ 1,4 STATIC HPF 
^^WI               ' Try setting filter 1 to '
^^WI               @ 1,5 STATIC _FIL1 '      0.001'
^^WI               @ 1,6 STRETCH VB HORIZONTAL
^^WI               @ 1,7 BUTTON RHELP   'Resolution Table'
^^WI               @ 2,7 BUTTON EHELP   'Explanation'
^^WI  }
^^WI  @ 8,2 GRID REFCON NROWS=2 NCOLS=5 OUTLINE='Reflections Accepted if:'
^^WI  {                                                                        
^^WI    @ 1,1 GRID NCYC NROWS=2 NCOLS=3                                          
^^WI    {                                                                        
^^WI     @ 2,1 STATIC T2 '/Ds/ > Filter(1)*sigma(Do)'                                             
^^WI     @ 2,3 EDITBOX NF1                                                        
%         TRANSFER "^^WI '  " // CHARACTER ( FILTER1 )  // "'" TO DISPLAY         
^^WI      CHARS=12 REAL                                                          
^^WI    }                                                                        
^^WI    @ 1,2 GRID NCYC NROWS=2 NCOLS=3                                          
^^WI    {                                                                        
^^WI     @ 2,1 STATIC T3 'As > Filter(2)*sigma(Ao)'
^^WI     @ 2,3 EDITBOX NF2                                                        
%         TRANSFER "^^WI '  " // CHARACTER ( FILTER2 )  // "'" TO DISPLAY         
^^WI      CHARS=12 REAL                                                          
^^WI    }                                                                        
^^WI    @ 1,3 GRID NCYC NROWS=2 NCOLS=3                                          
^^WI    {                                                                        
^^WI     @ 2,1 STATIC T4 '/Do/ < Filter(3)*Ds(max)'
^^WI     @ 2,3 EDITBOX NF3                                                        
%         TRANSFER "^^WI '  " // CHARACTER ( FILTER3 )  // "'" TO DISPLAY         
^^WI      CHARS=12 REAL                                                          
^^WI    } 
^^WI    @ 1,4 GRID NCYC NROWS=2 NCOLS=3                                          
^^WI    {                                                                        
^^WI     @ 2,1 STATIC T4 'Probability > Filter(4)'
^^WI     @ 2,3 EDITBOX NF4                                                        
%         TRANSFER "^^WI '  " // CHARACTER ( FILTER4 )  // "'" TO DISPLAY         
^^WI      CHARS=12 REAL                                                          
^^WI    } 
^^WI    @ 1,5 GRID NCYC NROWS=2 NCOLS=3                                          
^^WI    {                                                                        
^^WI     @ 2,1 STATIC T5 'Quotient < Filter(5)'
^^WI     @ 2,3 EDITBOX NF5                                                        
%         TRANSFER "^^WI '  " // CHARACTER ( FILTER5 )  // "'" TO DISPLAY         
^^WI      CHARS=12 REAL                                                          
^^WI    } 
^^WI  }
^^WI }
^^WI SHOW
^^CR
%%
^^WI WINDOW RFNG 'Busy...' 
^^WI GRID RFNGM1 NROWS=3 NCOLS=3
^^WI { @ 2,2 STATIC T1 'Processing Reflections. Please wait.' }
^^WI SHOW
^^CR
% TRANSFER '#TON  PLOT=YES PUNCH=NO ' //  -
      ' TYPE  = ' //  CTYPE TO CRYSTALS
% COPY 'END'
^^WI DISPOSE RFNG
^^CR
%%
% VARIABLE INTEGER JLOOP
% LOOP
% EVALUATE JLOOP = LOOPCOUNTER
%%
%   VERIFY CALC RF_BXX SAVEVDO SAVEVAO SAVEVNPP SAVEVHIST SAVEVDIST -
             EXPOVHIST EXPOVAO  EXPOVDO EXPOVNPP EXPOVDIST HELP RHELP -
             EHELP ITYPEA ITYPEB ITYPEP
%   GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%   EVALUATE IDJW = VALUE
%   CASE VALUE
%     BLOCK  %CALCULATE%
%% first get updated filters
^^??  NF1 TEXT                                                                    
%     GET NOSTORE SILENT FINAL REAL 'Filter1' '0.0001'                          
%     EVALUATE FILTER1 = VALUE                                                    
^^??  NF2 TEXT                                                                    
%     GET NOSTORE SILENT FINAL REAL 'Filter2' '1.0'                          
%     EVALUATE FILTER2 = VALUE                                                    
^^??  NF3 TEXT                                                                    
%     GET NOSTORE SILENT FINAL REAL 'Filter3' '2.0'                          
%     EVALUATE FILTER3 = VALUE                                                    
^^??  NF4 TEXT                                                                    
%     GET NOSTORE SILENT FINAL REAL 'Filter4' '0.001'                          
%     EVALUATE FILTER4 = VALUE                                                    
%%
^^??  NF5 TEXT                                                                    
%     GET NOSTORE SILENT FINAL REAL 'Filter5' '2.5'                          
%     EVALUATE FILTER5 = VALUE                                                    
%%
^^??  IPUNCHA STATE
%     VERIFY ON OFF
%     GET SILENT NOSTORE FINAL ABBREVIATED ' '
%     IF VALUE .EQ. 1 THEN
%       EVALUATE IPUNCH = 0
%       EVALUATE CPUNCH = 'NO'
%     END IF
^^??  IPUNCHB STATE
%     VERIFY ON OFF
%     GET SILENT NOSTORE FINAL ABBREVIATED ' '
%     IF VALUE .EQ. 1 THEN
%       EVALUATE IPUNCH = 1
%       EVALUATE CPUNCH = 'TABLE'
%     END IF
^^??  IPUNCHC STATE
%     VERIFY ON OFF
%     GET SILENT NOSTORE FINAL ABBREVIATED ' '
%     IF VALUE .EQ. 1 THEN
%       EVALUATE IPUNCH = 2
%       EVALUATE CPUNCH = 'REST'
%     END IF
^^??  IPUNCHE STATE
%     VERIFY ON OFF
%     GET SILENT NOSTORE FINAL ABBREVIATED ' '
%     IF VALUE .EQ. 1 THEN
%       EVALUATE IPUNCH = 4
%       EVALUATE CPUNCH = 'SUMMARY'
%     END IF
%%
^^??  ITYPEC STATE
%     VERIFY ON OFF
%     GET SILENT NOSTORE FINAL ABBREVIATED ' '
%     IF VALUE .EQ. 1 THEN
%       EVALUATE JTYPE = 0
%       EVALUATE CWEIGHT = 'SIG'
%     END IF
^^??  ITYPED STATE
%     VERIFY ON OFF
%     GET SILENT NOSTORE FINAL ABBREVIATED ' '
%     IF VALUE .EQ. 1 THEN
%       EVALUATE JTYPE = 1
%       EVALUATE CWEIGHT = 'lsq'
%     END IF
%%
%      IF IPUNCH .EQ. 2 THEN
%       COPY '#RELEASE PUNCH flack.l16'
%      END IF
%      EVALUATE _BADNPP = 0
^^WI WINDOW RFNG 'Busy...' 
^^WI GRID RFNGM1 NROWS=3 NCOLS=3
^^WI { @ 2,2 STATIC T1 'Re-processing Reflections. Please wait.' }
^^WI SHOW
^^CR
%      TRANSFER '#TON  PLOT=YES ' //  -
       ' PUNCH = ' // CPUNCH TO CRYSTALS
%      TRANSFER 'CONT TYPE  = ' //  CTYPE TO CRYSTALS
%      TRANSFER 'CONT WEIGHT  = ' //  CWEIGHT TO CRYSTALS
%      TRANSFER 'CONT FILTER1= '  // CHARACTER ( FILTER1 ) // -
       ' FILTER2= '  // CHARACTER ( FILTER2 ) // -
       ' FILTER3= '  // CHARACTER ( FILTER3 ) TO CRYSTALS
%      TRANSFER 'CONT FILTER4= '  // CHARACTER ( FILTER4 ) // -
       ' FILTER5= '  // CHARACTER ( FILTER5 )  - 
       TO CRYSTALS
%      COPY 'END'
^^WI DISPOSE RFNG
^^CR
%      IF IPUNCH .EQ. 2 THEN
%       COPY '#RELEASE PUNCH bfile.pch'
%      END IF
%      IF _BADNPP .NE. 0 THEN
^^WI    WINDOW XINFO 'Warning'  MODAL
^^WI    GRID MAIN NROWS=3 NCOLS=3                   
^^WI     { 
^^WI      @ 1,2 STATIC T1 'Too many outliers in the Normal Probability Plot'
^^WI      @ 2,2 STATIC T2 'CRYSTALS suggests you check the Fo-Fc npp'
^^WI      @ 3,3 BUTTON CANCELBUTTON 'Close' CANCEL
^^WI     } 
^^WI    SHOW        
^^CR 
%       VERIFY  CANCELBUTTON 
%       GET SILENT NOSTORE ABBREVIATED ' ' ' '
%       CASE VALUE
%        BLOCK
%        END BLOCK
%       END CASE
^^WI    DISPOSE XINFO
^^CR
%      END IF
%     END BLOCK
%     BLOCK %CANCEL%
%        EVALUATE LOOPEND = LOOPCOUNTER
%        EVALUATE SERR = TRUE
^^CO DISPOSE A1
%        FINISH
%     END BLOCK
%     BLOCK  %SAVEVDO%
Saving Signal to noise image
^^CO SET _VDO SAVE 800 600 Signal-Noise_
%     END BLOCK
%     BLOCK  %SAVEVAO%
Saving Do-Ao image
^^CO SET _VAO SAVE 800 600 Do-Ao_
%     END BLOCK
%     BLOCK  %SAVEVNPP%
Saving NPP image
^^CO SET _VNPP SAVE 800 600 enantio-npp_
%     END BLOCK
%     BLOCK  %SAVEVHIST%
Saving histogram image
^^CO SET _VHIST SAVE 800 600 histogram_
%     END BLOCK
%     BLOCK  %SAVEVDIST%
Saving distribution image
^^CO SET _VDIST SAVE 800 600 distribution_
%     END BLOCK
%     BLOCK  %EXPOVHIST%
Saving histogram data
^^CO SET _VHIST EXPORT histogram_
%     END BLOCK
%     BLOCK  %EXPOVAO%
Saving histogram data
^^CO SET _VAO EXPORT DO-AO-
%     END BLOCK
%     BLOCK  %EXPOVDO%
Saving Do data
^^CO SET _VDO EXPORT Signal-noise_
%     END BLOCK
%     BLOCK  %EXPOVNPP%
Saving npp data
^^CO SET _VNPP EXPORT npp_
%     END BLOCK
%     BLOCK  %EXPOVDIST%
Saving distribution data
^^CO SET _VDIST EXPORT distribution_
%     END BLOCK
%     BLOCK  %HELP%
%        if ctype .eq. "PASCAL" THEN
%          COPY '#SCRIPT PASCAL-HELP'
%        ELSE
%          COPY '#SCRIPT FLACK-HELP'
%        END IF
%     END BLOCK
%     BLOCK  %RHELP%
%        COPY '#SCRIPT SINTHETA '
%     END BLOCK
%     BLOCK  %EHELP%
%        COPY '#SPAWN "CRYSDIR:manual/analyse/absolute.html"' 
%     END BLOCK
%     BLOCK %ITYPEA%
%       EVALUATE ITYPE = 0
%       EVALUATE CTYPE = 'DODC'
%       EVALUATE LGREYED = FALSE 
^^CO    SET IPUNCHA DISABLED=NO
^^CO    SET IPUNCHB DISABLED=NO
^^CO    SET IPUNCHC DISABLED=NO
^^CO    SET IPUNCHE DISABLED=NO
%     END BLOCK
%     BLOCK %ITYPEB%
%       EVALUATE ITYPE = 1
%       EVALUATE CTYPE = 'QOQC'
%       EVALUATE LGREYED = FALSE 
^^CO    SET IPUNCHA DISABLED=NO
^^CO    SET IPUNCHB DISABLED=NO
^^CO    SET IPUNCHC DISABLED=NO
^^CO    SET IPUNCHE DISABLED=NO
%     END BLOCK
%     BLOCK %ITYPEP%
%       EVALUATE ITYPE = 1
%       EVALUATE CTYPE = 'PASCAL'
%       EVALUATE LGREYED = TRUE
^^CO    SET IPUNCHA DISABLED=NO STATE=ON
^^CO    SET IPUNCHB DISABLED=NO
^^CO    SET IPUNCHC DISABLED=NO
^^CO    SET IPUNCHE DISABLED=YES 
%     END BLOCK
%   END CASE
% END LOOP
% IF SERR .EQ. TRUE THEN
^^CO DISPOSE A1
{E An error of some type has occured
{E This script will end
%   FINISH
% END IF
% END SCRIPT
