%SCRIPT XADDH2
% VARIABLE INTEGER LOOPAGAIN HTYPE NARGS
% VARIABLE CHARACTER CATOM CATOM1 CATOM2 CATOM3 CATOM4 CATOM5 CATOM6
% VARIABLE LOGICAL CALLED
% LOOP
%    EVALUATE LOOPAGAIN = 0
%    EVALUATE NARGS = 3
%    IF HTYPE .EQ. 3 THEN
%      EVALUATE NARGS = 4
%    END IF
%    IF HTYPE .EQ. 6 THEN
%      EVALUATE NARGS = 2
%    END IF
%    IF HTYPE .EQ. 7 THEN
%      EVALUATE NARGS = 6
%    END IF
%    IF HTYPE .EQ. 8 THEN
%      EVALUATE NARGS = 2
%    END IF
^^WI WINDOW ADDH 'Add hydrogen'
^^WI POSITION=CENTRED '_MAINTEXTOUTPUT'
^^WI GRID M NROWS=7 NCOLS=3 {
^^WI   @ 2,2 GRID E NROWS=2 NCOLS=1 {
^^WI     @ 1,1 STATIC S1 'Choose nearby atoms to define the'
^^WI     @ 2,1 STATIC S2 'geometry of the additional atoms.'
^^WI   }
^^WI   @ 4,2 GRID C NROWS=1 NCOLS=3 {
^^WI     @ 1,1 GRID HHH NROWS=1 NCOLS=1 { @ 1,1 BITMAP H33 FILE
%    CASE HTYPE
%         BLOCK %1
^^WI       'h33.bmp'
%         END BLOCK
%         BLOCK %2
^^WI       'h23.bmp'
%         END BLOCK
%         BLOCK %3
^^WI       'h13.bmp'
%         END BLOCK
%         BLOCK %4
^^WI       'h22.bmp'
%         END BLOCK
%         BLOCK %5
^^WI       'h12.bmp'
%         END BLOCK
%         BLOCK %6
^^WI       'h11.bmp'
%         END BLOCK
%         BLOCK %7
^^WI       'hphen.bmp'
%         END BLOCK
%         BLOCK %8
^^WI       'hbond.bmp'
%         END BLOCK
%    END CASE
^^WI  }  @ 1,3 GRID F NROWS=11 NCOLS=5 {
^^WI       @ 1,1 STATIC A1 'X'
^^WI       @ 3,1 STATIC A2 'R1'
%    CASE HTYPE
%           BLOCK %1
^^WI         @ 5,1 STATIC A3 'R2'
^^WI         @ 5,3 EDITBOX E3 '' CHARS=8
%           END BLOCK
%           BLOCK %2
^^WI         @ 5,1 STATIC A3 'R2'
^^WI         @ 5,3 EDITBOX E3 '' CHARS=8
%           END BLOCK
%           BLOCK %3
^^WI         @ 5,1 STATIC A3 'R2' 
^^WI         @ 5,3 EDITBOX E3 '' CHARS=8
^^WI         @ 7,1 STATIC A4 'R3'
^^WI         @ 7,3 EDITBOX E4 '' CHARS=8
%           END BLOCK
%           BLOCK %4
^^WI         @ 5,1 STATIC A3 'R2'
^^WI         @ 5,3 EDITBOX E3 '' CHARS=8
%           END BLOCK
%           BLOCK %5
^^WI         @ 5,1 STATIC A3 'R2'
^^WI         @ 5,3 EDITBOX E3 '' CHARS=8
%           END BLOCK
%           BLOCK %6
^^WI
%           END BLOCK
%           BLOCK %7
^^WI         @ 5,1 STATIC A3 'R2' 
^^WI         @ 5,3 EDITBOX E3 '' CHARS=8
^^WI         @ 7,1 STATIC A4 'R3'
^^WI         @ 7,3 EDITBOX E4 '' CHARS=8
^^WI         @ 9,1 STATIC A5 'R4'
^^WI         @ 9,3 EDITBOX E5 '' CHARS=8
^^WI         @ 11,1 STATIC A6 'R5'
^^WI         @ 11,3 EDITBOX E6 '' CHARS=8
%           END BLOCK

%           BLOCK %8
^^WI
%           END BLOCK


%    END CASE
%    IF CALLED THEN
^^WI       @ 1,5 STATIC T1 '< Click atom to go here'
^^WI       @ 3,5 STATIC T2 ''
%    ELSE
^^WI       @ 1,5 STATIC T1 ''
^^WI       @ 3,5 STATIC T2 '< Click atom to go here'
%    END IF
^^WI       @ 5,5 STATIC T3 ''
^^WI       @ 7,5 STATIC T4 ''
^^WI       @ 9,5 STATIC T5 ''
^^WI       @ 11,5 STATIC T6 ''
^^WI       @ 1,3 EDITBOX E1 '' CHARS=8
^^WI       @ 3,3 EDITBOX E2 '' CHARS=8 
^^WI     }
^^WI   }
^^WI   @ 6,2 GRID B NROWS=1 NCOLS=7 {
^^WI     @ 1,1 BUTTON BBB '< &Back'
^^WI     @ 1,3 BUTTON BOK '&Next' DISABLED=YES COMMIT DEFAULT
^^WI     @ 1,5 BUTTON BXX '&Cancel' CANCEL
^^WI     @ 1,7 BUTTON UNDO '&Undo'
^^WI   }
^^WI }
^^WI SHOW
^^CR
%    VARIABLE INTEGER POSN OPOSN
%    IF CALLED THEN
%     EVALUATE POSN = 1
%    ELSE
%     EVALUATE POSN = 2
%     TRANSFER "^^CO SET E1 TEXT '" // CATOM // "'" TO DISPLAY
%    END IF
%    EVALUATE OPOSN = 1
^^CO SET MODEL01 MOUSEACTION=HEADERATOM
%    LOOP
%     VERIFY BOK BBB BXX UNDO ATOM
%     GET NOSTORE SILENT FINAL ABBREVIATED ' ' ' '
%     CASE VALUE
%       BLOCK       %Next%
%         EVALUATE LOOPEND = LOOPCOUNTER
%         CLEAR
^^??      E1 TEXT
%         GET SILENT NOSTORE TEXT ' ' ' '
%         EVALUATE CATOM1 = CVALUE
^^??      E2 TEXT
%         GET SILENT NOSTORE TEXT ' ' ' '
%         EVALUATE CATOM2 = CVALUE
%         IF NARGS .GE. 3 THEN
^^??        E3 TEXT
%           GET SILENT NOSTORE TEXT ' ' ' '
%           EVALUATE CATOM3 = CVALUE
%         END IF
%         IF NARGS .GE. 4 THEN
^^??        E4 TEXT
%           GET SILENT NOSTORE TEXT ' ' ' '
%           EVALUATE CATOM4 = CVALUE
%         END IF
%         IF NARGS .EQ. 6 THEN
^^??        E5 TEXT
%           GET SILENT NOSTORE TEXT ' ' ' '
%           EVALUATE CATOM5 = CVALUE
^^??        E6 TEXT
%           GET SILENT NOSTORE TEXT ' ' ' '
%           EVALUATE CATOM6 = CVALUE
%         END IF
^^CO      DISPOSE ADDH
%       END BLOCK
%       BLOCK       %BACK BBB%
%         EVALUATE LOOPAGAIN = 1
^^CO      DISPOSE ADDH
%         FINISH
%       END BLOCK
%       BLOCK       %CANCEL BXX%
^^CO      DISPOSE ADDH
%         FINISH
%       END BLOCK
%       BLOCK       %UNDO - back one
%         EVALUATE OPOSN = POSN
%         EVALUATE POSN = POSN - 1
%         IF POSN .LT. 1 THEN
%           EVALUATE POSN = 1
%         END IF
%%
%%         Put together this command: ^^CO SET En TEXT ' '
%%
%         CLEAR
%         INSERT "^^CO SET E"
%         STORE FORMAT /(I1)/ LENGTH 1 INTEGER POSN
%         INSERT " TEXT ' '"
%         OUTPUT
%       END BLOCK
%       BLOCK       %ATOM - fill in atom name
%         GET SILENT TEXT ' ' ' '
%         CLEAR
%         INSERT '^^CO SET E'
%         STORE FORMAT /(I1)/ LENGTH 1 INTEGER POSN
%         TRANSFER " TEXT '" // CVALUE // "'" TO BUFFER
%         OUTPUT
%         EVALUATE OPOSN = POSN
%         EVALUATE POSN = POSN + 1
%       END BLOCK
%     END CASE
%%
%% Check for completion
%%
%     IF LOOPEND .NE. LOOPCOUNTER THEN
%      IF POSN .EQ. 1 THEN
^^CO    SET UNDO  DISABLED=YES
%      ELSE
^^CO    SET UNDO  DISABLED=NO
%      END IF
%%
%% Update "Click Next Atom" prompt
%%
%      IF OPOSN .LE. NARGS THEN
%       CLEAR
%       INSERT "^^CO SET T"
%       STORE FORMAT /(I1)/ LENGTH 1 INTEGER OPOSN
%       INSERT " TEXT ''"
%       OUTPUT
%      END IF
%      IF POSN .LE. NARGS THEN
%       CLEAR
%       INSERT "^^CO SET T"
%       STORE FORMAT /(I1)/ LENGTH 1 INTEGER POSN
%       INSERT " TEXT '< Click atom to go here'"
%       OUTPUT
^^CO    SET BOK DISABLED=YES
%      ELSE
%       EVALUATE POSN = NARGS + 1
%       CLEAR
%       INSERT "^^CO SET T"
%       STORE FORMAT /(I1)/ LENGTH 1 INTEGER ( POSN - 1 )
%       INSERT " TEXT ' Click Next'"
%       OUTPUT
^^CO    SET BOK DISABLED=NO
%      END IF
%     END IF
%    END LOOP
%    COPY '#SCRIPT XADDH3'
%    IF LOOPAGAIN .EQ. 0 THEN
%      EVALUATE LOOPEND = LOOPCOUNTER
%    END IF
% END LOOP
%END SCRIPT
