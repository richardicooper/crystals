%SCRIPT CIF2CRY                                                                 
%%                                                                              
%% Run cif2cry with common options.                                             
%%                                                                              
% VARIABLE CHARACTER CIF COUT CARG  FILE                                            
% VARIABLE LOGICAL RUNNOW                                                       
% EVALUATE RUNNOW = FALSE                                                       
% VARIABLE LOGICAL MATCH                                                       
% EVALUATE MATCH = FALSE                                                       
%%                                                                              
^^WI WINDOW CIF2CRY 'cif2cry' MODAL COMMIT='CFBOK' CANCEL='CFBXX'               
^^WI GRID GRIDM NROWS=10 NCOLS=4                                                
^^WI {                                                                          
^^WI  @ 2,2 GRID CFG NROWS=1 NCOLS=3 OUTLINE='CIF to convert:'                  
^^WI  {                                                                         
^^WI    @ 1,1 EDITBOX CIFILE 
%       TRANSFER "^^WI '" // FILE // "'" TO DISPLAY
^^WI    CHARS=48 INFORM=NO INPUT                       
^^WI    @ 1,3 BUTTON BROWSE 'Browse...'                                         
^^WI  }                                                                         
^^WI  @ 4,2 GRID TX NROWS=4 NCOLS=1 OUTLINE='Extract'                           
^^WI  {                                                                         
^^WI    @ 1,1 RADIOBUTTON FO 'first or only data block' INFORM        
^^WI    @ 2,1 RADIOBUTTON AO 'all data blocks to a single file' STATE=ON INFORM
^^WI    @ 3,1 RADIOBUTTON AN 'all data blocks to multiple files' INFORM        
^^WI  }                                                                         
^^WI  @ 6,2 GRID GB NROWS=3 NCOLS=3                                             
^^WI  {                                                                         
^^WI    @ 1,1 STATIC T1 'Output file:'                                          
^^WI    @ 3,1 STATIC T2 'Output extension:'                                     
^^WI    @ 1,3 EDITBOX OFNAM 'cif2cry.cry'                                       
^^WI    @ 3,3 EDITBOX OENAM '.cry' DISABLED=YES                                 
^^WI  }                                                                         
^^WI  @ 7,2 CHECKBOX RF 'Read converted file into CRYSTALS now' STATE=ON       
^^WI  @ 8,2 CHECKBOX XM 'Post-process now?' STATE=OFF 
^^WI  @ 8,3 BUTTON HELP '&Help'

      
^^WI  @ 9,2 GRID GB NROWS=1 NCOLS=3
^^WI  {                                                                         
^^WI    @ 1,1 BUTTON CFBOK '&Ok' DEFAULT                                        
^^WI    @ 1,3 BUTTON CFBXX '&Cancel'                                            
^^WI  }                                                                         
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
% LOOP                                                                          
%   VERIFY BROWSE CFBXX CFBOK FO AO AN  HELP                                        
%   GET SILENT NOSTORE FINAL ABBREVIATED 'BXX?' 'BXX'                           
%   CASE VALUE                                                                  
%     BLOCK                         %BROWSE%                                    
^^WI    SYSOPENFILE '*.cif;*.cmf;*.fcf' 'CIF format (*.cif,*.cmf,*.fcf)'        
^^CR                                                                            
%       GET SILENT NOSTORE TEXT 'New filename:' 'CANCEL'                                      
%       IF CVALUE .NE. 'CANCEL' THEN                                            
%         CLEAR                                                                 
^^WI      SET CIFILE TEXT                                                       
%         TRANSFER "^^WI '" // CVALUE // "'" TO DISPLAY                         
^^CR                                                                            
%         EVALUATE CIF = CVALUE                                                 
%         IF ( .NOT. ( FILEEXISTS CIF ) ) THEN                                  
{E Error: Filename given does not exist.                                        
%         END IF                                                                
%       END IF                                                                  
%     END BLOCK                                                                 
%     BLOCK                         %CFBXX%                                     
^^CO    DISPOSE CIF2CRY                                                         
%       FINISH                                                                  
%     END BLOCK                                                                 
%     BLOCK                         %CFBOK%                                     
^^??    CIFILE TEXT                                                             
%       GET SILENT TEXT 'CIF name ' 'CANCEL'                                            
%       IF CVALUE .NE. 'CANCEL' THEN                                            
%       EVALUATE CIF = CVALUE                                                   
%       IF ( .NOT. ( FILEEXISTS CIF ) ) THEN                                    
{E Error: Filename given does not exist.                                        
%       ELSE                                                                    
^^??      RF STATE                                                              
%         VERIFY ON OFF                                                         
% VARIABLE LOGICAL RUNNOW                                                       
% EVALUATE RUNNOW = FALSE                                                       
%         GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%         IF ( VALUE .EQ. 1 ) THEN                                              
%           EVALUATE RUNNOW = TRUE                                              
%         END IF                                                                
^^??      XM STATE                                                              
%         VERIFY ON OFF                                                         
%         GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%         IF ( VALUE .EQ. 1 ) THEN                                              
%           EVALUATE MATCH = TRUE                                              
%         ELSE
%           EVALUATE MATCH = FALSE
%         END IF 
^^??      AN STATE                                                              
%         VERIFY ON OFF                                                         
%         GET SILENT NOSTORE FINAL ABBREVIATED ' '                              
%         IF ( VALUE .EQ. 1 ) THEN                                              
%           EVALUATE CARG = ' -a -d CSD -n '                                           
^^??        OENAM TEXT                                                          
%           GET SILENT TEXT 'Output extension '                                 
%           EVALUATE COUT = CVALUE                                              
%         ELSE                                                                  
^^??        OFNAM TEXT                                                          
%           GET SILENT TEXT 'Output file '                                      
%           EVALUATE COUT = CVALUE                                              
^^??        FO STATE                                                            
%           VERIFY ON OFF                                                       
%           GET SILENT NOSTORE FINAL ABBREVIATED ' '                            
%           IF ( VALUE .EQ. 1 ) THEN                                            
%             EVALUATE CARG = ' -f -d CSD -o '                                         
%           ELSE                                                                
%             EVALUATE CARG = ' -a -d CSD -o '                                         
%           END IF                                                              
%         END IF                                                                
^^CO      DISPOSE CIF2CRY                                                       
%         EVALUATE LOOPEND = LOOPCOUNTER                                        
%       END IF                                                                  
%       ELSE
{E No filename given
%       END IF
%     END BLOCK                                                                 
%     BLOCK                         %FO%                                        
^^CO    SET OFNAM DISABLED=NO SET OENAM DISABLED=YES                            
^^CO    SET RF DISABLED=NO                                                      
%     END BLOCK                                                                 
%     BLOCK                         %AO%                                        
^^CO    SET OFNAM DISABLED=NO SET OENAM DISABLED=YES                            
^^CO    SET RF DISABLED=NO                                                      
%     END BLOCK                                                                 
%     BLOCK                         %AN%                                        
^^CO    SET OFNAM DISABLED=YES SET OENAM DISABLED=NO                            
^^CO    SET RF STATE=OFF DISABLED=YES                                           
%     END BLOCK                                                                 
%     BLOCK                         %HELP%
%        COPY '#SCRIPT XHELP1'
Clicking Post-Process causes the file WINCRYS\SCRIPT\CIFPROC.DAT 
to be executed.

The default file causes the molecules in Z'=2 structures to be MATCHED

If a file CIFPROC.DAT exists in the current folder, it will execute
in preference. This file can contain any CRYSTALS #COMMANDS.

%        COPY '#SCRIPT XHELP2'
%     END BLOCK
%   END CASE                                
% END LOOP                                                                      
%%                                                                              
%% create list 23 if it doesnt exist
%  IF ( EXISTS 23 .NE. 1 ) THEN                                                 
%    COPY '#LIST 23'                                                            
%    COPY 'END'                                                                 
%  END IF                                                                       

%% Launch CIF2CRY                                                               
%%                                                                              
% TRANSFER '#SPAWN + "CRYSDIR:diffin.exe" ' // CARG // '"' // COUT -           
  // '" "' // CIF // '"' TO CRYSTALS                                            
%%                                                                              
%% show runnow
%% set verify true
% IF RUNNOW THEN                                                                
%   TRANSFER '#USE ' // COUT TO CRYSTALS                                        
% END IF                                                                        
%%                                                                              
%END SCRIPT                                                                     
