%SCRIPT FINDH
% VARIABLE LOGICAL SCRIPTEXT
%% The variables needed by XQUESTIO
% VARIABLE CHARACTER QTITLE QLINE1 QLINE2 BUTTOK BUTTXX
% VARIABLE LOGICAL   ANSWER
%%
 
 * This SCRIPT permits you to remove all existing hydrogen atoms before
   starting the placement.
 * It computes the positions of hydrogen atoms on carbon atoms but does
   not include them in the structure factor calculation.
 * It then searches to confirm them in a difference map. Calculated (H)
   and found (QH) atoms are displayed, and the model may be edited.
 * Finally, any peaks close to a calculated H atom are eliminated, and
   any remaining peaks deleted.
 
% EVALUATE QTITLE = 'Delete Hydrogen'
% EVALUATE QLINE1 = 'Do you want to delete'
% EVALUATE QLINE2 = 'existing hydrogen atoms?'
% EVALUATE BUTTOK = '&No'
% EVALUATE BUTTXX = '&Yes'
% COPY '#SCRIPT XQUESTIO'
% IF ANSWER .EQ. FALSE THEN
%    COPY '#EDIT'
%    COPY 'SELECT TYPE NE H'
%    COPY 'END'
% END IF
% COPY '#SCRIPT CHECK12'
% COPY '#SFLS'
% COPY 'CALC'
% COPY 'END'
% COPY '#PERHYDRO'
% COPY 'END'
%%
% COPY '#FOUR'
% COPY 'MAP TYPE=DIFF'
% COPY 'PEAK HEIGHT=1'
% COPY 'END'
% COPY '#COLL 10 5'
% COPY 'SEL TYPE=PEAK'
% COPY 'END'
%%
% COPY '#EDIT'
% COPY 'TYPECHANGE TYPE EQ Q QH'
% COPY 'END'
%%
^^WI WINDOW CONTINUE 'Continue'
^^WI COMMIT='BOK' CANCEL='BXX'
^^WI GRID MAIN NROWS=3 NCOLS=5
^^WI {
^^WI   @ 2,2 GRID L NROWS=4 NCOLS=1
^^WI   {
% IF ( XAUTO12 ) THEN
^^WI     @ 1,1 STATIC T0 'Choose "Continue" to carry on'
^^WI     @ 2,1 STATIC T1 'with the guided refinement.'
^^WI     @ 3,1 STATIC T2 '(You can still choose CAMERON'
^^WI     @ 4,1 STATIC T3 ' and return here later)'
^^WI   }
^^WI   @ 2,4 GRID R NROWS=3 NCOLS=1 COMMAND='#SCRIPT FINH_NAUTO'
% ELSE
^^WI     @ 1,1 STATIC T0 'Choose "Continue" to complete'
^^WI     @ 2,1 STATIC T1 'the H positioning script.'
^^WI   }
^^WI   @ 2,4 GRID R NROWS=3 NCOLS=1 COMMAND='#SCRIPT FINH_NFIND'
% END IF
^^WI   {
^^WI     @ 1,1 BUTTON BOK '&Continue'
^^WI     @ 3,1 BUTTON BXX '&Cancel'
^^WI   }
^^WI }
^^WI SHOW
^^CR
 
 * Rename any QH peaks that you wish to keep to 'H'.
 
% COPY '#SCRIPT DIRECT'
%END SCRIPT
