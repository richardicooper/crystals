%SCRIPT XSHELXS                                                                 
%   VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2                       
%   VARIABLE LOGICAL ANSWER LCRY                                                
%   VARIABLE INTEGER IRUN                                                       
%   IF EXISTS 6 .LT. 1 THEN                                                     
            You do not have any reflections stored.                             
            It is impossible to proceed without data.                           
%       FINISH                                                                  
%   END IF                                                                      
%%                                                                              
%% write a SHELXS data file                                                     
%%                                                                              
%   BLOCK                                                                       
^^WI WINDOW SHELX 'Shelxs Preparation'                                          
^^WI   MODAL COMMIT='BOK' CANCEL='BXX'                                          
^^WI GRID GRIDLR NROWS=1 NCOLS=2                                                
^^WI {                                                                          
^^WI   @ 1,1 GRID GRIDL NROWS=5 NCOLS=3                                         
^^WI   {                                                                        
^^WI     @ 1,2 RADIOBUTTON RS '&Default Shelxs parameters'                      
^^WI           STATE=ON                                                         
^^WI     @ 2,2 RADIOBUTTON RD '&Alternative Shelxs parameters'                  
^^WI           STATE=OFF                                                        
^^WI     @ 3,2 RADIOBUTTON RP '&Shelxs Patterson'                               
^^WI           STATE=OFF                                                        
^^WI     @ 4,2 CHECKBOX CED '&Edit Shelxs file before processing'               
^^WI           STATE=OFF                                                        
^^WI     @ 5,2 CHECKBOX CRUN '&Run Shelxs now'                                  
^^WI           STATE=ON                                                         
^^WI   }                                                                        
^^WI   @ 1,2 GRID GRIDR NROWS=5 NCOLS=2                                         
^^WI   {                                                                        
^^WI     @ 2,1 BUTTON BOK '&Ok'                                                 
^^WI     @ 4,1 BUTTON BXX '&Cancel'                                             
^^WI   }                                                                        
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
%      VERIFY BOK BXX                                                           
%      GET SILENT NOSTORE ABBREVIATED 'BOK=Process'                             
%      IF VALUE .EQ. 2 THEN                                                     
^^WI      DISPOSE SHELX                                                         
^^CR                                                                            
%         FINISH                                                                
%      END IF                                                                   
%%                                                                              
^^??   RS STATE                                                                 
%      VERIFY ON OFF                                                            
%      GET SILENT NOSTORE ABBREVIATED 'On=Simple job'                           
%      IF ( VALUE .EQ. 1 ) THEN                                                 
%          COPY '#FOREIGN SHELXS '                                              
%          COPY 'END'                                                           
%      END IF                                                                   
%%                                                                              
^^??   RD STATE                                                                 
%      VERIFY ON OFF                                                            
%      GET SILENT NOSTORE ABBREVIATED 'On=Hard job'                             
%      IF ( VALUE .EQ. 1 ) THEN                                                 
%          COPY '#FOREIGN SHELXS DIFFICULT'                                     
%          COPY 'END'                                                           
%      END IF                                                                   
%%                                                                              
^^??   RP STATE                                                                 
%      VERIFY ON OFF                                                            
%      GET SILENT NOSTORE ABBREVIATED 'On=Patterson'                            
%      IF ( VALUE .EQ. 1 ) THEN                                                 
%          COPY '#FOREIGN SHELXS PATTERSON'                                     
%          COPY 'END'                                                           
%      END IF                                                                   
%%                                                                              
^^??   CRUN STATE                                                               
%      VERIFY ON OFF                                                            
%      GET SILENT NOSTORE ABBREVIATED 'ON=Run shelxs now'                       
%      EVALUATE IRUN = VALUE                                                    
^^??   CED STATE                                                                
%      VERIFY ON OFF                                                            
%      GET SILENT NOSTORE ABBREVIATED 'ON=Edit first'                           
^^WI   DISPOSE SHELX                                                            
^^CR                                                                            
%      IF ( VALUE .EQ. 1 ) THEN                                                 
+GID%           COPY '#SPAWN +notepad.exe shelxs.ins'                               
+GID%           EVALUATE QTITLE = 'Shelxs Preparation'                              
+GID%           EVALUATE QLINE1 = 'Do you want to'                                  
+GID%           EVALUATE QLINE2 = 'run SHELXS now?'                                 
+GID%           EVALUATE BUTTOK = '&Yes'                                            
+GID%           EVALUATE BUTTXX = '&No'                                             
+GID%           COPY '#SCRIPT XQUESTIO'                                             
+GID%           IF ANSWER .EQ. FALSE THEN                                           
+GID%               FINISH                                                          
+GID%           END IF                      
-GID%           EVALUATE EDITOR_FILE = 'shelxs.ins'
-GID%           COPY '#SCRIPT XEDITOR'
-GID%           IF ( .NOT. EDITOR_OK ) THEN
-GID{I User cancelled SHELXS run.
-GID%              FINISH
-GID%            END IF                                                                             
%      END IF                                                                   
%      IF ( IRUN .EQ. 2 ) THEN                                                  
%           FINISH                                                              
%      END IF                                                                   
%      COPY '#SPAWN % "CRYSDIR:shelxs.exe"'                                     
%      COPY '#TYPE shelxs.lis'                                                  
%   END BLOCK                                                                   
%   BLOCK                                                                       
%      EVALUATE QTITLE = 'Shelxs Results'                                       
%      EVALUATE QLINE1 = 'Do you want to use the structure'                     
%      EVALUATE QLINE2 = 'from Shelxs ?'                                        
%      EVALUATE BUTTOK = '&Yes'                                                 
%      EVALUATE BUTTXX = '&No'                                                  
%      COPY '#SCRIPT XQUESTIO'                                                  
%      IF ANSWER .EQ. FALSE THEN                                                
%          COPY '#SCRIPT XHELP1'                                                
   If the structure will not solve:                                             
   1) Try the "Alternative Shelxs parameters" option.                           
   2) Try DirDif or Sir92.                                                      
   3) Read the SHELXS manual for help. (not provided)                           
   4) Be sure that the space group is correct.                                  
%          COPY '#SCRIPT XHELP2'                                                
%          FINISH                                                               
%      END IF                                                                   
%%djwoct09      EVALUATE LCRY = TRUE                                                     
%      EVALUATE LCRY = FALSE                                                     
%%                                                                              
%      COPY '#OPEN SCPDATA shelxs.res'                                          
%      VARIABLE INTEGER ATOMCOUNT
%      COPY '#SCRIPT ZSHELXIN'                                                  
%%DJW      COPY '#SCRIPT INshelxs'
%      IF ATOMCOUNT .LE. 0 THEN
{E                                                   
{E     SHELXS failed to find any atoms                                              
{E                                                   
%        FINISH
%      END IF                                              
%      COPY '#CLOSE SCPDATA'                                                    
%      COPY '#EDIT'                                                             
%      COPY 'TYPECHANGE TYPE EQ Q C'                                            
%      COPY 'END'                                                               
%      COPY '#EDIT'                                                             
%      COPY 'MON OFF'                                                           
%      COPY 'CHANGE FIRST(OCC) UNTIL LAST 1.0'                                  
%      COPY 'MON MEDIUM'                                                        
%      COPY 'END'                                                               
%%                                                                              
%      BLOCK                                                                    
%      END BLOCK                                                                
%%
%      BLOCK                                                                    
%%  CENTRE STRUCTURE IN CELL                                                    
%       COPY '#SCRIPT XCENTRE'                                                  
%       COPY '#SCRIPT XRETYPE'
%       COPY '#SCRIPT XRENUMB'                                                  
%      END BLOCK                                                                
%%                                                                              
%%                                                                              
%      COPY '#SCRIPT FRESH'                                                     
%%                                                                              
%      COPY '#SCRIPT XSCALE'
%%
%% SET Solution type                                                            
%      COPY '#GENERALEDIT 30'                                                   
%      COPY 'LOCATE RECORDTYPE=106'                                             
%      COPY 'CHANGE OFFSET=12 MODE=INTEGER INTEGER=1'                           
%      COPY 'WRITE OVERWRITE=OVERWRITE'                                         
%      COPY 'END'                                                               
%   END BLOCK                                                                   
 * Done. Shelxs input complete.                                                 
%END SCRIPT                                                                     
                                                                                
                                                                                
                                                                                
