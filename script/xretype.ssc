%SCRIPT XRETYPE
% VARIABLE INTEGER N29 HSPOT
% VARIABLE CHARACTER CLTYPE
% VARIABLE LOGICAL BROKENEDIT RESET29
% EVALUATE BROKENEDIT = FALSE
% EVALUATE RESET29 = TRUE
%%
 * Manual retyping of structure using mouse.
^^WI WINDOW XRENAM 'Rename' MODAL KEEP SIZE LARGE COMMIT='BOK' CANCEL='BXX'
^^WI POSITION=CENTRED 'MODEL01'
^^WI GRID MAIN NROWS=7 NCOLS=3
^^WI {
^^WI   @ 2,2 GRID GRIDQ NROWS=1 NCOLS=3 OUTLINE=''
^^WI   {
^^WI     @ 1,1 ICON IM INFO
^^WI     @ 1,3 GRID EXP2 NROWS=3 NCOLS=1
^^WI     {
^^WI       @ 1,1 STATIC T1
^^WI 'If the stucture looks OK, just click done.'
^^WI       @ 2,1 STATIC T2
^^WI 'To change an element type, choose it from the list and click the atom.'
^^WI       @ 3,1 STATIC T3
^^WI 'To delete an atom choose the "Delete" option and click the atom.'
^^WI     }
^^WI   }
^^WI   @ 4,2 GRID TEXT NROWS=1 NCOLS=3
^^WI   {
^^WI      @ 1,1 GRID OPTIONS NROWS=3 NCOLS=1
^^WI      {
^^WI        @ 1,1 GRID CLICK NROWS=2 NCOLS=3 OUTLINE='Click action'
^^WI        {
^^WI           @ 1,1 STATIC _ELT 'change to:'
^^WI           @ 2,1 LISTBOX _ELS VISLINES=6 ADDTOLIST 'Q' 'NULL'
^^WI        }
^^WI        @ 3,1 GRID CLICK NROWS=8 NCOLS=1
^^WI        {
^^WI          @ 1,1 STATIC T1 'If you want an element'
^^WI          @ 2,1 STATIC T1 'type that is not'
^^WI          @ 3,1 STATIC T1 'available in the drop '
^^WI          @ 4,1 STATIC T1 'down list above'
^^WI          @ 5,1 STATIC T1 'click the "Edit '
^^WI          @ 6,1 STATIC T1 'Elements" button.'
^^WI          @ 8,1 BUTTON BED 'Edit Elements'
^^WI        }
^^WI      }
^^WI      @ 1,3 MODELWINDOW MODELRE 'N' NROWS=5 NCOLS=65
^^WI            ATTACH L5 RADSCALE=250 RADTYPE=COV
^^WI            MOUSEACTION=HEADERATOM
^^WI   }
^^WI   @ 6,2 GRID BTNS NROWS=1 NCOLS=7
^^WI   {
^^WI     @ 1,1 BUTTON BOK 'D&one' DEFAULT COMMIT
^^WI     @ 1,3 BUTTON BXX '&Cancel' CANCEL
^^WI     @ 1,5 BUTTON BHELP '&Help' 
^^WI     @ 1,7 STATIC T2
^^WI '"Done" to apply the new types or "Cancel" to quit without changes.' 
^^WI   }
^^WI }
^^WI SHOW
^^CR
%%
%  VARIABLE INTEGER OLDSER
%  COPY '#GENERALEDIT 5'
%  COPY 'GETSERIAL OLDSER'
%  COPY 'END'
%%
%  VARIABLE CHARACTER CATOM CEL
%  CLEAR
%  LOOP
%   ON ERROR TERMINATE
%   IF ( RESET29 ) THEN
%     EVALUATE HSPOT = 0
^^CO  SET _ELS REMOVE 0
^^WI  SET _ELS ADDTOLIST
%     EVALUATE N29 = 0
%     IF ( EXISTS 29 .EQ. 1 ) THEN
%       COPY '#GENERALEDIT 29'
%       COPY 'LOCATE RECORDTYPE=101'
%       COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%       LOOP
%         ON EDITERROR TERMINATE
%         COPY 'TRANSFER FROM OFFSET=0 TO CLTYPE'
%         TRANSFER '^^WI ' // CLTYPE TO DISPLAY
%         EVALUATE N29 = N29 + 1
%         IF ( CLTYPE .EQ. 'H' ) THEN
%           EVALUATE HSPOT = N29
%         END IF
%         COPY 'NEXT'
%       END LOOP
%       COPY 'END'
%     END IF
^^WI   'Q' 'Delete' 'NULL'
^^CR
%     EVALUATE RESET29 = FALSE
%     COPY '#EDIT'
%     COPY 'CLASH FIXLATTER'
%     COPY 'GUI HIGH'
%     COPY 'EXEC'
%   END IF
%   VERIFY BOK BXX ATOM BHELP BED NULL
%   GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%   CASE VALUE
%    BLOCK      %BOK
 * Applying new types.
%      COPY 'END'
^^CO   DISPOSE XRENAM
%      FINISH
%    END BLOCK
%    BLOCK      %BXX
^^CO DISPOSE XRENAM
 * Keeping old types and numbering scheme.
%      COPY 'QUIT'
%      COPY 'END'
%      IF ( BROKENEDIT ) THEN
%        COPY '#DISK'
%        CLEAR
%        INSERT 'RESET 5 '
%        STORE INTEGER OLDSER
%        SEND
%        COPY '#SUM L 5'
%        COPY 'END'
%      END IF
%      FINISH
%    END BLOCK
%    BLOCK      %ATOM
%      GET SILENT NOSTORE TEXT ' ' ' '
^^CW
%      EVALUATE CATOM = CVALUE
%
%      CLEAR
%      INSERT '^^?? _ELS LISTTEXT '
^^??     _ELS SELECTED
%      GET SILENT INTEGER ' '
%      OUTPUT
%      GET SILENT NOSTORE KEYWORD ' ' 'ERROR'
%      EVALUATE CEL = CVALUE
%      IF ( ( UPPERCASE CEL ) .EQ. 'DELETE' ) THEN
%        TRANSFER 'DELETE ' // CATOM TO CRYSTALS
%        COPY 'EXEC'
%      ELSE
%        TRANSFER 'DRENAME ' // CATOM // ' ' // CEL // '(' // -
         CHARACTER ( FIRSTINT ( CATOM ) ) // ')' TO CRYSTALS
%        COPY 'EXEC'
%      END IF
%    END BLOCK
%    BLOCK      %HELP
%      COPY '#SCRIPT XHELP1'
  Click on the atoms in the molecule to either change their
  type to the current selection, or delete them.

  Click done when complete.



%      COPY '#SCRIPT XHELP2'
%    END BLOCK
%    BLOCK     %BED
%       EVALUATE BROKENEDIT = TRUE
%       EVALUATE RESET29 = TRUE
%       COPY 'monitor OFF'
%       COPY 'END'
%       COPY '#SCRIPT XINLIST3'
%    END BLOCK
%    BLOCK     %NULL
Call from add H script.
%       IF HSPOT .GT. 0 THEN
%         CLEAR
%         INSERT '^^CO SET _ELS SELECTION= '
%         STORE INTEGER HSPOT
%         OUTPUT
%       END IF
%    END BLOCK
%   END CASE
%  END LOOP
%END SCRIPT

