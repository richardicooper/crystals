%SCRIPT XRETYPE
% VARIABLE INTEGER N29
% VARIABLE CHARACTER CLTYPE
%%
 * Manual retyping of structure using mouse.
^^WI WINDOW XRENAM 'Rename' MODAL KEEP SIZE COMMIT='BOK' CANCEL='BXX'
^^WI POSITION=CENTRED 'MODEL01'
^^WI GRID MAIN NROWS=7 NCOLS=3
^^WI {
^^WI   @ 2,2 GRID TEXT NROWS=3 NCOLS=1
^^WI   {
^^WI     @ 1,1 STATIC T1
^^WI 'Click on atom to change to currently selected type, or to delete.'
^^WI   }
^^WI   @ 4,2 GRID TEXT NROWS=1 NCOLS=3
^^WI   {
^^WI      @ 1,1 GRID OPTIONS NROWS=2 NCOLS=3 OUTLINE='Click action'
^^WI      {
^^WI         @ 1,1 RADIOBUTTON _ELT 'Change to' STATE=ON
^^WI         @ 1,3 DROPDOWN _ELS ADDTOLIST
%  EVALUATE N29 = 0
%  IF ( EXISTS 29 .EQ. 1 ) THEN
%    COPY '#GENERALEDIT 29'
%    COPY 'LOCATE RECORDTYPE=101'
%    COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%    LOOP
%      ON EDITERROR TERMINATE
%      COPY 'TRANSFER FROM OFFSET=0 TO CLTYPE'
%      TRANSFER '^^WI ' // CLTYPE TO DISPLAY
%      EVALUATE N29 = N29 + 1
%      COPY 'NEXT'
%    END LOOP
%    COPY 'END'
%  END IF
%  IF ( N29 .EQ. 0 ) THEN
^^WI    'C' 'H' 'N' 'O'
%  END IF
^^WI         'Q' 'NULL'
^^WI        @ 2,1 RADIOBUTTON _DEL 'Delete atom' STATE=OFF
^^WI      }
^^WI      @ 1,3 MODELWINDOW MODELRE 'N' NROWS=5 NCOLS=65
^^WI            ATTACH L5 RADSCALE=250 RADTYPE=COV
^^WI            MOUSEACTION=HEADERATOM
^^WI   }
^^WI   @ 6,2 GRID BTNS NROWS=1 NCOLS=7
^^WI   {
^^WI     @ 1,1 BUTTON BOK 'D&one' DEFAULT COMMIT
^^WI     @ 1,3 BUTTON BXX '&Cancel' CANCEL
^^WI     @ 1,5 BUTTON BHELP '&Help' 
^^WI     @ 1,7 STATIC T2
^^WI '"Done" to apply the new types or "Cancel" to quit without changes.' 
^^WI   }
^^WI }
^^WI SHOW
^^CR
%%
%%
%  VARIABLE CHARACTER CATOM CEL
%  COPY '#EDIT'
%  COPY 'CLASH FIXLATTER'
%  COPY 'GUI HIGH'
%  COPY 'EXEC'
%  CLEAR
%  LOOP
%   ON ERROR TERMINATE
%   VERIFY BOK BXX ATOM BHELP
%   GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%   CASE VALUE
%    BLOCK      %BOK
 * Applying new types.
%      COPY 'END'
^^CO   DISPOSE XRENUM
%      FINISH
%    END BLOCK
%    BLOCK      %BXX
^^CO DISPOSE XRENUM
 * Keeping old types and numbering scheme.
%      COPY 'QUIT'
%      COPY 'END'
%      FINISH
%    END BLOCK
%    BLOCK      %ATOM
%      GET SILENT NOSTORE TEXT ' ' ' '
^^CW
%      EVALUATE CATOM = CVALUE
^^??   _DEL STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%      IF VALUE .EQ. 1 THEN
%        TRANSFER 'DELETE ' // CATOM TO CRYSTALS
%        COPY 'EXEC'
%      ELSE
%        CLEAR
%        INSERT '^^?? _ELS LISTTEXT '
^^??     _ELS SELECTED
%        GET SILENT INTEGER ' '
%        OUTPUT
%        GET SILENT NOSTORE KEYWORD ' ' 'ERROR'
%        EVALUATE CEL = CVALUE
%        TRANSFER 'RENAME ' // CATOM // ' ' // CEL // '(' // -
         CHARACTER ( FIRSTINT ( CATOM ) ) // ')' TO CRYSTALS
%        COPY 'EXEC'
%      END IF
%    END BLOCK
%    BLOCK      %HELP
%      COPY '#SCRIPT XHELP1'
  Click on the atoms in the molecule in the order
  you want them to be numbered. When you are finished
  click DONE to apply the new numbering scheme, or
  CANCEL to keep the old numbering scheme.
%      COPY '#SCRIPT XHELP2'
%    END BLOCK
%   END CASE
%  END LOOP
%END SCRIPT

