%SCRIPT XROTAX
% VARIABLE INTEGER NFOM
% VARIABLE REAL TWINTOL BINMAX
% VARIABLE LOGICAL L ADD25 CANCEL25
% VARIABLE REAL MATRIX:M11 MATRIX:M12 MATRIX:M13
% VARIABLE REAL MATRIX:M21 MATRIX:M22 MATRIX:M23
% VARIABLE REAL MATRIX:M31 MATRIX:M32 MATRIX:M33
%% The variables needed by XQUESTIO
% VARIABLE CHARACTER QTITLE QLINE1 QLINE2 BUTTOK BUTTXX
% VARIABLE LOGICAL   ANSWER
^^WI WINDOW ROTAX 'Rotax' MODAL
^^WI POSITION=CENTRED 'MODEL01'
^^WI GRID M NROWS=6 NCOLS=3 {
^^WI  @ 2,2 GRID E NROWS=2 NCOLS=9 {
^^WI     @ 1,1 STATIC RT_T1 'Search for twin laws involving a'
^^WI     @ 1,5 EDITBOX RT_ANGLE '180.0' CHARS=9 REAL
^^WI     @ 1,7 STATIC RT_T2 'degree'
^^WI     @ 1,9 DROPDOWN RT_PORI
^^WI           ADDTOLIST 'rotation' 'rotoinversion' 'NULL'
^^WI  }
^^WI  @ 3,2 GRID Q NROWS=2 NCOLS=3 {
^^WI     @ 1,1 STATIC RT_T3 'Show results with figure of merit less than'
^^WI     @ 1,3 EDITBOX RT_FOM '5.0' CHARS=7 REAL
^^WI  }
%%^^WI  @ 4,2 GRID S NROWS=6 NCOLS=6 {
%%^^WI     @ 1,1 STATIC RT_T5 'Reject at least'
%%^^WI     @ 1,3 EDITBOX RT_MIN '0' CHARS=7 INTEGER
%%^^WI     @ 1,5 STATIC RT_T6 'worst agreements.'
%%^^WI     @ 3,1 STATIC RT_T7 'Reject at most'
%%^^WI     @ 3,3 EDITBOX RT_MAX '15' CHARS=7 INTEGER
%%^^WI     @ 3,5 STATIC RT_T8 'worst agreements.'
%%^^WI     @ 3,6 STRETCH RT_STR1 HORIZONTAL
%%^^WI     @ 5,1 STATIC RT_T3 'Reject indices >'
%%^^WI     @ 5,3 EDITBOX RT_OUT '2.0' CHARS=7 REAL
%%^^WI     @ 5,5 STATIC RT_T4 'sigma from lattice points.'
%%^^WI  }
^^WI  @ 5,2 GRID B NROWS=2 NCOLS=5 {
^^WI     @ 1,1 STRETCH RT_STR2 HORIZONTAL
^^WI     @ 1,2 BUTTON RT_BOK '&OK' DEFAULT COMMIT
^^WI     @ 1,4 BUTTON RT_BXX '&Cancel' CANCEL
^^WI   }
^^WI }
^^WI SHOW
^^CR
% VERIFY RT_BXX RT_BOK
% GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
% CASE VALUE
%   BLOCK               %CANCEL
^^CO  DISPOSE ROTAX
%     FINISH
%   END BLOCK
%   BLOCK               %ROTAX
%     COPY '#CLOSE SCPDATA'
%     COPY '#RELE PUNCH rotax.pch'
%     EVALUATE L = FILEDELETE ( 'rotax.lis' )
%     COPY '#RELE PRINT rotax.lis'
%     COPY '#ROTAX'
%     COPY 'OUTPUT PUNCH=YES'
%     CLEAR
%     INSERT 'TOLERANCE FOM='
^^?? RT_FOM TEXT
%     GET SILENT REAL ' '
%%%     INSERT 'REJ='
%%^^?? RT_OUT TEXT
%%%     GET SILENT REAL ' '
%%%     INSERT 'MIN='
%%^^?? RT_MIN TEXT
%%%     GET SILENT INTEGER ' '
%%%     INSERT 'MAX='
%%^^?? RT_MAX TEXT
%%%     GET SILENT INTEGER ' '
%     SEND
%     CLEAR
%     INSERT 'ROTATION ANGLE='
^^?? RT_ANGLE TEXT
%     GET SILENT REAL ' '
^^?? RT_PORI SELECTED
%     VERIFY 1 2
%     GET SILENT FINAL NOSTORE ABBREVIATED ' '
%     CASE VALUE
%        INSERT 'INVERT=NO'
%        INSERT 'INVERT=YES'
%     END CASE
^^CO  DISPOSE ROTAX
%     SEND
%     COPY 'END'
%     COPY '#RELEASE PUNCH bfile.pch'
%     COPY '#RELEASE PRINT bfile.lis'
%     COPY '#OPEN SCPDATA rotax.pch'
^^WI WINDOW ROTAX2 'Rotax results' MODAL SIZE KEEP
^^WI POSITION=CENTRED '_MAIN'
^^WI GRID M NROWS=3 NCOLS=5 {
^^WI @ 2,2 GRID MN NROWS=6 NCOLS=1
^^WI {
^^WI  @ 2,1 GRID F NROWS=1 NCOLS=10 {
%     BLOCK
%       ON ERROR TERMINATE
%       ON END TERMINATE
%       EXTRACT NEXT
%       CLEAR
%       EXTRACT TRANSFER COMMAND
%       EXECUTE SEND
%       EXTRACT TRANSFER INPUT
%       CLEAR
%       INSERT !^^WI @ 1,3 STATIC RT_ANGLE '!
%       GET NOPROMPT KEYWORD ' ' 'ERROR'
%       INSERT !'!
%       OUTPUT
%       CLEAR
%       GET NOPROMPT INTEGER ' ' '100'
%       IF VALUE .EQ. 100  THEN
{E Error reading proper/improper flag from rotax.pch
^^WI     @ 1,7 STATIC RT_PORI 'ERROR'
%       ELSE IF VALUE .GT. 0 THEN
^^WI     @ 1,7 STATIC RT_PORI 'rotation'
%       ELSE
^^WI     @ 1,7 STATIC RT_PORI 'rotoinversion'
%       END IF
%     END BLOCK
^^WI     @ 1,1 STATIC RT_T2 'Results for'
^^WI     @ 1,5 STATIC RT_T2 'degree'
^^WI     @ 1,10 BUTTON RT_BDT '&View Details'
^^WI  }
^^WI  @ 3,1 GRID G NROWS=3 NCOLS=3 {
^^WI     @ 2,1 GRID GA NROWS=4 NCOLS=1 {
^^WI        @ 1,1 STATIC RT_T4 'Select a low'
^^WI        @ 2,1 STATIC RT_T4 'figure of merit (f.o.m):'
^^WI        @ 4,1 LISTBOX RT_RES VISLINES=9 INFORM=YES
^^WI              ADDTOLIST
{I Extracting FOM's from ROTAX output.
%     EXTRACT REWIND
%     EXTRACT NEXT
%     LOOP
%       ON ERROR TERMINATE
%       ON END TERMINATE
%%
%% Get the fom for each result
%       EXTRACT NEXT
%       CLEAR
%%       EXTRACT TRANSFER COMMAND
%%       EXECUTE SEND
%       EXTRACT TRANSFER INPUT
%       CLEAR
%       INSERT !^^WI '!
%       GET NOPROMPT REAL ' ' '-1.0'
%       IF VALUE .GT. - 0.01  THEN
%          INSERT !'!
%          OUTPUT
%       END IF
%       EXTRACT NEXT
%       EXTRACT NEXT
%     END LOOP
^^WI              'NULL'
^^WI     }
^^WI     @ 2,3 GRID GB NROWS=3 NCOLS=1 OUTLINE='Details for this f.o.m.' {
^^WI        @ 1,1 GRID GBA NROWS=3 NCOLS=3 {
^^WI           @ 1,1 STATIC RT_T3 'Lattice:'
^^WI           @ 1,3 STATIC RT_RORD 'Reciprocal'
^^WI           @ 2,1 STATIC RT_T3 'Direction:'
^^WI           @ 2,3 STATIC RT_DIRE '-00 -00 -00'
^^WI           @ 3,1 STATIC RT_T3 'Outliers:'
^^WI           @ 3,3 STATIC RT_OUT '00'
^^WI        }
^^WI        @ 3,1 GRID GBB NROWS=5 NCOLS=5 {
^^WI           @ 1,1 EDITBOX M11 '1.000' CHARS=9 REAL
^^WI           @ 1,3 EDITBOX M12 '0.000' CHARS=9 REAL
^^WI           @ 1,5 EDITBOX M13 '0.000' CHARS=9 REAL
^^WI           @ 3,1 EDITBOX M21 '0.000' CHARS=9 REAL
^^WI           @ 3,3 EDITBOX M22 '1.000' CHARS=9 REAL
^^WI           @ 3,5 EDITBOX M23 '0.000' CHARS=9 REAL
^^WI           @ 5,1 EDITBOX M31 '0.000' CHARS=9 REAL
^^WI           @ 5,3 EDITBOX M32 '0.000' CHARS=9 REAL
^^WI           @ 5,5 EDITBOX M33 '1.000' CHARS=9 REAL
^^WI        }
^^WI     }
^^WI  }
^^WI  @ 4,1 GRID B NROWS=2 NCOLS=7 {
^^WI     @ 1,1 STRETCH RT_STR2 HORIZONTAL
^^WI     @ 1,2 BUTTON RT_BOK '&Apply twin law' DEFAULT COMMIT
^^WI     @ 1,4 BUTTON RT_BXX '&Cancel' CANCEL
^^WI  }
^^WI  @ 5,1 GRID E NROWS=2 NCOLS=1 {
^^WI   @ 1,1 GRID EXP1 NROWS=1 NCOLS=3 OUTLINE='' {
^^WI    @ 1,1 ICON IM INFO
^^WI    @ 1,3 GRID EXP2 NROWS=8 NCOLS=1 {
^^WI     @ 1,1 STATIC XRT1 'For two component twins you can use this dialog'
^^WI     @ 2,1 STATIC XRT2 'to choose a suitable twin law and it will'
^^WI     @ 3,1 STATIC XRT3 'be automatically included in the refinement'
^^WI     @ 5,1 STATIC XRT3 'The graph on the right shows how far the'
^^WI     @ 6,1 STATIC XRT3 '30 bad reflections deviate from exact lattice'
^^WI     @ 7,1 STATIC XRT3 'points when transformed by the twin-law matrix'
^^WI     @ 8,1 STATIC XRT3 'above. Green bars are not included in the f.o.m.'
^^WI    }
^^WI   }
^^WI  }
^^WI }
^^WI @ 2,4 PLOTWINDOW _VROTAX NROWS=20 NCOLS=64 }
^^WI SHOW
^^CR
%     LOOP
%%
%% Flush input buffer:
%       GET NOPROMPT NOSTORE TEXT ' ' ' '
%%
%% Loop back here every time. First, fill in the details for the
%% currently selected FOM.
%%
^^??    RT_RES SELECTED
%       GET NOSTORE SILENT INTEGER ' ' ' '
%       EVALUATE NFOM = VALUE
%%
%%
%%
%% Rewind rotax.pch
%       EXTRACT REWIND
%% Skip header line.
%       EXTRACT NEXT
%% Skip (NFOM-1 * 3) lines
%       LOOP ( ( NFOM - 1 ) * 3 ) TIMES
%         ON ERROR TERMINATE
%         ON END TERMINATE
%         EXTRACT NEXT
%       END LOOP
%%
%% Get the fom, the direct/recip text, the direction
%%
%       BLOCK
%         ON ERROR TERMINATE
%         ON END TERMINATE
%         EXTRACT NEXT
%         CLEAR
%%         EXTRACT TRANSFER COMMAND
%%         EXECUTE SEND
%         EXTRACT TRANSFER INPUT
%         GET NOSTORE NOPROMPT REAL ' ' '-1.0'
%         IF VALUE .LT. - 0.01  THEN
{E Error reading FOM from rotax.pch. Requested FOM not found.
%         END IF
%         CLEAR
%         INSERT !^^CO SET RT_RORD TEXT '!
%         GET NOPROMPT KEYWORD ' ' 'error'
%         INSERT !'!
%         OUTPUT
%         CLEAR
%         INSERT !^^CO SET RT_DIRE TEXT '!
%         GET NOPROMPT TEXT ' ' 'error'
%         INSERT !'!
%         OUTPUT
%       END BLOCK
%%
%% Get the outliers and the twin law matrix.
%%
%       BLOCK
%         ON ERROR TERMINATE
%         ON END TERMINATE
%         EXTRACT NEXT
%         CLEAR
%         EXTRACT TRANSFER INPUT
%         CLEAR
%         INSERT !^^CO SET RT_OUT TEXT '!
%         GET NOPROMPT INTEGER ' ' '-1'
%         IF VALUE .LT. 0  THEN
{E Error reading outliers from rotax.pch. Integer not found.
%           INSERT !ERROR!
%         END IF
%         INSERT !'!
%         OUTPUT
%%
%         CLEAR
%         INSERT !^^CO SET M11 TEXT '!
%         GET NOPROMPT REAL ' ' '100.0'
%         IF VALUE .GT. 99.  THEN
%           INSERT !ERROR!
%         END IF
%         INSERT !'!
%         OUTPUT
%         CLEAR
%         INSERT !^^CO SET M12 TEXT '!
%         GET NOPROMPT REAL ' ' '100.0'
%         IF VALUE .GT. 99.  THEN
%           INSERT !ERROR!
%         END IF
%         INSERT !'!
%         OUTPUT
%         CLEAR
%         INSERT !^^CO SET M13 TEXT '!
%         GET NOPROMPT REAL ' ' '100.0'
%         IF VALUE .GT. 99.  THEN
%           INSERT !ERROR!
%         END IF
%         INSERT !'!
%         OUTPUT
%         CLEAR
%         INSERT !^^CO SET M21 TEXT '!
%         GET NOPROMPT REAL ' ' '100.0'
%         IF VALUE .GT. 99.  THEN
%           INSERT !ERROR!
%         END IF
%         INSERT !'!
%         OUTPUT
%         CLEAR
%         INSERT !^^CO SET M22 TEXT '!
%         GET NOPROMPT REAL ' ' '100.0'
%         IF VALUE .GT. 99.  THEN
%           INSERT !ERROR!
%         END IF
%         INSERT !'!
%         OUTPUT
%         CLEAR
%         INSERT !^^CO SET M23 TEXT '!
%         GET NOPROMPT REAL ' ' '100.0'
%         IF VALUE .GT. 99.  THEN
%           INSERT !ERROR!
%         END IF
%         INSERT !'!
%         OUTPUT
%         CLEAR
%         INSERT !^^CO SET M31 TEXT '!
%         GET NOPROMPT REAL ' ' '100.0'
%         IF VALUE .GT. 99.  THEN
%           INSERT !ERROR!
%         END IF
%         INSERT !'!
%         OUTPUT
%         CLEAR
%         INSERT !^^CO SET M32 TEXT '!
%         GET NOPROMPT REAL ' ' '100.0'
%         IF VALUE .GT. 99.  THEN
%           INSERT !ERROR!
%         END IF
%         INSERT !'!
%         OUTPUT
%         CLEAR
%         INSERT !^^CO SET M33 TEXT '!
%         GET NOPROMPT REAL ' ' '100.0'
%         IF VALUE .GT. 99.  THEN
%           INSERT !ERROR!
%         END IF
%         INSERT !'!
%         OUTPUT
%       END BLOCK
%%
%% Get the max outlier value and the binned outlier data.
%%
%       BLOCK
%         ON ERROR TERMINATE
%         ON END TERMINATE
%         EXTRACT NEXT
%         CLEAR
%         EXTRACT TRANSFER INPUT
^^PL PLOTDATA _CLASS BARGRAPH ATTACH _VROTAX KEY
^^PL XAXIS TITLE 'Distance from nearest lattice point (A\**-1)'
^^PL YAXIS TITLE 'Number of reflections'
^^PL NSERIES=2 LENGTH=13
^^PL SERIES 1 SERIESNAME 'Reflections used for f.o.m.'
^^PL SERIES 2 SERIESNAME 'Rejected reflections'
%         CLEAR
%         GET NOSTORE NOPROMPT REAL ' ' '-1.0'
%         EVALUATE BINMAX = VALUE / 1000.0
%         IF VALUE .LT. 0.0  THEN
{E Error reading outliers from rotax.pch. Binmax not found.
%         ELSE
%          LOOP 12 TIMES
%           ON ERROR TERMINATE
%           ON END TERMINATE
%           CLEAR
%           INSERT "^^PL LABEL "
%           IF ( LOOPCOUNTER .EQ. 1 -
            .OR. LOOPCOUNTER .EQ. 5 .OR. LOOPCOUNTER .EQ. 9 ) THEN
%             STORE FORMAT /(F15.6)/ LENGTH 15 REAL -
   (  ( REAL ( LOOPCOUNTER - 1 ) ) * BINMAX / 12.0 )
%           ELSE
%             INSERT "' '"
%           END IF
%           INSERT ' DATA '
%           GET NOPROMPT INTEGER ' ' '-1'
%           GET NOPROMPT INTEGER ' ' '-1'
%           OUTPUT
%          END LOOP
%          CLEAR
%          INSERT "^^PL LABEL "
%          STORE FORMAT /(F15.6)/ LENGTH 15 REAL BINMAX
%          INSERT ' DATA 0 0'
%          OUTPUT
%         END IF
^^PL SHOW
^^CR
%%
%         CLEAR
%         INSERT !^^CO SET M11 TEXT '!
%         GET NOPROMPT REAL ' ' '100.0'
%         IF VALUE .GT. 99.  THEN
%           INSERT !ERROR!
%         END IF
%       END BLOCK
%%
%%
%%
%       VERIFY RT_BXX RT_BDT RT_BOK RT_RES
%       GET NOSTORE SILENT FINAL ABBREVIATED ' ' ' '
%       CASE VALUE
%         BLOCK   %RT_BXX%
^^CO        DISPOSE ROTAX2
%           FINISH
%         END BLOCK
%         BLOCK   %RT_BDT%
%           COPY '#spawn rotax.lis'
%         END BLOCK
%         BLOCK
%%
%%
%%                             
                                                 
%      EVALUATE QTITLE = 'Backup before ROTAX'                              
%      EVALUATE QLINE1 = 'Do you want to backup your'                           
%      EVALUATE QLINE2 = 'dsc before running ROTAX?'                         
%      EVALUATE BUTTOK = '&Yes'                                                 
%      EVALUATE BUTTXX = '&No'                                                  
%      COPY '#SCRIPT XQUESTIO'                                                  
%      IF ANSWER .EQ. TRUE THEN                                                 
%        COPY '#PURGE pre-rotax'                                            
%        copy 'end'                                                             
%      END IF                                                                   
%%                                                                              

%%
%%
^^??        M11 TEXT            
%           GET NOSTORE SILENT REAL ' ' '1.0'
%           EVALUATE MATRIX:M11 = VALUE
^^??        M12 TEXT
%           GET NOSTORE SILENT REAL ' ' '0.0'
%           EVALUATE MATRIX:M12 = VALUE
^^??        M13 TEXT
%           GET NOSTORE SILENT REAL ' ' '0.0'
%           EVALUATE MATRIX:M13 = VALUE
^^??        M21 TEXT
%           GET NOSTORE SILENT REAL ' ' '0.0'
%           EVALUATE MATRIX:M21 = VALUE
^^??        M22 TEXT
%           GET NOSTORE SILENT REAL ' ' '1.0'
%           EVALUATE MATRIX:M22 = VALUE
^^??        M23 TEXT
%           GET NOSTORE SILENT REAL ' ' '0.0'
%           EVALUATE MATRIX:M23 = VALUE
^^??        M31 TEXT
%           GET NOSTORE SILENT REAL ' ' '0.0'
%           EVALUATE MATRIX:M31 = VALUE
^^??        M32 TEXT
%           GET NOSTORE SILENT REAL ' ' '0.0'
%           EVALUATE MATRIX:M32 = VALUE
^^??        M33 TEXT
%           GET NOSTORE SILENT REAL ' ' '1.0'
%           EVALUATE MATRIX:M33 = VALUE
%           EVALUATE ADD25 = TRUE
%           COPY '#SCRIPT ED25'
%           IF ( .NOT. CANCEL25 ) THEN
^^CO          DISPOSE ROTAX2
%             FINISH
%           END IF
%         END BLOCK
%         BLOCK        %%RT_RES
%           GET NOSTORE SILENT INTEGER ' ' ' '
%         END BLOCK
%       END CASE
%     END LOOP
%   END BLOCK
% END CASE
%END SCRIPT
                                  
