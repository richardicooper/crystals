%SCRIPT XPREFS
%%
% VARIABLE INTEGER FFSQ
% VARIABLE CHARACTER CFILE
% EVALUATE FFSQ = 0
%% }
%%
%% Check for already stored user preference about FSq
^^?? GETKEY FSQORF
%    GET SILENT NOSTORE FINAL TEXT 'NONE' 'NONE'
%    IF CVALUE .EQ. 'F' THEN
%      EVALUATE FFSQ = 1
%    ELSE IF CVALUE .EQ. 'FSQ' THEN
%      EVALUATE FFSQ = 2
%    END IF
%%
^^WI WINDOW XPREFS 'Preferences' MODAL COMMIT='PREFS_OK' CANCEL='PREFS_XX'  
^^WI GRID MAIN NROWS=9 NCOLS=3 
^^WI {
^^WI   @ 2,2 GRID Opts NROWS=3 NCOLS=3
^^WI   {
^^WI     @ 1,1 STATIC S1 'Default minimisation function'
^^WI     @ 1,3 GRID Gr NROWS=1 NCOLS=5
^^WI     {
^^WI       @ 1,1 RADIOBUTTON FFSQASK 'Ask me'
%          IF ( FFSQ .EQ. 0 ) THEN
^^WI          STATE=ON                                                    
%          END IF
^^WI       @ 1,3 RADIOBUTTON FFSQF 'w[Fo-Fc]^2'
%          IF ( FFSQ .EQ. 1 ) THEN
^^WI          STATE=ON                                                    
%          END IF
^^WI       @ 1,5 RADIOBUTTON FFSQF2 'w[Fo^2-Fc^2]^2'
%          IF ( FFSQ .EQ. 2 ) THEN
^^WI          STATE=ON                                                    
%          END IF
^^WI     }
^^WI   }
^^WI   @ 4,2 GRID PLATO NROWS=1 NCOLS=5
^^WI   {
^^WI     @ 1,1 STATIC TP1 'Platon executable'
^^WI     @ 1,3 EDITBOX XPLAT ' ' CHARS=32
^^WI     @ 1,5 BUTTON BPLAT 'Browse...'
^^WI   }   
^^WI   @ 6,2 GRID EDITO NROWS=1 NCOLS=5
^^WI   {
^^WI     @ 1,1 STATIC TE1 'External editor'
^^WI     @ 1,3 EDITBOX XEDITO ' ' CHARS=32
^^WI     @ 1,5 BUTTON BEDITO 'Browse...'
^^WI   }   
^^WI   @ 8,2 GRID BTNS NROWS=1 NCOLS=5
^^WI   {                                                                        
^^WI     @ 1,1 STRETCH MF_ST HORIZONTAL                                         
^^WI     @ 1,2 BUTTON PREFS_OK '&OK' LENGTH='Cancel' DEFAULT COMMIT                         
^^WI     @ 1,4 BUTTON PREFS_XX '&Cancel' CANCEL                     
^^WI     @ 1,5 STRETCH MF_ST HORIZONTAL                                         
^^WI   }
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
%%
%%
^^CO GETKEY PLATONDIR                                                                                                                                         
%  GET SILENT NOSTORE FINAL TEXT '1' '1'                                        
%  EVALUATE CFILE = CVALUE                                         
%  IF CFILE .EQ. '1' THEN
^^CO  SET XPLAT TEXT ' '
%  ELSE IF FILEEXISTS ( CFILE ) .EQ. FALSE THEN 
^^CO  SET XPLAT TEXT ' '
{E Stored platon.exe location does not exist.
%  ELSE
%    TRANSFER "^^CO SET XPLAT TEXT '" // CFILE // "'" TO DISPLAY
%  END IF
%%
^^CO GETKEY CRYEDITOR
%  GET SILENT NOSTORE FINAL TEXT '1' '1'                                        
%  EVALUATE CFILE = CVALUE                                         
%  IF CFILE .EQ. '1' THEN
^^CO  SET XEDITO TEXT ' '
%  ELSE IF FILEEXISTS ( CFILE ) .EQ. FALSE THEN 
^^CO  SET XEDITO TEXT ' '
{E Stored editor executable does not exist.
%  ELSE
%    TRANSFER "^^CO SET XEDITO TEXT '" // CFILE // "'" TO DISPLAY
%  END IF
%%
% LOOP
%   VERIFY PREFS_OK PREFS_XX BPLAT BEDITO
%   GET SILENT NOSTORE FINAL ABBREVIATED ' ' 'PREFS_OK'
%   CASE VALUE
%     BLOCK
%       VERIFY ON OFF                                                             
^^??      FFSQASK STATE
%       GET SILENT NOSTORE FINAL ABBREVIATED ' '                                  
%       IF VALUE .EQ. 1 THEN
^^CO      SETKEY FSQORF ASK
%       ELSE
^^??      FFSQF STATE
%         GET SILENT NOSTORE FINAL ABBREVIATED ' '                                  
%         IF VALUE .EQ. 1 THEN
^^CO        SETKEY FSQORF F
%         ELSE
^^CO        SETKEY FSQORF FSQ
%         END IF
%       END IF
^^??    XPLAT TEXT
%       CLEAR                                                                      
%       INSERT '^^CO SETKEY PLATONDIR "'                                           
%       GET SILENT TEXT 'Platon location:' ' '                                         
%       INSERT '"'                                                               
%       OUTPUT                                                                   
^^??    XEDITO TEXT
%       CLEAR                                                                      
%       INSERT '^^CO SETKEY CRYEDITOR "'                                           
%       GET SILENT TEXT 'Editor location:' ' '                                         
%       INSERT '"'                                                               
%       OUTPUT                                                                   
^^CO    DISPOSE XPREFS                                                           
%       FINISH                                                                   
%     END BLOCK                                                                   
%     BLOCK                                                                       
^^CO    DISPOSE XPREFS                                                           
%       FINISH                                                                   
%     END BLOCK                                                                   
%     BLOCK   % BPLAT
^^CO SYSOPENFILE 'Platon*.exe' 'Platon executable'
%     GET NOSTORE SILENT TEXT 'Platon location:'  
%      IF CVALUE .NE. 'CANCEL' THEN               
%       EVALUATE CFILE = CVALUE                   
%       TRANSFER "^^CO SET XPLAT TEXT '" // CFILE // "'" TO DISPLAY
%      END IF
%     END BLOCK
%     BLOCK   % BEDITO
^^CO SYSOPENFILE '*.exe' 'Editor executable'
%     GET NOSTORE SILENT TEXT 'Editor location:'  
%      IF CVALUE .NE. 'CANCEL' THEN               
%       EVALUATE CFILE = CVALUE                   
%       TRANSFER "^^CO SET XEDITO TEXT '" // CFILE // "'" TO DISPLAY
%      END IF
%     END BLOCK
%   END CASE
% END LOOP
%END SCRIPT                                                                    

