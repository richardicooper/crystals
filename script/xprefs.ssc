%SCRIPT XPREFS
%%
% VARIABLE INTEGER FFSQ
% EVALUATE FFSQ = 0
%% 
%%
%% Check for already stored user preference about FSq
^^?? GETKEY FSQORF
%    GET SILENT NOSTORE FINAL TEXT 'NONE' 'NONE'
%    IF CVALUE .EQ. 'F' THEN
%      EVALUATE FFSQ = 1
%    ELSE IF CVALUE .EQ. 'FSQ' THEN
%      EVALUATE FFSQ = 2
%    END IF
%%
^^WI WINDOW XPREFS 'Preferences' MODAL COMMIT='PREFS_OK' CANCEL='PREFS_XX'  
^^WI GRID MAIN NROWS=5 NCOLS=3 
^^WI {
^^WI   @ 2,2 GRID Opts NROWS=3 NCOLS=3
^^WI   {
^^WI     @ 1,1 STATIC S1 'Default minimisation function'
^^WI     @ 1,3 GRID Gr NROWS=1 NCOLS=5
^^WI     {
^^WI       @ 1,1 RADIOBUTTON FFSQASK 'Ask me'
%          IF ( FFSQ .EQ. 0 ) THEN
^^WI          STATE=ON                                                    
%          END IF
^^WI       @ 1,3 RADIOBUTTON FFSQF 'w[Fo-Fc]^2'
%          IF ( FFSQ .EQ. 1 ) THEN
^^WI          STATE=ON                                                    
%          END IF
^^WI       @ 1,5 RADIOBUTTON FFSQF2 'w[Fo^2-Fc^2]^2'
%          IF ( FFSQ .EQ. 2 ) THEN
^^WI          STATE=ON                                                    
%          END IF
^^WI     }
^^WI   }
^^WI   @ 4,2 GRID BTNS NROWS=1 NCOLS=5
^^WI   {                                                                        
^^WI     @ 1,1 STRETCH MF_ST HORIZONTAL                                         
^^WI     @ 1,2 BUTTON PREFS_OK '&OK' LENGTH='Cancel' DEFAULT COMMIT                         
^^WI     @ 1,4 BUTTON PREFS_XX '&Cancel' CANCEL                     
^^WI     @ 1,5 STRETCH MF_ST HORIZONTAL                                         
^^WI   }
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
%   VERIFY PREFS_OK PREFS_XX
%   GET SILENT NOSTORE FINAL ABBREVIATED ' ' 'PREFS_OK'
%   CASE VALUE
%     BLOCK
%       VERIFY ON OFF                                                             
^^??      FFSQASK STATE
%       GET SILENT NOSTORE FINAL ABBREVIATED ' '                                  
%       IF VALUE .EQ. 1 THEN
^^CO      SETKEY FSQORF ASK
%       ELSE
^^??      FFSQF STATE
%         GET SILENT NOSTORE FINAL ABBREVIATED ' '                                  
%         IF VALUE .EQ. 1 THEN
^^CO        SETKEY FSQORF F
%         ELSE
^^CO        SETKEY FSQORF FSQ
%         END IF
%       END IF
^^CO    DISPOSE XPREFS                                                           
%       FINISH                                                                   
%     END BLOCK                                                                   
%     BLOCK                                                                       
^^CO    DISPOSE XPREFS                                                           
%       FINISH                                                                   
%     END BLOCK                                                                   
%   END CASE
%END SCRIPT                                                                    

