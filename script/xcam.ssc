%SCRIPT XCAM
% VARIABLE LOGICAL NOXCAMQ LNEWL5
% EVALUATE LNEWL5 = TRUE
% IF ( NOXCAMQ ) THEN
%    EVALUATE LNEWL5 = TRUE
% ELSE
%%   find out if the caller wants CRYSTALS to write new files
%    VERIFY YES NO
%    GET NOSTORE FINAL ABBREVIATED -
     'Send current structure to CAMERON?' 'YES'
%    IF VALUE .NE. 1 THEN
%       EVALUATE LNEWL5 = FALSE
%    END IF
% END IF
^^WI WINDOW _CAMWINDOW 'Cameron' KEEP SIZE MODAL CANCEL='END'
%% The first grid splits the main area from the status bar.
^^WI GRID SPLIT NROWS=2 NCOLS=1
^^WI {
^^WI   @ 2,1 EDITBOX _CAMTEXTINPUT '' NCOLS=1
^^WI         LIMIT=80 SENDONRETURN=YES INPUT
^^WI   @ 1,1 RESIZE _CAMRSZ1 HORIZONTAL
^^WI   {
^^WI     ITEM GRID _CAMDISPBTNS NROWS=1 NCOLS=1
^^WI     {
^^WI       @ 1,1 RESIZE _CAMRSZ2 VERTICAL
^^WI       {
^^WI         ITEM GRID _CAMDISP NROWS=1 NCOLS=1
^^WI         {
^^WI            @ 1,1 CHARTWINDOW _CAMERONVIEW NROWS=25 NCOLS=45
^^WI            INFORM=YES
^^WI         }
^^WI         ITEM GRID _CAMBTNS NROWS=9 NCOLS=1
^^WI         {
^^WI           @ 1,1 GRID INCRE NROWS=3 NCOLS=3
^^WI           {
^^WI               @ 2,1 STATIC   TH 'Increment'
^^WI               @ 2,3 DROPDOWN XC_BL ADDTOLIST
^^WI               '.1' '.2' '.5' '1.' '5.' '10.' '45.'
^^WI               'NULL'
^^WI           }
^^WI           @ 2,1 GRID MOVE NROWS=3 NCOLS=5
^^WI                 COMMAND='#SCRIPT xcrotate'
^^WI           {
^^WI             @ 1,1 BUTTON XROTP '+X'     @ 1,2 BUTTON XROTN '-X'
^^WI             @ 2,1 BUTTON YROTP '+Y'     @ 2,2 BUTTON YROTN '-Y'
^^WI             @ 3,1 BUTTON ZROTP '+Z'     @ 3,2 BUTTON ZROTN '-Z'
^^WI             @ 1,3 BUTTON XROT90 '+90'   @ 2,3 BUTTON YROT90 '+90'
^^WI             @ 3,3 BUTTON ZROT90 '+90'
^^WI             @ 1,4 BUTTON AXISA 'Axis a' @ 1,5 BUTTON PLANE 'Plane'
^^WI             @ 2,4 BUTTON AXISB 'Axis b' @ 2,5 BUTTON HKL   'hkl'
^^WI             @ 3,4 BUTTON AXISC 'Axis c' @ 3,5 BUTTON CURSO 'Cursor'
^^WI           }
^^WI           @ 4,1 GRID STYLE NROWS=4 NCOLS=7
^^WI                 COMMAND='#SCRIPT xnstyle'
^^WI           {
^^WI             @ 1,2 DROPDOWN LSTYLE INFORM=YES ADDTOLIST
^^WI             'Spheres' 'Lines' 'Ellipses' 'NULL'
^^WI             @ 1,4 DROPDOWN LCOLOR INFORM=YES ADDTOLIST
^^WI              'Default' 'B&W' 'B&W-2' 'NULL'
^^WI             @ 3,2 DROPDOWN LPACK INFORM=YES ADDTOLIST
^^WI             'Unpack' 'Complete' 'Centroid' 'Cut' 'Window' 'NULL'
^^WI             @ 3,4 BUTTON COLBUT 'Colours'
^^WI           }
^^WI           @ 7,1 GRID STUFF NROWS=5 NCOLS=2
^^WI                 COMMAND='#SCRIPT xcalter'
^^WI           {
^^WI              @ 1,1 BUTTON OHCONT 'Find O---H'
^^WI              @ 1,2 BUTTON VIEW   'View'
^^WI              @ 2,1 BUTTON SLABEL 'Labels on'
^^WI              @ 2,2 BUTTON MLABEL 'Label move'
^^WI              @ 3,1 BUTTON HHYDRO 'Hide H'
^^WI              @ 3,2 BUTTON HPEAK  'Hide Q'
^^WI              @ 4,1 BUTTON SCELL  'Show cell'
^^WI              @ 4,2 BUTTON INCAL  'Show all'
^^WI              @ 5,1 BUTTON INCAR 'Incl area'
^^WI              @ 5,2 BUTTON EXCAR 'Excl area'
^^WI           }
^^WI         }
^^WI       }
^^WI     }
^^WI     ITEM GRID _CAMTXOUT NROWS=1 NCOLS=1
^^WI     {
^^WI       @ 1,1 TEXTOUT _CAMTEXT '' NROWS=40 NCOLS=10
^^WI     }
^^WI   }
^^WI }
^^WI DEFINEMENU MAIN 'NULL'
^^WI  MENU _FILE '&File'
^^WI   ITEM _SAVE '&Save Image' '#SCRIPT XPOST'
^^WI   ITEM _CHIME 'Save &Chime' 'OUTPUT CHIME'
^^WI   SPLIT
^^WI   ITEM _CLOSE '&Exit Cameron' 'END'
^^WI  ENDMENU
^^WI  MENU STYLE '&Style'
^^WI   ITEM _ELLI '&Ellipse' 'ELLIPSE TYPE ALL 3 VIEW _N'
^^WI   ITEM _BALL '&Ball' 'BALL ALL VIEW _N'
^^WI   ITEM _LINE '&Line' 'LINE ALL VIEW _N'
^^WI   MENU _TYPE 'Ellipse &Types'
^^WI    ITEM _ELL1 'Plain' 'ELLIPSE TYPE ALL 1 VIEW _N'
^^WI    ITEM _ELL1 'Bounded' 'ELLIPSE TYPE ALL 2 VIEW _N'
^^WI    ITEM _ELL1 'Axes' 'ELLIPSE TYPE ALL 3 VIEW _N'
^^WI    ITEM _ELL1 'Shaded' 'ELLIPSE TYPE ALL 4 VIEW _N'
^^WI   ENDMENU
^^WI   SPLIT
^^WI   ITEM _FILL '&Filled' 'FILL VIEW _N'
^^WI   ITEM _FILL '&Empty' 'UNFILL VIEW _N'
^^WI  ENDMENU
^^WI  MENU VISI '&Visible'
^^WI   ITEM _EXCH '&Hide hydrogen' '#SCRIPT XCALTER_NHHYDRO'
^^WI   ITEM _INCH '&Show hydrogen' '#SCRIPT XCALTER_NSHYDRO'
^^WI   ITEM _EXCQ '&Hide peaks'    '#SCRIPT XCALTER_NHPEAK'
^^WI   ITEM _INCQ '&Show peaks'    '#SCRIPT XCALTER_NSPEAK'
^^WI   SPLIT
^^WI   ITEM _EXCL '&Show labels'    '#SCRIPT XCALTER_NSLABEL'
^^WI   ITEM _INCL '&Hide labels'    '#SCRIPT XCALTER_NHLABEL'
^^WI  ENDMENU
^^WI  MENU _ORIE '&Orientation'
^^WI   ITEM _ORIENT '&Orientation...' '#SCRIPT XORIENT'
^^WI  ENDMENU
^^WI  MENU PACK '&Packing'
^^WI   ITEM _COMP 'Com&plete' 'PACK CELL COMP VIEW _N'
^^WI   ITEM _CENT '&Centroid' 'PACK CELL CENT VIEW _N'
^^WI   ITEM _COMP 'Cu&t'      'PACK CELL CUT  VIEW _N'
^^WI   ITEM _COMP '&Window'   '#SCRIPT XCWINDOW'
^^WI   SPLIT
^^WI   ITEM _UNPK '&Unpack'   'UNPACK VIEW _N'
^^WI   SPLIT
^^WI   ITEM _SCELL '&Show cell' 'INCLUDE CELL VIEW _N'
^^WI   ITEM _HCELL '&Hide cell' 'EXCLUDE CELL VIEW _N'
^^WI  ENDMENU
^^WI ENDDEFINEMENU
^^WI SHOW
^^CR
^^WI SENDTEXTTO _CAMTEXT
^^CR
%% Set increment default to 10 degrees
^^CO SET XC_BL SELECTION=6
%VARIABLE INTEGER ATOMSIN ATOMSOUT ATOMCHANGE
%%
%% COUNT THE NUMBER OF ATOMS PASSED TO CAMERON.
%%
% COPY '#SCRIPT NATOMS'
% EVALUATE ATOMSIN = ATOMSOUT
% COPY 'END'
% IF LNEWL5 .EQ. TRUE THEN
%   COPY '#XCAM '
% ELSE
% COPY '#XCAM MODE = SPECIAL'
% END IF
% COPY 'END'
% COPY ' '
% COPY 'BALL ALL PLANE ALL VIEW'
% COPY ' '
% LOOP
%  ON END TERMINATE
%  CLEAR
%  GET SILENT TEXT ' ' ' '
%  TRANSFER CVALUE TO CRYSTALS
% END LOOP
% COPY ' '
% COPY 'END'
% COPY ' '
^^WI DISPOSE _CAMWINDOW
^^CR
%   VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2
%   VARIABLE LOGICAL ANSWER
%   EVALUATE QTITLE = 'Return to Crystals'
%   EVALUATE QLINE1 = 'Do you want to apply any changes'
%   EVALUATE QLINE2 = 'made in CAMERON?'
%   EVALUATE BUTTOK = '&Yes'
%   EVALUATE BUTTXX = '&No'
%   COPY '#SCRIPT XQUESTIO'
%   IF ANSWER .EQ. TRUE THEN
 
 * Reading list of atoms.
%           COPY '#USE CAMERON.L5'
%   ELSE
%       FINISH
%   END IF
%% COUNT THE NUMBER OF ATOMS PASSED BACK FROM CAMERON.
 * Checking atoms.
%   COPY '#SCRIPT NATOMS'
%       EVALUATE ATOMCHANGE = ATOMSOUT - ATOMSIN
%   IF ATOMCHANGE .GT. 0 THEN
 
 
  WW    WW   AAAA   RRRR   NN   NN  IIIIII  NN   NN   GGGG
  WW    WW  AA  AA  RR RR  NNN  NN    II    NNN  NN  GG  GG
  WW WW WW  AAAAAA  RRRR   NN N NN    II    NN N NN  GG
  WW WW WW  AA  AA  RR RR  NN  NNN    II    NN  NNN  GG GGGG
   WW  WW   AA  AA  RR RR  NN   NN  IIIIII  NN   NN   GGGG
 
  Very Important:
 
  The number of atoms that has returned to CRYSTALS is larger
  than the number that was sent to CAMERON....
 
  This could mean that the structure was PACKed when you exited
  Cameron. It is strongly recommended that you revert to the
  previous structural model, and run CAMERON again.
 
%    EVALUATE QTITLE = 'Too many atoms?'
%    EVALUATE QLINE1 = 'The number of atoms returned is larger than'
%    EVALUATE QLINE2 = 'the number sent. Restore previous atom list?'
%    EVALUATE BUTTOK = '&Yes'
%    EVALUATE BUTTXX = '&No'
%    COPY '#SCRIPT XQUESTIO'
%    IF ANSWER .EQ. TRUE THEN
 * Reading original list of atoms.
%          COPY '#USE CAMERON.L5I'
%    ELSE
%          FINISH
%    END IF
%   ELSE
%       FINISH
%   END IF
%END SCRIPT
