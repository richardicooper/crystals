%SCRIPT XCAM                                                                    
% VARIABLE LOGICAL NOXCAMQ LNEWL5                                               
% VARIABLE CHARACTER LLINE                                                      
% IF .NOT. ( NOXCAMQ ) THEN                                                     
%    EVALUATE LNEWL5 = TRUE                                                     
%% find out if the caller wants CRYSTALS to write new files                     
%    VERIFY YES NO                                                              
%    GET NOSTORE FINAL ABBREVIATED -                                            
     'Send current structure to CAMERON?' 'YES'                                 
%    IF VALUE .NE. 1 THEN                                                       
%       EVALUATE LNEWL5 = FALSE                                                 
%    END IF                                                                     
% END IF                                                                        
^^WI WINDOW _CAMWINDOW 'Cameron' KEEP SIZE LARGE MODAL CANCEL='END'             
%% The first grid splits the main area from the status bar.                     
^^WI GRID SPLIT NROWS=2 NCOLS=1                                                 
^^WI {                                                                          
^^WI   @ 2,1 GRID _INPUTSTUFF NROWS=1 NCOLS=3                                   
^^WI   {                                                                        
^^WI     @ 1,1 EDITBOX _CAMTEXTINPUT '' NCOLS=1                                 
^^WI         LIMIT=80 SENDONRETURN=YES INPUT                                    
^^WI     @ 1,3 BUTTON CONTINUE 'Continue line'                                  
^^WI   }                                                                        
^^WI   @ 1,1 RESIZE _CAMRSZ1 HORIZONTAL                                         
^^WI   {                                                                        
^^WI     ITEM GRID _CAMDISPBTNS NROWS=1 NCOLS=1                                 
^^WI     {                                                                      
^^WI       @ 1,1 RESIZE _CAMRSZ2 VERTICAL                                       
^^WI       {                                                                    
^^WI         ITEM GRID _CAMDISP NROWS=1 NCOLS=1                                 
^^WI         {                                                                  
^^WI            @ 1,1 CHARTWINDOW _CAMERONVIEW NROWS=30 NCOLS=45                
^^WI            INFORM=YES                                                      
^^WI         }                                                                  
^^WI         ITEM GRID _CAMBTNS NROWS=6 NCOLS=3                                 
^^WI         {                                                                  
^^WI           @ 1,2 GRID INCRE NROWS=3 NCOLS=5                                 
^^WI           {                                                                
^^WI               @ 2,1 STATIC TH 'Increment'                                  
^^WI               @ 2,3 DROPDOWN XC_BL ADDTOLIST                               
^^WI               '.1' '.2' '.5' '1.' '5.' '10.' '45.'                         
^^WI               'NULL'                                                       
^^WI               @ 2,5 STATIC TD 'degrees'                                    
^^WI           }                                                                
^^WI           @ 2,2 GRID MOVE NROWS=3 NCOLS=5                                  
^^WI                 COMMAND='#SCRIPT xcrotate'                                 
^^WI           {                                                                
^^WI             @ 1,1 BUTTON XROTP '+X'     @ 1,2 BUTTON XROTN '-X'            
^^WI             @ 2,1 BUTTON YROTP '+Y'     @ 2,2 BUTTON YROTN '-Y'            
^^WI             @ 3,1 BUTTON ZROTP '+Z'     @ 3,2 BUTTON ZROTN '-Z'            
^^WI             @ 1,3 BUTTON XROT90 '+90'   @ 2,3 BUTTON YROT90 '+90'          
^^WI             @ 3,3 BUTTON ZROT90 '+90'                                      
^^WI             @ 1,4 BUTTON AXISA 'Axis a' @ 1,5 BUTTON PLANE 'Plane'         
^^WI             @ 2,4 BUTTON AXISB 'Axis b' @ 2,5 BUTTON HKL   'hkl'           
^^WI             @ 3,4 BUTTON AXISC 'Axis c' @ 3,5 BUTTON CURSO 'Cursor'        
^^WI           }                                                                
^^WI           @ 4,2 GRID STYLE NROWS=6 NCOLS=3                                 
^^WI                 COMMAND='#SCRIPT xnstyle'                                  
^^WI           {                                                                
^^WI             @ 1,1 DROPDOWN LSTYLE INFORM=YES ADDTOLIST                     
^^WI             'Spheres' 'Lines' 'Ellipses' 'NULL'                            
^^WI             @ 1,3 BUTTON COLBUT 'Colour Scheme'                                  
^^WI             @ 3,1 DROPDOWN LPACK INFORM=YES ADDTOLIST                      
^^WI             'Unpack' 'Complete' 'Centroid' 'Cut' 'Window' 'NULL'           
^^WI             @ 3,3 DROPDOWN LCOLOR INFORM=YES ADDTOLIST                     
^^WI              'Coloured' 'B&W' 'B&W-2' 'Obey' 'NULL'                                
^^WI             @ 5,1 DROPDOWN LFONT INFORM=YES ADDTOLIST                      
^^WI             'Font 4' 'Font 6'
^^WI             'Font 8' 'Font 9' 'Font 10' 'Font 12' 'Font 16'                
^^WI             'Font 20' 'Font 24' 'Font 28' 'Font 32'                        
^^WI             'Font 48' 'Font 72' 'NULL'                                     
^^WI           }                                                                
^^WI           @ 6,2 GRID STUFF NROWS=6 NCOLS=2                                 
^^WI                 COMMAND='#SCRIPT xcalter'                                  
^^WI           {                                                                
^^WI              @ 1,1 BUTTON OHCONT 'Find-Hbond'                              
^^WI              @ 1,2 BUTTON VIEW   'View'                                    
^^WI              @ 2,1 BUTTON SLABEL 'Labels on'                               
^^WI              @ 2,2 BUTTON MLABEL 'Label move'                              
^^WI              @ 3,1 BUTTON HHYDRO 'Hide H'                                  
^^WI              @ 3,2 BUTTON HPEAK  'Hide Q'                                  
^^WI              @ 4,1 BUTTON SCELL  'Show cell'                               
^^WI              @ 4,2 BUTTON INCAL  'Show all'                                
^^WI              @ 5,1 BUTTON INCAR 'Incl area'                                
^^WI              @ 5,2 BUTTON EXCAR 'Excl area'                                
^^WI              @ 6,1 BUTTON SSYMM  'Show Symm'                               
^^WI           }                                                                
^^WI         }                                                                  
^^WI       }                                                                    
^^WI     }                                                                      
^^WI     ITEM GRID _CAMTXOUT NROWS=1 NCOLS=1                                    
^^WI     {                                                                      
^^WI       @ 1,1 TEXTOUT _CAMTEXT '' NROWS=5 NCOLS=10                           
^^WI     }                                                                      
^^WI   }                                                                        
^^WI }                                                                          
^^WI DEFINEMENU MAIN 'NULL'                                                     
^^WI  MENU _FILE '&File'                                                        
^^WI   ITEM _SAVE 'Save Pos&tscript' '#SCRIPT XPOST'                            
^^WI   ITEM _SAVE '&Save metafile' '#SCRIPT XWMF_N_CAMERONVIEW'                 
^^WI   ITEM _SAVE '&Save enhanced metafile' '#SCRIPT XEMF_N_CAMERONVIEW'        
^^WI   ITEM _PDB  'Save &PDB' 'OUTPUT CHIME'                                 
^^WI   SPLIT                                                                    
^^WI   ITEM _ARCH 'Archive present view' '#SCRIPT ARCHIVE_CAM'                  
^^WI   ITEM _RETR 'Retrieve view from file' '#SCRIPT RETRIEVE_CAM'              
^^WI   SPLIT                                                                    
^^WI   ITEM _PRINT 'Print...' '^^CO SET _CAMERONVIEW CHARTPRINT'                
^^WI   ITEM _CLOSE '&Exit Cameron' 'END'                                        
^^WI  ENDMENU                                                                   
^^WI  MENU STYLE '&Style'                                                       
^^WI   ITEM _ELLI '&Ellipse' 'ELLIPSE TYPE ALL 3 VIEW _N'                       
^^WI   ITEM _BALL '&Ball' 'BALL ALL VIEW _N'                                    
^^WI   ITEM _LINE '&Line' 'LINE ALL VIEW _N'                                    
^^WI   MENU _TYPE 'Ellipse &Types'                                              
^^WI    ITEM _ELL1 'Plain' 'ELLIPSE TYPE ALL 1 VIEW _N'                         
^^WI    ITEM _ELL1 'Bounded' 'ELLIPSE TYPE ALL 2 VIEW _N'                       
^^WI    ITEM _ELL1 'Axes' 'ELLIPSE TYPE ALL 3 VIEW _N'                          
^^WI    ITEM _ELL1 'Shaded' 'ELLIPSE TYPE ALL 4 VIEW _N'                        
^^WI   ENDMENU                                                                  
^^WI   SPLIT                                                                    
^^WI   ITEM _FILL '&Filled' 'FILL VIEW _N'                                      
^^WI   ITEM _FILL '&Empty' 'UNFILL VIEW _N'                                     
^^WI  ENDMENU                                                                   
^^WI  MENU VISI '&Visible'                                                      
^^WI   ITEM _INCH '&Hide hydrogen' '#SCRIPT XCALTER_NHHYDRO'                    
^^WI   ITEM _EXCQ '&Hide peaks'    '#SCRIPT XCALTER_NHPEAK'                     
^^WI   ITEM _INCQ '&Show peaks'    '#SCRIPT XCALTER_NSPEAK'                     
^^WI   SPLIT                                                                    
^^WI   ITEM _INCL '&Show labels'    '#SCRIPT XCALTER_NSLABEL'                   
^^WI   ITEM _SSYMM '&Show symmetry'  '#SCRIPT XCALTER_NSSYMM'                   
^^WI  ENDMENU                                                                   
^^WI  MENU _ORIE '&Orientation'                                                 
^^WI   ITEM _ORIENT '&Orientation...' '#SCRIPT XORIENT'                         
^^WI  ENDMENU                                                                   
^^WI  MENU PACK '&Packing'                                                      
^^WI   ITEM _COMP 'Com&plete' 'PACK CELL COMP VIEW _N'                          
^^WI   ITEM _CENT '&Centroid' 'PACK CELL CENT VIEW _N'                          
^^WI   ITEM _COMP 'Cu&t'      'PACK CELL CUT  VIEW _N'                          
^^WI   ITEM _COMP '&Window'   '#SCRIPT XCWINDOW'                                
^^WI   SPLIT                                                                    
^^WI   ITEM _UNPK '&Unpack'   'UNPACK VIEW _N'                                  
^^WI   SPLIT                                                                    
^^WI   ITEM _SCELL '&Show cell' '#SCRIPT XCALTER_NSCELL'                        
^^WI  ENDMENU                                                                   
^^WI  MENU HELP '&Help'                                                         
^^WI   ITEM HTIPS '&Tip of the day'                                             
^^WI   '#SCRIPT TIPS'                                                           
^^WI   ITEM P1 '&Primer: Seeing the structure'                                  
^^WI   '#SPAWN CRYSDIR:manual/primer-9.html'                                    
^^WI   MENU M1 '&Cameron Manual'                                                
^^WI   ITEM C1 '&Contents'                                                      
^^WI   '#SPAWN CRYSDIR:manual/cameron.html'                                     
^^WI   ITEM C1 '&Introduction'                                                  
^^WI   '#SPAWN CRYSDIR:manual/cameron-1.html'                                   
^^WI   ITEM C1 '&Getting Started'                                               
^^WI   '#SPAWN CRYSDIR:manual/cameron-2.html'                                   
^^WI   ITEM C1 '&Data Input'                                                    
^^WI   '#SPAWN CRYSDIR:manual/cameron-3.html'                                   
^^WI   ITEM C1 '&Outputting data'                                               
^^WI   '#SPAWN CRYSDIR:manual/cameron-4.html'                                   
^^WI   ITEM C1 '&Editing the atom list'                                         
^^WI   '#SPAWN CRYSDIR:manual/cameron-5.html'                                   
^^WI   ITEM C1 '&Obeying files'                                                 
^^WI   '#SPAWN CRYSDIR:manual/cameron-6.html'                                   
^^WI   ITEM C1 '&Archiving Views'                                               
^^WI   '#SPAWN CRYSDIR:manual/cameron-7.html'                                   
^^WI   ITEM C1 '&Output devices'                                                
^^WI   '#SPAWN CRYSDIR:manual/cameron-8.html'                                   
^^WI   ITEM C1 '&View direction control'                                        
^^WI   '#SPAWN CRYSDIR:manual/cameron-9.html'                                   
^^WI   ITEM C1 '&Include and exclude'                                           
^^WI   '#SPAWN CRYSDIR:manual/cameron-10.html'                                  
^^WI   ITEM C1 '&Drawing style control'                                         
^^WI   '#SPAWN CRYSDIR:manual/cameron-11.html'                                  
^^WI   ITEM C1 '&Connectivity control'                                          
^^WI   '#SPAWN CRYSDIR:manual/cameron-12.html'                                  
^^WI   ITEM C1 '&Control of colour'                                             
^^WI   '#SPAWN CRYSDIR:manual/cameron-13.html'                                  
^^WI   ITEM C1 '&Atom labelling'                                                
^^WI   '#SPAWN CRYSDIR:manual/cameron-14.html'                                  
^^WI   ITEM C1 '&Other picture control commands'                                
^^WI   '#SPAWN CRYSDIR:manual/cameron-15.html'                                  
^^WI   ITEM C1 '&Symmetry input'                                                
^^WI   '#SPAWN CRYSDIR:manual/cameron-16.html'                                  
^^WI   ITEM C1 '&Crystal packing commands'                                      
^^WI   '#SPAWN CRYSDIR:manual/cameron-17.html'                                  
^^WI   ITEM C1 '&Add and Move (More symmetry)'                                  
^^WI   '#SPAWN CRYSDIR:manual/cameron-18.html'                                  
^^WI   ITEM C1 '&Distances and angles'                                          
^^WI   '#SPAWN CRYSDIR:manual/cameron-19.html'                                  
^^WI   ITEM C1 '&Data held in the program'                                      
^^WI   '#SPAWN CRYSDIR:manual/cameron-20.html'                                  
^^WI   ITEM C1 '&Group definitions'                                             
^^WI   '#SPAWN CRYSDIR:manual/cameron-21.html'                                  
^^WI   ITEM C1 '&Misc. commands'                                                
^^WI   '#SPAWN CRYSDIR:manual/cameron-22.html'                                  
^^WI   ITEM C1 '&Stopping the program'                                          
^^WI   '#SPAWN CRYSDIR:manual/cameron-23.html'                                  
^^WI   ITEM C1 '&Menu definition'                                               
^^WI   '#SPAWN CRYSDIR:manual/cameron-24.html'                                  
^^WI   ITEM C1 '&Some useful ideas'                                             
^^WI   '#SPAWN CRYSDIR:manual/cameron-25.html'                                  
^^WI   ENDMENU                                                                  
^^WI   SPLIT                                                                    
^^WI   ITEM CK '&Check for updates'                                             
^^WI   '#SPAWN http://www.xtl.ox.ac.uk/download.html?d=may02'                   
^^WI   ITEM HWEB '&Oxford Web Page' '#SPAWN http://www.xtl.ox.ac.uk/'           
^^WI   ITEM HWEB '&Bug report form'                                             
^^WI   '#SPAWN http://www.xtl.ox.ac.uk/bug.html'                                
^^WI   ITEM HWEB '&News'                                                        
^^WI   '#SPAWN http://www.xtl.ox.ac.uk/news.html'                               
^^WI   SPLIT                                                                    
^^WI   ITEM ABOUT '&About Crystals' '#SCRIPT XREFER'                            
^^WI  ENDMENU                                                                   
^^WI ENDDEFINEMENU                                                              
^^WI SHOW                                                                       
^^CR                                                                            
^^WI SENDTEXTTO _CAMTEXT                                                        
^^CR                                                                            
%% Set increment default to 5 degrees                                           
^^CO SET XC_BL SELECTION=5                                                      
^^CO SET LFONT SELECTION=4                                                      
%VARIABLE INTEGER ATOMSIN ATOMSOUT ATOMCHANGE                                   
%%                                                                              
%% COUNT THE NUMBER OF ATOMS PASSED TO CAMERON.                                 
%%                                                                              
% COPY '#SCRIPT NATOMS'                                                         
% EVALUATE ATOMSIN = ATOMSOUT                                                   
% COPY 'END'                                                                    
% IF LNEWL5 .EQ. TRUE THEN                                                      
%   COPY '#XCAM '                                                               
% ELSE                                                                          
%   COPY '#XCAM MODE = SPECIAL'                                                 
% END IF                                                                        
% COPY 'END'                                                                    
^^?? _CAMERONVIEW EXISTS                                                        
% GET NOSTORE SILENT TEXT ' ' ' '                                               
% COPY ' '                                                                      
% COPY 'BALL ALL PLANE ALL VIEW'                                                
% COPY ' '                                                                      
% LOOP                                                                          
%  ON END TERMINATE                                                             
%  CLEAR                                                                        
%  GET NOSTORE SILENT TEXT ' ' ' '                                              
%  IF CVALUE .EQ. 'CONTINUE' THEN                                               
^^?? _CAMTEXTINPUT TEXT                                                         
%    GET NOSTORE SILENT TEXT ' ' ' '                                            
%    EVALUATE LLINE = CVALUE // '+'                                             
%    TRANSFER LLINE TO CRYSTALS                                                 
^^CO SET _CAMTEXTINPUT TEXT ' '                                                 
%  ELSE                                                                         
%    TRANSFER CVALUE TO CRYSTALS                                                
%  END IF                                                                       
% END LOOP                                                                      
% COPY ' '                                                                      
% COPY 'END'                                                                    
% COPY ' '                                                                      
^^WI DISPOSE _CAMWINDOW                                                         
^^CR                                                                            
%   VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2                       
%   VARIABLE LOGICAL ANSWER                                                     
%   EVALUATE QTITLE = 'Return to Crystals'                                      
%   EVALUATE QLINE1 = 'Do you want to apply any changes'                        
%   EVALUATE QLINE2 = 'made in CAMERON?'                                        
%   EVALUATE BUTTOK = '&Yes'                                                    
%   EVALUATE BUTTXX = '&No'                                                     
%   COPY '#SCRIPT XQUESTIO'                                                     
%   IF ANSWER .EQ. TRUE THEN                                                    
                                                                                
 * Reading list of atoms.                                                       
%           COPY '#USE CAMERON.L5'                                              
%   ELSE                                                                        
%       FINISH                                                                  
%   END IF                                                                      
%% COUNT THE NUMBER OF ATOMS PASSED BACK FROM CAMERON.                          
 * Checking atoms.                                                              
%   COPY '#SCRIPT NATOMS'                                                       
%       EVALUATE ATOMCHANGE = ATOMSOUT - ATOMSIN                                
%   IF ATOMCHANGE .GT. 0 THEN                                                   
                                                                                
                                                                                
  WW    WW   AAAA   RRRR   NN   NN  IIIIII  NN   NN   GGGG                      
  WW    WW  AA  AA  RR RR  NNN  NN    II    NNN  NN  GG  GG                     
  WW WW WW  AAAAAA  RRRR   NN N NN    II    NN N NN  GG                         
  WW WW WW  AA  AA  RR RR  NN  NNN    II    NN  NNN  GG GGGG                    
   WW  WW   AA  AA  RR RR  NN   NN  IIIIII  NN   NN   GGGG                      
                                                                                
  Very Important:                                                               
                                                                                
  The number of atoms that has returned to CRYSTALS is larger                   
  than the number that was sent to CAMERON....                                  
                                                                                
  This could mean that the structure was PACKed when you exited                 
  Cameron. It is strongly recommended that you revert to the                    
  previous structural model, and run CAMERON again.                             
                                                                                
%    EVALUATE QTITLE = 'Too many atoms?'                                        
%    EVALUATE QLINE1 = 'The number of atoms returned is larger than'            
%    EVALUATE QLINE2 = 'the number sent. Restore previous atom list?'           
%    EVALUATE BUTTOK = '&Yes'                                                   
%    EVALUATE BUTTXX = '&No'                                                    
%    COPY '#SCRIPT XQUESTIO'                                                    
%    IF ANSWER .EQ. TRUE THEN                                                   
 * Reading original list of atoms.                                              
%          COPY '#USE CAMERON.L5I'                                              
%    ELSE                                                                       
%          FINISH                                                               
%    END IF                                                                     
%   ELSE                                                                        
%       FINISH                                                                  
%   END IF                                                                      
%END SCRIPT                                                                     
