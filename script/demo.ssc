%SCRIPT DEMO
% VARIABLE INTEGER CHOICE CLEAN
% VARIABLE CHARACTER CDIR CFDIR
% VARIABLE LOGICAL T
%%
%% Close down guide if left open...
%%
^^?? XGUIDER EXISTS
%    VERIFY TRUE FALSE
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%    IF VALUE .EQ. 1  THEN
^^CO   DISPOSE XGUIDER
%    END IF
^^?? XGUIDES EXISTS
%    VERIFY TRUE FALSE
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%    IF VALUE .EQ. 1  THEN
^^CO   DISPOSE XGUIDES
%    END IF
^^?? XGUIDEI EXISTS
%    VERIFY TRUE FALSE
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%    IF VALUE .EQ. 1  THEN
^^CO   DISPOSE XGUIDEI
%    END IF
%%
%% Choose between demog (rolling), demop (pausing) or neither.
%%
^^WI WINDOW QUERY 'CRYSTALS Demo / Workshop' MODAL
^^WI COMMIT='BXX'  CANCEL='BXX' POSITION=CENTRED '_MAIN'
^^WI GRID GTB NROWS=7 NCOLS=3
^^WI {
^^WI  @ 4,2 GRID GDEM NROWS=3 NCOLS=1 OUTLINE='Demo'
^^WI  {
^^WI   @ 1,1 GRID GINF NROWS=1 NCOLS=3
^^WI   {
^^WI     @ 1,1 ICON MYICON QUERY
^^WI     @ 1,3 GRID TL NROWS=4 NCOLS=5
^^WI     {
^^WI      @ 1,3 STATIC T1 'You may run the demo in two modes:'
^^WI      @ 2,3 STATIC T2 'Continuous will roll through the demo without'
^^WI      @ 3,3 STATIC T1 'stopping.'
^^WI      @ 4,3 STATIC T2 'Interactive will wait between each section.'
^^WI     }
^^WI   }
^^WI   @ 3,1 GRID GBTN NROWS=1 NCOLS=5
^^WI   {
^^WI     @ 1,1 STRETCH XQ_S1 HORIZONTAL
^^WI     @ 1,2 BUTTON B1 'Continuous'
^^WI     @ 1,4 BUTTON B2 'Interactive'
^^WI     @ 1,5 STRETCH XQ_S2 HORIZONTAL
^^WI   }
^^WI  }
^^WI  @ 2,2 GRID GWORK NROWS=6 NCOLS=1 OUTLINE='Workshops'
^^WI  {
^^WI   @ 1,1 STATIC SP1 'Choose a workshop structure:'
^^WI   @ 2,1 LISTBOX CHSTR VISLINES=7
^^WI         ADDTOLIST
^^WI     'Nket'
^^WI     'Cyclo'
^^WI     'Quick'
^^WI     'Edit examples'
^^WI     'Tin Twin'
^^WI     'As19a2 Twin'
^^WI     'B10F12 Twin'
^^WI     'Ru structure (disorder)'
^^WI     'ZnGaPO (disorder)'
^^WI     'Ylid'
^^WI     'Peach'
^^WI     'Keen'
^^WI     'Hex'
^^WI     'Mogul'       NULL
^^WI   @ 4,1 CHECKBOX XWIP 'Clean start' STATE=ON
^^WI   @ 6,1 GRID BT NROWS=1 NCOLS=3
^^WI   {
^^WI    @ 1,1 STRETCH SX HORIZONTAL
^^WI    @ 1,2 BUTTON WKOK 'Open workshop structure'
^^WI    @ 1,3 STRETCH SY HORIZONTAL
^^WI   }
^^WI  }
^^WI  @ 6,2 GRID GCAN NROWS=1 NCOLS=2
^^WI  {
^^WI    @ 1,1 STRETCH SX HORIZONTAL
^^WI    @ 1,2 BUTTON BXX '&Cancel' DEFAULT
^^WI  }
^^WI }
^^WI SHOW
^^CR
%%
% VERIFY B1 B2 WKOK BXX
% GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
% CASE VALUE
%   BLOCK
^^CO  DISPOSE QUERY
%     COPY '#SCRIPT DEMOG'
%   END BLOCK
%   BLOCK
^^CO  DISPOSE QUERY
%     COPY '#SCRIPT DEMOP'
%   END BLOCK
%   BLOCK
^^??  CHSTR SELECTED
%     GET NOSTORE SILENT INTEGER ' ' ' '
%     EVALUATE CHOICE = VALUE
^^??  XWIP STATE
%     VERIFY ON OFF
%     GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%     CASE VALUE
%       EVALUATE CLEAN = 1
%       EVALUATE CLEAN = 0
%     END CASE
^^CO  DISPOSE QUERY
%     CASE CHOICE
%          EVALUATE CDIR = 'CRYSDIR:nket/'
%          EVALUATE CDIR = 'CRYSDIR:demo/cyclo/'
%          EVALUATE CDIR = 'CRYSDIR:demo/quick/'
%          EVALUATE CDIR = 'CRYSDIR:demo/editor/'
%          EVALUATE CDIR = 'CRYSDIR:demo/twin/'
%          EVALUATE CDIR = 'CRYSDIR:demo/twin2/'
%          EVALUATE CDIR = 'CRYSDIR:demo/twin3/'
%          EVALUATE CDIR = 'CRYSDIR:demo/shape/'
%          EVALUATE CDIR = 'CRYSDIR:demo/shape2/'
%          EVALUATE CDIR = 'CRYSDIR:demo/ylid/'
%          EVALUATE CDIR = 'CRYSDIR:demo/peach/'
%          EVALUATE CDIR = 'CRYSDIR:demo/keen/'
%          EVALUATE CDIR = 'CRYSDIR:example/'
%          EVALUATE CDIR = 'CRYSDIR:demo/mogul/'
%     END CASE
%     EVALUATE CFDIR = GETENV ( CDIR ) 
%     IF ( .NOT. ( FILEEXISTS CFDIR ) ) THEN
%         TRANSFER '{E Cannot find ' // CFDIR TO DISPLAY
 {I Suggest that you reinstall CRYSTALS to restore the workshop data.
%         FINISH
%     END IF
%     IF ( CLEAN .EQ. 1 ) THEN
%         EVALUATE T = FILEDELETE ( CFDIR // 'crfilev2.dsc' )
%         EVALUATE T = FILEDELETE ( CFDIR // 'sir9x.ins' )
%         EVALUATE T = FILEDELETE ( CFDIR // 'pretwin6.dat' )
%         IF CHOICE .EQ. 1 THEN
%TRANSFER '#SPAWN copy "' // CFDIR // 'nket.dsc" "' // CFDIR // -
'crfilev2.dsc"' TO CRYSTALS
%         END IF
%     END IF
^^WI  SET _MAIN TEXT=
%     TRANSFER "^^WI 'Crystals - " // CFDIR // "'" TO DISPLAY
^^CR
%     COPY '#SCRIPT TABRESET'
%     TRANSFER "^^CO RESTART '" // CFDIR // -
                     "' NEWFILE=crfilev2.dsc" TO DISPLAY
%%
%% MAKE SURE THE GUI HAS RECEIVED THESE COMMANDS BEFORE CLOSING DOWN!
%% We query the value of any object by name.
%% A value will not return until the GUI has caught up.
%%
^^??  _MAIN EXISTS
%     GET NOSTORE TEXT 'About to restart in new directory...' ' '
^^CO  SET _MAINTEXTOUTPUT EMPTY
%     COPY '#END'
%     FINISH
%   END BLOCK
%   BLOCK
^^CO  DISPOSE QUERY
%     FINISH
%   END BLOCK
% END CASE
^^CO DISPOSE QUERY
%END SCRIPT
