%SCRIPT INCAD4
%% CRYSTALS CAD4 reflection input script
%%
% VARIABLE INTEGER REDRAW OLDPROG SIMPLE
% EVALUATE REDRAW = 1
%  LOOP
%  ON END TERMINATE
%  IF REDRAW .EQ. 1 THEN
^^WI WINDOW INCAD4 'Input CAD4 data' MODAL
^^WI STARTGRID MAIN 'N' NROWS=2 NCOLS=1
^^WI  STARTGRID ROW=1 COL=1 UPPER 'N' NROWS=2 NCOLS=3
^^WI   STATIC ROW=2 COL=2 TITLE 'Read in data from a CAD4 Diffractometer'
^^WI  ENDGRID
^^WI  STARTGRID ROW=2 COL=1 LOWER 'N' NROWS=2 NCOLS=2
^^WI   STARTGRID ROW=1 COL=1 LLEFT 'N' NROWS=6 NCOLS=2
%%34567890% 34567890% 34567890% 34567890% 34567890% 34567890% 34567890% 34567890
^^WI    STARTGRID ROW=2 COL=2 BOX1 'Input mode' NROWS=2 NCOLS=3 OUTLINE
^^WI     RADIOBUTTON ROW=1 COL=2 XSIMPLE 'Simple default' STATE=ON IGNORE
^^WI     RADIOBUTTON ROW=2 COL=2 XDETAIL 'Detailed questions' STATE=OFF IGNORE
^^WI    ENDGRID
^^WI    STARTGRID ROW=4 COL=2 BOX2 'Program used' NROWS=2 NCOLS=2 OUTLINE
^^WI     RADIOBUTTON ROW=1 COL=2 XRC85 'RC-85' STATE=OFF IGNORE
^^WI     RADIOBUTTON ROW=2 COL=2 XRC93 'RC-93' STATE=ON IGNORE
^^WI    ENDGRID
^^WI   ENDGRID
^^WI   STARTGRID ROW=1 COL=2 LRIGH 'N' NROWS=6 NCOLS=3
^^WI    BUTTON ROW=2 COL=2 BUTTONOK '&Ok' DEFAULT
^^WI    BUTTON ROW=4 COL=2 BUTTONXX '&Cancel'
^^WI   ENDGRID
^^WI  ENDGRID
^^WI ENDGRID
^^WI SHOW
^^CR
^^WI SET _MAINTEXTOUTPUT NOECHO
^^CR
%    EVALUATE REDRAW = 0
%  END IF
%  VERIFY BUTTONOK BUTTONXX
%  GET NOSTORE FINAL ABBREVIATED ' ' ' '
%  CASE VALUE
%       BLOCK     %BUTTONOK%
%% XSimple state
^^WI            SET _MAINTEXTOUTPUT NOECHO
^^WI            GETVALUE XSIMPLE
^^CR
%       VERIFY ON OFF
%       GET NOSTORE FINAL ABBREVIATED ' ' ' '
%       CASE VALUE
%           BLOCK    %ON%
%               EVALUATE SIMPLE = 1
%           END BLOCK
%           BLOCK    %OFF%
%               EVALUATE SIMPLE = 0
%           END BLOCK
%       END CASE
%% XRC85 state
^^WI        SET _MAINTEXTOUTPUT NOECHO
^^WI        GETVALUE XRC85
^^CR
%       VERIFY ON OFF
%       GET NOSTORE FINAL ABBREVIATED ' ' ' '
%       CASE VALUE
%           BLOCK    %ON%
%               EVALUATE OLDPROG = 1
%           END BLOCK
%           BLOCK    %OFF%
%               EVALUATE OLDPROG = 0
%           END BLOCK
%       END CASE
^^WI            DISPOSE INCAD4
^^CR
%%%%%%
%       IF SIMPLE .EQ. 0 THEN
%           COPY '#SCRIPT CADSLOW'
%       ELSE IF OLDPROG .EQ. 0 THEN
%           COPY '#SCRIPT XINRC93'
%       ELSE
%           COPY '#SCRIPT CADQUICK'
%       END IF
%       FINISH
%   END BLOCK
%   BLOCK     %BUTTONXX%
^^WI            DISPOSE INCAD4
^^CR
%       FINISH
%   END BLOCK
%  END CASE
%  END LOOP
%END SCRIPT
