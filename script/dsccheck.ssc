%SCRIPT DSCCHECK                                                                
%% The variables needed by XQUESTIO                                             
% VARIABLE CHARACTER QTITLE QLINE1 QLINE2 BUTTOK BUTTXX                         
% VARIABLE LOGICAL ANSWER                                                       
%%                                                                              
%%                                                                              
% VARIABLE INTEGER MD                                                           
% VARIABLE REAL UISOF                                                           
% VARIABLE LOGICAL OLDDSC1 OLDDSC2 SLOWDSC LNEW30 LNEW29                        
% EVALUATE OLDDSC1 = FALSE                                                      
% EVALUATE OLDDSC2 = FALSE                                                      
%%                                                                              
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%                                        
%%                                    %%                                        
%% LOOK FOR FAR TOO MANY ATOMS        %%                                        
%%                                    %%                                        
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%                                        
%%                                                                              
% IF ( EXISTS  5 .EQ. 1 ) THEN                                                  
%  COPY '#GENERALEDIT 5'                                                        
%  CLEAR                                                                        
%  COPY 'TRANSHEAD FROM OFFSET = 3 TO MD'                                       
%  IF ( MD .GT. 300 ) THEN                                                      
%     EVALUATE SLOWDSC = TRUE                                                   
%  END IF                                                                       
%  COPY 'END'                                                                   
%  IF ( SLOWDSC ) THEN                                                          
                                                                                
%CLEAR                                                                          
%INSERT '{E    You are opening a DSC file with'                                 
%STORE FORMAT /(I10)/ LENGTH 10 INTEGER MD                                      
%INSERT 'atoms.'                                                                
%OUTPUT                                                                         
                                                                                
{I  You have two options:                                                       
                                                                                
{R  Ignore this warning - Carry on working with this number of atoms,           
{R                        your model window may take a very long time           
{R                        to update.                                            
{R  Turn off updating   - The model will only be updated when you type          
{R                        #SUM L 5                                              
{R                        END                                                   
                                                                                
%   EVALUATE QTITLE = 'Disable updates'                                         
%   EVALUATE QLINE1 = 'Lots of atoms. Updates of model window may be slow.'     
%   EVALUATE QLINE2 = 'Disable automatic updates?'                              
%   EVALUATE BUTTOK = '&Disable updates'                                        
%   EVALUATE BUTTXX = '&Update automatically'                                   
%   COPY '#SCRIPT XQUESTIO'                                                     
%   IF ( ANSWER ) THEN                                                          
%     COPY '#SCRIPT UPDTOFF'                                                    
%   ELSE                                                                        
%     COPY '#SET AUTOUPDT ON'                                                   
%   END IF                                                                      
%  ELSE                                                                         
%     COPY '#SET AUTOUPDT ON'                                                   
%  END IF                                                                       
% ELSE                                                                          
%     COPY '#SET AUTOUPDT ON'                                                   
% END IF                                                                        
%%                                                                              
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%                                        
%%                                    %%                                        
%% LOOK FOR OLD DEFINTION OF U[ISO]   %%                                        
%%                                    %%                                        
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%                                        
%%                                                                              
% IF ( EXISTS  5 .EQ. 1 ) THEN                                                  
%  COPY '#GENERALEDIT 5'                                                        
%  COPY 'LOCATE RECORDTYPE=101'                                                 
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'                      
%% RIC2003: Only check first twenty atoms for speed.                            
%% RIC2004: But the H's are usually at the end - read the lot.                  
%  LOOP                                                                         
%     ON EDITERROR TERMINATE                                                    
%     COPY 'TRANSFER FROM OFFSET=3 TO UISOF'                                    
%     IF ( UISOF .GT. 0.001 ) .AND. ( UISOF .LT. 0.98 ) THEN                    
%         EVALUATE OLDDSC1 = TRUE                                               
%     END IF                                                                    
%     COPY 'NEXT'                                                               
%  END LOOP                                                                     
%  COPY 'END'                                                                   
%%                                                                              
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%                                        
%%                                    %%                                        
%% LOOK FOR SHORT ATOM RECORDS        %%                                        
%%                                    %%                                        
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%                                        
%%                                                                              
%  COPY '#GENERALEDIT 5'                                                        
%  CLEAR                                                                        
%  COPY 'TRANSHEAD FROM OFFSET = 2 TO MD '                                      
%  IF ( MD .NE. 18 ) THEN                                                       
%    EVALUATE OLDDSC2 = TRUE                                                    
%  END IF                                                                       
%  COPY 'END'                                                                   
%%                                                                              
%  IF ( OLDDSC1 ) .OR. ( OLDDSC2 ) THEN                                         
                                                                                
{E WARNING: You are opening an OLD format DSC file.                             
{I It is possible to automatically reformat                                     
{I your current LIST 5 so that you can                                          
{I continue to use it with the new program.                                     
                                                                                
{I  You have two options:                                                       
                                                                                
{R  Ignore this warning - Carry on working with this                            
{R                        DSC file. Any calculation                             
{R                        using Uiso will be incorrect.                         
                                                                                
{R  Reformat the list 5 - Change the current LIST 5 so                          
{R                        that its format is correct.                           
{R                        (Your old list 5 will be written                      
{R                        to disk for safety).                                  
                                                                                
%   EVALUATE QTITLE = 'Reformat List 5'                                         
%   EVALUATE QLINE1 = 'Old format atom list detected. Update automatically,'    
%   EVALUATE QLINE2 = '(old L5 backed up), or ignore (U[iso] will be wrong).'   
%   EVALUATE BUTTOK = '&Update automatically'                                   
%   EVALUATE BUTTXX = '&Ignore warning'                                         
%   COPY '#SCRIPT XQUESTIO'                                                     
%   IF ( ANSWER ) THEN                                                          
%    COPY '#RELEASE PUNCH OLDLIST5.DAT'                                         
%    COPY '#PUNCH 5'                                                            
%    COPY 'END'                                                                 
%    COPY '#RELEASE PUNCH bfile.pch'                                            
                                                                                
{I A copy of your current atom list has been saved in                           
{I the current directory as 'OLDLIST5.DAT'                                      
                                                                                
{I If you return to using a pre-December 1998 version                           
{I of CRYSTALS, then type '#USE OLDLIST5.DAT' when                              
{I you start up CRYSTALS.                                                       
                                                                                
%    IF ( OLDDSC1 ) THEN                                                        
%     COPY '#EDIT'                                                              
%     COPY 'REFORMAT ALL'                                                       
%     COPY 'END'                                                                
%    END IF                                                                     
%%                                                                              
%    IF ( OLDDSC2 ) THEN                                                        
%%  REINPUT OLD LIST 5 TO GET CORRECT NUMBER OF ATOM KEYS                       
%     COPY '#RELEASE PUNCH TMPLIST5.DAT'                                        
%     COPY '#PUNCH 5'                                                           
%     COPY 'END'                                                                
%     COPY '#RELEASE PUNCH bfile.pch'                                           
%     COPY '#USE TMPLIST5.DAT'                                                  
%    END IF                                                                     
%%                                                                              
{I Your present LIST 5 has been converted to the new                            
{I format. You may now continue safely.                                         
                                                                                
%   END IF                                                                      
%  END IF                                                                       
% END IF                                                                        
%%                                                                              
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%                                        
%%                                    %%                                        
%% LOOK FOR SHORT L29 RECORDS         %%                                        
%%                                    %%                                        
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%                                        
%%                                                                              
% IF ( EXISTS  29 .EQ. 1 ) THEN                                                 
%  EVALUATE OLDDSC1 = FALSE                                                     
%  COPY '#GENERALEDIT 29'                                                       
%  CLEAR                                                                        
%  COPY 'TRANSHEAD FROM OFFSET = 2 TO MD '                                      
%  IF ( MD .NE. 8 ) THEN                                                        
%    EVALUATE OLDDSC1 = TRUE                                                    
%  END IF                                                                       
%  COPY 'END'                                                                   
%  IF ( OLDDSC1 ) THEN                                                          
                                                                                
{E  List 29 (atomic properties) has no element colour entries.                  
                                                                                
{I  You have two options:                                                       
                                                                                
{R  Ignore this warning - Carry on working with this                            
{R                        DSC file. Colour schemes may                          
{R                        be weird.                                             
                                                                                
{R  Update the element properties automatically (recommended).                  
                                                                                
%   EVALUATE QTITLE = 'Old atomic properties'                                   
%   EVALUATE QLINE1 = 'Old atom properties list found (no colour scheme).'      
%   EVALUATE QLINE2 = 'Update to get colours in the display.'                   
%   EVALUATE BUTTOK = '&Update automatically'                                   
%   EVALUATE BUTTXX = '&Ignore warning'                                         
%   COPY '#SCRIPT XQUESTIO'                                                     
%   IF ( ANSWER ) THEN                                                          
%     EVALUATE LNEW29 = TRUE                                                    
%     COPY '#SCRIPT XINLIST3'                                                   
%     COPY '#BONDCALC FORCE'                                                    
%     COPY 'END'                                                                
%   END IF                                                                      
%  END IF                                                                       
% END IF                                                                        
%%                                                                              
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%                                        
%%                                    %%                                        
%% LOOK FOR SHORT L30 RECORDS         %%                                        
%%                                    %%                                        
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%                                        
%%                                                                              
%%  LOOK FOR SHORT CIF RECORDS                                                  
% IF ( EXISTS  30 .EQ. 1 ) THEN                                                 
%  EVALUATE OLDDSC1 = FALSE                                                     
%  COPY '#GENERALEDIT 30'                                                       
%  CLEAR                                                                        
%  COPY 'TRANSHEAD FROM OFFSET = 34 TO MD '                                     
%     IF ( MD .NE. 19 ) THEN                                                    
%         EVALUATE OLDDSC1 = TRUE                                               
%     END IF                                                                    
%  COPY 'END'                                                                   
%  IF ( OLDDSC1 ) THEN                                                          

{E You have old format CIF info (List 30). CRYSTALS can not continue safely.    
{I  You have two options:                                                       
{R  Ignore this warning - Carry on working with this DSC file.                  
{R                        CRYSTALS will eventually crash.                       
{R  Automatically recreate a new List 30 from the old one.                      

%   EVALUATE QTITLE = 'Old CIF data'                                            
%   EVALUATE QLINE1 = 'Old format CIF info found. You must update this now'     
%   EVALUATE QLINE2 = 'or CRYSTALS will crash later.'                           
%   EVALUATE BUTTOK = '&Update automatically'                                   
%   EVALUATE BUTTXX = '&Ignore warning'                                         
%   COPY '#SCRIPT XQUESTIO'                                                     
%   IF ( ANSWER ) THEN                                                          
%    EVALUATE LNEW30 = TRUE                                                     
%    COPY '#SCRIPT ED30'                                                        
%   ELSE                                                                        
                                                                                
{E You have chosen not to update List 30. CRYSTALS will crash if you            
{E attempt any structure factor calculations (including refinement).            
                                                                                
%   END IF                                                                      
%  END IF                                                                       
% END IF                                                                        
%END SCRIPT                                                                     
