%SCRIPT DSCCHECK
%%
%%
%  VARIABLE REAL UISOF
%  VARIABLE LOGICAL OLDDSC
%  EVALUATE OLDDSC = FALSE
%%
%  COPY '#GENERALEDIT 5'
%  COPY 'LOCATE RECORDTYPE=101'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  LOOP
%     ON EDITERROR TERMINATE
%     COPY 'TRANSFER FROM OFFSET=3 TO UISOF'
%     IF ( UISOF .GT. 0.001 ) .AND. ( UISOF .LT. 0.98 ) THEN
%         EVALUATE OLDDSC = TRUE
%     END IF
%     COPY 'NEXT'
%  END LOOP
%  COPY 'END'
%%
%  IF ( OLDDSC ) THEN

  +------------------------------------------------+
  |                                                |
  |                 W A R N I N G                  |
  |                                                |
  |  You are opening what appears to be an OLD     |
  |  format DSC file.                              |
  |                                                |
  |  ( Specifically, column 3 of LIST 5 (atoms)    |
  |  has Uiso values in, which are now located     |
  |  in column 7 )                                 |
  |                                                |
  |  It is possible to automatically reformat      |
  |  your current LIST 5 so that you can           |
  |  continue to use it with the new program.      |
  |                                                |
  +------------------------------------------------+

% VERIFY IGNORE REFORMAT
  You have two options:

  IGNORE -   Carry on working with this DSC file.
             Any calculation using Uiso will be
             incorrect.
  REFORMAT - Change the current LIST 5 so that its
             format is correct. ( Your old list 5
             will be written to disk for safety ).

%  GET NOSTORE FINAL ABBREVIATED 'Do you want me to -
automatically reformat LIST 5 for you?' 'YES'
%   IF VALUE .EQ. 2 THEN
%    COPY '#RELEASE PUNCH OLDLIST5.DAT'
%    COPY '#PUNCH 5'
%    COPY 'END'
%    COPY '#RELEASE PUNCH CRFILE.PCH'

 A copy of your current atom list has been saved in
 the current directory as 'OLDLIST5.DAT'

 If you return to using a pre-December 1998 version
 of CRYSTALS, then type '#USE OLDLIST5.DAT' when
 you start up CRYSTALS.

%    COPY '#EDIT'
%    COPY 'REFORMAT ALL'
%    COPY 'END'

 Your present LIST 5 has been converted to the new
 format. You may now continue safely.

%   END IF
%  END IF
%END SCRIPT
