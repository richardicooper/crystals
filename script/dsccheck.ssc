%SCRIPT DSCCHECK
%%
%%
%  VARIABLE INTEGER MD
%  VARIABLE REAL UISOF
%  VARIABLE LOGICAL OLDDSC
%  EVALUATE OLDDSC = FALSE
%%
%% LOOK FOR OLD DEFINTION OF U[ISO]
% IF ( EXISTS  5 .EQ. 1 ) THEN
%  COPY '#GENERALEDIT 5'
%  COPY 'LOCATE RECORDTYPE=101'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  LOOP
%     ON EDITERROR TERMINATE
%     COPY 'TRANSFER FROM OFFSET=3 TO UISOF'
%     IF ( UISOF .GT. 0.001 ) .AND. ( UISOF .LT. 0.98 ) THEN
%         EVALUATE OLDDSC = TRUE
%     END IF
%     COPY 'NEXT'
%  END LOOP
%  COPY 'END'
%%
%%  LOOK FOR SHORT ATOM RECORDS
%  COPY '#GENERALEDIT 5'
%  CLEAR
%  COPY 'TRANSHEAD FROM OFFSET = 2 TO MD '
%     IF ( MD .NE. 18 ) THEN
%         EVALUATE OLDDSC = TRUE
%     END IF
%  COPY 'END'
%%
%  IF ( OLDDSC ) THEN
 
  +------------------------------------------------+
  |                                                |
  |                 W A R N I N G                  |
  |                                                |
  |  You are opening what appears to be an OLD     |
  |  format DSC file.                              |
  |                                                |
  |  It is possible to automatically reformat      |
  |  your current LIST 5 so that you can           |
  |  continue to use it with the new program.      |
  |                                                |
  +------------------------------------------------+
 
% VERIFY NO YES
  You have two options:
 
  Ignore this warning - Carry on working with this
                        DSC file. Any calculation
                        using Uiso will be incorrect.
  Reformat the list 5 - Change the current LIST 5 so
                        that its format is correct.
                        (Your old list 5 will be written
                         to disk for safety).
 
%  GET NOSTORE FINAL ABBREVIATED 'Do you want me to -
automatically reformat LIST 5 for you?' 'YES'
%   IF VALUE .EQ. 2 THEN
%    COPY '#RELEASE PUNCH OLDLIST5.DAT'
%    COPY '#PUNCH 5'
%    COPY 'END'
%    COPY '#RELEASE PUNCH CRFILE.PCH'
 
 A copy of your current atom list has been saved in
 the current directory as 'OLDLIST5.DAT'
 
 If you return to using a pre-December 1998 version
 of CRYSTALS, then type '#USE OLDLIST5.DAT' when
 you start up CRYSTALS.
 
%%  REINPUT OLD LIST 5 TO GET CORRECT NUMBER OF ATOM KEYS
%    COPY '#USE OLDLIST5.DAT'
%    COPY '#EDIT'
%    COPY 'REFORMAT ALL'
%    COPY 'END'
 
 Your present LIST 5 has been converted to the new
 format. You may now continue safely.
 
%   END IF
%  END IF
% END IF
%% NOW CHECK LIST 29!
%%  LOOK FOR SHORT ATOM RECORDS
% IF ( EXISTS  29 .EQ. 1 ) THEN
%  EVALUATE OLDDSC = FALSE
%  COPY '#GENERALEDIT 29'
%  CLEAR
%  COPY 'TRANSHEAD FROM OFFSET = 2 TO MD '
%     IF ( MD .NE. 8 ) THEN
%         EVALUATE OLDDSC = TRUE
%     END IF
%  COPY 'END'
%  IF ( OLDDSC ) THEN
  +----------------------------------------------------------------+
  |                                                                |
  | Your database knows nothing about colours schemes for elements.|
  |                                                                |
  +----------------------------------------------------------------+
 
% VERIFY NO YES
  You have two options:
 
  Ignore this warning - Carry on working with this
                        DSC file. Colour schemes may
                        be weird.
 
  Update the element properties. This requires you to re-input
                        the empirical formula for the contents
                        of the asymmetric unit.
 
%  COPY '#SUM L 29'
%  COPY 'END'
 
%  GET NOSTORE FINAL ABBREVIATED 'Do you want to re-input the -
 empirical formula?' 'YES'
%   IF VALUE .EQ. 2 THEN
%     COPY '#SCRIPT INLIST29'
%   END IF
%  END IF
% END IF
%%
%%
%%  CHECK LIST 30
%%  LOOK FOR SHORT CIF RECORDS
% IF ( EXISTS  30 .EQ. 1 ) THEN
%  EVALUATE OLDDSC = FALSE
%  COPY '#GENERALEDIT 30'
%  CLEAR
%  COPY 'TRANSHEAD FROM OFFSET = 34 TO MD '
%     IF ( MD .NE. 19 ) THEN
%         EVALUATE OLDDSC = TRUE
%     END IF
%  COPY 'END'
%  IF ( OLDDSC ) THEN
  +----------------------------------------------------------------+
  |                                                                |
  | You have old-style cif information.                            | 
  | CRYSTALS cannot continue safely.                               |
  |                                                                |
  +----------------------------------------------------------------+

% VERIFY NO YES
  You have two options:

1  Ignore this warning - Carry on working with this DSC file.
                         CRYSTALS will eventually crash.
                        
2  Delete the existing LIST 30, re-input any LIST 30 created by an
   X-ray data pre-processor (DIPIN, KCCDIN, RC93), manually update
   LIST 30 and then continue.


%  GET NOSTORE FINAL ABBREVIATED 'Do you want to re-input list -
30?' 'YES'
%   IF VALUE .EQ. 2 THEN
%    COPY '#SUM L 30'
%    COPY 'END'
%    VERIFY YES NO
%    GET NOSTORE FINAL ABBREVIATED -
 'Do you a file containing LIST 30 information?' 'NO'
%    IF VALUE .EQ. 1 THEN
%      CLEAR
%      GET FINAL KEYWORD -
  'Please give the full name of the file containing LIST 30'
%      VARIABLE CHARACTER FILENAME
%      EVALUATE FILENAME = CVALUE
%      TRANSFER '#USE ' //  FILENAME // ' ' TO CRYSTALS
%    ELSE
%     COPY '#LIST 30'
%     COPY 'END'
%    END IF
%    COPY '#SCRIPT ED30'
%   END IF
%  END IF
% END IF
%END SCRIPT
