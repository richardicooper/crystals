%SCRIPT XFLIP
%   VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2
%   VARIABLE LOGICAL ANSWER LCRY T
%   VARIABLE INTEGER IRUN
%   IF EXISTS 6 .LT. 1 THEN
            You do not have any reflections stored.
            It is impossible to proceed without data.
%       FINISH
%   END IF
%   IF ( FILEEXISTS ( 'sflip.inflip' ) ) THEN
%      EVALUATE T = FILEDELETE ( 'sflip.inflip' )
%   END IF
%   IF ( FILEEXISTS ( 'sflip.sflog' ) ) THEN
%          EVALUATE T = FILEDELETE ( 'sflip.sflog' )
%   END IF
%       IF ( FILEEXISTS ( 'sflip.inflip' ) ) THEN
%          EVALUATE T = FILEDELETE ( 'sflip.ins' )
%   END IF
%%
%% write a Superflip data file
%%
%   BLOCK
^^WI WINDOW FLIP 'Superflip Preparation'
^^WI   MODAL COMMIT='BOK' CANCEL='BXX'
^^WI GRID GRIDLR NROWS=1 NCOLS=2
^^WI {
^^WI   @ 1,1 GRID GRIDL NROWS=6 NCOLS=3
^^WI   {
^^WI     @ 1,2 RADIOBUTTON RS '&Default Superflip parameters'
^^WI           STATE=ON
^^WI     @ 2,2 RADIOBUTTON RD '&Superflip parameters for weak data'
^^WI           STATE=OFF
^^WI     @ 3,2 CHECKBOX CED '&Edit Superflip file before processing'
^^WI           STATE=OFF
^^WI     @ 4,2 CHECKBOX CRUN '&Run Superflip now'
^^WI           STATE=ON
^^WI     @ 5,2 CHECKBOX CME '&Merge before going on'
^^WI           STATE=OFF
^^WI     @ 6,2 STATIC T1 'Note: data should be merged for Superflip'
^^WI   }
^^WI   @ 1,2 GRID GRIDR NROWS=5 NCOLS=2
^^WI   {
^^WI     @ 2,1 BUTTON BOK '&Ok'
^^WI     @ 4,1 BUTTON BXX '&Cancel'
^^WI   }
^^WI }
^^WI SHOW
^^CR
%      VERIFY BOK BXX
%      GET SILENT NOSTORE ABBREVIATED 'BOK=Process'
%      IF VALUE .EQ. 2 THEN
^^WI      DISPOSE FLIP
^^CR
%         FINISH
%      END IF
^^??   RS STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE ABBREVIATED 'On=Simple job'
%      IF ( VALUE .EQ. 1 ) THEN
%          COPY '#FOREIGN SUPERFLIP '
%          COPY 'END'
%      ELSE
%          COPY '#FOREIGN SUPERFLIP DIFFICULT'
%          COPY 'END'
%      END IF
^^??   CME STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE ABBREVIATED 'ON=Merge first'
%      IF ( VALUE .EQ. 1 ) THEN
%         COPY '#SYSTEMATIC'
%         COPY 'END'
%         CLEAR
%         COPY '#SORT'
%         COPY 'END'
%         CLEAR
%         COPY '#MERGE'
%         COPY 'END'
%         COPY '#LIST 6'
%         COPY 'READ TYPE = COPY'
%         COPY 'END'
%      END IF
^^??   CRUN STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE ABBREVIATED 'ON=Run Superflip now'
%      EVALUATE IRUN = VALUE
^^??   CED STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE ABBREVIATED 'ON=Edit first'
^^WI   DISPOSE FLIP
^^CR
%      IF ( VALUE .EQ. 1 ) THEN
%           COPY '#SPAWN +notepad.exe sflip.inflip'
%%++GILLIN%           COPY '#SPAWN +nedit SFLIP.INFLIP'
%           EVALUATE QTITLE = 'Superflip Preparation'
%           EVALUATE QLINE1 = 'Do you want to'
%           EVALUATE QLINE2 = 'run Superflip now?'
%           EVALUATE BUTTOK = '&Yes'
%           EVALUATE BUTTXX = '&No'
%           COPY '#SCRIPT XQUESTIO'
%           IF ANSWER .EQ. FALSE THEN
%               FINISH
%           END IF
%      END IF
%      IF ( IRUN .EQ. 2 ) THEN
%           FINISH
%      END IF
%      COPY '#SPAWN + "superflip.EXE" sflip.inflip'
%      COPY '#SPAWN + "edma.EXE" sflip.inflip'
%%++LINGIL%      COPY '#SPAWN % "superflip"'
%      COPY '#TYPE sflip.sflog'
%   END BLOCK
%   BLOCK
%      EVALUATE QTITLE = 'Superflip Results'
%      EVALUATE QLINE1 = 'Do you want to use the structure'
%      EVALUATE QLINE2 = 'from Superflip ?'
%      EVALUATE BUTTOK = '&Yes'
%      EVALUATE BUTTXX = '&No'
%      COPY '#SCRIPT XQUESTIO'
%      IF ANSWER .EQ. FALSE THEN
%          COPY '#SCRIPT XHELP1'
   If the structure will not solve:
   1) Try the "Alternative Superflip parameters" option.
   2) Try DirDif or Sir92.
   3) Read the Superflip manual for help.
   4) Be sure that the space group is correct.
%          COPY '#SCRIPT XHELP2'
%          FINISH
%      END IF
%      EVALUATE LCRY = TRUE
%%AvdL it is here that the output file should be read
%%AvdL Do a collect atoms by symmetry, necessary!
%      COPY '#SCRIPT FRESH'
%%
%%      COPY '#OPEN SCPDATA sflip.ins'
%      COPY '#SCRIPT XSHELXFLIPIN'
%%DJW      COPY '#SCRIPT INshelxs'
%%      COPY '#CLOSE SCPDATA'
%      COPY '#SCRIPT XCOLLECT'
%      COPY '#EDIT'
%      COPY 'MON OFF'
%      COPY 'CHANGE FIRST(OCC) UNTIL LAST 1.0'
%      COPY 'MON MEDIUM'
%      COPY 'END'
%%
%      BLOCK
%      EVALUATE QTITLE = 'Use Cameron?'
%      EVALUATE QLINE1 = 'Do you want to use CAMERON '
%      EVALUATE QLINE2 = 'to examine the model '
%      EVALUATE BUTTOK = '&Yes'                                                 
%      EVALUATE BUTTXX = '&No'                                                  
%      COPY '#SCRIPT XQUESTIO'                                                  
%      IF ANSWER .EQ. TRUE THEN                                                 
%       VARIABLE LOGICAL NOXCAMQ LNEWL5                                         
%       EVALUATE NOXCAMQ = TRUE                                                 
%       EVALUATE LNEWL5 = TRUE                                                  
%       COPY '#SCRIPT XCAM'                                                     
%      END IF                                                                   
%      END BLOCK                                                                
%%                                                                              
%      BLOCK                                                                    
%       COPY '#SCRIPT XRENUMB'                                                  
%      END BLOCK                                                                
%% SET Solution type                                                            
%%                                                                              
%      COPY '#GENERALEDIT 30'                                                   
%      COPY 'LOCATE RECORDTYPE=106'                                             
%      COPY 'CHANGE OFFSET=12 MODE=INTEGER INTEGER=1'                           
%      COPY 'WRITE OVERWRITE=OVERWRITE'                                         
%      COPY 'END'                                                               
%   END BLOCK                                                                   
 * Done. Superflip input complete.                                              
%END SCRIPT                                                                     
                                                                                
                                                                                
                                                                                
