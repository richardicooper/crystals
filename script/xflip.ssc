%SCRIPT XFLIP                                                                   
%  VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2                        
%  VARIABLE LOGICAL ANSWER LCRY T                                               
%  VARIABLE INTEGER IRUN F67                                                    
%  VARIABLE LOGICAL TWINNED MERGED DOCALC DOSCALE          
#ifdef CRY_USEWX
%  VARIABLE LOGICAL EDITOR_OK
%  VARIABLE CHARACTER EDITOR_FILE                     
#endif
%  EVALUATE DOCALC = FALSE                                                      
%  EVALUATE DOSCALE = FALSE                                                     
%% variables used by the question box below                                     
%  VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2                        
%  VARIABLE LOGICAL ANSWER                                                      
%  IF EXISTS 6 .LT. 1 THEN                                                      
 You do not have any reflections stored.                                        
 It is impossible to proceed without data.                                      
%    FINISH                                                                     
%  END IF                                                                       
%%
{1,9  Please note that superflip determines space group operators               
{1,9  from a Fourier map computed in P1.                                        
{1,9  It then compiles a space group symbol from the operators.                 
{1,9  There is no unique relationship between the symbol and the                
{1,9  operators (more than one set of operators can give the same               
{1,9  symbol) so that the superflip operators may be different from             
{1,9  those generated by programs which use the symbol.                         
{1,9  This may be a problem if you try to use Sir92 to improve the
{1,9  solution, but not for CRYSTALS itself, PLATON, Mercury or SHELXL.                   
%%                                                                              
%  IF ( FILEEXISTS ( 'sflip.inflip' ) ) THEN                                    
%    EVALUATE T = FILEDELETE ( 'sflip.inflip' )                                 
%  END IF                                                                       
%  COPY '#CLOSE SCPDATA'                                                        
%  IF ( FILEEXISTS ( 'sflip.sflog' ) ) THEN                                     
%    EVALUATE T = FILEDELETE ( 'sflip.sflog' )                                  
%  END IF                                                                       
%  IF ( FILEEXISTS ( 'sflip.inflip' ) ) THEN                                    
%    EVALUATE T = FILEDELETE ( 'sflip.inflip' )                                 
%  END IF                                                                       
%  IF ( FILEEXISTS ( 'sflip.ins' ) ) THEN                                       
%    EVALUATE T = FILEDELETE ( 'sflip.ins' )                                    
%  END IF                                                                       
%  IF ( FILEEXISTS ( 'sflip_sym.ins' ) ) THEN                                   
%    EVALUATE T = FILEDELETE ( 'sflip_sym.ins' )                                
%  END IF                                                                       
%  IF ( FILEEXISTS ( 'sflip.m80' ) ) THEN                                       
%    EVALUATE T = FILEDELETE ( 'sflip.m80' )                                    
%  END IF                                                                       
%  IF ( FILEEXISTS ( 'sflip.m81' ) ) THEN                                       
%    EVALUATE T = FILEDELETE ( 'sflip.m81' )                                    
%  END IF                                                                       
%  IF ( FILEEXISTS ( 'sflip.coo' ) ) THEN                                       
%    EVALUATE T = FILEDELETE ( 'sflip.coo' )                                    
%  END IF                                                                       
%%                                                                              
%% write a Superflip data file                                                  
%%                                                                              
%  BLOCK                                                                        
^^WI WINDOW FLIP 'Superflip Preparation'                                        
^^WI MODAL COMMIT='BOK' CANCEL='BXX'                                            
^^WI GRID GRIDLR NROWS=1 NCOLS=2                                                
^^WI {                                                                          
^^WI @ 1,1 GRID GRIDL NROWS=5 NCOLS=7
^^WI {                                                                          
^^WI @ 1,2 STATIC T1 'Note: data should be merged for Superflip'                
^^WI @ 2,2 CHECKBOX CME '&Merge before going on'                                
^^WI STATE=ON                                                                   
^^WI @ 3,2 CHECKBOX BKUP '&Backup dsc before doing anything else'
^^WI STATE=ON
%%                                           
^^WI @ 2,4 RADIOBUTTON RD '&Default Superflip parameters'                       
^^WI STATE=ON                        
^^WI @ 3,4 RADIOBUTTON RS '&Superflip parameters for difficult structures'      
^^WI STATE=OFF                                                                  
^^WI @ 4,4 CHECKBOX CED '&Edit Superflip file before processing'                
^^WI STATE=OFF                                                                  
^^WI @ 2,6 CHECKBOX CRUN '&Run Superflip now'                                   
^^WI STATE=ON                                                                   
^^WI }                                                                          
^^WI @ 1,2 GRID GRIDR NROWS=5 NCOLS=2                                           
^^WI {                                                                          
^^WI @ 2,1 BUTTON BOK '&Ok'                                                     
^^WI @ 4,1 BUTTON BXX '&Cancel'                                                 
^^WI }                                                                          
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
%    VERIFY BOK BXX                                                             
%    GET SILENT NOSTORE ABBREVIATED 'BOK=Process'                               
%    IF VALUE .EQ. 2 THEN                                                       
^^WI DISPOSE FLIP                                                               
^^CR                                                                            
%      FINISH                                                                   
%    END IF                                                                     
%%                                                                              
^^?? BKUP STATE
%    VERIFY ON OFF                                                              
%    GET SILENT NOSTORE ABBREVIATED 'ON=bachup now'                            
%    IF ( VALUE .EQ. 1 ) THEN                                                   
%        COPY '#PURGE pre-superflip'                                            
%        copy 'end'                                                             
%    END IF                                                                     
%%                                                                              
%    VARIABLE CHARACTER C67                                                     
%    EVALUATE C67 = '6'                                                         
%    EVALUATE F67 = 6                                                           
^^?? CME STATE                                                                  
%    VERIFY ON OFF                                                              
%    GET SILENT NOSTORE ABBREVIATED 'ON=Merge first'                            
%    IF ( VALUE .EQ. 1 ) THEN                                                   
%      EVALUATE MERGED = FALSE                                                  
%    END IF                                                                     
%%                                                                              
%    IF MERGED .EQ. FALSE THEN                                                  
%      COPY '#SCRIPT COPY67'                                                    
%      COPY '#SYST IN=7 FRIED=YES'                                              
%      COPY 'END'                                                               
%      COPY '#SORT IN=7'                                                        
%      COPY 'END'                                                               
%      COPY '#MERGE 7'                                                          
%      COPY 'END'                                                               
%      COPY '#PURGE  LIST=7'                                                    
%      COPY 'END'                                                               
%      EVALUATE C67 = '7'                                                       
%      EVALUATE F67 = 7                                                         
%    END IF                                                                     
%%                                                                              
^^?? RS STATE                                                                   
%    VERIFY ON OFF                                                              
%    GET SILENT NOSTORE ABBREVIATED 'On=Simple job'                             
%    IF ( VALUE .EQ. 1 ) THEN                                                   
%      TRANSFER '#FOREIGN SUPERFLIP IN= ' // C67 // ' ' TO CRYSTALS             
%      COPY 'END'                                                               
%      ELSE                                                                     
%      TRANSFER '#FOREIGN SUPERFLIP DIFFICULT IN= ' // C67 // ' ' TO CRYSTALS   
%      COPY 'END'                                                               
%    END IF                                                                     
^^?? CRUN STATE                                                                 
%    VERIFY ON OFF                                                              
%    GET SILENT NOSTORE ABBREVIATED 'ON=Run Superflip now'                      
%    EVALUATE IRUN = VALUE                                                      
^^?? CED STATE                                                                  
%    VERIFY ON OFF                                                              
%    GET SILENT NOSTORE ABBREVIATED 'ON=Edit first'                             
^^WI DISPOSE FLIP                                                               
^^CR                                                                            
%    IF ( VALUE .EQ. 1 ) THEN                                                   
#ifndef CRY_USEWX
%      COPY '#SPAWN +notepad.exe sflip.inflip'                                  
%      EVALUATE QTITLE = 'Superflip Preparation'                                
%      EVALUATE QLINE1 = 'Do you want to'                                       
%      EVALUATE QLINE2 = 'run Superflip now?'                                   
%      EVALUATE BUTTOK = '&Yes'                                                 
%      EVALUATE BUTTXX = '&No'                                                  
%      COPY '#SCRIPT XQUESTIO'                                                  
%      IF ANSWER .EQ. FALSE THEN                                                
%        FINISH                                                                 
%      END IF  
#else
%      EVALUATE EDITOR_FILE = 'sflip.inflip'
%      COPY '#SCRIPT XEDITOR'
%      IF ( .NOT. EDITOR_OK ) THEN
 User cancelled super flip run.
%         FINISH
%      END IF                                                                             
#endif
%    END IF                                                                     
%    IF ( IRUN .EQ. 2 ) THEN                                                    
%      FINISH                                                                   
%    END IF                                                                     
%    copy '#script xfliprun'                                                    
%   END BLOCK                                                                   
%%                                                                              
%END SCRIPT                                                                     
