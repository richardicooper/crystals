%SCRIPT CHECKATOM
%%
%% Variables from calling routine
%%
%  VARIABLE CHARACTER CATOMLABEL
%  VARIABLE LOGICAL LATOMEXISTS LSHOWATOM
%%
%% Local variable for checking the edit status
%%
%  VARIABLE CHARACTER CELEM ATYPE
%  VARIABLE INTEGER ISERI PRTNUM
%  VARIABLE REAL RSERI
%%
%  EVALUATE CELEM = FIRSTSTR ( CATOMLABEL )
%  EVALUATE ISERI = FIRSTINT ( CATOMLABEL )
%  EVALUATE PRTNUM = 0
%%
%  EVALUATE LATOMEXISTS = FALSE
%%
%  BLOCK
%    ON EDITERROR TERMINATE
%    COPY '#GENERALEDIT 5'
%    COPY 'LOCATE RECORDTYPE=101'
%    COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%    LOOP
%      ON EDITERROR TERMINATE
%      COPY 'TRANSFER FROM OFFSET=0 TO ATYPE'
%      COPY 'TRANSFER FROM OFFSET=1 TO RSERI'
%      IF ( ( CELEM .EQ. ATYPE ) .AND. ( ISERI .EQ. INTEGER ( RSERI ) ) ) THEN
%% Keep part number for later reference in other contexts
%        COPY 'TRANSFER FROM OFFSET=14 TO PRTNUM'
%        EVALUATE LATOMEXISTS = TRUE
%        EVALUATE LOOPEND = LOOPCOUNTER
%      END IF
%      COPY 'NEXT'
%    END LOOP
%    COPY 'END'
%  END BLOCK
%%
%  IF LATOMEXISTS .AND. LSHOWATOM THEN
%    TRANSFER '^^CO SET MODEL01 SELECT ' // CATOMLABEL // ' YES' TO DISPLAY
%    COPY '#PAUSE 1'
%    TRANSFER '^^CO SET MODEL01 SELECT ' // CATOMLABEL // ' NO' TO DISPLAY
%  END IF
%%
%END SCRIPT