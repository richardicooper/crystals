%SCRIPT DIFFEXEC
%% execute files created by xdiffin/open
%%
%  VARIABLE INTEGER INBASICS FTYPE
%  VARIABLE LOGICAL LFILE IFILE HFILE
%  VARIABLE LOGICAL ANSWER LCANCEL XQMERGE                                      
%  VARIABLE CHARACTER CEXTN
%%  check if files have been created
%%                                                                              
%  BLOCK                                                                        
%    COPY '#DISK'                                                               
%    COPY 'EXTEND SIZE = 10 TRIES  = 1000'                                      
%    COPY 'END'                                                                 
Files INITIAL.* will be created in your directory. They may be important.       
%    COPY '#RELEASE PRINT initial.lis'                                          
%    COPY '#RELEASE LOG initial.dat'                                            
%  END BLOCK                                                                    
%% 
%% Read bits and pieces from Bruker pcf file
%  if ( cextn .eq. 'PCF' ) then                                                                            
%   IF FILEEXISTS ( 'from-cif.cry' ) THEN                                                                        
%      ON END TERMINATE                                                         
%      ON ERROR TERMINATE                                                       
%      COPY '#USE from-cif.cry' 
%   END IF
%  end if
%% 
%  if ( cextn .eq. 'CIF' ) .or. ( cextn .eq. 'CIF_OD' ) then                                                                            
%   IF FILEEXISTS ( 'from-cif.cry' ) THEN                                                                        
%      ON END TERMINATE                                                         
%      ON ERROR TERMINATE                                                       
%      CLEAR                                                                    
%      TRANSFER '#USE from-cif.cry' TO CRYSTALS                                     
%      COPY '#SCRIPT X30BITS'
%      BLOCK                                                                    
%            ON ERROR REPEAT                                                    
%%           WE NEED BASIC LISTS FOR DATA REDUCTION                             
%            CLEAR                                                              
%            COPY '#SCRIPT XINBASIC'                                            
%            IF INBASICS .NE. 1 THEN                                            
{E  Basic Cell, Space Group or composition information is missing.              
{E  This SCRIPT will be abandoned without reading the reflections.              
%                  FINISH                                                       
%            END IF                                                             
%            IF LCANCEL .EQ. TRUE THEN                                          
{E  Basic Cell, Space Group or composition information is missing.              
{E  This SCRIPT will be abandoned without reading the reflections.              
%                  FINISH                                                       
%            END IF                                                             
%      END BLOCK                                                                
%%                                                                              
%      BLOCK                                                                    
%            ON ERROR REPEAT                                                    
%            IF ( EXISTS 14 .NE. 1 ) THEN                                       
%              VERIFY YES NO                                                    
%            GET FINAL ABBREVIATED  -                                           
  'Do you know the asymmetric part of the unit cell?' 'YES'                     
%              IF ( VALUE .EQ. 1 ) THEN                                         
%                COPY '#SCRIPT XLIST14'                                         
%              ELSE                                                             
{R  You will need this information later when you do Fourier maps. Look in      
{R  International Tables before then.                                           
%              END IF                                                           
%            END IF                                                             
%%                                                                              
%      END BLOCK                                                                
%   END IF                                                                    
%  end if
%% 
%%
%%
%  if ( exists 1 .ne. 1 ) then
{I  CRYSTALS has not found any cell parameters.
{I  If you have a SHELX ins/res file, input it from the Data menu.
%   FINISH
%  end if
%%

%  if ( exists 2 .eq. 1 ) .and. ( exists 5 .eq. 1 ) then
{S sort out atoms
%% variables used by the question box below                                     
% VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2                         
% VARIABLE LOGICAL ANSWER                                                       
%%
%     EVALUATE QTITLE = 'Renumber Atoms?'                              
%     EVALUATE QLINE1 = 'Do you want to change atom'                  
%     EVALUATE QLINE2 = 'numbers to CRYSTALS system?'                             
%     EVALUATE BUTTOK = '&Yes'                                         
%     EVALUATE BUTTXX = '&No'                                          
%     COPY '#SCRIPT XQUESTIO'                                          
%     IF ANSWER .EQ. TRUE THEN                                         
%        COPY '#EDIT'
%        COPY 'INSERT ELECTRON'
%        COPY 'DSORT SPARE'
%        COPY 'KEEP 1 ALL'
%        COPY 'END'
%        COPY '#SCRIPT XCENTRE'                                             
%%       RESOLVE H ATOMS                                                             
%        COPY '#SCRIPT HNAM'
%        copy '#EDIT'
%        COPY 'SORT SERIAL'
%        COPY 'END'                                                    
%     END IF
%%                                                                              
%  end if

%%                                                                             
%  if ( cextn .eq. 'FCF' ) then                                                                            
%   if ( .not. FILEEXISTS ( 'cif-reflections.cry' ) ) THEN 
{I CRYSTALS could find no reflections in the cif file.  
{I If you have a SHELX hklf file, input that from the Data menu.
%     FINISH
%   else
%     TRANSFER '#USE cif-reflections.cry' TO CRYSTALS   
%   end if
%  else
%   if ( .not. FILEEXISTS ( 'from-cif.hkl' ) ) THEN 
{I CRYSTALS could find no reflections in the cif file.  
{I If you have a SHELX hklf file, input that from the Data menu.
%    FINISH
%   end if
%  end if
%%
%    COPY '#SCRIPT XFRIEDEL'                                                      
%%                                                                              
{S * Removing systematically absent reflections                                 
%    COPY '#SYSTEMATIC'                                                           
%    COPY 'END'                                                                   
{S * Sorting reflections                                                        
%    COPY '#SORT'                                                                 
%    COPY 'END'                                                                   
%    IF ( XQMERGE ) THEN                                                          
{S * Merging equivalent reflections                                             
%      CLEAR                                                                    
%      COPY '#MERGE'                                                            
%      COPY 'END'                                                               
%    END IF                                                                       
%                                                                               
{S * Final copy to DSC file                                                     
%    COPY '#LIST 6'                                                               
%    COPY 'READ TYPE = COPY'                                                      
%    COPY 'END'                  
%%                                                                              
%%   Set list 4, 23, 28                                                                        
%    COPY '#SCRIPT XFRESH6'
%%                                                                              
{R * Done. Diffractometer data input complete.                                            
% END SCRIPT
