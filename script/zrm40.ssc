%SCRIPT ZRM40
%%
%% Removes mentions of a bond from List 40 make or break sections.
%% /e.g./ If you want to add a MAKE bond, you can be sure it isn't
%% already in the BREAK section and /vice versa/.
%%
%% Public:
% VARIABLE CHARACTER ATOM1 ATOM2
%% Private:
% VARIABLE CHARACTER ELEM1 ELEM2 ELEMA ELEMB
% VARIABLE INTEGER   SERI1 SERI2 SERIA SERIB
%% Code:
% EVALUATE ELEM1 = FIRSTSTR ( ATOM1 )
% EVALUATE ELEM2 = FIRSTSTR ( ATOM2 )
% EVALUATE SERI1 = FIRSTINT ( ATOM1 )
% EVALUATE SERI2 = FIRSTINT ( ATOM2 )
%%
% COPY "#GENERALEDIT 40"
% COPY "LOCATE RECORDTYPE=104"
% COPY "ERROR MESSAGE=NO SIGNAL=NONE ACTION=CONTING NAME=EDITERROR"
% COPY "TOP"
% LOOP
%   ON EDITERROR TERMINATE
%   COPY 'TRANSFER FROM OFFSET=0 TO ELEMA'
%   COPY 'TRANSFER FROM OFFSET=1 TO SERIA'
%   COPY 'TRANSFER FROM OFFSET=7 TO ELEMB'
%   COPY 'TRANSFER FROM OFFSET=8 TO SERIB'

%   IF ( ( ELEM1 .EQ. ELEMA ) .AND. ( SERI1 .EQ. SERIA ) .AND. -
         ( ELEM2 .EQ. ELEMB ) .AND. ( SERI2 .EQ. SERIB ) ) THEN
%     COPY 'DELETE'
%   ELSE IF ( ( ELEM1 .EQ. ELEMB ) .AND. ( SERI1 .EQ. SERIB ) .AND. -
              ( ELEM2 .EQ. ELEMA ) .AND. ( SERI2 .EQ. SERIA ) ) THEN
%     COPY 'DELETE'
%   END IF
%   COPY 'NEXT'
% END LOOP
% COPY "LOCATE RECORDTYPE=105"
% COPY "ERROR MESSAGE=NO SIGNAL=NONE ACTION=CONTING NAME=EDITERROR"
% COPY "TOP"
% LOOP
%   ON EDITERROR TERMINATE
%   COPY 'TRANSFER FROM OFFSET=0 TO ELEMA'
%   COPY 'TRANSFER FROM OFFSET=1 TO SERIA'
%   COPY 'TRANSFER FROM OFFSET=7 TO ELEMB'
%   COPY 'TRANSFER FROM OFFSET=8 TO SERIB'
%   IF ( ( ELEM1 .EQ. ELEMA ) .AND. ( SERI1 .EQ. SERIA ) .AND. -
         ( ELEM2 .EQ. ELEMB ) .AND. ( SERI2 .EQ. SERIB ) ) THEN
%     COPY 'DELETE'
%   ELSE IF ( ( ELEM1 .EQ. ELEMB ) .AND. ( SERI1 .EQ. SERIB ) .AND. -
              ( ELEM2 .EQ. ELEMA ) .AND. ( SERI2 .EQ. SERIA ) ) THEN
%     COPY 'DELETE'
%   END IF
%   COPY 'NEXT'
%  END LOOP
%% Use Overwrite flag - if list hasn't changed length, no disk space
%% is wasted. If list has changed the disc routines will know not
%% to overwrite.
%  COPY 'WRITE OVERWRITE=OVERWRITE'
%  COPY 'END'
%END SCRIPT
