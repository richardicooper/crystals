%SCRIPT EDLIST16
    This SCRIPT puts you into a system editor to modify the refinement
    restraint instructions.
%%
%  IF ( EXISTS 16 .NE. 1 ) THEN
%    COPY '#LIST 16'
%    COPY 'NO'
%    COPY 'END'
%  END IF
%%
%  COPY '#RELEASE PUNCH scporg16.dat'
%  TRANSFER "# This file will be read by CRYSTALS" TO PUNCH
%  TRANSFER "# when you close it, so save any changes that you make." TO PUNCH
%  TRANSFER "# To abandon changes: either don't save, or, delete all" TO PUNCH
%  TRANSFER "# lines from this file before saving." TO PUNCH
%  TRANSFER "# " TO PUNCH
%  TRANSFER "# To remove all restraints, replace them with the" TO PUNCH
%  TRANSFER "# single keyword NO, so that the file reads:" TO PUNCH
%  TRANSFER "#     #LIST 16" TO PUNCH
%  TRANSFER "#     NO" TO PUNCH
%  TRANSFER "#     END" TO PUNCH
%  COPY '#PUNCH 16'
%  COPY 'END'
%  COPY '#RELEASE PUNCH scplst16.dat'
%  TRANSFER "# This file will be read by CRYSTALS" TO PUNCH
%  TRANSFER "# when you close it, so save any changes that you make." TO PUNCH
%  TRANSFER "# To abandon changes: either don't save, or, delete all" TO PUNCH
%  TRANSFER "# lines from this file before saving." TO PUNCH
%  TRANSFER "# " TO PUNCH
%  TRANSFER "# To remove all restraints, replace them with the" TO PUNCH
%  TRANSFER "# single keyword NO, so that the file reads:" TO PUNCH
%  TRANSFER "#     #LIST 16" TO PUNCH
%  TRANSFER "#     NO" TO PUNCH
%  TRANSFER "#     END" TO PUNCH
%  COPY '#PUNCH 16'
%  COPY 'END'
%  COPY '#RELEASE PUNCH bfile.pch'
%%
%  VARIABLE LOGICAL ISERR
%%
%  VARIABLE LOGICAL EDITED_ONCE
%  EVALUATE EDITED_ONCE = FALSE
%%
%  LOOP
%%
#ifdef __GID__
^^WI WINDOW BLOCK16 'Waiting' MODAL CANCEL='^^CO DISPOSE BLOCK16'
^^WI GRID MAIN NROWS=4 NCOLS=3 { @ 2,2 STATIC T1 'Starting editor...'
^^WI @ 3,2 STATIC T1 'Close notepad to continue.' } SHOW
^^CR
%    EVALUATE ISERR = TRUE
%    BLOCK
%      ON ERROR TERMINATE
%    COPY '#use crysdir:script/edlist16.dat'
%%
%      EVALUATE ISERR = FALSE
%%
%    END BLOCK
%%
^^CO DISPOSE BLOCK16
%%

%    IF ( ISERR ) THEN
{E Missing script file (crysdir:script/edlist16.dat).
{I Cannot edit restraints. Maybe a reinstall of CRYSTALS would help.
%      FINISH
%    END IF
#else
%    VARIABLE CHARACTER EDITOR_FILE EDITOR_ORIG
%    VARIABLE LOGICAL EDITOR_OK
%    EVALUATE EDITOR_FILE = 'scplst16.dat'
%    EVALUATE EDITOR_ORIG = 'scporg16.dat'
%    COPY '#SCRIPT xeditorgl'
%    IF ( .NOT. EDITOR_OK ) THEN
%      IF ( EDITED_ONCE ) THEN
%        COPY '#USE scporg16.dat'
 Original list 16 restored.
%      ELSE
 Changes abandoned by user. List 16 has not been changed.
%      END IF
%      FINISH
%    END IF
%    EVALUATE EDITED_ONCE = TRUE
#endif
%%
%    EVALUATE ISERR = TRUE
%    BLOCK
%      ON ERROR TERMINATE
%      COPY '#USE scplst16.dat'
%%
%      EVALUATE ISERR = FALSE
%%
%    END BLOCK
%%
%    IF ( ISERR ) THEN
^^WI WINDOW EDERR1 'Error reading file' MODAL
^^WI COMMIT='B1'  CANCEL='B2' POSITION=CENTRED '_MAIN'
^^WI GRID GTB NROWS=5 NCOLS=5
^^WI {
^^WI   @ 2,2 ICON MYICON HAND
^^WI   @ 2,4 GRID TL NROWS=3 NCOLS=1
^^WI   {
^^WI @ 1,1 STATIC T1 'There is an error in the file that is being'
^^WI @ 2,1 STATIC T2 'read back into CRYSTALS.'
^^WI @ 3,1 STATIC T2 'Check the command syntax is valid.'
^^WI   }
^^WI   @ 4,4 GRID LOWER NROWS=1 NCOLS=7
^^WI   {
^^WI     @ 1,1 STRETCH XQ_S1 HORIZONTAL
^^WI @ 1,2 BUTTON EDB1 'Edit again' DEFAULT 
^^WI @ 1,4 BUTTON EDB2 'Lose changes'
^^WI @ 1,6 BUTTON EDB3 'Ignore'
^^WI     @ 1,7 STRETCH XQ_S2 HORIZONTAL
^^WI   }
^^WI }
^^WI SHOW
^^CR
%%
%      VERIFY EDB1 EDB2 EDB3
%      GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%      CASE VALUE
%        BLOCK 
%          TRANSFER '{I Re-opening file' TO DISPLAY
%        END BLOCK 
%        BLOCK 
%          COPY '#USE scporg16.dat'
%          EVALUATE LOOPEND = LOOPCOUNTER
%          COPY '#SUM L 16'
%          COPY 'END'
%        END BLOCK 
%        BLOCK 
%          TRANSFER '{I Continuing, with errors' TO DISPLAY
%          EVALUATE LOOPEND = LOOPCOUNTER
%        END BLOCK 
%      END CASE
^^CO   DISPOSE EDERR1
%    ELSE 
%      EVALUATE ISERR = TRUE
%      BLOCK
%        ON ERROR TERMINATE
%        COPY '#SUMMARY LIST 16'
%        COPY 'END'
%%   
%        EVALUATE ISERR = FALSE
{I List 16 updated.
%      END BLOCK
%%
%      IF ( ISERR ) THEN
^^WI WINDOW EDERR2 'Error in restraints' MODAL
^^WI COMMIT='B1'  CANCEL='B2' POSITION=CENTRED '_MAIN'
^^WI GRID GTB NROWS=5 NCOLS=5
^^WI {
^^WI   @ 2,2 ICON MYICON HAND
^^WI   @ 2,4 GRID TL NROWS=3 NCOLS=1
^^WI   {
^^WI @ 1,1 STATIC T1 'There is a syntax error in the LIST 16 directives'
^^WI @ 2,1 STATIC T2 '(restraints) that have been read back into CRYSTALS.'
^^WI @ 3,1 STATIC T2 'Perhaps a keyword (DIST,ANGLE etc) is misspelled.'
^^WI   }
^^WI   @ 4,4 GRID LOWER NROWS=1 NCOLS=7
^^WI   {
^^WI     @ 1,1 STRETCH XQ_S1 HORIZONTAL
^^WI @ 1,2 BUTTON EDB4 'Edit again' DEFAULT 
^^WI @ 1,4 BUTTON EDB5 'Lose changes'
^^WI @ 1,6 BUTTON EDB6 'Ignore'
^^WI     @ 1,7 STRETCH XQ_S2 HORIZONTAL
^^WI   }
^^WI }
^^WI SHOW
^^CR
%%
%        VERIFY EDB4 EDB5 EDB6
%        GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
^^CO     DISPOSE EDERR2
%        CASE VALUE
%          BLOCK 
%            TRANSFER '{I Re-opening file' TO DISPLAY
%          END BLOCK 
%          BLOCK 
%            COPY '#USE scporg16.dat'
%            COPY '#SUM L 16'
%            COPY 'END'
%            FINISH
%          END BLOCK 
%          BLOCK 
%            TRANSFER '{I Continuing, with errors' TO DISPLAY
%            FINISH
%          END BLOCK 
%        END CASE
%      ELSE
%        FINISH
%      END IF
%    END IF
%  END LOOP
%END SCRIPT
