%SCRIPT YSIMU
%%
% VARIABLE CHARACTER KEYBREAK
%%
^^WI WINDOW YSIMU 'Restrain thermal similarity' MODAL
^^WI COMMIT='BOK' CANCEL='BXX'
^^WI GRID MAIN NROWS=3 NCOLS=5
^^WI {
^^WI   @ 2,2 GRID LT NROWS=5 NCOLS=1
^^WI   {
^^WI     @ 1,1 STATIC T0 'Restrain all 1-2 and 1-3 neighbouring'
^^WI     @ 2,1 STATIC T1 'atoms to have similar Uij parameters:'
^^WI     @ 4,1 GRID LEFT NROWS=3 NCOLS=5
^^WI     {
^^WI       @ 1,1 STATIC T3 'E.s.d for 1-2 atoms:'
^^WI       @ 1,3 EDITBOX E12 '0.04' CHARS=9 REAL
^^WI       @ 1,5 STATIC T3 'angstroms squared.'
^^WI       @ 3,1 STATIC T3 'E.s.d for 1-3 atoms:'
^^WI       @ 3,3 EDITBOX E13 '0.08' CHARS=9 REAL
^^WI       @ 3,5 STATIC T3 'angstroms squared.'
^^WI     }
^^WI     @ 5,1 CHECKBOX EDIT16 'Edit/view new restraints.' STATE=ON
^^WI   }
^^WI   @ 2,4 GRID RIGHT NROWS=5 NCOLS=1
^^WI   {
^^WI     @ 1,1 BUTTON BOK '&OK' COMMIT DEFAULT
^^WI     @ 3,1 BUTTON BXX '&Cancel' CANCEL
^^WI     @ 5,1 BUTTON BHH '&Help'
^^WI   }
^^WI }
^^WI  SHOW
^^CR
% LOOP
%  VERIFY BXX BHH BOK
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%  CASE VALUE
%   BLOCK                      %BXX
^^CO DISPOSE YSIMU
%    FINISH
%   END BLOCK
%   BLOCK                      %BHH
%    COPY '#SCRIPT XHELP1'
Thermal similarity restraints can be used to control refinements
where there is not enough information to satisfactorily refine
all the thermal parameters independently.
The assumption to be made is that atoms which are either bonded
or 1-3 contacts will have fairly similar anisotropic displacement
parameters.
Note that the e.s.d. on the resulting adps will depend upon the
e.s.d. given to this restraint.
If the e.s.d given is very small, the refinement will become
unstable.
%    COPY '#SCRIPT XHELP2'
%   END BLOCK
%   BLOCK                      %BOK
%     COPY '#RELEASE PUNCH new16bit.pch'
%     COPY '#DIST'
%     COPY 'SEL RAN=L41'
%     CLEAR
%     INSERT 'OUT PUNC=SIMU S1DEV='
^^??  E12 TEXT
%     GET SILENT REAL 'Esd 1' '0.04'
%     INSERT ' S2DEV='
^^??  E13 TEXT
%     GET SILENT REAL 'Esd 2' '0.08'
%     SEND
%     COPY 'EXCLUDE H'
%     COPY 'END'
%     COPY '#RELEASE PUNCH bfile.pch'
%%
%     EVALUATE KEYBREAK = 'THERMSIM'
%     COPY '#SCRIPT ZADDLI16'
%%
 * Done. Thermal similarity restraints added.
^^??  EDIT16 STATE
%     VERIFY ON OFF
%     GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%     IF VALUE .EQ. 1 THEN
%       COPY '#SCRIPT EDLIST16'
%     END IF
^^CO DISPOSE YSIMU
%    FINISH
%   END BLOCK
%  END CASE
% END LOOP
%END SCRIPT
