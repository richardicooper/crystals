%SCRIPT XCELLIN
%% CRYSTALS cell parameter input script
%  VARIABLE REAL A B C AL BE GA VAR
%  VARIABLE REAL SA SB SC SAL SBE SGA
%  VARIABLE REAL AMULT BMULT RTD RTDSQ
%  EVALUATE AMULT = 0.0001
%  EVALUATE BMULT = 1. / AMULT
%  EVALUATE RTD = 57.29577951
%  EVALUATE RTDSQ = BMULT / ( RTD * RTD )
%  VARIABLE INTEGER SWITCH
%  VARIABLE LOGICAL LCANCEL
%  EVALUATE LCANCEL = FALSE
%  EVALUATE A = 1.0
%  EVALUATE B = 1.0
%  EVALUATE C = 1.0
%  EVALUATE AL = 90.0
%  EVALUATE BE = 90.0
%  EVALUATE GA = 90.0
%  EVALUATE SA = 0.0
%  EVALUATE SB = 0.0
%  EVALUATE SC = 0.0
%  EVALUATE SAL = 0.0
%  EVALUATE SBE = 0.0
%  EVALUATE SGA = 0.0
%  BLOCK
%   ON ERROR TERMINATE
%   IF EXISTS 1 .GE. 1 THEN
%   COPY '#GENERALEDIT 1'
%   COPY 'LOCATE RECORD=101'
%   COPY 'TRANSFER FROM OFFSET=0 TO A'
%   COPY 'TRANSFER FROM OFFSET=1 TO B'
%   COPY 'TRANSFER FROM OFFSET=2 TO C'
%   COPY 'TRANSFER FROM OFFSET=3 TO AL'
%   COPY 'TRANSFER FROM OFFSET=4 TO BE'
%   COPY 'TRANSFER FROM OFFSET=5 TO GA'
%   COPY 'END'
%   EVALUATE AL = AL * RTD
%   EVALUATE BE = BE * RTD
%   EVALUATE GA = GA * RTD
%   END IF
%  END BLOCK
%  BLOCK
%   ON ERROR TERMINATE
%   VARIABLE REAL MFAC
%   IF EXISTS 1 .GE. 1 THEN
%   COPY '#GENERALEDIT 31'
%   COPY 'LOCATE RECORD=101'
%   COPY 'TRANSFER FROM OFFSET=0 TO MFAC'
%   COPY 'LOCATE RECORD=102'
%   COPY 'TRANSFER FROM OFFSET=0 TO SA'
%   COPY 'TRANSFER FROM OFFSET=6 TO SB'
%   COPY 'TRANSFER FROM OFFSET=11 TO SC'
%   COPY 'TRANSFER FROM OFFSET=15 TO SAL'
%   COPY 'TRANSFER FROM OFFSET=18 TO SBE'
%   COPY 'TRANSFER FROM OFFSET=20 TO SGA'
%   COPY 'END'
%   EVALUATE SA = SQRT ( SA * MFAC )
%   EVALUATE SB = SQRT ( SB * MFAC )
%   EVALUATE SC = SQRT ( SC * MFAC )
%   EVALUATE SAL = SQRT ( SAL * MFAC ) * RTD
%   EVALUATE SBE = SQRT ( SBE * MFAC ) * RTD
%   EVALUATE SGA = SQRT ( SGA * MFAC ) * RTD
%   END IF
%  END BLOCK
^^WI WINDOW INLIST1 'Cell parameters' MODAL COMMIT='CELLBOK' CANCEL='BXX'
^^WI GRID             GRIDM NROWS=1 NCOLS=2
^^WI {
^^WI  @ 1,1 GRID GRIDL NROWS=5 NCOLS=2
^^WI  {
^^WI   @ 2,2 GRID GRITL NROWS=2 NCOLS=1
^^WI   {
^^WI    @ 1,1 STATIC    TEXT1 'Enter cell lengths in angstroms,'
^^WI    @ 2,1 STATIC    TEXT1 'and cell angles in degrees.'
^^WI   }
^^WI   @ 4,2 GRID GRIDBL NROWS=6 NCOLS=7
^^WI   {
^^WI    @ 1,1 STATIC ST01 'A:'
^^WI    @ 2,1 STATIC ST02 'B:'
^^WI    @ 3,1 STATIC ST03 'C:'
^^WI    @ 4,1 STATIC ST04 'Alpha:'
^^WI    @ 5,1 STATIC ST05 'Beta:'
^^WI    @ 6,1 STATIC ST06 'Gamma:'
^^WI    @ 1,5 STATIC ST07 'sigma(A):'
^^WI    @ 2,5 STATIC ST08 'sigma(B):'
^^WI    @ 3,5 STATIC ST09 'sigma(C):'
^^WI    @ 4,5 STATIC ST10 'sigma(Alpha):'
^^WI    @ 5,5 STATIC ST11 'sigma(Beta):'
^^WI    @ 6,5 STATIC ST12 'sigma(Gamma):'
% CLEAR
% INSERT !^^WI @ 1,3 EDITBOX E1 '!
% STORE FORMAT /(F11.6)/ LENGTH 11 REAL A
% INSERT !'!
% OUTPUT
^^WI CHARS=16 REAL INFORM=YES
% CLEAR
% INSERT !^^WI @ 2,3 EDITBOX E2 '!
% STORE FORMAT /(F11.6)/ LENGTH 11 REAL B
% INSERT !'!
% OUTPUT
^^WI CHARS=16 REAL INFORM=YES
% CLEAR
% INSERT !^^WI @ 3,3 EDITBOX E3 '!
% STORE FORMAT /(F11.6)/ LENGTH 11 REAL C
% INSERT !'!
% OUTPUT
^^WI CHARS=16 REAL INFORM=YES
% CLEAR
% INSERT !^^WI @ 4,3 EDITBOX E4 '!
% STORE FORMAT /(F11.6)/ LENGTH 11 REAL AL
% INSERT !'!
% OUTPUT
^^WI CHARS=16 REAL INFORM=YES
% CLEAR
% INSERT !^^WI @ 5,3 EDITBOX E5 '!
% STORE FORMAT /(F11.6)/ LENGTH 11 REAL BE
% INSERT !'!
% OUTPUT
^^WI CHARS=16 REAL INFORM=YES
% CLEAR
% INSERT !^^WI @ 6,3 EDITBOX E6 '!
% STORE FORMAT /(F11.6)/ LENGTH 11 REAL GA
% INSERT !'!
% OUTPUT
^^WI CHARS=16 REAL INFORM=YES
% CLEAR
% INSERT !^^WI @ 1,7 EDITBOX E1S '!
% STORE FORMAT /(F11.9)/ LENGTH 11 REAL SA
% INSERT !'!
% OUTPUT
^^WI CHARS=16 REAL INFORM=YES
% CLEAR
% INSERT !^^WI @ 2,7 EDITBOX E2S '!
% STORE FORMAT /(F11.9)/ LENGTH 11 REAL SB
% INSERT !'!
% OUTPUT
^^WI CHARS=16 REAL INFORM=YES
% CLEAR
% INSERT !^^WI @ 3,7 EDITBOX E3S '!
% STORE FORMAT /(F11.9)/ LENGTH 11 REAL SC
% INSERT !'!
% OUTPUT
^^WI CHARS=16 REAL INFORM=YES
% CLEAR
% INSERT !^^WI @ 4,7 EDITBOX E4S '!
% STORE FORMAT /(F11.9)/ LENGTH 11 REAL SAL
% INSERT !'!
% OUTPUT
^^WI CHARS=16 REAL INFORM=YES
% CLEAR
% INSERT !^^WI @ 5,7 EDITBOX E5S '!
% STORE FORMAT /(F11.9)/ LENGTH 11 REAL SBE
% INSERT !'!
% OUTPUT
^^WI CHARS=16 REAL INFORM=YES
% CLEAR
% INSERT !^^WI @ 6,7 EDITBOX E6S '!
% STORE FORMAT /(F11.9)/ LENGTH 11 REAL SGA
% INSERT !'!
% OUTPUT
^^WI CHARS=8 REAL INFORM=YES
^^WI   }
^^WI  }
^^WI  @ 1,2 GRID GRIDR NROWS=5 NCOLS=3
^^WI  {
^^WI   @ 2,2 BUTTON CELLBOK '&Ok' DEFAULT DISABLED=YES
^^WI   @ 4,2 BUTTON BXX '&Cancel'
^^WI  }
^^WI }
^^WI SHOW
^^CR
%%
%%
%  LOOP
%   ON ERROR REPEAT
%   ON END   TERMINATE
%   VERIFY E1 E2 E3 E4 E5 E6 E1S E2S E3S E4S E5S E6S BXX CELLBOK
%   GET SILENT NOSTORE FINAL VERIFIED ' ' ' '
%   EVALUATE SWITCH = VALUE
%%
%% This bit just checks for a change to any of the input boxes,
%% and then enables the OK button.
%%
%   IF ( SWITCH .LE. 12 ) THEN
%     GET SILENT NOSTORE REAL ' '
^^CO    SET CELLBOK DISABLED=NO
%%
%% This bit responds to BXX (Cancel or escape) and closes the window.
%%
%   ELSE IF ( SWITCH .EQ. 13 ) THEN
^^CO  DISPOSE INLIST1
%     EVALUATE LCANCEL = TRUE
%     FINISH
%%
%% This bit responds to CELLBOK (OK or return) and processes the information.
%%
%   ELSE
%     CLEAR
%     COPY '#LIST 1'
%     INSERT 'REAL '
^^?? E1 TEXT
%     INSERT ' '
%     GET SILENT REAL 'A'
^^?? E2 TEXT
%     INSERT ' '
%     GET SILENT REAL 'B'
^^?? E3 TEXT
%     INSERT ' '
%     GET SILENT REAL 'C'
^^?? E4 TEXT
%     INSERT ' '
%     GET SILENT REAL 'ALPHA'
^^?? E5 TEXT
%     INSERT ' '
%     GET SILENT REAL 'BETA'
^^?? E6 TEXT
%     INSERT ' '
%     GET SILENT REAL 'GAMMA'
%     SEND
%     COPY 'END'
%     COPY '#LIST 31'
%     TRANSFER  'AMULT ' // CHARACTER ( AMULT ) TO CRYSTALS
%     CLEAR
^^?? E1S TEXT
%     GET SILENT NOSTORE REAL 'SIGMA (a)' '.001'
%     EVALUATE VAR = VALUE * VALUE * BMULT
%     INSERT 'MATRIX '
%     STORE FORMAT /(F12.10)/ LENGTH 12 REAL VAR
%     INSERT ' 0.0 0.0 0.0 0.0 0.0 '
%     SEND
%     CLEAR
^^?? E2S TEXT
%     GET SILENT NOSTORE REAL 'SIGMA (b)' '.001'
%     EVALUATE VAR = VALUE * VALUE * BMULT
%     INSERT 'CONTINUE '
%     STORE FORMAT /(F12.10)/ LENGTH 12 REAL VAR
%     INSERT ' 0.0 0.0 0.0 0.0 '
%     SEND
%     CLEAR
^^?? E3S TEXT
%     GET SILENT NOSTORE REAL 'SIGMA (c)' '.001'
%     EVALUATE VAR = VALUE * VALUE * BMULT
%     INSERT 'CONTINUE '
%     STORE FORMAT /(F12.10)/ LENGTH 12 REAL VAR
%     INSERT ' 0.0 0.0 0.0 '
%     SEND
%     CLEAR
^^?? E4S TEXT
%     GET SILENT NOSTORE REAL 'SIGMA (alpha)' '.02'
%     EVALUATE VAR = VALUE * VALUE * RTDSQ
%     INSERT 'CONTINUE '
%     STORE FORMAT /(F12.10)/ LENGTH 12 REAL VAR
%     INSERT ' 0.0 0.0 '
%     SEND
%     CLEAR
^^?? E5S TEXT
%     GET SILENT NOSTORE REAL 'SIGMA (beta)' '.02'
%     EVALUATE VAR = VALUE * VALUE * RTDSQ
%     INSERT 'CONTINUE '
%     STORE FORMAT /(F12.10)/ LENGTH 12 REAL VAR
%     INSERT ' 0.0 '
%     SEND
%     CLEAR
^^?? E6S TEXT
%     GET SILENT NOSTORE FINAL REAL  'SIGMA (gamma)' '.02'
%     EVALUATE VAR = VALUE * VALUE * RTDSQ
%     INSERT 'CONTINUE '
%     STORE FORMAT /(F12.10)/ LENGTH 12 REAL VAR
%     SEND
%     CLEAR
%     COPY 'END'
^^WI       DISPOSE INLIST1
^^CR
%          FINISH
%   END IF
%  END LOOP
%END SCRIPT
