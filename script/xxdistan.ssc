%SCRIPT XXDISTANCES
%% CRYSTALS cell parameter input script
%  VARIABLE CHARACTER AT1 AT2
%  EVALUATE AT1 = ' ' 
%  EVALUATE AT2 = ' '
%  VARIABLE INTEGER AT1OK AT2OK ATNEXT
%  EVALUATE ATNEXT = 1
%  EVALUATE AT1OK = 0
%  EVALUATE AT2OK = 0
%  COPY '#SUM L 5'
%  COPY 'END'
^^WI WINDOW DIST 'Distance Calculation' SIZE KEEP
^^WI GRID MAIN NROWS=9 NCOLS=5
^^WI {
^^WI    @ 2,2 GRID TLFT NROWS=3 NCOLS=9
^^WI    {
^^WI     @ 2,2 STATIC  T1 'Atom 1:'
^^WI     @ 2,4 EDITBOX E1 ' ' CHARS=6 READONLY
^^WI     @ 2,6 STATIC  T2 'Atom 2:'
^^WI     @ 2,8 EDITBOX E2 ' ' CHARS=6 READONLY
^^WI    }
^^WI    @ 4,2 STATIC  T4 
^^WI           'Click the atoms required, or &All for a full listing'
^^WI    @ 6,2 MODELWINDOW MODELDD 'N' NROWS=20 NCOLS=50
^^WI           INFORM=NO ATTACH=L5
^^WI           RADSCALE=500 RADTYPE=COV MOUSEACTION=HEADERATOM
^^WI    @ 6,4 GRID RGHT NROWS=5 NCOLS=3
^^WI    {
^^WI      @ 2,2 BUTTON  AL '&All'
^^WI      @ 4,2 BUTTON  XX '&Close' CANCEL COMMIT DEFAULT
^^WI    }
^^WI    @ 8,2 TEXTOUT _DISTTEXT '' NROWS=10 NCOLS=80 
^^WI }
^^WI SHOW
^^CR
^^WI SENDTEXTTO _DISTTEXT
^^CR
%%
%  LOOP
%      ON ERROR REPEAT
%      ON END   TERMINATE
%      VERIFY AL XX ATOM
%      GET SILENT FINAL NOSTORE ABBREVIATED ' '
%      CASE VALUE
%         BLOCK             %ALL%
^^WI         DISPOSE DIST
^^CR
%            COPY '#DISTANCE'
%            COPY 'OUTPUT MONITOR = DISTANCES'
%            COPY 'SELECT ALL=YES, COORD=YES, SORT=YES, RANGE=LIMITS'
%            COPY 'LIMITS DMAX=2.5'
%            COPY 'END'
%            FINISH
%         END BLOCK
%         BLOCK             %CLOSE%
^^WI         DISPOSE DIST
^^CR
%            FINISH
%         END BLOCK
%         BLOCK             %ATOM%
%            GET NOSTORE SILENT TEXT ' '
%            IF ATNEXT .EQ. 1 THEN
%               EVALUATE AT1 = CVALUE
%               TRANSFER "^^WI SET E1 TEXT '" // AT1 // "'" TO DISPLAY
%               TRANSFER "^^WI SET E2 TEXT ' '" TO DISPLAY
%               EVALUATE ATNEXT = 2
%               EVALUATE AT1OK = 1
%            ELSE
%               EVALUATE AT2 = CVALUE
%               TRANSFER "^^WI SET E2 TEXT '" // AT2 // "'" TO DISPLAY
%               EVALUATE ATNEXT = 1
%               EVALUATE AT2OK = 1
%            END IF
^^CR
%            IF ( AT1OK .EQ. 1 ) .AND. ( AT2OK .EQ. 1 ) THEN 
%               TRANSFER 'Distance calculation for ' // AT1 // ' - ' -
                                 // AT2 TO DISPLAY
%               COPY '#DISTANCES'
%               COPY 'OUTPUT MONITOR=DIST'
%               COPY 'EXCLUDE ALL'
%               TRANSFER 'INCLUDE ' // AT1 // ' ' // AT2 TO CRYSTALS
%               COPY 'END'
%               EVALUATE AT1OK = 0
%               EVALUATE AT2OK = 0
%            END IF
%         END BLOCK
%      END CASE
%  END LOOP
%END SCRIPT
