%SCRIPT xfovsfc
%    VARIABLE LOGICAL DO_SFLS
%    EVALUATE DO_SFLS = TRUE
^^WI WINDOW xfovsfc 'Fo versus Fc' KEEP SIZE MODAL
^^WI COMMIT='EXT_BOK' CANCEL='EXT_BOK'
^^WI GRID MAIN NROWS=2 NCOLS=1
^^WI {
^^WI  @ 1,1 GRID PRIME NROWS=7 NCOLS=3
^^WI  {
^^WI   @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI   {
^^WI    @ 1,3 GRID EXP2 NROWS=4 NCOLS=3
^^WI    {
^^WI     @ 1,1 STATIC XFW1
^^WI 'You are looking at a scatter plot of Fo versus Fc.'
^^WI     @ 1,2 STATIC XFW2
^^WI 'There should be a good correlation between them.'
^^WI     @ 2,1 STATIC XFW5
^^WI 'A general flattening at high values could be a sign of'
^^WI     @ 2,2 STATIC XFW6
^^WI 'extinction.'
^^WI     @ 3,1 STATIC XFW3
^^WI 'Green crosses are reflections filtered out by LIST 28.'
^^WI     @ 4,1 STATIC XFW4
^^WI 'Right-click on any outliers to add them to the Omited'
^^WI     @ 4,2 STATIC XFW4
^^WI 'list (LIST28)'
^^WI    }
^^WI    @ 1,1 ICON IM INFO
^^WI   }
^^WI   @ 4,2 PLOTWINDOW _VFOFC NROWS=20 NCOLS=64
^^WI     DEFINEPOPUPMENU POPUP 'Selection'
^^WI       ITEM _SDELA '&Omit _L' 'OMIT_N_L'
^^WI     ENDDEFINEMENU
^^WI   @ 6,2 GRID EXBT NROWS=1 NCOLS=10
^^WI   {
^^WI    @ 1,1 STRETCH VB HORIZONTAL
^^WI    @ 1,2 RADIOBUTTON EXT_ALL 'All Data' STATE=ON INFORM
^^WI    @ 1,4 RADIOBUTTON EXT_OBS 'Observed' STATE=OFF INFORM
^^WI    @ 1,6 BUTTON EXT_SAV 'Save Image' 
^^WI    @ 1,8 BUTTON EXT_EXP 'Export Data' 
^^WI    @ 1,10 BUTTON EXT_BOK 'Close' DEFAULT
^^WI   }
^^WI  }
^^WI  @ 2,1 PROGRESS _FOFCPR 'Please wait'
^^WI }
^^WI SHOW
^^CR
^^CO SENDPROGRESSTO _FOFCPR
%  LOOP
%    IF DO_SFLS .EQ. TRUE THEN
%      COPY '#SFLS' 
%      COPY 'CALC' 
%      COPY 'END'
%      EVALUATE DO_SFLS = FALSE
%    END IF
%    ON ERROR CONTINUE
^^?? EXT_ALL STATE
%    VERIFY ON OFF
%    GET SILENT NOSTORE FINAL ABBREVIATED ' '
%    CASE VALUE
%     BLOCK
%      COPY '#sum list 6 level=PLOT'
%     END BLOCK
%     BLOCK
%      COPY '#sum list 6 level=OBSERVED'
%     END BLOCK
%    END CASE
%    COPY 'END'
%    VERIFY EXT_ALL  EXT_OBS EXT_BOK EXT_SAV EXT_EXP OMIT
%    GET SILENT NOSTORE FINAL ABBREVIATED '?' 'EXT_BOK'
%    CASE VALUE
%    BLOCK
ALL
%    END BLOCK
%    BLOCK
OBS
%    END BLOCK
%    BLOCK
^^CO    DISPOSE xfovsfc
%       FINISH
%    END BLOCK
%%
%    BLOCK
Saving Image
^^CO SET _VFOFC SAVE 800 600 FOvsFC
%    END BLOCK
%%
%    BLOCK
^^CO SET _VFOFC EXPORT FOvsFC.csv
%    END BLOCK
%%
%    BLOCK
%      EVALUATE DO_SFLS = TRUE   
%      ON EDITERROR TERMINATE
%      COPY '#GENERALEDIT 28'
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%      COPY 'LOCATE RECORDTYPE = 104'
%      COPY 'INSERT'
%      CLEAR
%      INSERT 'CHANGE 0 REAL REAL ='
%      GET INTEGER ' ' ' '
%      SEND
%      OUTPUT
%      CLEAR
%      INSERT 'CHANGE 1 REAL REAL ='
%      GET INTEGER ' ' ' '
%      SEND
%      OUTPUT
%      CLEAR
%      INSERT 'CHANGE 2 REAL REAL ='
%      GET INTEGER ' ' ' '
%      SEND
%      OUTPUT
%      CLEAR
%      COPY 'WRITE'
%      COPY 'END'
%    END BLOCK
%    END CASE
%  END LOOP
%% Should not get here unless #SUM L 6 failed 
^^CO    DISPOSE xfovsfc
%END SCRIPT

