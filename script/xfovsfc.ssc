%SCRIPT xfovsfc
^^WI WINDOW xfovsfc 'Fo versus Fc' KEEP SIZE MODAL
^^WI COMMIT='EXT_BOK' CANCEL='EXT_BOK'
^^WI GRID MAIN NROWS=2 NCOLS=1
^^WI {
^^WI  @ 1,1 GRID PRIME NROWS=7 NCOLS=3
^^WI  {
^^WI   @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI   {
^^WI    @ 1,3 GRID EXP2 NROWS=3 NCOLS=4
^^WI    {
^^WI     @ 1,1 STATIC XFW1
^^WI 'You are looking at a scatter plot of Fo versus Fc. There should be'
^^WI     @ 2,1 STATIC XFW2
^^WI 'a good correlation between them. Investigate any outliers.'
^^WI     @ 3,1 STATIC XFW3
^^WI 'A general flattening at high values could be a sign of extinction.'
^^WI     @ 2,4 STATIC XFW4
^^WI 'Right-click on any outliers to add them to the Omited list (LIST28)'
^^WI    }
^^WI    @ 1,1 ICON IM INFO
^^WI   }
^^WI   @ 4,2 PLOTWINDOW _VFOFC NROWS=20 NCOLS=64
^^WI     DEFINEPOPUPMENU POPUP 'Selection'
^^WI       ITEM _SDELA '&Omit _L' 'OMIT_N_L'
^^WI     ENDDEFINEMENU
^^WI   @ 6,2 GRID EXBT NROWS=1 NCOLS=3
^^WI   {
^^WI    @ 1,1 BUTTON EXT_BOK 'Close' DEFAULT
^^WI    @ 1,3 BUTTON EXT_EXP 'Export' 
^^WI   }
^^WI  }
^^WI  @ 2,1 PROGRESS _FOFCPR 'Please wait'
^^WI }
^^WI SHOW
^^CR
^^CO SENDPROGRESSTO _FOFCPR
%  LOOP
%    ON ERROR CONTINUE
%    COPY '#sum list 6 level=plot'
%    COPY 'END'
%    VERIFY EXT_BOK EXT_EXP OMIT
%    GET SILENT NOSTORE FINAL ABBREVIATED '?' 'EXT_BOK'
%    CASE VALUE
%    BLOCK
^^CO    DISPOSE xfovsfc
%       FINISH
%    END BLOCK
%    BLOCK
^^CO SET _VFOFC EXPORT FOvsFC.csv
%    END BLOCK
%    BLOCK
%      ON EDITERROR TERMINATE
%      COPY '#GENERALEDIT 28'
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%      COPY 'LOCATE RECORDTYPE = 104'
%      COPY 'INSERT'
%      CLEAR
%      INSERT 'CHANGE 0 REAL REAL ='
%      GET INTEGER ' ' ' '
%      SEND
%      OUTPUT
%      CLEAR
%      INSERT 'CHANGE 1 REAL REAL ='
%      GET INTEGER ' ' ' '
%      SEND
%      OUTPUT
%      CLEAR
%      INSERT 'CHANGE 2 REAL REAL ='
%      GET INTEGER ' ' ' '
%      SEND
%      OUTPUT
%      CLEAR
%      COPY 'WRITE'
%      COPY 'END'
%    END BLOCK
%    END CASE
%  END LOOP
%% Should not get here unless #SUM L 6 failed 
^^CO    DISPOSE xfovsfc
%END SCRIPT

