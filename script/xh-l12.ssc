%SCRIPT XH-L12

%%   Local variables:
%  VARIABLE CHARACTER CTYPE
%  VARIABLE REAL SERIAL
%  VARIABLE LOGICAL HRIDE
%% Initialise to no hydrogen, but if any found, refine them.
%% see also XWRITE5.SSC, which tests REFLAG for each atom
%  EVALUATE HRIDE = FALSE

%  IF EXISTS 5 .LE. 0 THEN

You have no atom for this structure

%   FINISH
%  END IF
%     QUEUE REWIND
%     COPY '#GENERALEDIT 5'
%     COPY 'LOCATE RECORDTYPE=101'
%     COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%     LOOP
%       ON EDITERROR TERMINATE
%       COPY 'TRANSFER FROM OFFSET=0 TO CTYPE'
%       COPY 'TRANSFER FROM OFFSET=1 TO SERIAL'
%       IF ( CTYPE .EQ. 'H' )  THEN
%         EVALUATE HRIDE = TRUE
%         CLEAR
%         INSERT 'CONT H('
%         STORE FORMAT /(I5)/ LENGTH 5 INTEGER INTEGER SERIAL
%         INSERT ')'
%%         OUTPUT
%         QUEUE SEND
%       ELSE IF ( CTYPE .EQ. 'D' )  THEN
%         EVALUATE HRIDE = TRUE
%         CLEAR
%         INSERT 'CONT D('
%         STORE FORMAT /(I5)/ LENGTH 5 INTEGER INTEGER SERIAL
%         INSERT ')'
%%         OUTPUT
%         QUEUE SEND
%       END IF
%       COPY 'NEXT'
%     END LOOP
%     COPY 'END'
%% If HRIDE is still set, we found some atoms to ride.
%%
%  IF ( HRIDE ) THEN
%     COPY '#RELEASE PUNCH h-constraints.dat'
%     COPY '#DISTANCES'
%     COPY 'OUTPUT MON=OFF PUNCH=SCRIPT'
%     COPY 'SELECT SYMMETRY=NONE  RANGE=L41'
%     COPY 'BONDED'
%     QUEUE PROCESS
%     COPY 'END'
%     COPY '#RELEASE PUNCH bfile.pch'

Hydrogen RIDE constraints have been put into {&h-constraints.dat{&


%  ELSE

 No hydrogen atoms to constrain

%  END IF
%%
% END SCRIPT
