%SCRIPT XXANGLES
%% CRYSTALS cell parameter input script
%  VARIABLE CHARACTER AT1 AT2 AT3
%  EVALUATE AT1 = ' ' 
%  EVALUATE AT2 = ' '
%  EVALUATE AT3 = ' '
%  VARIABLE INTEGER AT1OK AT2OK AT3OK ATNEXT
%  EVALUATE ATNEXT = 1
%  EVALUATE AT1OK = 0
%  EVALUATE AT2OK = 0
%  EVALUATE AT3OK = 0
%  COPY '#SUM L 5'
%  COPY 'END'
^^WI WINDOW ANGLES 'Angle Calculation' MODAL SIZE
^^WI GRID MAIN NROWS=3 NCOLS=3
^^WI {
^^WI  @ 1,2 GRID UPPR NROWS=2 NCOLS=2
^^WI  {
^^WI   @ 1,1 GRID LEFT NROWS=2 NCOLS=1
^^WI   {
^^WI    @ 1,1 GRID TLFT NROWS=3 NCOLS=13
^^WI    {
^^WI     @ 2,2 STATIC  T1 'Atom 1:'
^^WI     @ 2,4 EDITBOX E1 ' ' CHARS=6 READONLY
^^WI     @ 2,6 STATIC  T2 'Atom 2:'
^^WI     @ 2,8 EDITBOX E2 ' ' CHARS=6 READONLY
^^WI     @ 2,10 STATIC  T3 'Atom 3:' 
^^WI     @ 2,12 EDITBOX E3 ' ' CHARS=6 READONLY
^^WI    }
^^WI    @ 2,1 GRID BLFT NROWS=4 NCOLS=3
^^WI    {
^^WI     @ 1,2 STATIC  T4 
^^WI           'Click the atom required, or &All for a full listing'
^^WI     @ 3,2 MODELWINDOW MODELAA 'N' NROWS=20 NCOLS=50
^^WI           INFORM=NO ATTACH=L5
^^WI           RADSCALE=500 RADTYPE=COV MOUSEACTION=HEADERATOM
^^WI    }
^^WI   }
^^WI   @ 1,2 GRID RGHT NROWS=5 NCOLS=3
^^WI   {
^^WI    @ 2,2 BUTTON  AL '&All'
^^WI    @ 4,2 BUTTON  XX '&Close' CANCEL COMMIT DEFAULT
^^WI   }
^^WI  }
^^WI  @ 2,2 MULTIEDIT _ANGLETEXT '' NROWS=10 NCOLS=80 
^^WI        IGNORE FIXEDFONT=YES DISABLED=YES
^^WI }
^^WI SHOW
^^CR
^^WI SENDTEXTTO _ANGLETEXT
^^CR
%%
%  LOOP
%      ON ERROR REPEAT
%      ON END   TERMINATE
%      VERIFY AL XX ATOM
%      GET FINAL SILENT NOSTORE ABBREVIATED ' '
%      CASE VALUE
%         BLOCK             %ALL%
^^WI         DISPOSE ANGLES
^^CR
%            COPY '#DISTANCE'
Warning, angles calculation does not yet output to VDU routine.
%%            COPY 'OUTPUT MONITOR = ANGLES'
%            COPY 'SELECT ALL=YES, COORD=YES, SORT=YES, RANGE=LIMITS'
%            COPY 'LIMITS DMAX=2.5, AMAX=2.0'
%            COPY 'END'
%            FINISH
%         END BLOCK
%         BLOCK             %CLOSE%
^^WI         DISPOSE ANGLES
^^CR
%            FINISH
%         END BLOCK
%         BLOCK             %ATOM%
%            GET SILENT NOSTORE TEXT ' '
%            IF ATNEXT .EQ. 1 THEN
%               EVALUATE AT1 = CVALUE
%               TRANSFER "^^WI SET E1 TEXT '" // AT1 // "'" TO DISPLAY
%               TRANSFER "^^WI SET E2 TEXT ' '" TO DISPLAY
%               TRANSFER "^^WI SET E3 TEXT ' '" TO DISPLAY
%               EVALUATE ATNEXT = 2
%               EVALUATE AT1OK = 1
%            ELSE IF ATNEXT .EQ. 2 THEN
%               EVALUATE AT2 = CVALUE
%               TRANSFER "^^WI SET E2 TEXT '" // AT2 // "'" TO DISPLAY
%               EVALUATE ATNEXT = 3
%               EVALUATE AT2OK = 1
%            ELSE
%               EVALUATE AT3 = CVALUE
%               TRANSFER "^^WI SET E3 TEXT '" // AT3 // "'" TO DISPLAY
%               EVALUATE ATNEXT = 1
%               EVALUATE AT3OK = 1
%            END IF
^^CR
%            IF ( AT1OK .EQ. 1 ) .AND. ( AT2OK .EQ. 1 ) - 
                                 .AND. ( AT3OK .EQ. 1 ) THEN
          
%               TRANSFER 'Angle calculation for ' // AT1 // ' - ' -
                                 // AT2 // ' - '  // AT3 TO DISPLAY
%               COPY '#DISTANCES'

Warning, angles calculation does not yet output to VDU routine.

%%               COPY 'OUTPUT MONITOR=ANGLES'
%               COPY 'EXCLUDE ALL'
%               TRANSFER 'INCLUDE ' // AT1 // ' ' -
                                 // AT2 // ' '  // AT3 TO CRYSTALS
%               COPY 'END'
%               EVALUATE AT1OK = 0
%               EVALUATE AT2OK = 0
%               EVALUATE AT3OK = 0
%            END IF
%         END BLOCK
%      END CASE
%  END LOOP
%END SCRIPT
