%SCRIPT XCHECK6
% VARIABLE LOGICAL MERGER ABSENT LNEWRAT LNEWRHO I6DONE I6MASK LMAONCE
% VARIABLE REAL NEWRHO NEWRAT THFULL MRGR
% VARIABLE REAL VOLPRATM NNONHATM CELLVOL SGMULT NALLOWREF ESTNPARA ESTOTP
% VARIABLE INTEGER ABSPRINT ABSTOTAL
% EVALUATE MERGER = FILEEXISTS 'mergingr.dat'
% EVALUATE ABSENT = FILEEXISTS 'absences.dat'
% EVALUATE LMAONCE = TRUE
%%
% IF ( EXISTS 6 .NE. 1 ) THEN

{E No reflections stored. Use "X-ray data->Input reflections".

%   FINISH
% END IF
%%
% EVALUATE I6DONE = TRUE
% EVALUATE I6MASK = TRUE
% COPY '#SCRIPT XPUTINFO'
%%
%%
^^WI WINDOW XCHECK6 'Initial Analyses' LARGE MODAL KEEP SIZE
^^WI COMMIT='XC_BOK' CANCEL='XC_BOK'
^^WI GRID MAIN NROWS=3 NCOLS=1
^^WI {
^^WI  @ 1,1 GRID TOP NROWS=3 NCOLS=3
^^WI  {
^^WI   @ 2,2 TABCTRL _MAINTABS
^^WI   {
^^WI    TAB _MAINTAB2a 'Completeness' GRID _MAINTG2 NROWS=5 NCOLS=3
^^WI    { 
^^WI     @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI     {
^^WI      @ 1,3 GRID EXP2 NROWS=2 NCOLS=1
^^WI      {
^^WI       @ 1,1 STATIC XFW1
^^WI 'You are looking at the completeness of the data set'
^^WI       @ 2,1 STATIC XFW2
^^WI 'across the range ( 0.75 - 1.0 ) * theta max.'
^^WI      }
^^WI      @ 1,1 ICON IM INFO
^^WI     }
^^WI     @ 4,2 PLOTWINDOW _VCOMPL NROWS=20 NCOLS=64
^^WI      DEFINEPOPUPMENU POPUP 'Selection'
^^WI        ITEM _SDELA '&Manually set theta full = _L' 'SIGFULL_N_L'
^^WI      ENDDEFINEMENU
^^WI    }
^^WI    TAB _MAINTAB2 'Sigma freq' GRID _MAINTG2 NROWS=5 NCOLS=3
^^WI    { 
^^WI     @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI     {
^^WI      @ 1,3 GRID EXP2 NROWS=3 NCOLS=1
^^WI      {
^^WI       @ 1,1 STATIC XFW1
^^WI 'You are looking at the frequency of I/sigma(I). The blue line shows'
^^WI       @ 2,1 STATIC XFW2
^^WI 'number of reflections above a given I/sigma(I) ratio.'
^^WI       @ 3,1 STATIC XFW3 'Right-click on a bar for data cut-off options.'
^^WI      }
^^WI      @ 1,1 ICON IM INFO
^^WI     }
^^WI     @ 4,2 PLOTWINDOW _VSIGLEVL NROWS=20 NCOLS=64
^^WI      DEFINEPOPUPMENU POPUP 'Selection'
^^WI        ITEM _SDELA '&Reject data: I/sigma(I) < _X' 'SIGCUT_N_X'
^^WI      ENDDEFINEMENU
^^WI    }
^^WI    TAB _MAINTAB2 'Sigma vs Res' GRID _MAINTG2 NROWS=5 NCOLS=3
^^WI    { 
^^WI     @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI     {
^^WI      @ 1,3 GRID EXP2 NROWS=3 NCOLS=1
^^WI      {
^^WI       @ 1,1 STATIC XFW1
^^WI 'You are looking at the distribution of I/sigma(I) against '
^^WI       @ 2,1 STATIC XFW2
^^WI '(sin(theta)/lambda)**2'
^^WI       @ 3,1 STATIC XFW3 'Right-click a point for data cut-off options.'
^^WI      }
^^WI      @ 1,1 ICON IM INFO
^^WI     }
^^WI     @ 4,2 PLOTWINDOW _VSIGRES NROWS=20 NCOLS=64
^^WI      DEFINEPOPUPMENU POPUP 'Selection'
^^WI        ITEM _SDELA '&Reject data: rho > _X' 'RHOMIT_N_X'
^^WI        ITEM _SDELA '&Reject data: I/sigma(I) < _Y' 'SIGCUT_N_Y'
^^WI      ENDDEFINEMENU
^^WI    }
^^WI    TAB _MAINTAB3 'Merging R' GRID _MAINTG3 NROWS=5 NCOLS=3
^^WI    {
% IF ( MERGER ) THEN
^^WI     @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI     {
^^WI       @ 1,3 GRID EXP2 NROWS=3 NCOLS=1
^^WI      {
^^WI       @ 1,1 STATIC XFW1
^^WI  'You are looking at merging R statistics.'
^^WI       @ 2,1 STATIC XFW2 ' '
^^WI       @ 3,1 STATIC XFW3 'Right-click on a bar for data cut-off options.'
^^WI      }
^^WI      @ 1,1 ICON IM INFO
^^WI     }
^^WI     @ 4,2 PLOTWINDOW _VRMERGE NROWS=20 NCOLS=64
^^WI      DEFINEPOPUPMENU POPUP 'Selection'
^^WI        ITEM _SDELA '&Reject data: I/sigma(I) < _X' 'SIGCUT_N_X'
^^WI      ENDDEFINEMENU
% ELSE
^^WI     @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI     {
^^WI      @ 1,3 GRID EXP2 NROWS=3 NCOLS=1
^^WI      {
^^WI       @ 1,1 STATIC XFW1 'No merging-R file found. Perhaps data have'
^^WI       @ 2,1 STATIC XFW2 'not been merged (#SORT,#MERGE), or perhaps'
^^WI       @ 3,1 STATIC XFW3 'they were merged by other software.'
^^WI      }
^^WI      @ 1,1 ICON IM ERROR
^^WI     }
% END IF
^^WI    }
^^WI    TAB _MAINTAB1 'Absences' GRID _MAINTG1 NROWS=5 NCOLS=3
^^WI    {
% IF ( ABSENT ) THEN
^^WI     @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI     {
^^WI      @ 1,3 GRID EXP2 NROWS=3 NCOLS=1
^^WI      {
^^WI       @ 1,1 STATIC XFW1
^^WI  'You are looking at a systematic absence analysis.'
^^WI       @ 2,1 STATIC XFW2 ' '
^^WI       @ 3,1 STATIC XFW3 ' '
^^WI      }
^^WI      @ 1,1 ICON IM INFO
^^WI     }
^^WI     @ 4,2 PLOTWINDOW _VABSENT NROWS=20 NCOLS=64
% ELSE
^^WI     @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI     {
^^WI      @ 1,3 GRID EXP2 NROWS=3 NCOLS=1
^^WI      {
^^WI       @ 1,1 STATIC XFW1 'No absences file found. Perhaps absences have'
^^WI       @ 2,1 STATIC XFW2 'not been removed (#SYST), or perhaps they were'
^^WI       @ 3,1 STATIC XFW3 'removed by other software.'
^^WI      }
^^WI      @ 1,1 ICON IM ERROR
^^WI     }
% END IF
^^WI    }
^^WI     TAB _MAINTAB0 'Wilson' GRID _MAINTG0 NROWS=7 NCOLS=3
^^WI    {
^^WI     @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI     {
^^WI      @ 1,3 GRID EXP2 NROWS=3 NCOLS=1
^^WI      {
^^WI       @ 1,1 STATIC XFW1
^^WI  'You are looking at a Wilson Plot.'
^^WI       @ 2,1 STATIC XFW2 ' '
^^WI       @ 3,1 STATIC XFW3 'Right-click a point for data cut-off options.'
^^WI      }
^^WI      @ 1,1 ICON IM INFO
^^WI     }
^^WI     @ 4,2 PLOTWINDOW _VWILSON NROWS=20 NCOLS=64
^^WI      DEFINEPOPUPMENU POPUP 'Selection'
^^WI        ITEM _SDELA '&Reject data: rho > _Y ' 'RHOMIT_N_Y'
^^WI      ENDDEFINEMENU
^^WI     @ 6,2 GRID GSTAT NROWS=1 NCOLS=7
^^WI     {
^^WI       @ 1,1 STATIC WBFS 'B factor:'
^^WI       @ 1,3 STATIC _MW_BFACTR '0000.0000'
^^WI       @ 1,5 STATIC WBFS 'Scale:'
^^WI       @ 1,7 STATIC _MW_BSCALE '0000.0000'
^^WI     }
^^WI    }
^^WI    TAB _MAINTAB4 'N(Z) distribution' GRID _MAINTG0 NROWS=6 NCOLS=3
^^WI    {
^^WI     @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI     {
^^WI      @ 1,3 GRID EXP2 NROWS=3 NCOLS=1
^^WI      {
^^WI       @ 1,1 STATIC XFW1
^^WI 'You are looking at N(Z) cumulative probability distribution functions'
^^WI       @ 2,1 STATIC XFW2 ' '
^^WI       @ 3,1 STATIC XFW3 ' '
^^WI      }
^^WI      @ 1,1 ICON IM INFO
^^WI     }
^^WI     @ 4,2 PLOTWINDOW _VNZDIST NROWS=20 NCOLS=64
^^WI     @ 6,2 GRID GSTAT2 NROWS=1 NCOLS=3
^^WI     {
^^WI       @ 1,1 STATIC WBFS 'MOD(E**2-1)'
^^WI       @ 1,3 STATIC _MW_E2MIN1 '000000.000'
^^WI     }
^^WI    }
^^WI    TAB _MAINTAB5 'Cell content' GRID _MAINTG0 NROWS=5 NCOLS=3
^^WI    {
^^WI     @ 2,2 GRID CGRID NROWS=3 NCOLS=1 OUTLINE='Cell contents'
^^WI     {
^^WI      @ 1,1 GRID CGRID1 NROWS=5 NCOLS=5
^^WI      {
^^WI       @ 1,1 STATIC C11 'Cell contents (List 29), indicate'
^^WI       @ 1,3 STATIC C12 '00000000'
^^WI       @ 1,5 STATIC C13 'non-H atoms in the asymmetric unit.'
^^WI       @ 2,1 STATIC C21 'Cell volume (List 1) is'
^^WI       @ 2,3 STATIC C22 '00000000'
^^WI       @ 2,5 STATIC C23 'cubic Angstroms.'
^^WI       @ 3,1 STATIC C31 'Space group multiplicity (List 2) is'
^^WI       @ 3,3 STATIC C32 '00000000'
^^WI       @ 5,1 STATIC C41 'Therefore, volume per non-H atom is'
^^WI       @ 5,3 STATIC C42 '00000000'
^^WI       @ 5,5 STATIC C43 'cubic Angstroms. (18.0 would be typical)'
^^WI      }
^^WI      @ 3,1 GRID CGRID2 NROWS=1 NCOLS=4
^^WI      {
^^WI       @ 1,1 STRETCH CS2 HORIZONTAL
^^WI       @ 1,2 BUTTON ED29 'Change cell contents'
^^WI       @ 1,4 BUTTON ED02 'Change space group'
^^WI      }
^^WI     }
^^WI     @ 4,2 GRID PGRID NROWS=6 NCOLS=1 OUTLINE='Obs/param ratio'
^^WI     {
^^WI      @ 1,1 GRID PGRID1 NROWS=1 NCOLS=7
^^WI      {
^^WI       @ 1,1 STATIC P11 'Based on'
^^WI       @ 1,3 STATIC P12 '000000'
^^WI       @ 1,5 STATIC P13
^^WI       'non-h atoms, estimated final number of lsq parameters is'
^^WI       @ 1,7 STATIC P14 '000000'
^^WI      }
^^WI      @ 2,1 GRID PGRID2 NROWS=1 NCOLS=5
^^WI      {
^^WI       @ 1,1 STATIC P21 'List 28 currently allows'
^^WI       @ 1,3 STATIC P22 '00000000'
^^WI       @ 1,5 STATIC P23 'reflections to be used for refinement.'
^^WI      }
^^WI      @ 4,1 GRID PGRID3 NROWS=1 NCOLS=4
^^WI      {
^^WI       @ 1,1 STATIC P31 'Estimated final obs:parameter ratio is'
^^WI       @ 1,3 STATIC P32 '000000'
^^WI       @ 1,4 STATIC P33 ':  1.        (Ideal for publication is > 8.0)'
^^WI      }
^^WI      @ 6,1 GRID PGRID3 NROWS=1 NCOLS=2
^^WI      {
^^WI       @ 1,1 STRETCH CS2 HORIZONTAL
^^WI       @ 1,2 BUTTON ED28 'Change reflection filters'
^^WI      }
^^WI     }
^^WI    }
^^WI   }
^^WI  }
^^WI  @ 2,1 GRID BUTT NROWS=2 NCOLS=5
^^WI  {
^^WI   @ 1,1 STRETCH RS HORIZONTAL
^^WI   @ 1,2 BUTTON XC_REF 'Refresh' 
^^WI   @ 1,4 BUTTON XC_BOK 'Close' COMMIT DEFAULT CANCEL
^^WI  }
^^WI  @ 3,1 PROGRESS _CHECKPR 'Please wait'
^^WI }
^^WI SHOW
^^CR
^^CO SENDPROGRESSTO _CHECKPR
%%
% LOOP
{I Computing completeness.
^^CO SET PROGOUTPUT TEXT 'Computing completeness'
%  COPY '#THLIM'
%  COPY 'OUTPUT PLOT=YES'
%  COPY 'END'
{I Done.
%%
{I Computing Wilson statistics.
^^CO SET PROGOUTPUT TEXT 'Computing Wilson statistics'
%  COPY '#WILSON'
%  COPY 'OUTPUT PLOT=YES NZ=YES STATS=YES'
%  COPY 'END'
{I Done.
%%
%  IF ( ABSENT .AND. LMAONCE ) THEN
%    COPY '#OPEN SCPDATA absences.dat'
^^CO SET PROGOUTPUT TEXT 'Reading systematic absences from absences.dat'
{I About to get absence data.
%    EVALUATE ABSPRINT = 0
%    EVALUATE ABSTOTAL = 0
%    EXTRACT REWIND
%% Skip first line:
%    EXTRACT NEXT
%%
^^PL PLOTDATA _CLASS SCATTER ATTACH _VABSENT
^^PL XAXIS TITLE 'Fo**2/sigma(Fo**2)'
^^PL NSERIES=1 LENGTH=40 YAXIS TITLE 'Fo**2'
%%
%    LOOP
%      ON ERROR TERMINATE
%      ON END TERMINATE
%%
%% Get the data for each result
%%
%      EXTRACT NEXT
%      CLEAR
%      EXTRACT TRANSFER INPUT
%      CLEAR
%      INSERT !^^PL LABEL '!
%      GET NOPROMPT INTEGER ' ' '-99'
%      GET NOPROMPT INTEGER ' ' '-99'
%      GET NOPROMPT INTEGER ' ' '-99'
%      INSERT !' DATA !
%      GET NOPROMPT REAL ' ' '-1.0'
%      GET NOPROMPT REAL ' ' '-1.0'
%      OUTPUT
%      EVALUATE ABSPRINT = ABSPRINT + 1
%      IF ABSPRINT .GE. 500 THEN
%        EVALUATE ABSTOTAL = ABSTOTAL + ABSPRINT
%        EVALUATE ABSPRINT = 0
%        TRANSFER 'Read ' // CHARACTER ABSTOTAL // ' absences from file' -
 TO DISPLAY
%      END IF
%    END LOOP
%    COPY '#CLOSE SCPDATA'
^^PL SHOW
^^CR
{I Done.
%  END IF
%%
^^CO SET PROGOUTPUT TEXT 'Calculating I/u(I) and I/u(I) vs rho distribution'
{I Calculating I/u(I) distribution.
%  COPY '#SIGMADIST'
%  COPY 'OUTPUT PLOT=YES RES=YES'
%  COPY 'END'
{I Done.
%%
^^CO SET PROGOUTPUT TEXT 'Reading merging-R data from mergingr.dat'
%  IF ( MERGER .AND. LMAONCE ) THEN
%    COPY '#OPEN SCPDATA mergingr.dat'
{I Fetching merging R data.
%    EXTRACT REWIND
%% Skip first three lines:
%    EXTRACT NEXT
%    EXTRACT NEXT
%    EXTRACT NEXT
%%
^^PL PLOTDATA _CLASS BARGRAPH ATTACH _VRMERGE KEY
^^PL XAXIS TITLE 'I/sigma(I)'
^^PL YAXIS ZOOM 0 50 TITLE 'Merging R factor(%) / N contributors'
^^PL YAXISRIGHT TITLE 'Number Of Reflections'
^^PL NSERIES=3 LENGTH=100
^^PL SERIES 1 SERIESNAME 'Merging R factor'
^^PL SERIES 2 SERIESNAME 'Number contributors' TYPE LINE 
^^PL SERIES 3 SERIESNAME 'Number > given I/sigma(I)' TYPE LINE USERIGHTAXIS
%%
% LOOP
%  ON ERROR TERMINATE
%  ON END TERMINATE
%%
%% Get the data for each result
%%
%  EXTRACT NEXT
%  CLEAR
%  EXTRACT TRANSFER INPUT
%  CLEAR
%  INSERT !^^PL LABEL !
%  GET NOPROMPT REAL ' ' '-1.0'
%  INSERT ! DATA !
%  GET NOSTORE NOPROMPT REAL ' ' '-1.0'
%  EVALUATE MRGR = VALUE
%  IF MRGR .LT. 0.0 THEN
%   EVALUATE MRGR = - MRGR
%  END IF
%  STORE REAL MRGR
%  GET NOPROMPT INTEGER ' ' '-1'
%  GET NOPROMPT INTEGER ' ' '-1'
%  OUTPUT
% END LOOP
^^PL SHOW
^^CR
%    COPY '#CLOSE SCPDATA'
{I Done.
%  END IF
%  EVALUATE LMAONCE = FALSE
%%
^^CO SET PROGOUTPUT TEXT 'Calculating data:param ratio. Density etc.'
%  COPY '#SCRIPT CHECKDENS'
%%
%  CLEAR
%  INSERT "^^CO SET C12 TEXT '"
%  STORE FORMAT /(F7.2)/ LENGTH 7 REAL NNONHATM
%  INSERT "'"
%  OUTPUT
%%
%  CLEAR
%  INSERT "^^CO SET P12 TEXT '"
%  STORE FORMAT /(F7.2)/ LENGTH 7 REAL NNONHATM
%  INSERT "'"
%  OUTPUT
%%
%  CLEAR
%  INSERT "^^CO SET C22 TEXT '"
%  STORE FORMAT /(F9.3)/ LENGTH 9 REAL CELLVOL
%  INSERT "'"
%  OUTPUT
%%
%  CLEAR
%  INSERT "^^CO SET C32 TEXT '"
%  STORE FORMAT /(I4)/ LENGTH 4 INTEGER INTEGER SGMULT
%  INSERT "'"
%  OUTPUT
%%
%  CLEAR
%  INSERT "^^CO SET C42 TEXT '"
%  STORE FORMAT /(F6.2)/ LENGTH 6 REAL VOLPRATM
%  INSERT "'"
%  OUTPUT
%%
%  CLEAR
%  INSERT "^^CO SET P14 TEXT '"
%  STORE FORMAT /(I6)/ LENGTH 6 INTEGER INTEGER ESTNPARA
%  INSERT "'"
%  OUTPUT
%%
%  CLEAR
%  INSERT "^^CO SET P22 TEXT '"
%  STORE FORMAT /(I7)/ LENGTH 7 INTEGER INTEGER NALLOWREF
%  INSERT "'"
%  OUTPUT
%%
%  CLEAR
%  INSERT "^^CO SET P32 TEXT '"
%  STORE FORMAT /(F6.2)/ LENGTH 6 REAL ESTOTP
%  INSERT "'"
%  OUTPUT
^^CO SET PROGOUTPUT TEXT 'Ready'
%%
%  LOOP
%    VERIFY XC_BOK XC_REF RHOMIT SIGCUT ED28 ED02 ED29 SIGFULL
%    GET NOSTORE SILENT FINAL ABBREVIATED ' ' ' '
%    CASE VALUE
%      BLOCK    %XC_BOK
^^CO     DISPOSE XCHECK6
%        FINISH
%      END BLOCK
%      BLOCK    %XC_REF
%        EVALUATE LOOPCOUNTER = LOOPEND
%      END BLOCK
%      BLOCK    %RHOMIT
%        GET SILENT NOSTORE REAL ' ' ' '
%        EVALUATE NEWRHO = VALUE
%        EVALUATE LNEWRAT = FALSE
%        EVALUATE LNEWRHO = TRUE
%        COPY '#SCRIPT XLIST28'
%        EVALUATE LOOPCOUNTER = LOOPEND
%      END BLOCK
%      BLOCK    %SIGCUT
%        GET NOSTORE SILENT REAL ' ' ' '
%        EVALUATE NEWRAT = VALUE
%        EVALUATE LNEWRAT = TRUE
%        EVALUATE LNEWRHO = FALSE
%        COPY '#SCRIPT XLIST28'
%        EVALUATE LOOPCOUNTER = LOOPEND
%      END BLOCK
%      BLOCK    %ED28
%        EVALUATE LNEWRAT = FALSE
%        EVALUATE LNEWRHO = FALSE
%        COPY '#SCRIPT XLIST28'
%        EVALUATE LOOPCOUNTER = LOOPEND
%      END BLOCK
%      BLOCK    %ED02
%        COPY '#SCRIPT XSPACE'
%        EVALUATE LOOPCOUNTER = LOOPEND
%      END BLOCK
%      BLOCK    %ED29
%        COPY '#SCRIPT XINLIST3'
%        EVALUATE LOOPCOUNTER = LOOPEND
%      END BLOCK
%      BLOCK    %SIGFULL
%        IF EXISTS ( 30 ) .EQ. 0 THEN
%          COPY '#LIST 30'
%          COPY 'END'
%        END IF
%        COPY '#GENERALEDIT 30'
%        COPY 'LOCATE RECORDTYPE = 109'
%        GET NOSTORE SILENT FINAL REAL ' ' ' '
%        EVALUATE THFULL = - VALUE
%        COPY 'TRANSFER TO OFFSET = 10 FROM THFULL'
%        COPY 'WRITE'
%        COPY 'END'
%        TRANSFER '{I Theta full manually set to ' // -
            CHARACTER ( - THFULL ) TO DISPLAY
%      END BLOCK
%    END CASE
%  END LOOP
% END LOOP
%END SCRIPT
