%SCRIPT XEXTIN12
%  VARIABLE LOGICAL EDONE EMASK DOEXTNC REFREQD
%  EVALUATE EDONE = TRUE
%  EVALUATE EMASK = TRUE
%%
%% Without asking, sneakily turn on anomalous scattering correction:
%% This script is only called by the guide. And the guide should always
%% do the right thing.
%  VARIABLE INTEGER IANOMA
%  EVALUATE IANOMA = 0
%  COPY '#GENERALEDIT 23'
%  COPY 'LOCATE RECORDTYPE = 101'
%  COPY 'TRANSFER TO OFFSET = 0 FROM IANOMA'
%  COPY 'WRITE'
%  COPY 'END'
%%
%  COPY '#SCRIPT XPUTINFO'
^^WI WINDOW XEXTINC 'Extinction check' KEEP SIZE LARGE MODAL
^^WI COMMIT='EXT_BOK' CANCEL='EXT_BXX'
^^WI GRID MAIN NROWS=3 NCOLS=1 {
^^WI @ 1,1 GRID MAIN NROWS=7 NCOLS=3
^^WI {
^^WI   @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI   {
^^WI    @ 1,3 GRID EXP2 NROWS=3 NCOLS=1
^^WI    {
^^WI     @ 1,1 STATIC XFW1
^^WI 'You are looking at a scatter plot of Fo versus Fc.'
^^WI     @ 2,1 STATIC XFW2
^^WI 'Flattening out at high values could be a sign of extinction.'
^^WI     @ 3,1 STATIC XFW3
^^WI 'Right-click to omit outliers. '
^^WI    }
^^WI    @ 1,1 ICON IM INFO
^^WI   }
^^WI  @ 4,2 PLOTWINDOW _VFOFC NROWS=20 NCOLS=64
^^WI     DEFINEPOPUPMENU POPUP 'Selection'
^^WI       ITEM _SDELA '&Omit _L' 'OMIT_N_L'
^^WI     ENDDEFINEMENU
^^WI  @ 6,2 GRID EXBT NROWS=1 NCOLS=3
^^WI  {
^^WI   @ 1,1 BUTTON EXT_BOK 'Add extinction parameter' 
^^WI @ 1,3 BUTTON EXT_BXX 'Do not' LENGTH = 'Add extinction parameter'  DEFAULT
^^WI  }
^^WI }
^^WI @ 3,1 PROGRESS _EXTNPR 'Please wait' }
^^WI SHOW
^^CR
^^CO SENDPROGRESSTO _EXTNPR
% LOOP
%  COPY '#sfls'
%  COPY 'calc'
%  COPY 'END'
%  COPY '#sum list 6 level=plot'
%  COPY 'END'
%  VERIFY EXT_BOK EXT_BXX OMIT
%  GET SILENT NOSTORE FINAL ABBREVIATED -
 'Do you want to refine an extinction parameter?' 'EXT_BOK'
%  EVALUATE DOEXTNC = FALSE
%  IF VALUE .EQ. 1 THEN
%     EVALUATE DOEXTNC = TRUE
^^CO  DISPOSE XEXTINC
%     FINISH
%  ELSE IF  VALUE .EQ. 2 THEN
%     EVALUATE DOEXTNC = FALSE
^^CO  DISPOSE XEXTINC
%     FINISH
%  END IF
%  BLOCK
%      ON EDITERROR TERMINATE
%      EVALUATE REFREQD = TRUE
%      COPY '#GENERALEDIT 28'
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%      COPY 'LOCATE RECORDTYPE = 104'
%      COPY 'INSERT'
%      CLEAR
%      INSERT 'CHANGE 0 REAL REAL ='
%      GET SILENT INTEGER ' ' ' '
%      SEND
%%      OUTPUT
%      CLEAR
%      INSERT 'CHANGE 1 REAL REAL ='
%      GET SILENT INTEGER ' ' ' '
%      SEND
%%      OUTPUT
%      CLEAR
%      INSERT 'CHANGE 2 REAL REAL ='
%      GET SILENT INTEGER ' ' ' '
%      SEND
%%      OUTPUT
%      CLEAR
%      COPY 'WRITE'
%      COPY 'END'
%  END BLOCK
% END LOOP
%END SCRIPT

