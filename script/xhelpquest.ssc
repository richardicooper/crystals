%SCRIPT XHELPQUEST
%% The variabled QLINE1, QLINE2 BUTTXX and BUTTOK are set in the calling script
% VARIABLE CHARACTER QLINE1 QLINE2 QTITLE BUTTOK BUTTXX
%% BUTINFO is shown on he button
%% BUTFILE is name of the SDA file containing the information
% VARIABLE CHARACTER BUTINFO BUTFILE
% VARIABLE LOGICAL ANSWER
% VARIABLE CHARACTER MYFILE
% EVALUATE MYFILE =  'CRYSDIR:script/' // BUTFILE
%%
% TRANSFER "^^WI WINDOW QUESTION '" // QTITLE // "' MODAL" TO DISPLAY
^^WI COMMIT='B1'  CANCEL='B2'  POSITION=CENTRED '_MAIN'
^^WI GRID GTB NROWS=5 NCOLS=5
^^WI {
^^WI   @ 2,2 ICON MYICON QUERY
^^WI   @ 2,4 GRID TL NROWS=3 NCOLS=5
^^WI   {
% TRANSFER "^^WI @ 1,3 STATIC T1 '" // QLINE1 // "'" TO DISPLAY
% TRANSFER "^^WI @ 2,3 STATIC T2 '" // QLINE2 // "'" TO DISPLAY
^^WI   }
^^WI   @ 4,4 GRID LOWER NROWS=1 NCOLS=7
^^WI   {
^^WI     @ 1,1 STRETCH XQ_S1 HORIZONTAL
% TRANSFER "^^WI @ 1,2 BUTTON B1 '" // BUTTOK // "' DEFAULT" TO DISPLAY
% TRANSFER "^^WI @ 1,4 BUTTON B2 '" // BUTTXX // "'" TO DISPLAY
%   IF FILEEXISTS MYFILE THEN
% TRANSFER "^^WI @ 1,6 BUTTON BINFO '" // BUTINFO // "'" TO DISPLAY
%   END IF
^^WI     @ 1,7 STRETCH XQ_S2 HORIZONTAL
^^WI   }
^^WI }
^^WI SHOW
^^CR
%%
%%
% LOOP
%%
% VERIFY B1 B2 BINFO
% GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
% CASE VALUE
%  BLOCK
%    EVALUATE ANSWER = TRUE
^^WI DISPOSE QUESTION
%    FINISH
%  END BLOCK
%  BLOCK
%    EVALUATE ANSWER = FALSE
^^WI DISPOSE QUESTION
%    FINISH
%  END BLOCK
%  BLOCK
%     TRANSFER '#TYPE "' // MYFILE // '"' TO CRYSTALS                       
%  END BLOCK
% END CASE
% END LOOP
^^WI DISPOSE QUESTION
^^CR
%END SCRIPT
