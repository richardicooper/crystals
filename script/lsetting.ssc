%SCRIPT LSETTING
%%
%% This is a general purpose script for displaying a list of options
%% to the user.
%% On the left is some text. ie. "Choose an option". This can be up to 
%% 20 lines and is held in QLINE1, QLINE2 ... QNLINES holds the total
%% number.
%% In the center is a list box with the options. When the user single 
%% clicks an options, or moves to it with the cursor keys some help
%% text is displayed at the bottom of the box. The list items are held
%% in QLIST1, QLIST2 ... QNITEMS holds the total number. The help text
%% is in QHELP1, QHELP2 ... again QNITEMS holds the total.
%% On the right are two buttons, their text is in BUTTOK and BUTTXX
%% The return values for the calling script are in QANSWER which is
%% TRUE if BUTTOK was clicked, or if the user double-clicked a list
%% item. QCANSWER contains the text of the list item selected, and 
%% QIANSWER contains its index. (Starting from 1 for the first item).
%%  
%% Inputs...
%VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QHELP1
%VARIABLE INTEGER QNITEMS QVLINES QNLINES
%% Don't forget QHELP1-20, QLINE1-20 and QTEXT1-20. These are declared
%% on the fly if needed!
%%
%% Outputs...
%VARIABLE LOGICAL   QANSWER
%VARIABLE CHARACTER QCANSWER
%VARIABLE INTEGER   QIANSWER
%%
%% Internal only...
%VARIABLE INTEGER N
%VARIABLE CHARACTER QNAME QTEXT QHELP CDLINE
%VARIABLE REAL RVAL
%%
%BLOCK
%VERIFY 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20
^^WI WINDOW SELECTI 'Least-squares' MODAL SIZE
%%MAIN GRID IS THREE HORIZONTAL CELLS
^^WI GRID GRMAIN NROWS=5 NCOLS=3
^^WI {
%% THIS GRID IS THREE VERTICAL CELLS IN THE MAIN TOP CELL
^^WI   @ 2,2 GRID GRLCR  NROWS=1 NCOLS=3
^^WI   {
%% THIS GRID IS THE LEFT VERTICAL CELL it contains QNLINES of text
^^WI    @ 1,1 GRID GRTEX  NROWS=4 NCOLS=1
^^WI    {
^^WI      @ 1,1 STATIC T1 'Choose an option'
^^WI      @ 4,1 STATIC RVAL 'R-Factor = 000.000'
^^WI    }
%% THIS GRID IS THE CENTRE VERTICAL CELL it contains a listbox with QVLINES visible
^^WI    @ 1,2 GRID GRLIST NROWS=1 NCOLS=3
^^WI    {
^^WI       @ 1,2 LISTBOX LIST01 VISLINES=4 INFORM=YES
^^WI 	         ADDTOLIST
^^WI             'Change the refinement settings'
^^WI             'Carry out some refinement'
^^WI             'Add hydrogens to the structure'
^^WI             'View the structure'
^^WI             'NULL'
^^WI    }
%% THIS GRID IS THE RIGHT VERTICAL CELL it contains the Ok and Cancel buttons
^^WI    @ 1,3 GRID GRBUTT NROWS=3 NCOLS=3
^^WI    {
^^WI       @ 1,2 BUTTON BUTTONOK '&Ok' COMMIT
^^WI       @ 3,2 BUTTON BUTTONXX '&Cancel' CANCEL
^^WI    }
^^WI   }
%% THIS GRID IS THE BOTTOM ROW OF THE MAIN GRID it contains a help text.
%% The only reason for using a grid is to add the outline to it.
^^WI   @ 4,2 GRID GRIDHLP NROWS=1 NCOLS=1 OUTLINE=''
^^WI   {
^^WI     @ 1,1 STATIC HELP 
^^WI     'Define which parameters will be refined.                      ' 
^^WI   }
^^WI }
^^WI SHOW
^^CR
% END BLOCK
%%
%%END OF DEFINITION, HERE THE PROCESSING BEGINS:-
%%
% BLOCK
% LOOP
%  BLOCK
%     COPY '#GENERALEDIT 30'
%     COPY 'LOCATE RECORDTYPE=103'
%     COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%     ON EDITERROR TERMINATE
%     COPY 'TRANSFER FROM OFFSET=0 TO RVAL'
%     CLEAR
%     INSERT !%EVALUATE CDLINE = "^^WI SET RVAL TEXT='R-Factor = !
%     STORE FORMAT /(F6.2)/ LENGTH 6 REAL RVAL
%     INSERT !'"!
%     EXECUTE
%     SHOW CDLINE
%  END BLOCK
^^WI SET _MAINTEXTOUTPUT NOECHO
^^CR
%   VERIFY BUTTONOK BUTTONXX LIST01
%   GET FINAL FILL NOSTORE ABBREVIATED ' ' ' '
%   CASE VALUE
%       BLOCK                        %OK BUTTON%
^^WI       SET _MAINTEXTOUTPUT NOECHO
^^CR
^^??       LIST01 SELECTED
%          GET NOSTORE INTEGER ' '
%          CASE VALUE
%           BLOCK                                         %SETTINGS%
%            COPY '#SCRIPT SETTINGS'
%           END BLOCK
%           BLOCK                                         %REFINE%
%            COPY '#SCRIPT LREFINE'
%           END BLOCK
%           BLOCK                                         %ADDH%
%            COPY '#PERHYDRO'
%            COPY 'END'
%            COPY '#FOURIER'
%            COPY 'MAP TYPE=DIFF'
%            COPY 'PEAK HEIGHT=1'
%            COPY 'END'
%            COPY '#PEAKS'
%            COPY 'END'
%            COPY '#COLL'
%            COPY 'SEL TYPE=PEAK'
%            COPY 'END'
%            COPY '#SUM L 5'
%            COPY 'END'
%            COPY '#SCRIPT LVIEW'
%            COPY '#SCRIPT XDELQ'

 Any remaining 'Q' atoms have been removed from the stucture.

%           END BLOCK
%           BLOCK                                         %VIEW%
%            COPY '#SCRIPT LVIEW'
%           END BLOCK
%          END CASE
%          
%       END BLOCK
%       BLOCK                       %CANCEL BUTTON%
^^WI       DISPOSE SELECTI
^^CR
%          FINISH
%       END BLOCK
%       BLOCK                       %LIST CHANGED%
^^WI            SET _MAINTEXTOUTPUT NOECHO
^^CR
%               VERIFY 1 2 3 4
%               GET NOSTORE FINAL ABBREVIATED ' ' ' '
%               CASE VALUE
% BLOCK
^^WI SET HELP TEXT='Define which parameters will be refined.' 
% END BLOCK
% BLOCK
^^WI SET HELP TEXT='Refine the parameters.'
% END BLOCK
% BLOCK
^^WI SET HELP TEXT='Add hydrogen atoms to the model.' 
% END BLOCK
% BLOCK
^^WI SET HELP TEXT='View the structure in a graphics package.' 
% END BLOCK
%               END CASE
^^CR
%       END BLOCK
%   END CASE
%END LOOP
%END BLOCK
%END SCRIPT
