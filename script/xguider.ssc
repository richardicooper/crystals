% SCRIPT XGUIDER                                                                 
%%                                                                              
%% Determine current state of analysis.                                         
%% Decide on a course of action.                                                
%% Display non-modal window with available options.                             
%% Exit. - The window will callback XGUIDERC.SCP with events.                   
%%                                                                              
%  VARIABLE LOGICAL HDONE EDONE WDONE HINCELL                                   
%  VARIABLE INTEGER ADP                                                         
%  VARIABLE INTEGER FOLD                                                        
%%                                                                              
%  VARIABLE INTEGER KTDEFA                                                      
%% KTDEFA  =                                                                    
%%  1 View structure                                                            
%%  2 Fourier refinement                                                        
%%  3 Refine Postitions                                                         
%%  4 Refine pos and Uiso                                                       
%%  5 Refine pos and Uaniso                                                     
%%  6 Locate H                                                                  
%%  7 Check extinction.                                                         
%%  8 Check weighting scheme.                                                   
%%  9                                                                            
%% 10                                                                            
%% 11 Validate                                                                  
%% 12 Publish 
%%                                                                  
%% ----------The R-factor thresholds for various cases
%  VARIABLE REAL R1 R2 R3 R4 RMIN                                               
%%       Fourier might be better                                                      
%  EVALUATE R1 = 40.0                                                           
%%       Isotropic refinement                                                         
%  EVALUATE R2 = 20.0                                                           
%%       Aniso                                                                        
%  EVALUATE R3 = 10.0                                                           
%%       Tidy up                                                                      
%  EVALUATE R4 = 6.0                                                            
%%
%% ISTAGE - stage in the analysis
%%  0      No LIST 5
%%  1      No refinement yet
%%  2      Iso refinement done
%%  3      Aniso refinement done
%%
%% HINCELL T/F  true if hydrogen atoms in structure
%% EDONE   T/F  true if extinction tested 
%% REFREQD T/F  true if more refinement needed
%% HDONE   T/F  true if all hydrogen found
%% WDONE   T/F  true if weighting scheme optimised
%% 
%%                                                                              
%  VARIABLE REAL WAVE ZMAX
%  COPY '#SCRIPT XGETWZ'
%%                                                                              
%% Find out if  H is in list 29.                                                
%  COPY '#SCRIPT CHECK29'                                                       
%  IF ( HINCELL .EQ. FALSE ) THEN                                               
%% Set this to true, so that the logic thinks we've already found H's.          
%    EVALUATE HDONE = TRUE                                                      
%  END IF                                                                       
%%                                                                              
%%                                                                              
%  VARIABLE INTEGER ISTAGE                                                      
%% R, OSCALE, RATIO are set in SCRIPT RFACTOR                                   
%  VARIABLE REAL R OSCALE RATIO                                                 
%%                                                                              
%% May be set by calling scripts to recommend refinement:                       
%  VARIABLE LOGICAL REFREQD                                                     
%% May be set by calling scripts to recommend publication stage or              
%% further refinement:                                                          
%  VARIABLE LOGICAL VALIDPASS VALIDFAIL VALIDCYCLE VALIDWEIGHT                  
%%                                                                              
%  EVALUATE ISTAGE = 0                                                          
%%                                                                              
%%                                                                              
%% Find EDONE, HDONE and WDONE. These are stored in L39.                        
%  COPY '#SCRIPT XGETINFO'                                                      
%%                                                                              
%% Determine how much refinement has been done (ADP)                            
%  COPY '#SCRIPT ASSESS5'                                                       
%%                                                                              
                                                                             
%% Try to guess what stage we are at                                            
%  EVALUATE ISTAGE = ADP                                                        
%%                                                                              
%%                                                                              
%  COPY '#SCRIPT RFACTOR'                                                       
%  IF RATIO .GE. 1.3 THEN                                                       
{S * Need R-factor on a good scale. Recomputing scale factor and phases.        
%    COPY '#SFLS'                                                               
%    COPY 'SCALE'                                                               
%    COPY 'END'                                                                 
%  BLOCK                                                                        
%   EVALUATE FOLD = 1                                                           
%   IF EXISTS 23 .GE. 1 THEN                                                    
%    COPY '#GENERALEDIT 23'                                                     
%    COPY 'LOCATE RECORDTYPE = 103'                                             
%    COPY 'TRANSFER FROM OFFSET =  1 TO FOLD'                                   
%    COPY 'END'                                                                 
%    EVALUATE FOLD = FOLD + 2
%%   FOLD = 1 for F, or 2 for F**2                                         
%%                                                                              
%% Nov 09  Set weights and scale in XSCALE
%    COPY '#SCRIPT XSCALE'
%   END IF                                                                      
%  END BLOCK                                                                    
%    COPY '#SCRIPT RFACTOR'                                                     
%  END IF                                                                       
%  EVALUATE RMIN = R                                                            
%% Don't approve of this, what if no aniso yet?:                                
%%  IF RMIN .LE. R4 THEN                                                        
%%    EVALUATE ISTAGE = 3                                                       
%%  END IF                                                                      
%%                                                                              
%% Check for window already open                                                
%%                                                                              
^^?? XGUIDEI EXISTS                                                             
%  VERIFY TRUE FALSE                                                            
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                                 
%  IF VALUE .EQ. 1  THEN                                                        
^^CO DISPOSE XGUIDEI                                                            
%  END IF                                                                       
^^?? XGUIDES EXISTS                                                             
%  VERIFY TRUE FALSE                                                            
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                                 
%  IF VALUE .EQ. 1  THEN                                                        
^^CO DISPOSE XGUIDES                                                            
%  END IF                                                                       
^^?? XGUIDER EXISTS                                                             
%  VERIFY TRUE FALSE                                                            
%  GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                                 
%  IF VALUE .EQ. 2  THEN                                                        
%    CLEAR                                                                      
%    ON ERROR TERMINATE                                                         
%%                                                                              
^^WI WINDOW XGUIDER 'The GUIDE' POSITION CASCADE '_MAINTEXTOUTPUT'              
^^WI COMMAND='#SCRIPT XGUIDERC' DISABLEIF='IN' CANCEL='GR_BXX'                  
^^WI GRID HO NROWS=3 NCOLS=5                                                    
^^WI {                                                                          
^^WI  @ 2,2 GRID BM NROWS=1 NCOLS=1                                             
^^WI  {                                                                         
^^WI    @ 1,1 BITMAP GUIDEB TRANS FILE 'guide3.bmp'                             
^^WI  }                                                                         
^^WI  @ 2,4 GRID MID NROWS=3 NCOLS=3                                            
^^WI  {                                                                         
^^WI   @ 1,1 DROPDOWN _XG_LIST ADDTOLIST                                        
^^WI     'View structure'                                                       
^^WI     'Fourier refinement'                                                   
^^WI     'Refine positions'                                                     
^^WI     'Refine posn and iso'                                                  
^^WI     'Refine posn and aniso'                                                
^^WI     'Add hydrogen'                                                         
^^WI     'Check extinction'                                                     
^^WI     'Optimise weights'                                                     
^^WI     'n-Cycles'                                                             
^^WI     '6-Cycles'                                                             
^^WI     'Validate'                                                             
^^WI     'Publish'                                                              
^^WI     'Refine Hydrogen'                                                      
^^WI     NULL                                                                   
^^WI   @ 3,1 GRID BTNS NROWS=1 NCOLS=4                                          
^^WI   {                                                                        
^^WI     @ 1,1 STRETCH _H HORIZONTAL                                            
^^WI     @ 1,2 BUTTON GR_BRESET '&Facts' DISABLEIF='IN'                         
^^WI     @ 1,4 BUTTON GR_BHELP '&Help' DISABLEIF='IN'                           
^^WI   }                                                                        
^^WI   @ 1,3 BUTTON GR_BOK '&Ok' DISABLEIF='IN'                                 
^^WI   @ 3,3 BUTTON GR_BXX '&Hide' DISABLEIF='IN'                               
^^WI  }                                                                         
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
%  END IF                                                                       
{S * Deciding on the best course of action...                                   
%  CLEAR                                                                        
%%___________________________________________________________________           
%%                                                                              
%  IF R .GT. R1 THEN                                                            
%%                                                                              
 Large R factor.                                                                
   Recommended:   Fourier refinement.                                           
   Other options: Refine positions only.                                        
                  View the structure.                                           
%      EVALUATE KTDEFA = 2                                                      
%%___________________________________________________________________           
%%                                                                              
%  ELSE IF ( R .GT. R2 ) .OR. ( ISTAGE .LE. 1 ) THEN                            
%%                                                                              
 Medium R factor.                                                               
   Recommended:   Refine positions and Uiso.                                    
   Other options: View the structure                                            
%      EVALUATE KTDEFA = 4                                                      
%%___________________________________________________________________           
%%                                                                              
%  ELSE IF ( R .GT. R3 ) .AND. ( ISTAGE .LE. 2 ) THEN                           
%%                                                                              
 Fairly low R factor                                                            
   Recommended:   Refine positions and Uaniso                                   
   Other options: View the structure                                            
%      EVALUATE KTDEFA = 5                                                      
%%___________________________________________________________________           
%%                                                                              
%  ELSE IF ( ISTAGE .LE. 2 ) THEN                                               
%%                                                                              
 Low R factor but no anisotropic refinement done yet.                           
   Recommended:   Refine positions and Uaniso                                   
   Other options: View the structure                                            
%      EVALUATE KTDEFA = 5                                                      
%%                                                                              
%% ******************************************************************                                                                             
%%    DO Something Special for Cu and all-light atoms.
% ELSE IF ( R .GE. R3 ) .AND. ( WAVE .GT. 1.0 ) .AND. -
 ( ZMAX .LE. 18.0 ) .AND. ( HINCELL ) .AND.  -
 ( HDONE .EQ. FALSE ) THEN
%    IF ( REFREQD ) THEN
    Model recently changed
    Recommended:   A few cycles of refinement.                                  
%       EVALUATE KTDEFA = 5
%       EVALUATE REFREQD = FALSE
%    ELSE
 High R factor, Cu data, no heavy atoms, hydrogens to find                           
   Recommended:   Find and refine hydrogen                      
   Other options: View the structure
                  Try extinction                                            
%        EVALUATE KTDEFA = 6  
%    END IF
%% ******************************************************************                                                                             
%%___________________________________________________________________           
%%
%  ELSE IF ( R .GT. R3 ) .AND. ( EDONE .EQ. TRUE ) THEN                         
 R factor is large after aniso refinement.                                      
   Possible problems: Missing solvent.                                          
                      Disorder.                                                 
                      Extinction.                                               
   Recommended:       Get help.                                                 
   Other options:     View the structure.                                       
                                                                                
{0,4  CRYSTALS cannot guide you futher with this analysis.                      
{0,4  Please consult a professional crystallographer                            
                                                                                
 You may find the CRYSTALS primer has some useful ideas and                     
 instructions for handling non-routine structures.                              
 {&www.xtl.ox.ac.uk/primer.html{&                                               
                                                                                
%    EVALUATE KTDEFA = 1                                                        
%%___________________________________________________________________           
%%                                                                              
%  ELSE IF ( R .GT. R3 ) THEN                                                   
%%                                                                              
 R factor is large after aniso refinement.                                      
   Possible problems: Missing solvent.                                          
                      Disorder.                                                 
                      Extinction.                                               
   Recommended:       Try extinction correction.                                
   Other options:     View the structure.                                       
%      EVALUATE KTDEFA = 7                                                      
%%___________________________________________________________________           
%%                                                                              
%  ELSE IF ( REFREQD ) THEN                                                     
%      EVALUATE KTDEFA = 5                                                      
%      EVALUATE REFREQD = FALSE                                                 
%%                                                                              
 Very low R factor, but model recently changed...                               
    Recommended:   A few cycles of refinement.                                  
    Other options: Locate or place all hydrogen atoms.                          
                   Refine positions and Uaniso.                                 
                   View the structure.                                          
                                                                                
%%___________________________________________________________________           
%%                                                                              
%  ELSE IF ( HDONE .EQ. FALSE ) THEN                                            
%%                                                                              
%% Not all H found yet, but R < R3                                              
%      EVALUATE KTDEFA = 6                                                      
 Very low R factor - Begin to tidy up                                           
    Recommended:   Locate or place all hydrogen atoms.                          
    Other options: Refine positions and Uaniso.                                 
                   View the structure.                                          
%%___________________________________________________________________           
%%                                                                              
%  ELSE IF EDONE .EQ. FALSE THEN                                                
%%                                                                              
%% All H are found and R < R3 default to checking extinction.                   
%      EVALUATE KTDEFA = 7                                                      
    Recommended:   Check for extinction.                                        
    Other options: Refine positions and Uaniso.                                 
                   View the structure.                                          
                   Locate or place all hydrogen atoms.                          
%%___________________________________________________________________           
%%                                                                              
%  ELSE IF ( WDONE .EQ. FALSE  ) .AND. ( R .LE. R4 ) THEN                       
%%                                                                              
%% H are found, extinction checked, R < R4 default to weights                   
%      EVALUATE KTDEFA = 8                                                      
    Recommended:   Optimise least squares weights.                              
    Other options: Refine positions and Uaniso.                                 
                   View the structure.                                          
                   Relocate or replace all hydrogen atoms.                      
                   Check for extinction.                                        
%%___________________________________________________________________           
%%                                                                              
%  ELSE IF ( VALIDPASS ) THEN                                                   
%      EVALUATE KTDEFA = 12                                                     
    Recommended:   Produce publication material.                                
    Other options: Validate.                                                    
                   Refine positions and Uaniso.                                 
                   View the structure.                                          
                   Relocate or replace all hydrogen atoms.                      
                   Check for extinction.                                        
                   Optimise least squares weights.                              
                                                                                
%  ELSE IF ( VALIDFAIL ) THEN                                                   
%      EVALUATE KTDEFA = 11                                                     
    Recommended:   Fix the problems with the structure. See advice above.       
%      IF ( VALIDCYCLE ) THEN                                                   
%        EVALUATE KTDEFA = 10                                                   
%      ELSE IF ( VALIDWEIGHT ) THEN                                             
%        EVALUATE KTDEFA = 8                                                    
%      END IF                                                                   
%%                                                                              
%  ELSE                                                                         
%%                                                                              
%% H are found, extinction checked, R < R4 weights set. Validate.               
%      EVALUATE KTDEFA = 11                                                     
    Recommended:   Validate the structure.                                      
                   Refine positions and Uaniso.                                 
    Other options: View the structure.                                          
                   Relocate or replace all hydrogen atoms.                      
                   Check for extinction.                                        
                   Optimise least squares weights.                              
%  END IF                                                                       
%%                                                                              
%%___________________________________________________________________           
%%                                                                              
%%                                                                              
%  CLEAR                                                                        
%  INSERT '^^CO SET _XG_LIST SELECTION='                                        
%  STORE FORMAT /(I2)/ LENGTH 2 INTEGER KTDEFA                                  
%  OUTPUT                                                                       
% END SCRIPT                                                                    
%%                                                                              
%%                                                                              
%%                                                                              
%%                                                                              
%%                                                                              
%%                                                                              
%%  $Log: not supported by cvs2svn $
%%  Revision 1.20  2011/02/22 08:57:12  djw
%%  Look for hydrogen before extinction if using Cu data and no atoms heavier that Ar (18)
%%
%%  Revision 1.19  2011/02/18 17:09:01  djw
%%  Get hold of the wavelength and heaviest atom, four use with guided Cu data
%%
%%  Revision 1.18  2009/11/11 10:53:31  djw
%%  replace manual setting of weights and scalefactor woth a call to XSCALE
%%
%%  Revision 1.17  2009/09/29 08:26:25  djw
%%  More LIST 4 problems. In 2008 default SHELX type weights gave problems with "peach", so weights were
%%  retuned to simple 1. or 1/F type.  This causes problems with Oxford Diffraction data, which seems to be on a massive scale
%%  Restore SHELX weights for moment, but proble must be resolved in future
%%                                                       
%%  Revision 1.16  2008/12/18 16:48:46  djw                                     
%%  Still probelms with choosing weights at the start of a refinement - this tim
%%                                                                              
%%  Revision 1.15  2008/09/16 10:58:15  djw                                     
%%  Remove Fc from SHELX weigting until after a model has been input and a scale
%%                                                                              
%%  Revision 1.14  2007/03/08 08:51:38  djw                                     
%%  Provide menu item for refineing only H atoms                                
%%                                                                              
%%  Revision 1.13  2005/01/23 10:00:25  rich                                    
%%  Reinstate CVS history for scripts. History from Dec23 - Jan23 is lost, but  
%%  changes are not!                                                            
%%                                                                              
%%  Revision 1.1.1.1  2004/12/13 11:16:23  rich                                 
%%  New CRYSTALS repository                                                     
%%                                                                              
%%  Revision 1.12  2004/11/12 13:22:04  rich                                    
%%  Made residual peak check in TIDYUP equivalent to the one in ACTA (max       
%%  peak height is Zmax * 0.075 (i.e. about .45 e/A) for a light atom structure.
%%  Clever up the interaction between the acta validation and the guide so that 
%%  if the validation fails, it can automatically recommend six-cycle if the    
%%  structure is unconverged or optimise weights if the GoOF is wonky.          
%%                                                                              
%%  Revision 1.11  2004/10/01 10:31:39  rich                                    
%%  Removed calls to VALID12 where no longer necessary (SCALE & CALC no longer  
%%  require a valid constraint matrix).                                         
%%                                                                              
%%  Revision 1.10  2002/12/03 13:54:35  rich                                    
%%  Reduce output during assessment and refinement (GUIDE).                     
%%                                                                              
%%  Revision 1.9  2002/03/21 17:56:19  richard                                  
%%  Enclose bitmaps in GRID object to stop them stretching.                     
%%                                                                              
%%  Revision 1.8  2001/11/16 15:29:27  ckp2                                     
%%  Fix situation where R-factor is very good without aniso refinement and the g
%%  starts chuntering on about adding hydrogen before it's even recommended anis
%%  refinement.                                                                 
%%                                                                              
%%  Revision 1.7  2001/04/03 09:52:17  richard                                  
%%  Changed name from 'Supervisor' to 'The GUIDE' for consistency.              
%%                                                                              
%%  Revision 1.6  2001/03/21 18:59:11  richard                                  
%%  Cut down output after validate, to try to keep validation stuff on the scree
%%                                                                              
%%  Revision 1.5  2001/03/21 17:07:16  richard                                  
%%  Changed window layout and bitmap.                                           
%%                                                                              
%%  Revision 1.4  2001/03/13 09:36:46  richard                                  
%%  Re-instated cancel action. Made cancel action the default in the xguide?c sc
%%                                                                              
%%  Revision 1.3  2001/03/09 18:01:26  richard                                  
%%  Removed default actions for buttons, as these are now non-modal windows.    
%%  They were interfering with the user pressing RETURN in other scripts, while 
%%  the windows were open.                                                      
%%                                                                              
%%  Revision 1.2  2001/03/08 22:40:09  richard                                  
%%  Syntax errors.                                                              
%%                                                                              
%%  Revision 1.1  2001/03/08 14:00:23  richard                                  
%%  New GUIDE scripts. The main script, xguide, decides on the overall stage    
%%  and calls one of xguidei - for data import, xguides - for solving or        
%%  xguider - for refinement. These display a window recommending a course of   
%%  action and exit - thus allowing the user access to the full range of CRYSTAL
%%  commands even while being guided. The window knows to call one of xguideic, 
%%  or xguiderc respectively when the user does something to it e.g. pushes a bu
%%                                                                              
