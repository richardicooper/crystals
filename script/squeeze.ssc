%SCRIPT SQUEEZE
%  VARIABLE CHARACTER CDIR CFILE
%  VARIABLE LOGICAL T
%  COPY '#FOREIGN PLATON'
%  COPY 'END'
%  COPY '#RELE PUNCH PLATON6'
%  COPY '#PUNCH 6 C'
%  COPY 'END'
%  COPY '#RELE PUNCH BFILE.PCH'
%%
%%
^^CO GETKEY PLATONDIR
%%
%%
%  GET SILENT NOSTORE FINAL TEXT '1' '1'
%  EVALUATE CFILE = CVALUE
%  IF CFILE .EQ. '1' .OR. FILEEXISTS ( CFILE ) .EQ. FALSE THEN
 
  Where is the Platon executable?
 
^^CO SYSOPENFILE 'Platon*.exe' 'Platon executable'
 
%    CLEAR
%    INSERT '^^CO SETKEY PLATONDIR '
%    GET SILENT TEXT 'Platon location:'
%    IF CVALUE .NE. 'CANCEL' THEN
%      EVALUATE CFILE = CVALUE
%      OUTPUT
%    ELSE
%      FINISH
%    END IF
%  END IF
%%
%%
%  TRANSFER '#SPAWN + "' // CFILE // '" -q  platon.res' TO CRYSTALS
%%
%   VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2
%   VARIABLE LOGICAL ANSWER
%   BLOCK
%      EVALUATE QTITLE = 'Squeeze has ended'
%      EVALUATE QLINE1 = 'Do you want to apply the results'
%      EVALUATE QLINE2 = ' '
%      EVALUATE BUTTOK = '&Yes'
%      EVALUATE BUTTXX = '&No'
%      COPY '#SCRIPT XQUESTIO'
%      IF ANSWER .EQ. FALSE THEN
%          FINISH
%      END IF
%   END BLOCK
%% use the results
%  IF FILEEXISTS ( 'LIST6.ABC' ) .EQ. TRUE THEN
Trying to delete old Squeeze files
%     EVALUATE T = FILEDELETE ( 'LIST6.ABC' )
Deleted old Squeeze files
%  END IF
%  COPY '#SPAWN + "CRYSDIR:CONVPLAT.EXE"'
%  COPY '#USE LIST6.ABC'
%%
Update LIST 23 to show the A & B parts are stored, and inhibit
them being overwritten
% COPY '#GENERALEDIT 23'
% COPY 'LOCATE RECORDTYPE=101'
%% SET MODIFY PARTIAL=NO
% COPY 'CHANGE OFFSET=4 MODE=INTEGER INTEGER=0'
% COPY 'WRITE'
%% SET MODIFY UPDATE=YES
% COPY 'CHANGE OFFSET=5 MODE=INTEGER INTEGER=-1'
% COPY 'WRITE'
% COPY 'END'
% COPY '#SCRIPT VALID12'
% COPY '#SFLS'
% COPY 'SCALE'
% COPY 'END'
% COPY '#SFLS'
% COPY 'CALC'
% COPY 'END'
%%
%END SCRIPT
