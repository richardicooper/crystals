%SCRIPT SQUEEZE
%% variables used by the question box below                                     
%  VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2                        
%  VARIABLE LOGICAL ANSWER                                                      
% VARIABLE INTEGER IFLACK                                         
% COPY '#GENERALEDIT 23'                                                        
% COPY 'LOCATE RECORDTYPE = 101'                                                
% COPY 'TRANSFER FROM OFFSET = 6 TO IFLACK'                                     
%  IF ( IFLACK .EQ. 0 ) THEN                                         
{1,8 
{1,8 The Flack paramter is being used.
{1,8 Ton Spek only recommends the use of SQUEEZE for data
{1,8 with Friedel and other equivalent reflections merged.
{1,8 CRYSTALS recommends that you abaNdon SQUEEZING until
{1,8 you have ensured that the data is fully merged.
{1,8 
%      EVALUATE QTITLE = ' '                              
%      EVALUATE QLINE1 = 'Do you want to go ahead with'                           
%      EVALUATE QLINE2 = 'SQUEEZEing?'                         
%      EVALUATE BUTTOK = '&Yes'                                                 
%      EVALUATE BUTTXX = '&No'                                                  
%      COPY '#SCRIPT XQUESTIO'                                                  
%      IF ANSWER .EQ. FALSE THEN                                                 
%        FINISH                                                             
%      END IF                                                                   
%  END IF                                                            
%  VARIABLE CHARACTER CDIR CFILE
%  VARIABLE LOGICAL T
{1,9  Running PLATON/SQUEEZE in CRYSTALS modifies the Fcalc part
{1,9  of reflections by storing the real and imaginary parts of
{1,9  the structure factor.
{1,9  This change is recorded by setting a flag in LIST 23 and
{1,9  increasing the storage for LIST 6.
{1,9  CRYSTALS recommends that you save you dsc file before
{1,9  running SQUEEZE in case you wish to revert to a normal
{1,9  refinement
%%                                                                              
%      EVALUATE QTITLE = 'Backup before SQUEEZE'                              
%      EVALUATE QLINE1 = 'Do you want to backup your'                           
%      EVALUATE QLINE2 = 'dsc before running SQUEEZE?'                         
%      EVALUATE BUTTOK = '&Yes'                                                 
%      EVALUATE BUTTXX = '&No'                                                  
%      COPY '#SCRIPT XQUESTIO'                                                  
%      IF ANSWER .EQ. TRUE THEN                                                 
%        COPY '#PURGE pre-squeeze'                                            
%        copy 'end'                                                             
%      END IF                                                                   
%%                                                                              
%  IF EXISTS 6 .LT. 1 THEN                                                      
 You do not have any reflections stored.                                        
 It is impossible to proceed without data.                                      
%    FINISH                                                                     
%  END IF                                                                       
%  COPY '#FOREIGN PLATON'
%  COPY 'END'
%  COPY '#RELE PUNCH PLATON6'
%  COPY '#PUNCH 6 C'
%  COPY 'END'
%  COPY '#RELE PUNCH BFILE.PCH'
%%
%%
^^CO GETKEY PLATONDIR
%%
%%
%  GET SILENT NOSTORE FINAL TEXT '1' '1'
%  EVALUATE CFILE = CVALUE
%  IF CFILE .EQ. '1' .OR. FILEEXISTS ( CFILE ) .EQ. FALSE THEN
 
  Where is the Platon executable?
 
^^CO SYSOPENFILE 'Platon*.exe' 'Platon executable'
 
%    CLEAR
%    INSERT '^^CO SETKEY PLATONDIR "'
%    GET SILENT TEXT 'Platon location:'
%    IF CVALUE .NE. 'CANCEL' THEN
%      EVALUATE CFILE = CVALUE
%      INSERT '"'
%      OUTPUT
%    ELSE
%      FINISH
%    END IF
%  END IF
%%
%%
%  TRANSFER '#SPAWN + "' // CFILE // '" -q  platon.res' TO CRYSTALS
%%
%  IF ( .not. FILEEXISTS ( 'platon.hkp' ) ) THEN                                    
{1,9  PLATON/SQUEEZE has not created a suitable output file.
{1,9  There may not be any voids in your structure
%    FINISH
%  END IF                                                                       
%   BLOCK
%      EVALUATE QTITLE = 'Squeeze has ended'
%      EVALUATE QLINE1 = 'Do you want to apply the results'
%      EVALUATE QLINE2 = ' '
%      EVALUATE BUTTOK = '&Yes'
%      EVALUATE BUTTXX = '&No'
%      COPY '#SCRIPT XQUESTIO'
%      IF ANSWER .EQ. FALSE THEN
%          FINISH
%      END IF
%   END BLOCK
%% use the results
%  IF FILEEXISTS ( 'LIST6.ABC' ) .EQ. TRUE THEN
Trying to delete old Squeeze files
%     EVALUATE T = FILEDELETE ( 'LIST6.ABC' )
Deleted old Squeeze files
%  END IF
%  COPY '#SPAWN + "CRYSDIR:CONVPLAT.EXE"'
%  COPY '#USE LIST6.ABC'
%%
Update LIST 23 to show the A & B parts are stored, and inhibit
them being overwritten
% COPY '#GENERALEDIT 23'
% COPY 'LOCATE RECORDTYPE=101'
%% SET MODIFY PARTIAL=NO
% COPY 'CHANGE OFFSET=4 MODE=INTEGER INTEGER=0'
% COPY 'WRITE'
%% SET MODIFY UPDATE=YES
% COPY 'CHANGE OFFSET=5 MODE=INTEGER INTEGER=-1'
% COPY 'WRITE'
% COPY 'END'
% COPY '#SFLS'
% COPY 'SCALE'
% COPY 'END'
% COPY '#SFLS'
% COPY 'CALC'
% COPY 'END'
%%
%END SCRIPT
