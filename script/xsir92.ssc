%SCRIPT SIR92
%   VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2
%   VARIABLE LOGICAL ANSWER
%   VARIABLE INTEGER IRUN
%   IF EXISTS 6 .LT. 1 THEN
            You do not have any reflections stored.
            It is impossible to proceed without data.
%       FINISH
%   END IF
%%
%% write a SIR92 data file
%%
%   BLOCK
^^WI WINDOW SIR92 'Sir92 Options'
^^WI   MODAL COMMIT='BOK' CANCEL='BXX'
^^WI GRID GRIDLR NROWS=1 NCOLS=2
^^WI {
^^WI  @ 1,1 GRID GRIDL NROWS=5 NCOLS=3
^^WI  {
^^WI   @ 1,2 GRID GRIDLT NROWS=5 NCOLS=1
^^WI   {
^^WI    @ 1,1 RADIOBUTTON RS '&Default Sir92 parameters'
^^WI           STATE=ON
^^WI    @ 2,1 RADIOBUTTON RDS '&Alternative 1 (seminv and random)'
^^WI           STATE=OFF
^^WI    @ 3,1 RADIOBUTTON RDR '&Alternative 2 (seminv and 12 symbols)'
^^WI           STATE=OFF
^^WI    @ 4,1 RADIOBUTTON RD '&Alternative 3 (very long random phasing job)'
^^WI           STATE=OFF
^^WI    @ 5,1 RADIOBUTTON REX '&Do NOT create new SIR input file'
^^WI           STATE=OFF
^^WI   }
^^WI   @ 3,2 GRID GRIDLB NROWS=2 NCOLS=1
^^WI   {
^^WI    @ 1,1 RADIOBUTTON RNFLT '&Pass all reflections to Sir92'
^^WI           STATE=ON
^^WI    @ 2,1 RADIOBUTTON RFLT '&Filter reflections using List28 conditions'
^^WI           STATE=OFF
^^WI   }
^^WI   @ 5,2 GRID GRIDLB NROWS=2 NCOLS=1
^^WI   {
^^WI    @ 1,1 CHECKBOX CED '&Edit Sir92 file before processing'
^^WI           STATE=OFF
^^WI    @ 2,1 CHECKBOX CRUN '&Run Sir92 now'
^^WI           STATE=ON
^^WI   }
^^WI  }
^^WI  @ 1,2 GRID GRIDR NROWS=5 NCOLS=2
^^WI  {
^^WI   @ 2,1 BUTTON BOK '&Ok'
^^WI   @ 4,1 BUTTON BXX '&Cancel'
^^WI  }
^^WI }
^^WI SHOW
^^CR
%      VERIFY BOK BXX
%      GET SILENT NOSTORE ABBREVIATED 'BOK=Process'
%      IF VALUE .EQ. 2 THEN
^^CO      DISPOSE SIR92
%         FINISH
%      END IF
^^??   REX STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE ABBREVIATED 'On=Existing'
%      IF ( VALUE .EQ. 1 ) THEN
  Using existing data
%      ELSE
%        CLEAR
%        INSERT '#FOREIGN SIR92 '
^^??     RS STATE
%        VERIFY ON OFF
%        GET SILENT NOSTORE ABBREVIATED 'On=Simple job'
%        IF ( VALUE .EQ. 1 ) THEN
%            INSERT 'NORMAL '
%        ELSE
^^??       RDS STATE
%          VERIFY ON OFF
%          GET SILENT NOSTORE ABBREVIATED 'On=2nd job'
%          IF ( VALUE .EQ. 1 ) THEN
%            INSERT 'DIFFICULT '
%          ELSE
^^??         RDR STATE
%            VERIFY ON OFF
%            GET SILENT NOSTORE ABBREVIATED 'On=3rd job'
%            IF ( VALUE .EQ. 1 ) THEN
%              INSERT 'HARD '
%            ELSE
%              INSERT 'LONG '
%            END IF
%          END IF
%        END IF
^^??     RFLT STATE
%        VERIFY ON OFF
%        GET SILENT NOSTORE ABBREVIATED 'On=Simple job'
%        IF ( VALUE .EQ. 1 ) THEN
%            INSERT 'FILTERED'
%        ELSE
%            INSERT 'NORMAL'
%        END IF
%        SEND
%        COPY 'END'
%      END IF
^^??   CRUN STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE ABBREVIATED 'ON=Run sir92 now'
%      EVALUATE IRUN = VALUE
^^??   CED STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE ABBREVIATED 'On=Edit first'
^^WI   DISPOSE SIR92
^^CR
%      IF ( VALUE .EQ. 1 ) THEN
%           COPY '#SPAWN +notepad.exe sir92.ini'
%           EVALUATE QTITLE = 'Sir92 Preparation'
%           EVALUATE QLINE1 = 'Do you still want to'
%           EVALUATE QLINE2 = 'run the Sir92 job?'
%           EVALUATE BUTTOK = '&Yes'
%           EVALUATE BUTTXX = '&No'
%           COPY '#SCRIPT XQUESTIO'
%           IF ANSWER .EQ. FALSE THEN
%               FINISH
%           END IF
%      END IF
%      IF ( IRUN .EQ. 2 ) THEN
%           FINISH
%      END IF
%      COPY '#SPAWN + "CRYSDIR:oshell.bat" "CRYSDIR:SIR92.BAT"'
%   END BLOCK
%   BLOCK
%      EVALUATE QTITLE = 'Sir92 Results'
%      EVALUATE QLINE1 = 'Do you want to use the structure'
%      EVALUATE QLINE2 = 'from Sir92 ?'
%      EVALUATE BUTTOK = '&Yes'
%      EVALUATE BUTTXX = '&No'
%      COPY '#SCRIPT XQUESTIO'
%      IF ANSWER .EQ. FALSE THEN
^^WI WINDOW SIRFAIL 'Failed to solve' SIZE MODAL POSITION=ABOVE _MAIN
^^WI GRID OUTER NROWS=3 NCOLS=5
^^WI {
^^WI   @ 2,2 TEXTOUT XSIRTEXT ''
^^WI         NROWS=20 NCOLS=90
^^WI   @ 2,4 GRID BTN NROWS=3 NCOLS=1
^^WI   {
^^WI     @ 1,1 BUTTON HELP '&View Log'
^^WI     @ 3,1 BUTTON CLOSE '&Close' CANCEL DEFAULT COMMIT
^^WI   }
^^WI }
^^WI SHOW
^^CR
^^CO SENDTEXTTO XSIRTEXT
   If the structure will not solve:
   1) Be sure that the space group is correct.
   2) Click on 'View Log' and study the output of the
      Sir92 program for reasons for failure.
      a) If the Wilson Plot shows a weird B value, or the program crashes 
         computing the plot, try uncommenting (remove the >) the %normal 
         and BFAC lines.
      b) If the volume per atom is not approximately 20, check the formula
      c) If the first e-map looks hopeful, then deteriorates, inhibit 
         cycling. (remove the > from the ncycle line)
   3) Try one of the alternative Sir92 parameters options.
   4) Read the Sir92 guide in SIR92/GUIDE92.TXT for help.
   5) Try SHELXS
   6) Obtain SIR97 or DIRDIF
^^CO SENDTEXTTO _MAINTEXTOUTPUT
%          VERIFY CLOSE HELP
%          GET NOSTORE FINAL ABBREVIATED ' ' ' '
%          IF VALUE .EQ. 2 THEN
%             COPY '#SPAWN +notepad.exe sir92.lis'
%          END IF
^^CO DISPOSE SIRFAIL
%          FINISH
%      END IF
%%
%% Get the results back...
%%
%      COPY '#SCRIPT XSIR92IN'
%   END BLOCK
%END SCRIPT
 
 
 
