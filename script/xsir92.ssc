%SCRIPT SIR92
%   VARIABLE CHARACTER QTITLE BUTTOK BUTTXX QLINE1 QLINE2
%   VARIABLE LOGICAL ANSWER
%   VARIABLE INTEGER IRUN
%   IF EXISTS 6 .LT. 1 THEN
            You do not have any reflections stored.
            It is impossible to proceed without data.
%       FINISH
%   END IF
%%
%% write a SIR92 data file
%%
%   BLOCK
^^WI WINDOW SIR92 'Sir92 Options'
^^WI   MODAL COMMIT='BOK' CANCEL='BXX'
^^WI GRID GRIDLR NROWS=1 NCOLS=2
^^WI {
^^WI   @ 1,1 GRID GRIDL NROWS=7 NCOLS=3
^^WI   {
^^WI     @ 1,2 RADIOBUTTON RS '&Default Sir92 parameters'
^^WI           STATE=ON
^^WI    @ 2,2 RADIOBUTTON RDS '&Alternative 1 (seminv and random)'
^^WI           STATE=OFF
^^WI     @ 3,2 RADIOBUTTON RDR '&Alternative 2 (seminv and 12 symbols)'
^^WI           STATE=OFF
^^WI @ 4,2 RADIOBUTTON RD '&Alternative 3 (very long random phasing job)'
^^WI           STATE=OFF
^^WI     @ 6,2 CHECKBOX CED '&Edit Sir92 file before processing'
^^WI           STATE=OFF
^^WI     @ 7,2 CHECKBOX CRUN '&Run Sir92 now'
^^WI           STATE=ON
^^WI   }
^^WI   @ 1,2 GRID GRIDR NROWS=5 NCOLS=2
^^WI   {
^^WI     @ 2,1 BUTTON BOK '&Ok'
^^WI     @ 4,1 BUTTON BXX '&Cancel'
^^WI   }
^^WI }
^^WI SHOW
^^CR
%      VERIFY BOK BXX
%      GET SILENT NOSTORE ABBREVIATED 'BOK=Process'
%      IF VALUE .EQ. 2 THEN
^^WI      DISPOSE SIR92
^^CR
%         FINISH
%      END IF
^^??   RS STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE ABBREVIATED 'On=Simple job'
%      IF ( VALUE .EQ. 1 ) THEN
%          COPY '#FOREIGN SIR92 '
%          COPY 'END'
%      ELSE
^^??     RDS STATE
%        VERIFY ON OFF
%        GET SILENT NOSTORE ABBREVIATED 'On=2nd job'
%        IF ( VALUE .EQ. 1 ) THEN
%          COPY '#FOREIGN SIR92 DIFFICULT'
%          COPY 'END'
%        ELSE
^^??       RDR STATE
%          VERIFY ON OFF
%          GET SILENT NOSTORE ABBREVIATED 'On=3rd job'
%          IF ( VALUE .EQ. 1 ) THEN
%            COPY '#FOREIGN SIR92 HARD'
%            COPY 'END'
%          ELSE
%            COPY '#FOREIGN SIR92 LONG'
%            COPY 'END'
%          END IF
%        END IF
%      END IF
^^??   CRUN STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE ABBREVIATED 'ON=Run sir92 now'
%      EVALUATE IRUN = VALUE
^^??   CED STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE ABBREVIATED 'On=Edit first'
^^WI   DISPOSE SIR92
^^CR
%      IF ( VALUE .EQ. 1 ) THEN
%           COPY '#SPAWN start /w notepad.exe sir92.ini'
%           EVALUATE QTITLE = 'Sir92 Preparation'
%           EVALUATE QLINE1 = 'Do you still want to'
%           EVALUATE QLINE2 = 'run the Sir92 job?'
%           EVALUATE BUTTOK = '&Yes'
%           EVALUATE BUTTXX = '&No'
%           COPY '#SCRIPT XQUESTIO'
%           IF ANSWER .EQ. FALSE THEN
%               FINISH
%           END IF
%      END IF
%      IF ( IRUN .EQ. 2 ) THEN
%           FINISH
%      END IF
%      COPY '#SPAWN  COMMAND.COM /e:3072 /c CRYSDIR:SIR92.BAT'
%   END BLOCK
%   BLOCK
%      EVALUATE QTITLE = 'Sir92 Results'
%      EVALUATE QLINE1 = 'Do you want to use the structure'
%      EVALUATE QLINE2 = 'from Sir92 ?'
%      EVALUATE BUTTOK = '&Yes'
%      EVALUATE BUTTXX = '&No'
%      COPY '#SCRIPT XQUESTIO'
%      IF ANSWER .EQ. FALSE THEN
^^WI WINDOW SIRFAIL 'Failed to solve' SIZE MODAL POSITION=ABOVE _MAIN 
^^WI GRID OUTER NROWS=3 NCOLS=5
^^WI {
^^WI   @ 2,2 TEXTOUT XSIRTEXT ''
^^WI         NROWS=20 NCOLS=90
^^WI   @ 2,4 GRID BTN NROWS=3 NCOLS=1
^^WI   {
^^WI     @ 1,1 BUTTON HELP '&View Log'
^^WI     @ 3,1 BUTTON CLOSE '&Close' CANCEL DEFAULT COMMIT
^^WI   }
^^WI }
^^WI SHOW
^^CR
^^CO SENDTEXTTO XSIRTEXT
   If the structure will not solve:
   1) Try one of the alternative Sir92 parameters options.
   2) Try SHELXS or DirDif.
   3) Read the Sir92 guide in SIR92/GUIDE92.TXT for help.
   4) Be sure that the space group is correct.
   5) Click on 'View Log' and study the output of the 
      Sir92 program for reasons for failure.
^^CO SENDTEXTTO _MAINTEXTOUTPUT
%          VERIFY CLOSE HELP
%          GET NOSTORE FINAL ABBREVIATED ' ' ' '
%          IF VALUE .EQ. 2 THEN
%             COPY '#SPAWN start /w notepad.exe sir92.lis'
%          END IF
^^CO DISPOSE SIRFAIL
%          FINISH
%      END IF
%%
%% Get the results back...
%%
%      COPY '#USE SIR9X.INS'
%      COPY '#EDIT'
%      COPY 'MON OFF'
%      COPY 'REFORMAT ALL'
%      COPY 'CHANGE FIRST(OCC) UNTIL LAST 1.0'
%      COPY 'MON MEDIUM'
%      COPY 'END'
%      IF ( EXISTS ( 5 ) .NE. 1 ) THEN
%        FINISH
%      END IF
%%
%      BLOCK
%      EVALUATE QTITLE = 'Use Cameron?'
%      EVALUATE QLINE1 = 'Do you want to use CAMERON '
%      EVALUATE QLINE2 = 'to examine the model '
%      EVALUATE BUTTOK = '&Yes'
%      EVALUATE BUTTXX = '&No'
%      COPY '#SCRIPT XQUESTIO'
%      IF ANSWER .EQ. TRUE THEN
%       COPY '#SCRIPT XCAM'
%      END IF
%      END BLOCK
%%
%      BLOCK
%       COPY '#SCRIPT XRENUMB'
%      END BLOCK
%%
%      BLOCK
%        COPY '#DISTANCES'
%        COPY 'END'
%      END BLOCK
%%
%% SET Solution type
%%
%      COPY '#GENERALEDIT 30'
%      COPY 'LOCATE RECORDTYPE=106'
%      COPY 'CHANGE OFFSET=12 MODE=INTEGER INTEGER=3'
%      COPY 'WRITE'
%      COPY 'END'
%   END BLOCK
%END SCRIPT
 
 
 
