%SCRIPT GFINH
%% Similar to FINH, but called from GFINDH
% VARIABLE INTEGER RETURNTO
% VARIABLE LOGICAL HDONE HMASK
%% The variables needed by XQUESTIO
% VARIABLE CHARACTER QTITLE QLINE1 QLINE2 BUTTOK BUTTXX
% VARIABLE LOGICAL   ANSWER
% VERIFY FIND AUTO
% GET SILENT NOSTORE FINAL ABBREVIATED 'Return to' 'FIND'
% EVALUATE RETURNTO = VALUE
^^WI DISPOSE CONTINUE
^^CR
% VERIFY BOK BXX
% GET SILENT NOSTORE FINAL ABBREVIATED 'Cancel?' 'BOK'
% IF VALUE .EQ. 2 THEN
%   FINISH
% END IF
%%
^^CO FOCUS _MAIN
% EVALUATE QTITLE = 'Geometric or Found'
% EVALUATE QLINE1 = 'Keep the white H atoms (geometric) or adjust the'
% EVALUATE QLINE2 = 'positions using the pink QH Fourier map peaks.'
% EVALUATE BUTTOK = '&Geometric'
% EVALUATE BUTTXX = '&Map'
% COPY '#SCRIPT XQUESTIO'
% IF ANSWER .EQ. TRUE THEN
%       COPY '#PEAK 5 5'
%       COPY 'SEL TYPE=PEAK'
%       COPY 'END'
%       COPY '#EDIT'
%       COPY 'SEL TYPE NE QH'
%       COPY 'SEL TYPE NE Q'
%       COPY 'END'
% ELSE
%       COPY '#EDIT'
%       COPY 'TYPECHANGE TYPE EQ QH Q'
%       COPY 'END'
%       COPY '#PEAK 5 5'
%       COPY 'SEL REJECT = .0001 TYPE=PEAK'
%       COPY 'REFINE DIST = .3'
%       COPY 'END'
%       COPY '#SCRIPT PLOT'
%       COPY '#EDIT'
%       COPY 'SEL TYPE NE Q'
%       COPY 'END'
% END IF
% IF RETURNTO .EQ. 1 THEN
%      FINISH
% END IF
% VERIFY YES NO
 
 * Remember - the data may not be good enough to reveal all H atoms.

% EVALUATE QTITLE = 'Add Hydrogen'
% EVALUATE QLINE1 = 'Have all the hydrogen atoms been located?'
% EVALUATE QLINE2 = ' '
% EVALUATE BUTTOK = '&Yes'
% EVALUATE BUTTXX = '&No'
% COPY '#SCRIPT XQUESTIO'
% IF ANSWER .EQ. TRUE THEN
%% Store the fact that all H's have been found.
%   EVALUATE HDONE = TRUE
%   EVALUATE HMASK = TRUE
%   COPY '#SCRIPT XPUTINFO'
% END IF
% COPY '#SFLS'
% COPY 'CALC'
% COPY 'END'
% VARIABLE LOGICAL REFREQD
% EVALUATE REFREQD = TRUE
% COPY '#SCRIPT XGUIDE'
%END SCRIPT
