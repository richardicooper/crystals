%SCRIPT PUBLISH
% VARIABLE LOGICAL SCRIPTEXT
% IF SCRIPTEXT .EQ. TRUE THEN
 This SCRIPT helps with production of tables for publication. The output
 is displayed on the screen and listing file, together with a 'flat'    
 ASCII file suitable for sending to a laser printer or inputting to a   
 word processor.                                                       
 The esds of coordinates and distance-angles are derived from the full 
 least squares matrix. After the final round of least squares, DO NOT  
 MODIFY the atom coordinate list (LIST 5) IN ANY WAY at all, otherwise 
 CRYSTALS will detect a mis match with the matrix and abort the        
 calculation.                                                          
 The ASCII data is in the PUBLISH.LIS                                  
 A full analysis of residuals may reveal remaining deficiencies in the 
 refinement.                                                           
% ELSE
The SCRIPT to help produce tables for publication
% END IF
% VARIABLE INTEGER PFLAG
% EVALUATE PFLAG = 0
%%
       The publication tables are in a file 'PUBLISH.LIS'
                 The cif file is in  'PUBLISH.CIF'
%  LOOP
%    ON ERROR TERMINATE ABANDONED
%    CLEAR
%    VERIFY END HELP XYZ ANISO REFLECTIONS DISTANCES SUMMARY ANALYSE -
      GEOMETRY ARCHIVE
%    GET NOSTORE FINAL ABBREVIATED 'Next step' 'END'
%    IF VALUE .GE. 3 .AND. PFLAG .LE. 0 THEN
%      COPY '#RELEASE PUNCH PUBLISH.LIS'
%      EVALUATE PFLAG = 1
%    END IF
%    CASE VALUE
%        EVALUATE LOOPEND = LOOPCOUNTER              %END%
%      BLOCK                                         %HELP%
      The SCRIPT to help produce tables for publication
      NOTE that under some operating systems, the output from successive
      entries to this SCRIPT will overwrite previous output.
      BACK        End this script
      HELP        Display this help text
      XYZ         Coordinate listing
      ANISO       Temperature factor listing
      REFLECTIONS Reflection listing
      DISTANCES   Distance-angle listing
      SUMMARY     A summary of all data stored
      ANALYSE     A full analysis of residuals
      GEOMETRY    The SCRIPT for evaluating molecular parameters.
      ARCHIVE     A SCRIPT to save the best of the goodies.
      DIRECT      Go to CRYSTALS direct command input.
%%
%      END BLOCK
%%
%      BLOCK                                         %XYZ%
%        COPY '#SCRIPT PUBXYZ'
%      END BLOCK
%%
%      BLOCK                                         %ANISO%
%        COPY '#SCRIPT PUBANISO'
%      END BLOCK
%%
%      BLOCK                                         %REFLECTIONS%
%        COPY '#SCRIPT PUBREFL'
%      END BLOCK
%%
%      BLOCK                                         %DISTANCES%
%        COPY '#SCRIPT PUBDIST'
%      END BLOCK
%%
%      BLOCK                                         %SUMMARY%
%        COPY '#SUMMARY EVERYTHING'
%        COPY 'END'
%      END BLOCK
%%
%      BLOCK                                         %ANALYSE%
%        COPY '#ANALYSE'
%        COPY 'LIST LEVEL=HIGH'
%        COPY 'END'
%      END BLOCK
%%
%      BLOCK                                         %GEOMETRY%
%        COPY '#SCRIPT GEOMETRY'
%        COPY 'END'
%      END BLOCK
%%
%%
%      BLOCK                                         %ARCHIVE%
%        COPY '#SCRIPT ARCHIVE'
%        COPY 'END'
%      END BLOCK
%%
%    END CASE
%%
%     END LOOP
%%
%  VERIFY YES NO
%    GET NOSTORE FINAL ABBREVIATED 'Do you want a CIF file?' 'YES'
%    IF VALUE .EQ. 2 THEN
%            FINISH
%    ELSE 
%      BLOCK                                         %CIF%
%        COPY '#SCRIPT CIF'
%        COPY 'END'
%      END BLOCK
%    END IF
%END SCRIPT
