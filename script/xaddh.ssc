%SCRIPT XADDH
%% Add hydrogens to elements.
% VARIABLE CHARACTER CATOM
% VARIABLE INTEGER HTYPE LOOPAGAIN
% VARIABLE LOGICAL CALLED
%%
% IF CALLED .EQ. FALSE THEN
%   GET NOSTORE TEXT 'Atom to add to?' ' '
%   EVALUATE CATOM = CVALUE
% END IF
%%
%%
%%
% LOOP
%   EVALUATE LOOPAGAIN = 0
^^WI WINDOW ADDH 'Add hydrogen' MODAL
^^WI POSITION=CENTRED '_MAINTEXTOUTPUT'
^^WI GRID M NROWS=7 NCOLS=3 {
^^WI   @ 2,2 GRID E NROWS=2 NCOLS=4 {
^^WI     @ 1,1 STATIC T1 'Choose the geometry of the carbon'
^^WI     @ 2,1 STATIC T1 'and number of hydrogens to add.'
^^WI     @ 1,4 STATIC T2 'Hydrogen bonds can only be inserted'
^^WI     @ 2,4 STATIC T2 'for atoms visible in the model window'
^^WI   }
^^WI   @ 4,2 GRID C NROWS=5 NCOLS=11 {
^^WI     @ 1,3 RADIOBUTTON H33 'SP3, 3 H (methyl)' STATE=ON
^^WI     @ 1,1 GRID HHH NROWS=1 NCOLS=1 { @ 1,1 BITMAP H33 FILE h33.bmp }
^^WI     @ 3,3 RADIOBUTTON H23 'SP3, 2 H'
^^WI     @ 3,1 GRID HHH NROWS=1 NCOLS=1 { @ 1,1 BITMAP H23 FILE h23.bmp }
^^WI     @ 5,3 RADIOBUTTON H13 'SP3, 1 H'
^^WI     @ 5,1 GRID HHH NROWS=1 NCOLS=1 { @ 1,1 BITMAP H13 FILE h13.bmp }
^^WI     @ 1,7 RADIOBUTTON H22 'SP2, 2 H'
^^WI     @ 1,5 GRID HHH NROWS=1 NCOLS=1 { @ 1,1 BITMAP H22 FILE h22.bmp }
^^WI     @ 3,7 RADIOBUTTON H12 'SP2, 1 H'
^^WI     @ 3,5 GRID HHH NROWS=1 NCOLS=1 { @ 1,1 BITMAP H12 FILE h12.bmp }
^^WI     @ 5,7 RADIOBUTTON H11 'SP1, 1 H'
^^WI     @ 5,5 GRID HHH NROWS=1 NCOLS=1 { @ 1,1 BITMAP H11 FILE h11.bmp }
^^WI     @ 1,11 RADIOBUTTON PHE 'phenyl, 5H'
^^WI     @ 1,9 GRID HHH NROWS=1 NCOLS=1 { @ 1,1 BITMAP PHE FILE hphen.bmp }
^^WI     @ 3,11 RADIOBUTTON OH 'hydrogen bond'
^^WI     @ 3,9 GRID HHH NROWS=1 NCOLS=1 { @ 1,1 BITMAP OH FILE hbond.bmp }
^^WI   }
^^WI   @ 6,2 GRID B NROWS=1 NCOLS=3 {
^^WI     @ 1,1 BUTTON BOK '&Next >' DEFAULT COMMIT
^^WI     @ 1,3 BUTTON BXX '&Cancel' CANCEL
^^WI   }
^^WI }
^^WI SHOW
^^CR
%   VERIFY BOK BXX
%   GET NOSTORE SILENT FINAL ABBREVIATED ' ' ' '
%   CASE VALUE
%     BLOCK   %BOK%
^^??   H33 STATE
%      VERIFY ON OFF
%      GET SILENT NOSTORE FINAL ABBREVIATED ' '
%      CASE VALUE
%       BLOCK %ON%
%        EVALUATE HTYPE = 1
%       END BLOCK
%       BLOCK %OFF
^^??     H23 STATE
%         VERIFY ON OFF
%        GET SILENT NOSTORE FINAL ABBREVIATED ' '
%        CASE VALUE
%         BLOCK %ON%
%          EVALUATE HTYPE = 2
%         END BLOCK
%         BLOCK %OFF
^^??       H13 STATE
%          VERIFY ON OFF
%          GET SILENT NOSTORE FINAL ABBREVIATED ' '
%          CASE VALUE
%           BLOCK %ON%
%            EVALUATE HTYPE = 3
%           END BLOCK
%           BLOCK %OFF
^^??         H22 STATE
%            VERIFY ON OFF
%            GET SILENT NOSTORE FINAL ABBREVIATED ' '
%            CASE VALUE
%             BLOCK %ON%
%              EVALUATE HTYPE = 4
%             END BLOCK
%             BLOCK %OFF
^^??           H12 STATE
%              VERIFY ON OFF
%              GET SILENT NOSTORE FINAL ABBREVIATED ' '
%              CASE VALUE
%               BLOCK %ON%
%                EVALUATE HTYPE = 5
%               END BLOCK
%               BLOCK %OFF
^^??             H11 STATE
%                VERIFY ON OFF
%                GET SILENT NOSTORE FINAL ABBREVIATED ' '
%                CASE VALUE
%                 BLOCK %ON%
%                  EVALUATE HTYPE = 6
%                 END BLOCK
%                 BLOCK %OFF
^^??               PHE STATE
%                  VERIFY ON OFF
%                  GET SILENT NOSTORE FINAL ABBREVIATED ' '
%                  CASE VALUE
%                   BLOCK %ON%
%                    EVALUATE HTYPE = 7
%                   END BLOCK
%                   BLOCK %OFF
%                    EVALUATE HTYPE = 8
%                   END BLOCK
%                  END CASE
%                 END BLOCK
%                END CASE
%               END BLOCK
%              END CASE
%             END BLOCK
%            END CASE
%           END BLOCK
%          END CASE
%         END BLOCK
%        END CASE
%       END BLOCK
%      END CASE
^^CO  DISPOSE ADDH
%     COPY '#SCRIPT XADDH2'
%   END BLOCK
%   BLOCK   %BXX%
^^CO DISPOSE ADDH
%     FINISH
%   END BLOCK
%  END CASE
%  IF LOOPAGAIN .EQ. 0 THEN
%     EVALUATE LOOPEND = LOOPCOUNTER
%  END IF
% END LOOP
%END SCRIPT
