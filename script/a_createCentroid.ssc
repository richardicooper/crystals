%SCRIPT CREATECENTROID
%%
%% RETURN VALUES:
%%     CENTROID - Name of centroid created
%%
%% INPUT:
%%     The file AtomsToWorkOn.dat
%%
%  VARIABLE CHARACTER CATOM CATN CENTROID
%  VARIABLE INTEGER ICNT IATS
%%
%  VARIABLE CHARACTER CLTYPE
%  VARIABLE REAL SERIAL
%  VARIABLE REAL ATX ATY ATZ ATSX ATSY ATSZ ATMX ATMY ATMZ
%%
%% Variables for call to script nextfree
%%
%  VARIABLE INTEGER STARTFROM NEXTFREE
%  VARIABLE CHARACTER CTYPE
%%
%  EVALUATE ICNT = 0
%%
%  EVALUATE ATSX = 0.0
%  EVALUATE ATSY = 0.0
%  EVALUATE ATSZ = 0.0
%%
%% Get the atoms to be searched for a match from the file AtomsToWorkOn.dat
%%
%  COPY '#OPEN SCPDATA AtomsToWorkOn.dat'
%  LOOP
%    ON ERROR TERMINATE
%    ON END TERMINATE
%    EXTRACT NEXT
%    EXTRACT TRANSFER INPUT
%    CLEAR
%    GET TEXT 'Atom'
%    EVALUATE CATOM = CVALUE
%    IF CVALUE .EQ. 'END' THEN
%      EVALUATE LOOPEND = LOOPCOUNTER
%    ELSE
%% Find the coordinates
%      EVALUATE CATN = FIRSTSTR ( CATOM )
%      EVALUATE IATS = FIRSTINT ( CATOM )
%      COPY '#GENERALEDIT 5'
%      COPY 'LOCATE RECORDTYPE=101'
%      COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%      LOOP
%        ON EDITERROR TERMINATE
%        COPY 'TRANSFER FROM OFFSET=0 TO CLTYPE'
%        IF CATN .EQ. CLTYPE THEN
%          COPY 'TRANSFER FROM OFFSET=1 TO SERIAL'
%          IF IATS .EQ. INTEGER ( SERIAL ) THEN
%            COPY 'TRANSFER FROM OFFSET=4 TO ATX'
%            COPY 'TRANSFER FROM OFFSET=5 TO ATY'
%            COPY 'TRANSFER FROM OFFSET=6 TO ATZ'
%            EVALUATE LOOPEND = LOOPCOUNTER
%          END IF
%        END IF
%        COPY 'NEXT'
%      END LOOP
%      COPY 'END'
%      EVALUATE ATSX = ATSX + ATX
%      EVALUATE ATSY = ATSY + ATY
%      EVALUATE ATSZ = ATSZ + ATZ
%      EVALUATE ICNT = ICNT + 1
%    END IF
%  END LOOP
%%
%  COPY '#CLOSE SCPDATA'
%%
%% Check here if ICNT is < 0
%%
%  EVALUATE ATMX = ATSX / REAL ( ICNT )
%  EVALUATE ATMY = ATSY / REAL ( ICNT )
%  EVALUATE ATMZ = ATSZ / REAL ( ICNT )
%%
%% Now we need to find a free serial number for this new atom of type QH
%%
%  EVALUATE STARTFROM = 0
%  EVALUATE CTYPE = "QH"
%  COPY '#SCRIPT NEXTFREE'
%%
%  COPY '#EDIT'
%  COPY 'MONITOR OFF'
%  TRANSFER "ATOM " // CTYPE // " " // CHARACTER ( NEXTFREE ) // " X = " // CHARACTER ( ATMX ) // " " // CHARACTER ( ATMY ) // " " // CHARACTER ( ATMZ ) TO CRYSTALS
%  TRANSFER "CHANGE " // CTYPE // "(" // CHARACTER ( NEXTFREE ) // ", U[ISO]) 0.005" TO CRYSTALS
%  TRANSFER "CHANGE " // CTYPE // "(" // CHARACTER ( NEXTFREE ) // ", OCC) 0" TO CRYSTALS
%  COPY 'END'
%%
%  EVALUATE CENTROID = CTYPE // "(" // CHARACTER ( NEXTFREE ) // ")"
%%
%END SCRIPT