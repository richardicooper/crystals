%SCRIPT XCOLLECT
% VARIABLE INTEGER DOCOLLECT 
% VARIABLE LOGICAL COLLECTNEW
%%
A SCRIPT to help collect atoms into a molecule, or bring new peaks close
to existing atoms.
%% _______________________________________________________________
%% See if GLOBAL help is needed
%%
%  VARIABLE LOGICAL TOOL-HELP
%  VARIABLE CHARACTER TOOL-TEXT
%  VARIABLE LOGICAL RUNNOW
%  EVALUATE TOOL-TEXT = 'atom-collection'
%  COPY '#SCRIPT CALL-TOOL-HELP'
%  IF .NOT. RUNNOW THEN 
%    FINISH
%  END IF
%% _______________________________________________________________
%%

%%
%% check we have an atom list
%  IF EXISTS 5 .NE. 1 THEN
  You do not have an atom list.
%    FINISH
%  END IF
%%
^^WI WINDOW COLLECT 'Molecule Assembly' MODAL
^^WI COMMIT='BTNOK' CANCEL='BTNXX'
^^WI GRID MYMAIN NROWS=1 NCOLS=3
^^WI {
^^WI   @ 1,1 GRID GRIDL NROWS=3 NCOLS=2
^^WI   {
^^WI     @ 2,2 ICON IM QUERY
^^WI   }
^^WI   @ 1,2 GRID GRIDL NROWS=6 NCOLS=3
^^WI   {
% CLEAR
% IF COLLECTNEW .EQ. TRUE THEN
^^WI   @ 3,2 RADIOBUTTON XALL 'Collect all the atoms together.'
^^WI                                           STATE=OFF IGNORE
^^WI   @ 5,2 RADIOBUTTON XNEW 'Only Collect New Peaks.'
^^WI                                           STATE=ON IGNORE
% ELSE
^^WI   @ 3,2 RADIOBUTTON XALL 'Collect all the atoms together.'
^^WI                                           STATE=ON IGNORE
^^WI   @ 5,2 RADIOBUTTON XNEW 'Only Collect New Peaks.'
^^WI                                           STATE=OFF IGNORE
% END IF
^^WI   }
^^WI   @ 1,3 GRID GRIDR NROWS=7 NCOLS=3
^^WI   {
^^WI     @ 2,2 BUTTON BTNOK '&Ok' DEFAULT
^^WI     @ 4,2 BUTTON BTNXX '&Cancel'
^^WI     @ 6,2 BUTTON BTNHE '&Help'
^^WI   }
^^WI }
^^WI SHOW
^^CR
%  LOOP
%    VERIFY BTNOK BTNXX BTNHE
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%        CASE VALUE
%             BLOCK                %BUTTONOK%
%                 QUEUE REWIND
%                 CLEAR
^^WI              GETVALUE XALL
^^CR
%                 VERIFY ON OFF
%                 GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%                 EVALUATE DOCOLLECT = VALUE
%                 IF DOCOLLECT .EQ. 1 THEN
%                    INSERT 'SELECT TYPE = ALL'
%                    QUEUE SEND
%                    CLEAR

 * Collecting all atoms.
%                 ELSE
%                    INSERT 'SELECT TYPE = PEAKS'
%                    QUEUE SEND
%                    CLEAR

 * Collecting peaks.
%                 END IF
%                 COPY '#COLLECT'
%                 QUEUE PROCESS
%                 COPY 'CONT TOL=2.0'
%                 COPY 'END'

%                 COPY '#COLLECT'
%                 QUEUE PROCESS
%                 COPY 'END'
 * Done.
^^WI              DISPOSE COLLECT
^^CR
%                 FINISH
%             END BLOCK
%             BLOCK                %BUTTONXX%
^^WI              DISPOSE COLLECT
^^CR
%                 FINISH
%             END BLOCK
%             BLOCK                %BUTTONHE%
%                 COPY '#SCRIPT XHELP1'
Collect. The purpose of this routine is to move the atoms
coordinates using symmetry, so as to form a 'molecule' using
the covalent radii given in LIST 29.
If you choose "Collect all the atoms together", then all the
atoms will be moved around to form a molecule. If the
molecule is already assembled, then choose "Only collect new
peaks" to keep the existing atoms where they are.

%                 COPY '#SCRIPT XHELP2'
%             END BLOCK
%        END CASE
% END LOOP
%%
%END SCRIPT
