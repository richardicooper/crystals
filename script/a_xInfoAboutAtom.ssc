%SCRIPT XINFOABOUTATOM
%  VARIABLE CHARACTER MODE ATOM
%  VARIABLE CHARACTER CELE CCHK
%  VARIABLE INTEGER ISER
%  VARIABLE REAL RPKH RCHK
%  VARIABLE INTEGER IPRT IHYB
%  VARIABLE INTEGER IPEG IPEP
%  VARIABLE CHARACTER MESSAGE
%%
%  GET NOSTORE SILENT KEYWORD 'Atom?' ' '
%  EVALUATE ATOM = CVALUE
%%
%% TRANSFER '{I Parameters passed: ' // ATOM // '.' TO DISPLAY
%%
%  EVALUATE CELE = FIRSTSTR ( ATOM )
%  EVALUATE ISER = FIRSTINT ( ATOM )
%%
%% TRANSFER '{I Atom type: ' // CELE // ', serial: ' // CHARACTER ( ISER ) TO DISPLAY
%%
%  BLOCK
%    ON EDITERROR TERMINATE
%    COPY '#GENERALEDIT 5'
%    COPY 'LOCATE RECORDTYPE=101'
%    COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%    LOOP
%     ON EDITERROR TERMINATE
%     COPY 'TRANSFER FROM OFFSET=0 TO CCHK'
%     COPY 'TRANSFER FROM OFFSET=1 TO RCHK'
%     IF ( ( CELE .EQ. CCHK ) .AND. ( ISER .EQ. INTEGER ( RCHK ) ) ) THEN
%      COPY 'TRANSFER FROM OFFSET=14 TO IPRT'
%      COPY 'TRANSFER FROM OFFSET=16 TO IHYB'
%      COPY 'END'
%      EVALUATE LOOPEND = LOOPCOUNTER
%%
%%     EVALUATE IPEG = ( IPRT IDECMASK 111000 ) / 1000
%%     EVALUATE IPEP = IPRT IDECMASK 000111
%      COPY '#SCRIPT A_SPLITPARTNUMBER'
%%
%     END IF
%     COPY 'NEXT'
%    END LOOP
%  END BLOCK
%%
%% TRANSFER '{I ' // ATOM // ': residue  number is ' // CHARACTER ( IHYB ) // '.' TO DISPLAY
%% TRANSFER '{I ' // ATOM // ': part     number is ' // CHARACTER ( IPRT ) // '.' TO DISPLAY
%% TRANSFER '{I ' // ATOM // ': assembly number is ' // CHARACTER ( IPEG ) // '.' TO DISPLAY
%% TRANSFER '{I ' // ATOM // ': group    number is ' // CHARACTER ( IPEP ) // '.' TO DISPLAY
%%
%  EVALUATE MESSAGE = "Part of residue " // CHARACTER ( IHYB ) // ", assembly " // CHARACTER ( IPEG ) // " and group " // CHARACTER ( IPEP ) // "."
%% TRANSFER '{I ' // MESSAGE TO DISPLAY
%%
^^WI WINDOW XINFO
% TRANSFER "^^WI 'Info about Atom " // ATOM // ":'" TO DISPLAY
^^WI MODAL
^^WI GRID M NROWS=5 NCOLS=3 {
^^WI   @ 2,2 GRID B NROWS=1 NCOLS=1 {
% TRANSFER "^^WI     @ 1,1   STATIC XMSG '" // MESSAGE // "'" TO DISPLAY
^^WI   }
^^WI   @ 4,2 GRID C NROWS=9 NCOLS=3 {
% TRANSFER "^^WI     @ 1,1 BUTTON BSC 'Select atom " // ATOM // "' DISABLED=NO" TO DISPLAY
% TRANSFER "^^WI     @ 1,3 BUTTON BMC 'Modify atom " // ATOM // "' DISABLED=NO" TO DISPLAY
%%
% TRANSFER "^^WI     @ 3,1 BUTTON BSR 'Select residue " // CHARACTER ( IHYB ) // "' DISABLED=YES" TO DISPLAY
% TRANSFER "^^WI     @ 3,3 BUTTON BMR 'Modify residue " // CHARACTER ( IHYB ) // "' DISABLED=YES" TO DISPLAY
%%
% TRANSFER "^^WI     @ 5,1 BUTTON BSA 'Select assembly " // CHARACTER ( IPEG ) // "' DISABLED=YES" TO DISPLAY
% TRANSFER "^^WI     @ 5,3 BUTTON BMA 'Modify assembly " // CHARACTER ( IPEG ) // "' DISABLED=YES" TO DISPLAY
%%
% TRANSFER "^^WI     @ 7,1 BUTTON BSG 'Select group " // CHARACTER ( IPEP ) // "' DISABLED=YES" TO DISPLAY
% TRANSFER "^^WI     @ 7,3 BUTTON BMG 'Modify group " // CHARACTER ( IPEP ) // "' DISABLED=YES" TO DISPLAY
%%
^^WI     @ 9,1 BUTTON USA 'UnSelect all'
^^WI     @ 9,3 BUTTON BOK 'Close' DEFAULT COMMIT CANCEL
^^WI   }
^^WI }
^^WI SHOW
^^CR
%%
%  IF IHYB .NE. 0 THEN
^^CO   SET BSR DISABLED=NO
^^CO   SET BMR DISABLED=NO
%  END IF
%%
%  IF IPEG .NE. 0 THEN
^^CO   SET BSA DISABLED=NO
^^CO   SET BMA DISABLED=NO
%  END IF
%%
%  IF IPEP .NE. 0 THEN
^^CO   SET BSG DISABLED=NO
^^CO   SET BMG DISABLED=NO
%  END IF
%%
%  LOOP
%   VERIFY BOK USA BSC BSR BSA BSG BMC BMR BMA BMG
%   GET SILENT FINAL NOSTORE ABBREVIATED ' ' ' '
%   CASE VALUE
%%
%    BLOCK        % BOK - CLOSE window
^^CO   DISPOSE XPARA
%      FINISH
%    END BLOCK
%%
%    BLOCK        % USA - unselect all
%      TRANSFER '^^CO SET MODEL01 SELECT ALL=NO' TO DISPLAY
%    END BLOCK
%%
%    BLOCK        % BSC - Select ATOM
%      TRANSFER '^^CO SET MODEL01 SELECT ALL=NO' TO DISPLAY
%      TRANSFER '^^CO SET MODEL01 SELECT ' // ATOM // ' YES' TO DISPLAY
%    END BLOCK
%%
%    BLOCK        % BSR - Select residue
%      EVALUATE MODE = "RESIDUE"
%      COPY '#SCRIPT A_SELECTATOMS'
%    END BLOCK
%%
%    BLOCK        % BSA - Select assembly
%      TRANSFER '^^CO SET MODEL01 SELECT ALL=NO' TO DISPLAY
%      EVALUATE MODE = "ASSEMBLY"
%      COPY '#SCRIPT A_SELECTATOMS'
%    END BLOCK
%%
%    BLOCK        % BSG - Select group
%      TRANSFER '^^CO SET MODEL01 SELECT ALL=NO' TO DISPLAY
%      EVALUATE MODE = "GROUP"
%      COPY '#SCRIPT A_SELECTATOMS'
%    END BLOCK
%%
%    BLOCK        % BMR - Modify atom
%      COPY '#SCRIPT A_XPARAM'
^^CO   DISPOSE XPARA
%      FINISH
%    END BLOCK
%%
%    BLOCK        % BMR - Modify residue
%      EVALUATE MODE = "RESIDUE"
%      COPY '#SCRIPT A_XMODIFYATOMS'
^^CO   DISPOSE XPARA
%      FINISH
%    END BLOCK
%%
%    BLOCK        % BMA - Modify assembly
%      EVALUATE MODE = "ASSEMBLY"
%      COPY '#SCRIPT A_XMODIFYATOMS'
^^CO   DISPOSE XPARA
%      FINISH
%    END BLOCK
%%
%    BLOCK        % BMG - Modify group
%      EVALUATE MODE = "GROUP"
%      COPY '#SCRIPT A_XMODIFYATOMS'
^^CO   DISPOSE XPARA
%      FINISH
%    END BLOCK
%   END CASE
%  END LOOP
%%
%END SCRIPT