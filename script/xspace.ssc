%SCRIPT XSPACE
%% CRYSTALS space group input script
% VARIABLE LOGICAL SCRIPTEXT LCANCEL
% VARIABLE CHARACTER L2 A2 B2 C2
% VARIABLE CHARACTER SPACEHOOK
% IF SCRIPTEXT .EQ. TRUE THEN
%  COPY '#HELP SPACE'
% ELSE
 Generation of symmetry operators from space group symbol using code of
 Larson and Gabe.
% END IF
%   EVALUATE LCANCEL = FALSE
%% A SCRIPT TO UPDTE LIST 2
%  IF EXISTS 2 .LE. 0 THEN
%    EVALUATE L2 = 'P'
%    EVALUATE A2 = '1'
%    EVALUATE B2 = ' '
%    EVALUATE C2 = ' '
%  ELSE
%%
%    COPY '#GENERALEDIT 2'
%    COPY 'LOCATE RECORDTYPE = 106'
%    COPY 'TOP'
%    COPY 'TRANSFER FROM OFFSET =  0 TO L2'
%    COPY 'TRANSFER FROM OFFSET =  1 TO A2'
%    COPY 'TRANSFER FROM OFFSET =  2 TO B2'
%    COPY 'TRANSFER FROM OFFSET =  3 TO C2'
%    COPY 'END'
%  END IF
%%
%% CREATE LIST 2
%%^
^^WI WINDOW INLIST2 'Space Group Symbol' MODAL COMMIT='BOK' CANCEL='BXX'
^^WI GRID             GRIDM NROWS=1 NCOLS=2
^^WI {
^^WI  @ 1,1 GRID GRIDL NROWS=5 NCOLS=2
^^WI  {
%%
^^WI   @ 2,2 STATIC HOOK
% TRANSFER "^^WI '" // SPACEHOOK // "'" TO DISPLAY 
^^WI   @ 4,2 GRID GRIDML NROWS=2 NCOLS=10
^^WI   {
%%
^^WI    @ 1,2 STATIC    TEXT1 'Lattice'
^^WI    @ 1,4 STATIC    TEXT1 'a-axis'
^^WI    @ 1,6 STATIC    TEXT1 'b-axis'
^^WI    @ 1,8 STATIC    TEXT1 'c-axis'
%%
% CLEAR
% INSERT !^^WI @ 2,2 EDITBOX R1 '!
% STORE FORMAT /(A4)/ LENGTH 4 CHARACTER L2
% INSERT !'!
% OUTPUT
^^WI CHARS=8  INFORM=YES
% CLEAR
% INSERT !^^WI @ 2,4 EDITBOX R2 '!
% STORE FORMAT /(A4)/ LENGTH 4 CHARACTER A2
% INSERT !'!
% OUTPUT
^^WI CHARS=8  INFORM=YES
%%
% CLEAR
% INSERT !^^WI @ 2,6 EDITBOX R3 '!
% STORE FORMAT /(A4)/ LENGTH 4 CHARACTER B2
% INSERT !'!
% OUTPUT
^^WI CHARS=8  INFORM=YES
%%
% CLEAR
% INSERT !^^WI @ 2,8 EDITBOX R4 '!
% STORE FORMAT /(A4)/ LENGTH 4 CHARACTER C2
% INSERT !'!
% OUTPUT
^^WI CHARS=8  INFORM=YES
%%
^^WI   }
^^WI  }
^^WI  @ 1,2 GRID GRIDR NROWS=5 NCOLS=3
^^WI  {
^^WI   @ 2,2 BUTTON BOK '&Ok' DEFAULT
^^WI   @ 4,2 BUTTON BXX '&Cancel'
^^WI  }
^^WI }
^^WI SHOW
^^CR
%%
%%
%  LOOP
%   ON ERROR TERMINATE
%   ON END   TERMINATE
%   VERIFY R1 R2 R3 R4 BXX BOK
%   GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%   CASE VALUE
%    BLOCK             %LATTICE%
%     GET SILENT NOSTORE KEYWORD ' ' ' '
%     EVALUATE L2 = CVALUE
%    END BLOCK
%    BLOCK             %A=AXIS%
%     GET SILENT NOSTORE KEYWORD ' ' ' '
%     EVALUATE A2 = CVALUE
%    END BLOCK
%    BLOCK             %B-AXIS%
%     GET SILENT NOSTORE KEYWORD ' ' ' '
%     EVALUATE B2 = CVALUE
%    END BLOCK
%    BLOCK             %C-AXIS%
%     GET SILENT NOSTORE KEYWORD ' ' ' '
%     EVALUATE C2 = CVALUE
%    END BLOCK
%    BLOCK             %BXX%
^^WI  DISPOSE INLIST2
^^CR
%     EVALUATE LCANCEL = TRUE
%     FINISH
%    END BLOCK
%    BLOCK             %BOK%
%     CLEAR
^^WI       DISPOSE INLIST2
^^CR
%% NOW WRITE NEW LIST 2
%  BLOCK
%    ON END   TERMINATE
%%    ON ERROR RESTART
%    QUEUE REWIND
%    CLEAR
%    INSERT 'SYMBOL '
%    STORE FORMAT /(A4)/ LENGTH 4 CHARACTER L2
%    INSERT ' '
%    STORE FORMAT /(A4)/ LENGTH 4 CHARACTER A2
%    INSERT ' '
%    STORE FORMAT /(A4)/ LENGTH 4 CHARACTER B2
%    INSERT ' '
%    STORE FORMAT /(A4)/ LENGTH 4 CHARACTER C2
%    INSERT ' '
%    QUEUE SEND
%    CLEAR
%    COPY '#SPACEGROUP'
%    QUEUE PROCESS
%    COPY 'END'
%  END BLOCK
%%^
%          FINISH
%    END BLOCK
%   END CASE
%  END LOOP
%END SCRIPT
