%SCRIPT SG
%%
%% Script to determine a space group using external program
%% 1) Get options from user.
%% 2) Write a file
%% 3) Fire up space group program
%% 4) Read back, present, analyse and select result.
%%
%% Variables used by the script:
%%
% VARIABLE INTEGER CLAS CHIR 
% VARIABLE REAL A B C AL BE GA 
% VARIABLE CHARACTER CLASS1
%%
%% Do some initial checks that all data is present
%%
% IF ( EXISTS 1 .LE. 0 ) THEN
{E Missing unit cell
{I The unit cell is required to determine the space group.
%   FINISH
% END IF
% IF ( EXISTS 6 .LE. 0 ) THEN
{E Missing observations
{I Reflections (hkl data) are required to determine the space group.
%   FINISH
% END IF
%%
%%
%% STEP 1a - Display a dialog to the user
%%
^^WI WINDOW SG 'Space group determination' MODAL COMMIT='SGOK' CANCEL='SGXX'
^^WI GRID 'top' NROWS=7 NCOLS=3 {
^^WI    @ 2,2 GRID 'options' NROWS=1 NCOLS=3 {
^^WI       @ 1,1 STATIC 'T' 'Crystal class:'
^^WI       @ 1,3 DROPDOWN 'CLASS' ADDTOLIST
^^WI         'Automatic' '-1 Triclinic' '2/m 1 1 Monoclinic'
^^WI         '1 2/m 1 Monoclinic' '1 1 2/m Monoclinic'
^^WI         '2/m 2/m 2/m Orthorhombic' '4/m Tetragonal'
^^WI         '4/m m m Tetragonal'
^^WI         '-3 Rhombohedral' '-3 m 1 Rhombohedral'
^^WI         '-3 Trigonal' '-3 m 1 Trigonal' '-3 1 m Trigonal'
^^WI         '6/m Hexagonal' '6/m m m Hexagonal'
^^WI         'm -3 Cubic' 'm -3 m Cubic' NULL
^^WI    }
^^WI
^^WI    @ 4,2 CHECKBOX 'CHIRAL' 'Material is definitely chirally pure.'
^^WI
^^WI    @ 6,2 GRID 'buttons' NROWS=1 NCOLS=5 {
^^WI       @ 1,1 BUTTON  'SGOK' '&Ok' DEFAULT
^^WI       @ 1,3 BUTTON  'SGXX' '&Cancel'
^^WI       @ 1,4 STRETCH 'SGH' HORIZONTAL
^^WI       @ 1,5 BUTTON  'SGHELP' '&Help'
^^WI    }
^^WI }
^^WI SHOW
^^CR
%%
%% Step 1 - Read their responses.
%%
% LOOP
%%
%% Here are the 3 possible responses from the dialog:
%%
%   VERIFY SGXX SGHELP SGOK
%%
%% We read one, the variable VALUE is assigned its index in the Verify list:
%%
%   GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%%
%% We switch on that value:
%%
%   CASE VALUE
%     BLOCK     % SGXX - the user pressed cancel, kill window and exit script
^^CO    DISPOSE SG
%       FINISH
%     END BLOCK
%     BLOCK     % SGHELP - the user wants help.
%       COPY '#SCRIPT XHELP1'

 This dialog runs a program to help you to determine the space group
 of a crystal by analysing the systematic absences in the data.

 You may specify the Laue class to force the program to search
 for spacegroups in that class, otherwise the class will be
 determined automatically from merging statistics.

 If you KNOW that your material is chirally pure, you may check the
 'chirally pure' check box.
%       COPY '#SCRIPT XHELP2'
%     END BLOCK
%     BLOCK     % SGOK - exit this loop and continue
%       EVALUATE LOOPEND = LOOPCOUNTER
%     END BLOCK
%   END CASE
% END LOOP
%%
%%
% COPY '#CLOSE SCPDATA'
% EXTRACT CLOSE
% IF .NOT. ( FILEDELETE 'sg.dat' ) THEN
{E A previous output file sg.dat is in the way and cannot be deleted.
%   FINISH
% END IF

%%
%% Step 1c -  Collect the responses and close the dialog.
%%
^^?? CLASS SELECTED
% GET NOSTORE SILENT INTEGER ' '
% EVALUATE CLAS = VALUE
^^?? CHIRAL STATE
% VERIFY OFF ON
% GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
% EVALUATE CHIR = VALUE - 1
%%
%% Step 2 - WRITE A FILE
%%
% COPY '#RELEASE PUNCH sg.ini'
%%
%% The cell line:
%%
% COPY '#GENERALEDIT 1'
% COPY 'LOCATE RECORD=101'
% COPY 'TRANSFER FROM OFFSET=0 TO A'
% COPY 'TRANSFER FROM OFFSET=1 TO B'
% COPY 'TRANSFER FROM OFFSET=2 TO C'
% COPY 'TRANSFER FROM OFFSET=3 TO AL'
% COPY 'TRANSFER FROM OFFSET=4 TO BE'
% COPY 'TRANSFER FROM OFFSET=5 TO GA'
% COPY 'END'
% EVALUATE AL = AL * 57.29578
% EVALUATE BE = BE * 57.29578
% EVALUATE GA = GA * 57.29578
% TRANSFER 'CELL ' // CHARACTER A // ' ' // CHARACTER B // ' ' // -
  CHARACTER C // ' ' // CHARACTER AL // ' ' // CHARACTER BE // ' ' // -
  CHARACTER GA TO PUNCH
%%
%
%% The HKL line
%%
% TRANSFER 'HKL "sg.hkl"' TO PUNCH
%%
%% The CLASS line
%%
%%     'Automatic'   '-1'     '2/m 1 1' '1 2/m 1' '1 1 2/m'
%%     '2/m 2/m 2/m' '4/m'    '4/m m m' '-3 rhom' '-3 m 1 rhom'
%%     '-3'          '-3 m 1' '-3 1 m'  '6/m'     '6/m m m'
%%     'm -3'        'm -3 m' 
% CASE CLAS
%   TRANSFER 'Laue class will be determined automatically' TO DISPLAY
%   TRANSFER 'SYMMETRY -1' TO PUNCH
%   TRANSFER 'SYMMETRY 2/m 1 1' TO PUNCH
%   TRANSFER 'SYMMETRY 1 2/m 1' TO PUNCH
%   TRANSFER 'SYMMETRY 1 1 2/m' TO PUNCH
%   TRANSFER 'SYMMETRY 2/m 2/m 2/m' TO PUNCH
%   TRANSFER 'SYMMETRY 4/m' TO PUNCH
%   TRANSFER 'SYMMETRY 4/m m m' TO PUNCH
%   TRANSFER 'SYMMETRY -3 rhom' TO PUNCH
%   TRANSFER 'SYMMETRY -3 m 1 rhom' TO PUNCH
%   TRANSFER 'SYMMETRY -3' TO PUNCH
%   TRANSFER 'SYMMETRY -3 m 1' TO PUNCH
%   TRANSFER 'SYMMETRY -3 1 m' TO PUNCH
%   TRANSFER 'SYMMETRY 6/m' TO PUNCH
%   TRANSFER 'SYMMETRY 6/m m m' TO PUNCH
%   TRANSFER 'SYMMETRY m -3' TO PUNCH
%   TRANSFER 'SYMMETRY m -3 m' TO PUNCH
% END CASE
%%
%% The CHIRAL line:
%%
% IF ( CHIR .EQ. 0 ) THEN
%   TRANSFER 'CHIRAL UNKNOWN' TO PUNCH
% ELSE
%   TRANSFER 'CHIRAL YES' TO PUNCH
% END IF
%% 
%% The OUTPUT file:
%%
% TRANSFER 'OUTPUT "sg.dat"' TO PUNCH
%%
%% Switch PUNCH unit to sg.hkl
%%
% COPY '#RELEASE PUNCH sg.hkl'
%%
%% Output a plain HKLF4 format listing of reflections
%%
% COPY '#PUNCH 6 F'
% COPY 'END'
%%
%% Close punch file.
%%
% COPY '#RELEASE PUNCH bfile.pch'
%%
%% Run the program
%%
% COPY '#SPAWN + "CRYSDIR:Spacegroup.exe" -b sg.ini -t "CRYSDIR:tables.txt"'
%%
% COPY '#SCRIPT SGIN'
%END SCRIPT
