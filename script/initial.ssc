%SCRIPT INITIAL
 General SCRIPT for doing reflection input and data reduction
 ------- ------ --- ----- ---------- ----- --- ---- ---------
%  VARIABLE INTEGER RADIATION INBASICS SOLVEMETHOD
 To proceed you will need to know :-
            Your crystal's unit cell parameters
            Your crystal's symmetry
            The diffraction conditions used in the experiment
            The unique part of the asymmetric unit
 If you have used RC85 or 93, some of this information will already 
 be in a file. You will need to know its name, e.g.
                                                [CAD4]XTLn.SCF
%  BLOCK
%    ON ERROR REPEAT
%    VERIFY YES NO
%      GET NOSTORE FINAL ABBREVIATED  -
 'Do you have the necessary information' 'YES'
%    IF ( VALUE .NE. 1 ) THEN
%      FINISH
%    END IF
%  END BLOCK
%  BLOCK
%    ON ERROR REPEAT
%    ON END TERMINATE
%    VERIFY YES NO
%      GET NOSTORE FINAL ABBREVIATED  -
   'Do you have a basic data file e.g. [CAD4]XTLn.SCF' 'YES'
%    IF ( VALUE .EQ. 1 ) THEN
%      CLEAR
%      INSERT '#USE '
%      GET FINAL FILENAME 'File containing basic data'
%      SEND
%    END IF
%  END BLOCK
%%
%  BLOCK
%    ON ERROR REPEAT
%    COPY '#SCRIPT INBASICS'
%    IF INBASICS .LE. 0 THEN
      Problems during basic data input - try again with care
%       FINISH
%    END IF
%  END BLOCK
%%
%    BLOCK
%      ON ERROR REPEAT
%      IF ( EXISTS 14 .NE. 1 ) THEN
%        VERIFY YES NO
%      GET NOSTORE FINAL ABBREVIATED  -
 'Do you know the asymmetric part of the unit cell?' 'YES'
%        IF ( VALUE .EQ. 1 ) THEN
%          COPY '#SCRIPT INLIST14'
%        ELSE
  You will need this information later when you do Fourier maps. Look in
  International Tables before then.
%        END IF
%      END IF
%    END BLOCK
%%
%    COPY '#LIST 4'
%    COPY 'END'
%
%%                       DO DATA REDUCTION
%  ON ERROR TERMINATE ABANDONED
%  VERIFY YES NO
%      GET FINAL ABBREVIATED  -
   'Do you want to start reflection data processing? ' 'YES'
%  IF ( VALUE .EQ. 1 ) THEN
%    COPY '#SCRIPT INLIST6'
%  END IF
%
%%                        SOLVE THE STRUCTURE
%  ON ERROR TERMINATE ABANDONED
%  VERIFY YES NO
%      GET FINAL ABBREVIATED  -
      'Do you want to try to solve the structure? ' 'YES'
%  IF ( VALUE .EQ. 1 ) THEN
%    COPY '#SCRIPT STRUCTUR'
%  END IF
%
%  IF SOLVEMETHOD .NE. 0 THEN
%    COPY '#SCRIPT MODEL'
%  END IF
%
%END SCRIPT
