%SCRIPT XWILSON                                                                 
% VARIABLE REAL WORK1 WORK2 WORK3                                               
% VARIABLE LOGICAL MERGER ABSENT LNEWRAT LNEWRHO I6DONE I6MASK LMAONCE          
% VARIABLE LOGICAL WILFIL                                                       
% VARIABLE REAL NEWRHO NEWRAT THFULL MRGR                                       
% VARIABLE REAL VOLPRATM NNONHATM CELLVOL SGMULT NALLOWREF ESTNPARA ESTOTP      
% VARIABLE INTEGER ABSPRINT ABSTOTAL                                            
% EVALUATE MERGER = FILEEXISTS 'mergingr.dat'                                   
% EVALUATE ABSENT = FILEEXISTS 'absences.dat'                                   
% EVALUATE LMAONCE = TRUE                                                       
% EVALUATE WILFIL = FALSE                                                       
%%                                                                              
% IF ( EXISTS 6 .NE. 1 ) THEN                                                   
                                                                                
{E No reflections stored. Use "X-ray data->Input reflections".                  
                                                                                
%   FINISH                                                                      
% END IF                                                                        
%%                                                                              
% EVALUATE I6DONE = TRUE                                                        
% EVALUATE I6MASK = TRUE                                                        
% COPY '#SCRIPT XPUTINFO'                                                       
%% 
%%                                                                             
%%                                                                              
^^WI WINDOW XWILSON 'Wilson Plot' LARGE MODAL KEEP SIZE                    
^^WI COMMIT='XC_BOK' CANCEL='XC_BOK'                                            
^^WI GRID MAIN NROWS=3 NCOLS=1                                                  
^^WI {                                                                          
^^WI  @ 1,1 GRID TOP NROWS=3 NCOLS=3                                            
^^WI  {                                                                         
^^WI   @ 2,2 TABCTRL _MAINTABS                                                  
^^WI   {                                                                        
^^WI     TAB _MAINTAB0 'Wilson' GRID _MAINTG0 NROWS=9 NCOLS=3                   
^^WI    {                                                                       
^^WI     @ 2,2 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''                             
^^WI     {                                                                      
^^WI      @ 1,3 GRID EXP2 NROWS=6 NCOLS=1                                       
^^WI      {                                                                     
^^WI       @ 1,1 STATIC XFW1                                                    
^^WI 'The data should lie on a straight line rising from left to right'   
^^WI       @ 2,1 STATIC XFW2                                                    
^^WI 'Wiggles at top right are due to heavy atoms or repeated motifs'     
^^WI       @ 3,1 STATIC XFW2                                                    
^^WI 'Up-turn bottom left may indicate there is mainly noise at this resolution'  
^^WI       @ 4,1 STATIC XFW2                                                    
^^WI 'Right click on a GREEN point to set a resolution limit'     
^^WI       @ 5,1 STATIC XFW3 
^^WI 'Avoid setting LIST 28 filters to eliminate too many reflections'
^^WI       @ 6,1 STATIC XFW4 
^^WI '<|E^2-1|> less than 0.736 may indicate twinning'   
^^WI      }                                                                     
^^WI      @ 1,1 ICON IM INFO                                                    
^^WI     }                                                                      
^^WI     @ 4,2 PLOTWINDOW _VWILSON NROWS=20 NCOLS=64                            
^^WI      DEFINEPOPUPMENU POPUP 'Selection'                                     
^^WI        ITEM _SDELA '&Reject data: rho > _Y ' 'RHOMIT2_N_Y'                 
^^WI      ENDDEFINEMENU                                                         
^^WI     @ 6,2 CHECKBOX WILFILT1                                                
^^WI           'Show plot using only reflections allowed by List 28'            
^^WI           STATE=OFF INFORM=YES                                             
^^WI     @ 8,2 GRID GSTAT NROWS=1 NCOLS=11                                      
^^WI     {                                                                      
^^WI       @ 1,1 STATIC WBFS 'U factor:'                                        
^^WI       @ 1,3 STATIC _MW_BFACTR '0000.0000'                                  
^^WI       @ 1,5 STATIC WBFS 'Scale:'                                           
^^WI       @ 1,7 STATIC _MW_BSCALE '0000.0000'                                  
^^WI     @ 1,9 STATIC WBFS 'MOD(E**2-1)'                                        
^^WI     @ 1,11 STATIC _MW_E2MIN1 '000000.000'                                  
^^WI     }                                                                      
^^WI    }                                                                       
^^WI   }                                                                        
^^WI  }                                                                         
^^WI  @ 2,1 GRID BUTT NROWS=2 NCOLS=7                                           
^^WI  {                                                                         
^^WI   @ 1,1 STRETCH RS HORIZONTAL                                              
^^WI   @ 1,2 BUTTON XC_HELP 'Help'
^^WI   @ 1,4 BUTTON XC_REF 'Refresh'                                            
^^WI   @ 1,6 BUTTON XC_BOK 'Close' COMMIT DEFAULT CANCEL                        
^^WI  }                                                                         
^^WI  @ 3,1 PROGRESS _CHECKPR 'Please wait'                                     
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
^^CO SENDPROGRESSTO _CHECKPR                                                    
%%                                                                              
% LOOP                                                                          
%%                                                                              
^^CO SET PROGOUTPUT TEXT 'Computing Wilson statistics'                          
%  COPY '#WILSON'                                                               
%  COPY 'OUTPUT PLOT=YES NZ=YES STATS=YES'                                      
%  IF ( WILFIL ) THEN                                                           
%    COPY 'FILTER LIST28 = YES'                                                 
%  ELSE                                                                         
%    COPY 'FILTER LIST28 = NO'                                                  
%  END IF                                                                       
%  COPY 'END'                                                                   
%%                                                                              
%%                                                                              
%%                                                                              
^^CO SET PROGOUTPUT TEXT 'Ready'                                                
%%                                                                              
%  LOOP                                                                         
%    VERIFY XC_BOK XC_REF  RHOMIT2 SIGCUT ED28 ED02 ED29 SIGFULL -       
 WILFILT1   XC_HELP                                                            
%    GET NOSTORE SILENT FINAL ABBREVIATED ' ' ' '                               
%    CASE VALUE                                                                 
%      BLOCK    %XC_BOK                                                         
^^CO     DISPOSE XCHECK6                                                        
%        FINISH                                                                 
%      END BLOCK                                                                
%      BLOCK    %XC_REF                                                         
%        EVALUATE LOOPCOUNTER = LOOPEND                                         
%      END BLOCK                                                                
%      BLOCK    %RHOMIT2                                                        
%        GET SILENT NOSTORE REAL ' ' ' '                                        
%        EVALUATE NEWRHO = VALUE                                                
%        EVALUATE LNEWRAT = FALSE                                               
%        EVALUATE LNEWRHO = TRUE                                                
%        COPY '#SCRIPT XLIST28'                                                 
%        EVALUATE WILFIL = TRUE                                                 
^^CO     SET WILFILT1 STATE=ON                             
%        EVALUATE LOOPCOUNTER = LOOPEND                                         
%      END BLOCK                                                                
%      BLOCK    %SIGCUT                                                         
%        GET NOSTORE SILENT REAL ' ' ' '                                        
%        EVALUATE NEWRAT = VALUE                                                
%        EVALUATE LNEWRAT = TRUE                                                
%        EVALUATE LNEWRHO = FALSE                                               
%        COPY '#SCRIPT XLIST28'                                                 
%        EVALUATE LOOPCOUNTER = LOOPEND                                         
%      END BLOCK                                                                
%      BLOCK    %ED28                                                           
%        EVALUATE LNEWRAT = FALSE                                               
%        EVALUATE LNEWRHO = FALSE                                               
%        COPY '#SCRIPT XLIST28'                                                 
%        EVALUATE LOOPCOUNTER = LOOPEND                                         
%      END BLOCK                                                                
%      BLOCK    %ED02                                                           
%        COPY '#SCRIPT XSPACE'                                                  
%        EVALUATE LOOPCOUNTER = LOOPEND                                         
%      END BLOCK                                                                
%      BLOCK    %ED29                                                           
%        COPY '#SCRIPT XINLIST3'                                                
%        EVALUATE LOOPCOUNTER = LOOPEND                                         
%      END BLOCK                                                                
%      BLOCK    %SIGFULL                                                        
%        IF EXISTS ( 30 ) .EQ. 0 THEN                                           
%          COPY '#LIST 30'                                                      
%          COPY 'END'                                                           
%        END IF                                                                 
%        COPY '#GENERALEDIT 30'                                                 
%        COPY 'LOCATE RECORDTYPE = 109'                                         
%        GET NOSTORE SILENT FINAL REAL ' ' ' '                                  
%        EVALUATE THFULL = - VALUE                                              
%        COPY 'TRANSFER TO OFFSET = 10 FROM THFULL'                             
%        COPY 'WRITE'                                                           
%        COPY 'END'                                                             
%        TRANSFER '{I Theta full manually set to ' // -                         
            CHARACTER ( - THFULL ) TO DISPLAY                                   
%      END BLOCK                                                                
%      BLOCK    %WILFILT1                                                       
%        VERIFY ON OFF                                                          
%        GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                           
%        COPY '#WILSON'                                                         
%        COPY 'OUTPUT PLOT=YES NZ=YES STATS=YES'                                
%        CASE VALUE                                                             
%          BLOCK                                                                
%            EVALUATE WILFIL = TRUE                                             
%            COPY 'FILTER LIST28 = YES'                                         
%          END BLOCK                                                            
%          BLOCK                                                                
%            EVALUATE WILFIL = FALSE                                            
%            COPY 'FILTER LIST28 = NO'                                          
%          END BLOCK                                                            
%        END CASE                                                               
%        COPY 'END'                                                             
%      END BLOCK    
%      BLOCK    %HELP
%         COPY '#SPAWN "CRYSDIR:manual/analyse/tabbed-initial.html#wil"' 
{S Wilson Plot help
%      END BLOCK                                                            
%%                                                                                                                                              
%    END CASE                                                                   
%  END LOOP                                                                     
% END LOOP                                                                      
%END SCRIPT                                                                     
