%SCRIPT XSAVEAS
%  VARIABLE CHARACTER FTITLE CPATH CEXTN
%% Get the name of a new DISCFILE from the user and purge
%% to it and restart with it.
^^WI SYSSAVEFILE 'v2save.dsc' '*.dsc' 'CRYSTALS database (*.dsc)'
^^CW
Please give the new dsc file name.
%    GET SILENT TEXT 'New disk filename:' 'CANCEL'
%
% TRANSFER 'File is: ' // CVALUE TO DISPLAY
%    IF CVALUE .NE. 'CANCEL' THEN
%      EVALUATE FTITLE = GETTITLE ( CVALUE )
%      EVALUATE CPATH = GETPATH ( CVALUE )
%      EVALUATE CEXTN = GETEXTN ( CVALUE )
%%
%% Some checks
%%
%      IF FILEEXISTS ( CVALUE ) THEN

%         TRANSFER FTITLE // '.DSC' TO DISPLAY
  Error: File exists already.

%         FINISH
%      END IF
%      IF ( UPPERCASE CEXTN ) .NE. 'DSC' THEN

% TRANSFER CEXTN // ' is not a valid file extension.' TO DISPLAY
  The file must have a .DSC extension.
%         FINISH
%      END IF

%%      TRANSFER "#STORE CSYS NEWD '" // CVALUE // "'" TO CRYSTALS
%      TRANSFER "#PURGE " // FTITLE TO CRYSTALS
%%      COPY '#PURGE DSC'
%      COPY 'END'
^^WI RESTART
%      TRANSFER '^^WI "' // CPATH // '"' TO DISPLAY
^^WI NEWFILE=
%      TRANSFER '^^WI "' // FTITLE // '.DSC"' TO DISPLAY
^^CR
^^WI SET _MAIN TEXT=
% TRANSFER "^^WI 'Crystals - " // CVALUE // "'" TO DISPLAY
^^CR
%% MAKE SURE THE GUI HAS RECEIVED THESE COMMANDS
%% BEFORE CLOSING DOWN!
%% We query the value of any object by name.
%% There won't be an object called _ANYTHING_ so the
%% GUI returns FALSE.
%% This works because ?? commands force the emptying of any queued
%% commands before allowing CRYSTALS to continue.
Waiting to restart...
^^?? _ANYTHING_ EXISTS
% GET SILENT NOSTORE TEXT 'Waiting to restart' ' '
%      COPY '#END'
%    END IF
%END SCRIPT
