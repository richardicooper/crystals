%SCRIPT X30BITS
%%                                                                              
%% CHECK THE EXPERIMENTAL DETAILS
%% 
%%                                                                              
% VARIABLE LOGICAL LNEW30                                                       
% VARIABLE INTEGER SWITCH                                                       
%%                                                                              
% VARIABLE REAL MIN MED MAX KELVIN                                                     
% VARIABLE INTEGER SCAN INST
% VARIABLE REAL  CELLZ PRESS 
% VARIABLE INTEGER COL1 COL2 COL3 COL4 COL5 COL6 COL7 COL8                      
% VARIABLE INTEGER SHA1 SHA2 SHA3 SHA4 SHA5 SHA6 SHA7 SHA8                      
% VARIABLE LOGICAL FMIN FMED FMAX FTEMP  FCELLZ  FPRESS                      
% VARIABLE LOGICAL FCOLOUR FSHAPE   
%%                                                                              
% IF  EXISTS ( 30 ) .NE. 1 THEN                                                 
%   COPY '#LIST 30'                                                             
%   COPY 'END'                                                                  
% END IF                                                                        
% COPY '#GENERALEDIT 30'                                                        
% COPY 'LOCATE RECORDTYPE = 102'                                                
% COPY 'TRANSFER FROM OFFSET = 0 TO MIN'                                        
% COPY 'TRANSFER FROM OFFSET = 1 TO MED'                                        
% COPY 'TRANSFER FROM OFFSET = 2 TO MAX'                                        
% COPY 'TRANSFER FROM OFFSET = 6 TO KELVIN'                                       
% COPY 'LOCATE RECORDTYPE = 106'                                                
% COPY 'TRANSFER FROM OFFSET = 5 TO CELLZ'                                      
% COPY 'LOCATE RECORDTYPE = 107'                                                
% COPY 'TRANSFER FROM OFFSET = 0 TO COL1'                                       
% COPY 'TRANSFER FROM OFFSET = 1 TO COL2'                                       
% COPY 'TRANSFER FROM OFFSET = 2 TO COL3'                                       
% COPY 'TRANSFER FROM OFFSET = 3 TO COL4'                                       
% COPY 'TRANSFER FROM OFFSET = 4 TO COL5'                                       
% COPY 'TRANSFER FROM OFFSET = 5 TO COL6'                                       
% COPY 'TRANSFER FROM OFFSET = 6 TO COL7'                                       
% COPY 'TRANSFER FROM OFFSET = 7 TO COL8'                                       
% COPY 'LOCATE RECORDTYPE = 108'                                                
% COPY 'TRANSFER FROM OFFSET = 0 TO SHA1'                                       
% COPY 'TRANSFER FROM OFFSET = 1 TO SHA2'                                       
% COPY 'TRANSFER FROM OFFSET = 2 TO SHA3'                                       
% COPY 'TRANSFER FROM OFFSET = 3 TO SHA4'                                       
% COPY 'TRANSFER FROM OFFSET = 4 TO SHA5'                                       
% COPY 'TRANSFER FROM OFFSET = 5 TO SHA6'                                       
% COPY 'TRANSFER FROM OFFSET = 6 TO SHA7'                                       
% COPY 'TRANSFER FROM OFFSET = 7 TO SHA8'                                       
% COPY 'END'                                                                    
%%                                                                              
^^WI WINDOW NEW30 'Experimental Items' MODAL                                        
^^WI COMMIT='BOK' CANCEL='BXX'                                                  
^^WI GRID OUTER NROWS=1 NCOLS=2                                                 
^^WI {                                                                          
^^WI  @ 1,2 GRID GRIDB NROWS=7 NCOLS=3                                          
^^WI  {                                                                         
^^WI    @ 2,2 BUTTON BOK '&Ok' DEFAULT                                          
^^WI    @ 4,2 BUTTON BXX '&Cancel'                                              
^^WI    @ 6,2 BUTTON BHELP '&Help'                                              
^^WI  }                                                                         
^^WI  @ 1,1 GRID GRIDA NROWS=3 NCOLS=3                                          
^^WI  {                                                                         
^^WI   @ 2,2 GRID GRIDAC NROWS=3 NCOLS=1 OUTLINE='Experiment'                      
^^WI              COMMAND='CRYSTAL'                                             
^^WI   {                                                                        
^^WI    @ 1,1 GRID GRIDACA NROWS=1 NCOLS=11                                     
^^WI    {                                                                       
^^WI      @ 1,1  STATIC ED30T 'Dimensions (mm): Min'                            
^^WI      @ 1,3  EDITBOX MIN '0' CHARS=7 REAL INFORM=YES                        
^^WI      @ 1,5  STATIC ED30T 'Med'                                             
^^WI      @ 1,7  EDITBOX MED '0' CHARS=7 REAL INFORM=YES                        
^^WI      @ 1,9  STATIC ED30T 'Max'                                             
^^WI      @ 1,11 EDITBOX MAX '0' CHARS=7 REAL INFORM=YES                        
^^WI    }                                                                       
^^WI    @ 2,1 GRID GRIDACB NROWS=1 NCOLS=7
^^WI    {                                                                       
^^WI      @ 1,1  STATIC ED30T 'Crystal shape'                                   
^^WI      @ 1,3  EDITBOX SHAPE '0' CHARS=20 INFORM=YES                          
^^WI      @ 1,5  STATIC ED30T 'Colour'                                          
^^WI      @ 1,7  EDITBOX COLOUR '0' CHARS=20 INFORM=YES                         
^^WI    }                                                                       
^^WI    @ 3,1 GRID GRIDACC NROWS=1 NCOLS=11
^^WI    {                                                                       
^^WI      @ 1,1  STATIC ED30T 'Z'                                               
^^WI      @ 1,3  EDITBOX CELLZ '1' CHARS=6 REAL INFORM=YES                      
^^WI      @ 1,5  STATIC ED30T 'Pressure (kPa)'                                  
^^WI      @ 1,7  EDITBOX PRESS '0' CHARS=9 INFORM=YES                          
^^WI      @ 1,9 STATIC ED30T 'Temperature (K)'                                 
^^WI      @ 1,11 EDITBOX KELVIN '0' CHARS=9 REAL INFORM=YES                       
^^WI    }                                                                       
^^WI   }                                                                        
^^WI  }                                                                         
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
%  CLEAR                                                                        
%  INSERT '^^CO SET MIN TEXT '                                                  
%  STORE FORMAT /(F9.6)/ LENGTH 9 REAL MIN                                      
%  OUTPUT                                                                       
%  CLEAR                                                                        
%  INSERT '^^CO SET MED TEXT '                                                  
%  STORE FORMAT /(F9.6)/ LENGTH 9 REAL MED                                      
%  OUTPUT                                                                       
%  CLEAR                                                                        
%  INSERT '^^CO SET MAX TEXT '                                                  
%  STORE FORMAT /(F9.6)/ LENGTH 9 REAL MAX                                      
%  OUTPUT                                                                       
%  CLEAR                                                                        
%  INSERT '^^CO SET KELVIN TEXT '                                                 
%  STORE FORMAT /(F9.5)/ LENGTH 9 REAL KELVIN                                     
%  OUTPUT                                                                       
%  CLEAR                                                                        
%  INSERT "^^CO SET SHAPE TEXT '"                                               
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER SHA1                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER SHA2                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER SHA3                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER SHA4                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER SHA5                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER SHA6                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER SHA7                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER SHA8                                    
%  INSERT "'"                                                                   
%  OUTPUT                                                                       
%  CLEAR                                                                        
%  INSERT "^^CO SET COLOUR TEXT '"                                              
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER COL1                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER COL2                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER COL3                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER COL4                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER COL5                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER COL6                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER COL7                                    
%  STORE FORMAT /(A4)/ LENGTH 4 INTEGER COL8                                    
%  INSERT "'"                                                                   
%  OUTPUT                                                                       
%%
%%
%  CLEAR                                                                        
%  INSERT '^^CO SET CELLZ TEXT '                                                
%  STORE FORMAT /(F9.5)/ LENGTH 9 REAL CELLZ                                    
%  OUTPUT                                                                       
%  CLEAR                                                                        
%  INSERT '^^CO SET PRESS TEXT '                                                
%  STORE FORMAT /(F10.1)/ LENGTH 10 REAL PRESS                                  
%  OUTPUT                                                                       
%  CLEAR                                                                        
%  LOOP                                                                         
%    VERIFY CRYSTAL BXX BHELP BOK           
%    GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                             
%    EVALUATE SWITCH = VALUE                                                  
%    CASE SWITCH                                                                
%%
%      BLOCK %CRYSTAL%                                                          
%        VERIFY MIN MED MAX KELVIN COLOUR SHAPE -                      
                CELLZ PRESS                                   
%        GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '                           
%        CASE VALUE                                                             
%          EVALUATE FMIN    = TRUE                                              
%          EVALUATE FMED    = TRUE                                              
%          EVALUATE FMAX    = TRUE                                              
%          EVALUATE FTEMP   = TRUE                                              
%          EVALUATE FCOLOUR = TRUE                                              
%          EVALUATE FSHAPE  = TRUE                                              
%          EVALUATE FCELLZ  = TRUE                                              
%          EVALUATE FPRESS  = TRUE                                              
%        END CASE                                                               
%        GET NOSTORE SILENT TEXT ' ' ' '                                        
%      END BLOCK                                                                
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%      BLOCK %BXX%                                                              
                                                                                
 * List 30 has not been changed.                                                
                                                                                
^^CO         DISPOSE NEW30                                                       
%            EVALUATE LOOPEND = LOOPCOUNTER                                     
%      END BLOCK                                                                
%      BLOCK %BHELP%                                                            
%            COPY '#SCRIPT XHELP1'                                              
 Dimensions Min Med Max (mm)                                                    
    The crystal dimensions, in mm.                                              
 Temperature                                                                    
    Data collection temperature, Kelvin.                                        
 Colour                                                                         
    The crystal colour.                                                         
 Shape                                                                          
    The crystal shape.                                                          
 Z                                                                              
    Number of molecular entities in the unit cell.                              
%            COPY '#SCRIPT XHELP2'                                              
%      END BLOCK                                                                
%      BLOCK %BOK%                                                              
                                                                                
 * List 30 is about to change.                                                  
                                                                                
%            COPY '#GENERALEDIT 30'                                             
%%                                                                              
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%        
%            COPY 'LOCATE RECORDTYPE = 102'                                     
%%                                                                              
%            IF FMIN .OR. LNEW30 THEN                                           
 * Updating minimum crystal size.                                               
^^?? MIN TEXT                                                                   
%              GET NOSTORE SILENT FINAL REAL ' ' ' '                            
%              EVALUATE MIN = VALUE                                             
%              COPY 'TRANSFER TO OFFSET = 0 FROM MIN'                           
%            END IF                                                             
%            IF FMED .OR. LNEW30 THEN                                           
 * Updating medium crystal size.                                                
^^?? MED TEXT                                                                   
%              GET NOSTORE SILENT FINAL REAL ' ' ' '                            
%              EVALUATE MED = VALUE                                             
%              COPY 'TRANSFER TO OFFSET = 1 FROM MED'                           
%            END IF                                                             
%            IF FMAX .OR. LNEW30 THEN                                           
 * Updating maximum crystal size.                                               
^^?? MAX TEXT                                                                   
%              GET NOSTORE SILENT FINAL REAL ' ' ' '                            
%              EVALUATE MAX = VALUE                                             
%              COPY 'TRANSFER TO OFFSET = 2 FROM MAX'                           
%            END IF                                                             
%            IF FTEMP .OR. LNEW30 THEN                                          
 * Updating temperature.                                                        
^^?? KELVIN TEXT                                                                  
%              GET NOSTORE SILENT FINAL REAL ' ' ' '                            
%              EVALUATE KELVIN = VALUE                                            
%              COPY 'TRANSFER TO OFFSET = 6 FROM KELVIN'                          
%            END IF                                                             
%            COPY 'LOCATE RECORDTYPE = 106'                                     
%%                                                                              
%            IF FCELLZ .OR. LNEW30 THEN                                         
 * Updating Z.                                                                  
^^?? CELLZ TEXT                                                                 
%              GET NOSTORE SILENT FINAL REAL ' ' ' '                            
%              EVALUATE CELLZ = VALUE                                           
%              COPY 'TRANSFER TO OFFSET = 5 FROM CELLZ'                         
%            END IF                                                             
%%                                                                              
%            COPY 'LOCATE RECORDTYPE = 107'                                     
%%                                                                              
%            IF FCOLOUR .OR. LNEW30 THEN                                        
 * Updating crystal colour.                                                     
%              VARIABLE CHARACTER CTEM                                          
^^?? COLOUR TEXT                                                                
%              GET NOSTORE SILENT FINAL TEXT ' ' ' '                            
%              EVALUATE CTEM = CVALUE                                           
%              COPY 'TRANSFER TO LENGTH = 8 OFFSET = 0 FROM CTEM'               
%            END IF                                                             
%%                                                                              
%            COPY 'LOCATE RECORDTYPE = 108'                                     
%%                                                                              
%            IF FSHAPE .OR. LNEW30 THEN                                         
 * Updating crystal shape.                                                      
%              VARIABLE CHARACTER CTEM                                          
^^?? SHAPE TEXT                                                                 
%              GET NOSTORE SILENT FINAL TEXT ' ' ' '                            
%              EVALUATE CTEM = CVALUE                                           
%              COPY 'TRANSFER TO LENGTH = 8 OFFSET = 0 FROM CTEM'               
%            END IF                                                             
%%                                                                              
%            COPY 'LOCATE RECORDTYPE = 109'                                     
%%                                                                              
%            IF FPRESS .OR. LNEW30 THEN                                         
 * Updating Pressure.                                                           
^^?? PRESS TEXT                                                                 
%              GET NOSTORE SILENT FINAL REAL ' ' ' '                            
%              EVALUATE PRESS = VALUE                                           
%              COPY 'TRANSFER TO OFFSET = 12 FROM PRESS'                        
%            END IF                                                             
%%                                                                              
%%                                                                              
%            COPY 'WRITE OVERWRITE=OVERWRITE'                                   
%            COPY 'END'                                                         
%            EVALUATE LOOPEND = LOOPCOUNTER                                     
^^CO         DISPOSE NEW30                                                       
%      END BLOCK                                                                
%    END CASE                                                                   
%  END LOOP                                                                     
%END SCRIPT                                                                     
%%
%%  $Log: not supported by cvs2svn $
%%
