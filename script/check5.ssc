%SCRIPT CHECK5
%% A SCRIPT TO CHECK THAT LIST 5 IS IN THE CORRECT ORDER
%% AND FIND LAST NON-H ATOM

 * Checking the state of the atomic parameters list (5).
%  VARIABLE LOGICAL EXTINCT DOEXTINC
%  VARIABLE INTEGER ADP
%  EVALUATE EXTINCT = FALSE
%  VARIABLE CHARACTER CLAST CPARAM CPARAM2
%%
%  VARIABLE INTEGER ISER  
%  VARIABLE REAL SERIAL U11 FLAG UONE
%  VARIABLE CHARACTER CTYPE CISER
%% ADP = -1 INITIALISE
%%        0 NOT REFINED
%%        1 ISO
%%        2 ANISO
%  EVALUATE ADP = - 1
%       EVALUATE CLAST = " LAST"
%%
%  IF EXISTS 5 .LE. 0 THEN
WARNING - You have no atoms to refine
%    FINISH
%  END IF
%%
%% check if H atoms mixed with others
% TRANSFER "#GENERALEDIT 5" TO CRYSTALS
% TRANSFER "LOCATE RECORDTYPE=101" TO CRYSTALS
% TRANSFER "ERROR MESSAGE=NO SIGNAL=NONE ACTION=CONTING NAME=EDITERROR" -
  TO CRYSTALS
% TRANSFER "TOP" TO CRYSTALS
%% get serial and type
% EVALUATE ISER = - 1
% LOOP
%   ON EDITERROR TERMINATE
%   TRANSFER "NEXT" TO CRYSTALS
%%  get element type
%   TRANSFER "TRANSFER FROM OFFSET=0 TO CTYPE" TO CRYSTALS
%   IF ( ctype  .EQ. "H" ) THEN
%%        indicate H found
%         EVALUATE ISER = 1
%   ELSE
%         IF ISER .GE. 1 THEN
%%          LIST OUT OF ORDER
%           EVALUATE ISER = 0
%           EVALUATE LOOPEND = LOOPCOUNTER
%          END IF
%   END IF
% END LOOP
% TRANSFER "END" TO CRYSTALS
% IF ISER .EQ. 0 THEN
  CRYSTALS is sorting the atoms to put Hydrogens last
%  COPY '#EDIT'
%  COPY 'MONITOR OFF'
%  COPY 'INSERT ELECTRON'
%  COPY 'DSORT SPARE'
%  COPY 'MONITOR MEDIUM'
%  COPY 'END'
% END IF
%%
%%
%% Find the First hydrogen atoms
%     TRANSFER "#GENERALEDIT 5" TO CRYSTALS
%     TRANSFER "LOCATE RECORDTYPE=102" TO CRYSTALS
%     TRANSFER "TOP" TO CRYSTALS
%% Note - we will reuse UONE later
%     TRANSFER "TRANSFER FROM OFFSET=5 TO UONE" TO CRYSTALS
%     IF UONE .GE. 0.001 THEN
%       EVALUATE DOEXTINC = TRUE
%     ELSE
%       EVALUATE DOEXTINC = FALSE
%     END IF
%     TRANSFER "LOCATE RECORDTYPE=101" TO CRYSTALS
%     TRANSFER "ERROR MESSAGE=NO SIGNAL=NONE ACTION=CONTING NAME=EDITERROR" -
     TO CRYSTALS
%     TRANSFER "TOP" TO CRYSTALS
%% get serial and type
%     EVALUATE ISER = - 1
%     TRANSFER "TRANSFER FROM OFFSET=7 TO UONE" TO CRYSTALS
%     LOOP
%       ON EDITERROR TERMINATE
%       TRANSFER "NEXT" TO CRYSTALS
%%    get element type
%         TRANSFER "TRANSFER FROM OFFSET=0 TO CTYPE" TO CRYSTALS
%         TRANSFER "TRANSFER FROM OFFSET=3 TO FLAG" TO CRYSTALS
%         TRANSFER "TRANSFER FROM OFFSET=7 TO U11" TO CRYSTALS
%       IF ( ctype  .EQ. "H" ) THEN
%%        find previous atom
%         TRANSFER "PREVIOUS" TO CRYSTALS
%         TRANSFER "TRANSFER FROM OFFSET=0 TO CTYPE" TO CRYSTALS
%         TRANSFER "TRANSFER FROM OFFSET=1 TO SERIAL" TO CRYSTALS
%% djwjan99
%         EVALUATE ISER = INTEGER ( SERIAL )
%         EVALUATE CISER = CHARACTER ISER
%         CLEAR
%         EVALUATE CLAST = CTYPE // "(" // CISER // ") " 
%         QUEUE SEND
%         EVALUATE LOOPEND = LOOPCOUNTER
%       ELSE
%         IF  FLAG .LE. 0.0 THEN
%           EVALUATE ADP = 2
%         ELSE 
%           IF ADP .LT. 2 .AND. UONE .NE. U11 THEN
%            EVALUATE ADP = 1
%           ELSE IF ADP .LT. 1 THEN
%            EVALUATE ADP = 0
%           END IF
%         END IF
%       END IF
%     END LOOP
%     TRANSFER "END" TO CRYSTALS
%     IF ( ISER .LE. 0 ) THEN 
%%      no Hydrogen found
%       EVALUATE CLAST = " LAST"
%     END IF
 * Done.
%END SCRIPT
