%SCRIPT ZRETYPE
%VARIABLE INTEGER STARTFROM
%VARIABLE LOGICAL CALLED
%VARIABLE CHARACTER CATOM CTYPE
% IF CALLED .EQ. FALSE THEN
%     GET SILENT NOSTORE TEXT 'Atom?' ' '
%     EVALUATE CATOM = CVALUE
%     GET SILENT NOSTORE TEXT 'New type?' ' '
%     EVALUATE CTYPE = CVALUE
% END IF
%%
%% Need to find a free number for this new atom type
%%
%  VARIABLE INTEGER N
%  VARIABLE REAL SERIAL RN
%  VARIABLE CHARACTER CLTYPE
%  VARIABLE LOGICAL TRYAGAIN
%  EVALUATE TRYAGAIN = FALSE
%  COPY '#GENERALEDIT 5'
%  COPY 'LOCATE RECORDTYPE=101'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  LOOP
%  	EVALUATE N = LOOPCOUNTER + STARTFROM
%       EVALUATE RN = REAL N
%	COPY 'TOP'
%	LOOP
%		ON EDITERROR TERMINATE
%  	        COPY 'TRANSFER FROM OFFSET=0 TO CLTYPE'
%               IF CLTYPE .EQ. CTYPE THEN
%	  	        COPY 'TRANSFER FROM OFFSET=1 TO SERIAL'
%			IF RN .EQ. SERIAL THEN
%				EVALUATE TRYAGAIN = TRUE
%				EVALUATE LOOPEND = LOOPCOUNTER
%			END IF
%		END IF
%		COPY 'NEXT'
%	END LOOP
%       IF TRYAGAIN .EQ. FALSE THEN
%		EVALUATE LOOPEND = LOOPCOUNTER
%	END IF
%       EVALUATE TRYAGAIN = FALSE
%  END LOOP
%  COPY 'END'

%% N now holds the new serial for our atom so lets rename it
%     EVALUATE STARTFROM = N
%     IF ( CALLED .EQ. FALSE ) THEN
%       COPY '#EDIT'
%     END IF
%     CLEAR
%     INSERT 'RENAME '
%     TRANSFER '%INSERT "' // CATOM // ' ' // CTYPE // '"' TO SCRIPT
%     INSERT '('
%     STORE FORMAT /(I3)/ LENGTH 3 INTEGER N
%     INSERT ')'
%     IF ( CALLED ) THEN
%       QUEUE SEND
%     ELSE
%       SEND
%       COPY 'END'
%     END IF
%END SCRIPT
