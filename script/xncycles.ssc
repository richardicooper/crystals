%SCRIPT XNCYCLES                                                                
%%                                                                              
%  VARIABLE REAL SHIFT                                                                             
%  EVALUATE SHIFT = 1.0
%%                                                                              
%  VARIABLE INTEGER NCYCLE                                                      
%%                                                                              
%  VARIABLE REAL RMIN R OSCALE                                                  
%%                                                                              
%%                                                                              
^^WI WINDOW XREFINE 'Least Squares Refinement' MODAL                            
^^WI COMMIT='RF_BOK' CANCEL='RF_BXX'                                            
^^WI GRID MAIN NROWS=4 NCOLS=7                                                  
^^WI {                                                                          
^^WI  @ 2,2 GRID L NROWS=5 NCOLS=1                                              
^^WI  {                                                                         
^^WI   @ 1,1 GRID NCYC NROWS=3 NCOLS=3                                          
^^WI   {                                                                        
^^WI    @ 1,1 STATIC T1 'Maximum number of cycles of refinement'                
^^WI    @ 1,3 EDITBOX NC ' 5' CHARS=5 INTEGER 

^^WI    @ 3,1 STATIC T1 'Partial Shift Factor'                
^^WI    @ 3,3 EDITBOX NS                                                        
%        TRANSFER "^^WI '  " // CHARACTER ( SHIFT )  // "'" TO DISPLAY         
^^WI     CHARS=5 REAL
                                  
^^WI   }                                                                        
^^WI   @ 3,1 CHECKBOX ND                                                        
^^WI         'Minimise disk useage' STATE=ON                                    
^^WI   @ 4,1 CHECKBOX NF                                                        
^^WI         'Stop cycling if convergence is detected' STATE=OFF                
^^WI  }                                                                         
^^WI  @ 2,6 GRID R NROWS=3 NCOLS=1                                              
^^WI  {                                                                         
^^WI   @ 1,1 BUTTON RF_BOK '&OK' DEFAULT                                        
^^WI   @ 3,1 BUTTON RF_BXX '&Cancel'                                            
^^WI  }                                                                         
^^WI }                                                                          
^^WI SHOW                                                                       
^^CR                                                                            
%%                                                                              
%%                                                                              
%%                                                                              
% LOOP                                                                          
%  ON ERROR TERMINATE                                                           
%  VERIFY RF_BXX RF_BOK                                                         
%  GET NOSTORE SILENT FINAL ABBREVIATED ' ' ' '                                 
%  CASE VALUE                                                                   
%   BLOCK                                                                       
^^WI  DISPOSE XREFINE                                                           
^^CR                                                                            
%     FINISH                                                                    
%   END BLOCK                                                                   
%   BLOCK                                                                       
^^??   ND STATE                                                                 
%      VERIFY ON OFF                                                            
%      GET NOSTORE SILENT FINAL ABBREVIATED 'ONorOFF' ' '                       
^^CR                                                                            
%      IF VALUE .EQ. 1 THEN                                                     
%       COPY '#SUM LIST 12'                                                     
%       COPY 'END'                                                              
%       IF EXISTS 11 .EQ. 1 THEN                                                
%         COPY '#DISK'                                                          
%         COPY 'DELETE 11'                                                      
%         COPY 'END'                                                            
%       END IF                                                                  
%% Purge everything until bug is fixed.                                         
%       COPY '#PURGE LIST=11 MON=LOW'                                           
%%      COPY '#PURGE '                                                          
%       COPY 'END'                                                              
%       COPY '#LIST 22'                                                         
%       COPY 'END'                                                              
%      END IF                                                                   
%%                                                                              
%     COPY '#SCRIPT RFACTOR'                                                    
%     EVALUATE RMIN = R                                                         
% if ( oscale - 1.000 .ge. - .0001 ) .and. ( oscale - 1.000 .le. .0001 ) then   
%      copy '#sfls'                                                             
%      copy 'scale'                                                             
%      copy 'end'                                                               
%      end if                                                                   
                                                                                
%     COPY '#SCRIPT XDELQ'                                                      


^^?? NS TEXT                                                                    
%    GET NOSTORE SILENT FINAL REAL 'Partial Shift Factor' '1.0'                     
%    EVALUATE SHIFT = VALUE                                                    


^^??  NC TEXT                                                                   
%     GET NOSTORE SILENT FINAL INTEGER 'How many cycles' '5'                    
%     EVALUATE NCYCLE = VALUE                                                   
%     IF NCYCLE .GT.  0 THEN                                                    
^^??   NF STATE                                                                 
%      VERIFY ON OFF                                                            
%      GET NOSTORE SILENT FINAL ABBREVIATED 'ONorOFF' ' '                       
^^WI  DISPOSE XREFINE                                                           
^^CR                                                                            
%      CASE VALUE                                                               
%       BLOCK                                        % NORMAL                   
%%                                                                              
%% Cope with people who insist on more than 5 lsq cycles in one go.             
%% Do them in blocks of 5. Even if convergence is detected, we'll               
%% still have to do one from every block of 5.                                  
%%                                                                              
%         IF NCYCLE .GT. 5 THEN                                                 
%           LOOP                                                                
%             COPY '#SFLS'                                                      
%             LOOP 5 TIMES                                                      
%               COPY 'REFINE'                                                   
%              IF SHIFT .LT. 1.0 THEN
%                clear                                                                      
%                insert 'SHIFT GENERAL ='                                                            
%                store real  shift                                                       
%                send                                                                       
%              END IF
%               EVALUATE NCYCLE = NCYCLE - 1                                    
%               IF ( NCYCLE .LE. 0 ) THEN                                       
%                 EVALUATE LOOPEND = LOOPCOUNTER                                
%               END IF                                                          
%             END LOOP                                                          
%             COPY 'END'                                                        
%             IF ( NCYCLE .LE. 0 ) THEN                                         
%               EVALUATE LOOPEND = LOOPCOUNTER                                  
%             END IF                                                            
%           END LOOP                                                            
%         ELSE                                                                  
%%                                                                              
%% Normal less than or eq 5 lsq cycles.                                         
%%                                                                              
%           COPY '#SFLS'                                                        
%           LOOP NCYCLE TIMES                                                   
%             COPY 'REFINE'                                                     
%              IF SHIFT .LT. 1.0 THEN
%                clear                                                                      
%                insert 'SHIFT GENERAL ='                                                            
%                store real  shift                                                       
%                send                                                                       
%              END IF
%           END LOOP                                                            
%           COPY 'END'                                                          
%         END IF                                                                
%       END BLOCK                                                               
%       BLOCK                                        % FORCE                    
%%                                                                              
%% n forced cycles.                                                             
%%                                                                              
%         LOOP NCYCLE TIMES                                                     
%           COPY '#SFLS'                                                        
%           COPY 'REFINE'                                                       
%              IF SHIFT .LT. 1.0 THEN
%                clear                                                                      
%                insert 'SHIFT GENERAL ='                                                            
%                store real  shift                                                       
%                send                                                                       
%              END IF
%           COPY 'END'                                                          
%         END LOOP                                                              
%       END BLOCK                                                               
%      END CASE                                                                 
%     END IF                                                                    
%% Unless this line is here, error contingency doesn't work - 28-03-01          
%     COPY '#SCRIPT RFACTOR'                                                    
{S * Test for near convergence of the R factor...                               
%     CLEAR                                                                     
%     INSERT ' {R   Old R factor = '                                            
%     STORE FORMAT /(F7.2)/ LENGTH 7 REAL RMIN                                  
%     INSERT '%. New R factor = '                                               
%     STORE FORMAT /(F7.2)/ LENGTH 7 REAL R                                     
%     OUTPUT                                                                    
%     CLEAR                                                                     
%%                                                                              
%     IF R .GE. RMIN * 0.99 THEN                                                
{S * The refinement has almost converged.                                       
%     END IF                                                                    
%%                                                                              
%     EVALUATE RMIN = R                                                         
%     FINISH                                                                    
%   END BLOCK                                                                   
%  END CASE                                                                     
% END LOOP                                                                      
%END SCRIPT                                                                     
                                                                                
