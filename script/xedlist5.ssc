%SCRIPT XEDLIST5
%%
^^WI WINDOW XEDLIST5 'Atomic co-ordinates' SIZE
^^WI GRID OUTER NROWS=3 NCOLS=5
^^WI {
^^WI   @ 2,2 MULTIEDIT TEXT5 '' NROWS=10 NCOLS=80
^^WI        IGNORE DISABLED=NO
^^WI   @ 2,4 GRID BTNS NROWS=5 NCOLS=1
^^WI   {
^^WI     @ 1,1 BUTTON BOK '&Save' DEFAULT COMMIT
^^WI     @ 3,1 BUTTON BXX '&Cancel' CANCEL
^^WI     @ 5,1 BUTTON TXT '&Save to file'
^^WI   }
^^WI }
^^WI SHOW
^^CR
^^WI SENDTEXTTO TEXT5
^^CR
%  CLEAR
#     ELEM  SERI  OCC    FLAG   X      Y      Z
#                 U11    U22    U33    U23    U13    U12
%  VARIABLE CHARACTER CELE
%  VARIABLE REAL RSER ROCC RFLG RATX RATY RATZ
%  VARIABLE REAL RU11 RU22 RU33 RU23 RU13 RU12
%  COPY '#GENERALEDIT 5'
%  COPY 'LOCATE RECORDTYPE=101'
%  COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%  LOOP
%     ON EDITERROR TERMINATE

%        COPY 'TRANSFER FROM OFFSET=0 TO CELE'
%        COPY 'TRANSFER FROM OFFSET=1 TO RSER'
%        COPY 'TRANSFER FROM OFFSET=2 TO ROCC'
%        COPY 'TRANSFER FROM OFFSET=3 TO RFLG'
%        COPY 'TRANSFER FROM OFFSET=4 TO RATX'
%        COPY 'TRANSFER FROM OFFSET=5 TO RATY'
%        COPY 'TRANSFER FROM OFFSET=6 TO RATZ'
%        COPY 'TRANSFER FROM OFFSET=7 TO RU11'
%        COPY 'TRANSFER FROM OFFSET=8 TO RU22'
%        COPY 'TRANSFER FROM OFFSET=9 TO RU33'
%        COPY 'TRANSFER FROM OFFSET=10 TO RU23'
%        COPY 'TRANSFER FROM OFFSET=11 TO RU13'
%        COPY 'TRANSFER FROM OFFSET=12 TO RU12'
%        CLEAR
%        INSERT 'ATOM '
%        TRANSFER CELE TO BUFFER
%        STORE FORMAT /(F5.0)/ LENGTH 7 REAL RSER
%        STORE FORMAT /(F5.3)/ LENGTH 7 REAL ROCC
%        STORE FORMAT /(F2.0)/ LENGTH 6 REAL RFLG
%        STORE FORMAT /(F6.3)/ LENGTH 7 REAL RATX
%        STORE FORMAT /(F6.3)/ LENGTH 7 REAL RATY
%        STORE FORMAT /(F6.3)/ LENGTH 7 REAL RATZ
%        OUTPUT
%        CLEAR
%        INSERT 'CONT        U[11]='
%        STORE FORMAT /(F6.3)/ LENGTH 7 REAL RU11
%        STORE FORMAT /(F6.3)/ LENGTH 7 REAL RU22
%        STORE FORMAT /(F6.3)/ LENGTH 7 REAL RU33
%        STORE FORMAT /(F6.3)/ LENGTH 7 REAL RU23
%        STORE FORMAT /(F6.3)/ LENGTH 7 REAL RU13
%        STORE FORMAT /(F6.3)/ LENGTH 7 REAL RU12
%        OUTPUT
%        COPY 'NEXT'
%  END LOOP
%  COPY 'END'
^^CO SENDTEXTTO _MAINTEXTOUTPUT
% LOOP
%   VERIFY BOK BXX TXT
%   GET SILENT NOSTORE FINAL ABBREVIATED 'BOK?' ' '
%   IF VALUE .EQ. 1 THEN
%% Somehow we have to get that text from the multiedit...
%%
%     IF EXISTS ( 5 ) .EQ. 1 THEN
%      COPY '#RELEASE PUNCH EDATOMS.BAK'
%      COPY '#PUNCH 5'
%      COPY 'END'
Current atom list backed up as EDATOMS.BAK
%      COPY '#LIST 5'
%      COPY 'READ NATOM=0'
%      COPY 'END'
Atoms deleted.
%     END IF
%     COPY '#EDIT'
%     COPY 'GUI LOW'
^^CO SENDTEXTTO TEXT5
EOFXXX
^^CO SENDTEXTTO _MAINTEXTOUTPUT
^^CO SET TEXT5 SPEW
%     LOOP
%     ON ERROR TERMINATE
%       GET SILENT NOSTORE TEXT 'Line' ' '
%       IF CVALUE STARTSWITH 'EOFXXX' THEN
%         EVALUATE LOOPEND = LOOPCOUNTER
%       ELSE
%         TRANSFER CVALUE TO CRYSTALS
%       END IF
%     END LOOP
%     COPY 'END'
^^CO  DISPOSE XEDLIST5
%     FINISH
%   ELSE IF ( VALUE .EQ. 2 ) THEN
^^CO  DISPOSE XEDLIST5
%     FINISH
%   END IF
^^CO SET TEXT5 EDITSAVE 'text5test.txt'
% END LOOP
%END SCRIPT
