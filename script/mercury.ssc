%SCRIPT MERCURY
% VARIABLE CHARACTER FMERC
%
%%
%% Check registry for location of Mercury 1.2.1
%%
^^CO GETREG "Software\CCDC\Mercury\1.2.1\" "InstallDir"
% GET SILENT NOSTORE TEXT 'Latest mogul?' ' '
% EVALUATE FMERC = CVALUE // 'mercury.exe'

% TRANSFER FMERC TO DISPLAY

% IF ( .NOT. FILEEXISTS FMERC ) THEN
%%
^^WI WINDOW MERCMISS 'Mercury missing' MODAL KEEP SIZE LARGE
^^WI COMMIT='MM_OK' CANCEL='MM_OK' POSITION=CENTRED '_MAINTEXTOUTPUT'
^^WI GRID MMM NROWS=5 NCOLS=3
^^WI {
^^WI   @ 2,2 GRID TOP NROWS=1 NCOLS=3
^^WI   {
^^WI     @ 1,1 GRID LOG NROWS=1 NCOLS=1
^^WI     {
^^WI       @ 1,1 BITMAP LOGO TRANS FILE 'ccdclogo.bmp'
^^WI     }
^^WI     @ 1,3 GRID HOLD NROWS=1 NCOLS=1
^^WI     {
^^WI       @ 1,1 TEXTOUT MMEXPLAIN 'Could not find the Mercury executable.'
^^WI NROWS=10 NCOLS=40 TRANSPARENT
^^WI TEXT ' '
^^WI TEXT ' Is it installed?'
^^WI TEXT ' '
^^WI TEXT ' Mercury is a free program from CCDC for easy crystal structure'
^^WI TEXT ' visualisation and exploration.'
^^WI TEXT ' It can be downloaded from: '
^^WI TEXT '  {&http://www.ccdc.cam.ac.uk/products/csd_system/mercury/{&'
^^WI TEXT ' '
^^WI TEXT ' Close this dialog, download and install Mercury then try again.'
^^WI TEXT ' '
^^WI VIEWTOP
^^WI     }
^^WI   }
^^WI   @ 4,2 GRID BOTTOM NROWS=1 NCOLS=3
^^WI   {
^^WI     @ 1,1 STRETCH S1 HORIZONTAL
^^WI     @ 1,2 BUTTON MM_OK 'OK' COMMIT CANCEL DEFAULT LENGTH='Cancel'
^^WI     @ 1,3 STRETCH S2 HORIZONTAL
^^WI   }
^^WI }
^^WI SHOW
^^CR
^^CO SET MMEXPLAIN VIEWTOP
%   LOOP
%     VERIFY #$ MM_OK
%     GET SILENT NOSTORE ABBREVIATED ' ' ' '
%     CASE VALUE
%       BLOCK  %#$command%
%         CLEAR
%         INSERT '#SPAWN '
%         GET SILENT FINAL TEXT ' ' ' '
%         OUTPUT
%         SEND
%       END BLOCK
%       BLOCK                                %XX
^^CO      DISPOSE XMOGULGO
%         FINISH
%       END BLOCK
%     END CASE
%   END LOOP
% END IF
%%
%%
%% Check for reflections. Make dummy list 6 if none found.
%%
%%
% IF ( EXISTS 6 .NE. 1 ) THEN
{I Creating a dummy list 6 (reflections) to keep the CIF routines happy.
%  COPY '#LIST 6'
%  COPY 'READ NCOEFFICIENT = 9, TYPE = FIXED, UNIT = DATAFILE, CHECK=NO'
%  COPY 'INPUT H K L /FO/ SIGMA(/FO/) /FC/ PHASE SQRTW CORRECT'
%  COPY 'FORMAT (3F4.0, F10.2, F8.2, F10.2, F8.4, G12.5, F10.5)'
%  COPY 'END'
%  COPY '-512'
% END IF
% IF ( EXISTS 23 .NE. 1 ) THEN
{I Creating dummy list 23 (refinement control) to keep the CIF routines happy.
%  COPY '#LIST 23'
%  COPY 'END'
% END IF
% IF ( EXISTS 31 .NE. 1 ) THEN
{I Creating dummy list 31 (cell esds) to keep the CIF routines happy.
%  COPY '#LIST 31'
%  COPY 'END'
% END IF
%%
%%
%% CIF listing of all data
%%
%%
{I Creating a CIF
% COPY "#STORE CSYS CIF 'platon.cif'"
% COPY '#APPEND PUNCH '
% COPY '#CIF '
% COPY 'END'
% COPY '#PARAMETERS'
% COPY 'LAYOUT INSET = 1 NCHAR = 120 ESD=NO'
% COPY 'COORD SELECT=ALL MONITOR=LOW PRINT=YES PUNCH=CIF NCHAR=14'
% COPY "U'S MONITOR=OFF, PRINT=NO, PUNCH=NO, NCHAR=14"
% COPY 'END'
% COPY '#DIST'
% COPY 'SELECT RANGE=L41'
% COPY 'OUTPUT MON=DIST PUNCH=CIF'
% COPY 'END'
%%
%%
% TRANSFER '#SPAWN "' // FMERC // '" platon.cif' TO DISPLAY
% TRANSFER '#SPAWN "' // FMERC // '" platon.cif' TO CRYSTALS
%%
%END SCRIPT
