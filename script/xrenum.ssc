%SCRIPT XRENUM
 * Manual renumbering of structure using mouse.
^^WI WINDOW XRENUM 'Renumber' MODAL LARGE KEEP SIZE COMMIT='BOK' CANCEL='BXX'
^^WI POSITION=CENTRED 'MODEL01'
^^WI GRID MAIN NROWS=7 NCOLS=3
^^WI {
^^WI   @ 2,2 GRID TEXT NROWS=3 NCOLS=1
^^WI   {
^^WI     @ 1,1 STATIC T1
^^WI 'Atoms are numbered sequentially in the order that you click on them.'
^^WI   }
^^WI   @ 4,2 GRID TEXT NROWS=1 NCOLS=3
^^WI   {
^^WI      @ 1,1 GRID OPTIONS NROWS=4 NCOLS=1
^^WI      {
^^WI         @ 1,1 STATIC _TFS 'Next serial'
%% Put Editbox into a grid to stop it stretching.
^^WI         @ 2,1 GRID NOS NROWS=1 NCOLS=1 ISOLATE
^^WI         {
^^WI           @ 1,1 EDITBOX NFS '1' CHARS=6 INTEGER
^^WI         }
^^WI         @ 3,1 STRETCH _V VERTICAL
^^WI         @ 4,1 GRID OPTNS NROWS=3 NCOLS=1 OUTLINE='Remaining atoms'
^^WI         {
^^WI           @ 1,1 RADIOBUTTON RMND 'Delete' STATE=OFF
^^WI           @ 2,1 RADIOBUTTON RMNN 'Number sequentially' STATE=OFF
^^WI           @ 3,1 RADIOBUTTON RMNS 'Leave alone (except clashes)'
^^WI                                 STATE=ON
^^WI         }
^^WI      }
^^WI      @ 1,3 MODELWINDOW MODELRE 'N' NROWS=5 NCOLS=65
^^WI            ATTACH L5 RADSCALE=250 RADTYPE=COV
^^WI            MOUSEACTION=HEADERATOM
^^WI   }
^^WI   @ 6,2 GRID BTNS NROWS=1 NCOLS=7
^^WI   {
^^WI     @ 1,1 BUTTON BOK 'D&one' DEFAULT COMMIT
^^WI     @ 1,3 BUTTON BXX '&Cancel' CANCEL
^^WI     @ 1,5 BUTTON BHELP '&Help' 
^^WI     @ 1,7 STATIC T2
^^WI '"Done" to apply the new numbering or "Cancel" to quit without changes.' 
^^WI   }
^^WI }
^^WI SHOW
^^CR
%%
%% Remember this list serial number in case of a "Cancel"
%%
%  VARIABLE INTEGER OLDSER N5
%  COPY '#GENERALEDIT 5'
%  COPY 'GETSERIAL OLDSER'
%  COPY 'TRANSHEAD FROM OFFSET=3 TO N5'
%  COPY 'END'
%%
%%
%  VARIABLE INTEGER ICOUNTER INEXTS ISER
%  VARIABLE REAL RLAST RFIRST
%  VARIABLE CHARACTER CATOM CLAST CFIRST
%  EVALUATE ICOUNTER = 0
%  EVALUATE INEXTS   = - 1
%  COPY '#EDIT'
%  CLEAR
%  INSERT 'MOVE '
%  LOOP
%   ON ERROR TERMINATE
%   VERIFY BOK BXX ATOM BHELP
%   GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%   CASE VALUE
%    BLOCK      %BOK
 * Applying new numbering order
%%
%%     Add 10N5 to all the moved atoms. Need to find the last moved atom.
%%     Possibly do a keep on the rest. Need first non-moved atom.
%%
%      IF ( ICOUNTER .LT. N5 ) THEN
%        COPY 'END'
%%       Need to do something with remaining atoms.
         Need to do something with remaining atoms.
^^??     RMNS STATE
%        VERIFY OFF ON
%        GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%        IF ( VALUE .EQ. 1 ) THEN
^^??       RMND STATE
%          VERIFY ON OFF
%          GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%          COPY '#EDIT'
%          TRANSFER 'ADD ' // CHARACTER ( 10 * N5 ) // -
                    ' ALL(SERIAL)' TO CRYSTALS
%          COPY 'END'
%%
%%         Need to find first non-mover
%          COPY '#GENERALEDIT 5'
%          COPY 'LOCATE RECORDTYPE=101'
%          COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%          LOOP ICOUNTER TIMES
%            ON EDITERROR TERMINATE
%            COPY 'NEXT'
%          END LOOP
%          COPY 'TRANSFER FROM OFFSET=0 TO CFIRST'
%          COPY 'TRANSFER FROM OFFSET=1 TO RFIRST'
%          COPY 'END'
%%
%          IF ( INEXTS .LT. 1 ) THEN
%             EVALUATE INEXTS = 1
%          END IF
%%
%          IF ( VALUE .EQ. 1 ) THEN
%            COPY '#EDIT'
%            TRANSFER 'DELETE ' // CFIRST // '(' // -
                      CHARACTER INTEGER RFIRST // ') UNTIL LAST' TO CRYSTALS
%            COPY 'END'
%          ELSE
%            COPY '#EDIT'
%            TRANSFER 'AFTER ' // CFIRST // '(' // -
                      CHARACTER INTEGER RFIRST // ')' TO CRYSTALS
%            TRANSFER 'MOVE ' // CHARACTER INEXTS // ' ' // CFIRST // '(' // -
                      CHARACTER INTEGER RFIRST // ') UNTIL LAST' TO CRYSTALS
%            COPY 'END'
%          END IF
%        ELSE
%%
%%         Need to find last mover
%          COPY '#GENERALEDIT 5'
%          COPY 'LOCATE RECORDTYPE=101'
%          COPY 'ERROR MESS=NO SIGN=NONE ACTI=CONT NAME=EDITERROR'
%          LOOP ( ICOUNTER - 1 ) TIMES
%            ON EDITERROR TERMINATE
%            COPY 'NEXT'
%          END LOOP
%          COPY 'TRANSFER FROM OFFSET=0 TO CLAST'
%          COPY 'TRANSFER FROM OFFSET=1 TO RLAST'
%          COPY 'END'
%%
%          COPY '#EDIT'
%          TRANSFER 'ADD ' // CHARACTER ( 10 * N5 ) // -
                    ' FIRST(SERIAL) UNTIL ' // CLAST // -
                    '(' // CHARACTER INTEGER RLAST // ')' TO CRYSTALS
%          COPY 'END'
%        END IF
%        COPY '#EDIT'
%        COPY 'MONITOR OFF'
%        COPY 'CLASH FIXLATTER'
%        COPY 'END'
%      ELSE
%%       All atoms clicked. Just add 10*N5.
%        CLEAR
%        INSERT 'ADD '
%        STORE INTEGER ( 10 * N5 )
%        INSERT ' ALL(SERIAL)'
%        SEND
%        COPY 'END'
%      END IF
^^CO   DISPOSE XRENUM
%      FINISH
%    END BLOCK
%    BLOCK      %BXX
^^CO DISPOSE XRENUM
 * Keeping old atom numbering scheme.
%      COPY 'QUIT'
%      COPY 'END'
^^CO SET MODEL01 DISABLEATOM ALL NO
%      FINISH
%    END BLOCK
%    BLOCK      %ATOM
%      GET SILENT NOSTORE TEXT ' ' ' '
^^CW
%      EVALUATE CATOM = CVALUE
^^??   NFS TEXT
%      GET SILENT NOSTORE INTEGER ' ' ' '
%      IF INEXTS .LT. VALUE THEN
%       EVALUATE INEXTS = VALUE
%       TRANSFER 'Skipping to serial ' // CHARACTER ( INEXTS ) TO DISPLAY
%       STORE INTEGER ( INEXTS - 10 * N5 )
%       INSERT ' '
%      END IF
%      STORE CHARACTER CATOM
%      SEND
%      TRANSFER '^^CO SET MODEL01 DISABLEATOM ' // CATOM // ' YES' TO DISPLAY
%      EVALUATE ICOUNTER = ICOUNTER + 1
%      EVALUATE INEXTS = INEXTS + 1
%      TRANSFER '^^CO SET NFS TEXT ' // CHARACTER ( INEXTS ) TO DISPLAY
%      CLEAR
%      INSERT 'CONTINUE '
%    END BLOCK
%    BLOCK      %HELP
%      COPY '#SCRIPT XHELP1'
  Click on the atoms in the molecule in the order
  you want them to be numbered. When you are finished
  click DONE to apply the new numbering scheme, or
  CANCEL to keep the old numbering scheme.
%      COPY '#SCRIPT XHELP2'
%    END BLOCK
%   END CASE
%  END LOOP
%END SCRIPT
