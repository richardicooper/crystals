%SCRIPT XW
% IF EXISTS 6 .NE. 1 THEN
{I  No reflections have been stored.
{I  Agreement analysis is not possible.
%   FINISH
% END IF
%%
%%
^^WI WINDOW XFOPTW 'Chebychev Weighting analysis for Fsq' SIZE KEEP MODAL
^^WI GRID BOSS NROWS=7 NCOLS=3
^^WI {
^^WI  @ 2,2 GRID EXPL NROWS=1 NCOLS=5
^^WI  {
^^WI   @ 1,1 GRID EXP2 NROWS=1 NCOLS=3 OUTLINE=''
^^WI   {
^^WI    @ 1,1 ICON IM WARN
^^WI    @ 1,3 GRID EXP2 NROWS=3 NCOLS=1
^^WI    {
^^WI     @ 1,1 STATIC XFW1 'Do not use a calculated weighting scheme'
^^WI     @ 2,1 STATIC XFW2 'unless you are certain that your model is as'
^^WI     @ 3,1 STATIC XFW3 'correct and complete as possible!'
^^WI    }
^^WI   }
^^WI   @ 1,3 GRID EXP2 NROWS=1 NCOLS=5 OUTLINE=''
^^WI   {
^^WI    @ 1,1 GRID EXP2 NROWS=3 NCOLS=1
^^WI    {
^^WI     @ 1,1 STATIC XFW1 'To choose the best scheme: The curve of'
^^WI     @ 2,1 STATIC XFW2 'estimated <|Fo-Fc|>**2 (top) should vary'
^^WI     @ 3,1 STATIC XFW3 'smoothly, and have no pronounced local maxima.'
^^WI    }
^^WI    @ 1,3 ICON IM INFO
^^WI    @ 1,5 GRID EXP3 NROWS=3 NCOLS=1
^^WI    {
^^WI     @ 1,1 STATIC XFW1 'The red bars in the lower graphs show'
^^WI     @ 2,1 STATIC XFW2 '<w|Fo-Fc|>^2 they should be as close'
^^WI     @ 3,1 STATIC XFW3 'to unity as possible.'
^^WI    }
^^WI   }
^^WI  }
^^WI  @ 4,2 GRID GRAPHS NROWS=1 NCOLS=5
^^WI  {
^^WI    @ 1,1 GRID PAR5 NROWS=5 NCOLS=1 OUTLINE='5 parameters'
^^WI    {
^^WI      @ 1,1 PLOTWINDOW _RWGHT NROWS=20 NCOLS=48
^^WI      @ 3,1 PLOTWINDOW _VFO NROWS=20 NCOLS=48
^^WI      @ 5,1 BUTTON BTPAR5 'Use 5 parameter scheme'
^^WI    }
^^WI    @ 1,3 GRID PAR4 NROWS=5 NCOLS=1 OUTLINE='4 parameters'
^^WI     {
^^WI      @ 1,1 PLOTWINDOW _RWGH2 NROWS=20 NCOLS=48
^^WI      @ 3,1 PLOTWINDOW _VF2 NROWS=20 NCOLS=48
^^WI      @ 5,1 BUTTON BTPAR4 'Use 4 parameter scheme'
^^WI     }
^^WI    @ 1,5 GRID PAR3 NROWS=5 NCOLS=1 OUTLINE='3 parameters'
^^WI    {
^^WI      @ 1,1 PLOTWINDOW _RWGH3 NROWS=20 NCOLS=48
^^WI      @ 3,1 PLOTWINDOW _VF3 NROWS=20 NCOLS=48
^^WI      @ 5,1 BUTTON BTPAR3 'Use 3 parameter scheme' COMMIT DEFAULT
^^WI    }
^^WI  }
^^WI  @ 6,2 GRID STUFF NROWS=1 NCOLS=3
^^WI  {
^^WI    @ 1,1 GRID EXP3 NROWS=1 NCOLS=5 OUTLINE=''
^^WI    {
^^WI      @ 1,1 ICON IM INFO
^^WI      @ 1,3 GRID EXP1 NROWS=3 NCOLS=1
^^WI      {
^^WI        @ 1,1 STATIC XFW1 'The 3 parameter scheme'
^^WI        @ 2,1 STATIC XFW2 'is most often satisfactory.'
^^WI      }
^^WI      @ 1,5 GRID H1 NROWS=1 NCOLS=1
^^WI      {
^^WI        @ 1,1 BUTTON XWHELP '&Help'
^^WI      }
^^WI    }
^^WI    @ 1,2 STRETCH XWS HORIZONTAL
^^WI    @ 1,3 BUTTON XWCLOSE '&Cancel (use unit weights)' CANCEL 
^^WI  }
^^WI }
^^WI SHOW
^^CR
%%
%% Calculate the six graphs:
%%
%% NB output graphs from 'list 4' is always to _RWGHT and output from
%% 'analyse' is always to _VFO, therefore change names of plotwindows
%% in between each command to get the six separate graphs.
%%
{S * Chebychev weighting with 5 parameters
{I   Estimated delta(F)**2 as a function of F:
%          COPY '#LIST 4'
%          COPY 'SCHEME NUMBER=14, NPARAM=5 type=ch wei=1'
%          COPY 'END'
%          COPY '#WEIGHT'
%          COPY 'GUI GRAPH=ON'
%          COPY 'END'
%          COPY '#ANALYSE'
%          COPY 'LIST LEVEL=HIGH'
%          COPY 'PLOT FO=ON'
{I   Agreement analysis:
%          COPY 'END'
^^CO RENAME _VFO _VF1      RENAME _VF2 _VFO
^^CO RENAME _RWGHT _RWGH1  RENAME _RWGH2 _RWGHT
{S * Chebychev weighting with 4 parameters
{I   Estimated delta(F)**2 as a function of F:
%          COPY '#LIST 4'
%          COPY 'SCHEME NUMBER=14, NPARAM=4 type=ch wei=1'
%          COPY 'END'
%          COPY '#WEIGHT'
%          COPY 'GUI GRAPH=ON'
%          COPY 'END'
%          COPY '#ANALYSE'
%          COPY 'LIST LEVEL=HIGH'
%          COPY 'PLOT FO=ON'
{I   Agreement analysis:
%          COPY 'END'
^^CO RENAME _VFO _VF2       RENAME _VF3 _VFO
^^CO RENAME _RWGHT _RWGH2   RENAME _RWGH3 _RWGHT
{S * Chebychev weighting with 3 parameters
{I   Estimated delta(F)**2 as a function of F:
%          COPY '#LIST 4'
%          COPY 'SCHEME NUMBER=14, NPARAM=3 type=ch wei=1'
%          COPY 'END'
%          COPY '#WEIGHT'
%          COPY 'GUI GRAPH=ON'
%          COPY 'END'
%          COPY '#ANALYSE'
%          COPY 'LIST LEVEL=HIGH'
%          COPY 'PLOT FO=ON'
{I   Agreement analysis:
%          COPY 'END'
%%
%%
%% Wait for the users response:
%%
%%
% LOOP
%   VERIFY XWCLOSE XWHELP BTPAR3 BTPAR4 BTPAR5
%   GET SILENT NOSTORE FINAL ABBREVIATED ' ' ' '
%%
%%
%   CASE VALUE
%     BLOCK  %CLOSE%
^^CO     DISPOSE XFOPTW
%        COPY '#LIST 4'
%        COPY 'END'
%        COPY '#WEIGHT'
%        COPY 'END'

{S * Unit weights are being used.
{I   Useful early in refinement on F - not acceptable for publication.

%        FINISH
%     END BLOCK
%     BLOCK  %HELP%
%        COPY '#SCRIPT XHELP1'

Once a model is reasonably correct (say, hydrogens placed) the values
of Fc are likely to be quite reliable. A polynomial can be fitted
through the residual of Fo-Fc to produce a weighting scheme which, if Fc
really are correct, reflects the reliability of Fo.

This analysis shows schemes using 5, 4 and 3 term polynomials, you must
decide which is best.

The top graph shows the estimated reciprocal weight against Fo. This
should roughly parallel the GREEN bars in the lower graph (an analysis
of <|Fo-Fc|> for given bins of Fo. (That is to say: where the green bars
are high == bad agreement, thus the weight for those reflections should
be low == high reciprocal weight...)

The graph should be smoothly varying and have no pronounced local maxima -
in general it can peak at the ends of the graph, but not in the middle.

The bottom graph shows the residual itself. The green bars are simply
<|Fo-Fc|> and the red bars are <w|Fo-Fc|>. The red bars represent the
residual after the weighting scheme has been applied, in other words, how
well the scheme manages to model the differences in Fo and Fc. The red
bars should be as close to unity as possible across the whole range of /Fo/.

%        COPY '#SCRIPT XHELP2'
%     END BLOCK
%     BLOCK                %BTPAR3%
^^CO     DISPOSE XFOPTW
%        COPY '#LIST 4'
%        COPY 'SCHEME NUMBER=14, NPARAM=3 type=ch wei=1'
%        COPY 'END'
%        COPY '#WEIGHT'
%        COPY 'END'

{S * Weights are based on three-term Chebychev
{S   polynomial fitted to the residual.

%        FINISH
%     END BLOCK
%     BLOCK                %BTPAR4%
^^CO     DISPOSE XFOPTW
%        COPY '#LIST 4'
%        COPY 'SCHEME NUMBER=14, NPARAM=4 type=ch wei=1'
%        COPY 'END'
%        COPY '#WEIGHT'
%        COPY 'END'

{S * Weights are based on four-term Chebychev
{S   polynomial fitted to the residual.

%        FINISH
%     END BLOCK
%     BLOCK                %BTPAR5%
^^CO     DISPOSE XFOPTW
%        COPY '#LIST 4'
%        COPY 'SCHEME NUMBER=14, NPARAM=5 type=ch wei=1'
%        COPY 'END'
%        COPY '#WEIGHT'
%        COPY 'END'

{S * Weights are based on five-term Chebychev
{S   polynomial fitted to the residual.

%        FINISH
%     END BLOCK
%   END CASE
%%
%%
% END LOOP
%END SCRIPT
