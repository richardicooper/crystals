\                        EXTEND THE DISC FILE
\DISK
EXTEND FREE = 100
\SET WATCH 5
#clear 12
end
#clear 16
end
\GOODIES
DATRED  
CONT    NREFMES      =12000
CONT    NREFMERG     =2346
CONT    RMERGE     =3.2
CONT    NREFFRIED    =4612
CONT    RMERGFRIED   =2.8

CONDITIONS 
CONT    MINSIZE      =    .12
CONT    MEDSIZE      =     .25
CONT    MAXSIZE      =    1.15
CONT    NORIENT      =     25
CONT    THORIENTMIN     =    12.3
CONT    THORIENTMAX     =    27.8
CONT    TEMPERATURE  =     297
CONT    STANDARDS    =     25
CONT    DECAY        =     1.15
CONT    SCANMODE    = 2THETA/OME 

ABSORPTION  
CONT    ANALMIN    =     .87
CONT    ANALMAX    =     1.43
CONT    THETAMIN   =     .64
CONT    THETAMAX   =     .65
CONT    EMPMIN     =     .58
CONT    EMPMAX     =     1.87
CONT    DIFABSMIN  =     .6
CONT    DIFABSMAX  =     1.456

REFINEMENT 
CONT    R           =     2.5
CONT    RW          =     2.6
CONT    NPARAM      =     276
CONT    S           =     1.001
CONT    DELRHOMIN   =     -.15
CONT    DELRHOMAX   =     .17
CONT    RMSSHIFT    =     .001
CONT    NREFUSED   =3215
CONT    FMINFUNC    =     341542
CONT    RESTMINFUNC =     123
CONT    COEFFICIENT = F**2 

GENERAL
CONT    DOBS=     0.00000
CONT    DCALC =     0.00000

INDEXRANGE  
CONT    HMIN       =     0
CONT    HMAX       =     15
CONT    KMIN       =     0
CONT    KMAX       =     17
CONT    LMIN       =     -6
CONT    LMAX       =     6
CONT    THETAMIN   =     0
CONT    THETAMAX   =     72.5

COLOUR   BRick red

SHAPE   CUBIC RHOMBOID
END  
\
\TITLE TRANS- 1,4 - DIMETHYLCYCLOHEXANE. AT -90 C. 
\LIST 1 
REAL 6.094 5.512 11.762 90 76.12 
\SPACE
SYMBOL P 21/C
END
\LIST 3 
READ 2 
SCAT C 0 0 2.13 20.8439 1.02 10.2075 1.5886 .5687 .865 51.6512 .2156 
SCAT H 0 0 .493002 10.5109 .322912 26.1257 .140191 3.14236 .04081 
CONT 57.7997 .003038 
\LIST 29
READ NELEM = 2
ELEM C .8 1.5 .6 4 9.17 12
ELEM H .6 1.0 .4 8 .07 1
\LIST 5 
READ 4 
\
\           COORDINATES TAKEN FROM PARTIAL REFINEMENT DONE ELSEWHERE. 
\
ATOM C 1 X=-.23 .09 .07 
ATOM C 2 X=-.13 .15 -.06 
ATOM C 3 X=.04 -.04 -.13 
ATOM C 7 X= -.39 .28 .13 
\LIST 4 
END 
\                                         WEISSENBERG DIFFRACTOMETER. 
\LIST 13 
DIF EQUI 
COND 1.5418 
AXIS 1 0 0 
END 
\LIST 28
END
\ HOOK UP THE REFLECTION FILE
\OPEN    HKLI    CRDIR:hex.hkl
\LIST 6 
\                                             4 REFLECTIONS PER CARD 
\                              OUTPUT TO FILE FOR SORTING AND MERGING 
READ 4 FIXED FSQ 4 HKLI NO 
STORE MEDIUM = FILE
FORMAT (4(3F4.0,F8.0)) 
END 
\LP 
\SYST 
\SORT 
\MERGE 
REFLECTION LIST=OFF 
\LIST 6 
\                           STORE REFLECTIONS ON DISC FOR FUTURE USE 
READ TYPE = COPY 
END 
\LIST 23 
\            NOTE THAT THE EXTINCTION PARAMETER HAS A DEFAULT OF 0.0 
\                                 AND SO HAS NO EFFECT UNTIL REFINED 
MODIFY EXTINCTION=YES 
\SFLS
SCALE
\                              DO AN FOBS MAP AND FOURIER REFINEMENT
\FOURIER
PEAKS HEIGHT=10 NPEAK=16 REJECT=.0005
END
\PEAKS
SELECT REJECT =0.0005 SEQ=YES
REFINE DIST= .15
\                                             ASSEMBLE ONE MOLECULE
\REGROUP
SELECT MOVE=1.55 
\MOLAX
ATOM FIRST UNTIL LAST
PLOT
END
\
\                                              THE FIRST JOB WOULD END HERE
\
\SFLS
CALC
END
\                                                LOOK FOR THE HYDROGEN
\FOURIER
MAP TYPE=DIFF
PEAKS HEIGHT = 3 NPEAK=16 REJECT=.0005
END
\PEAKS 10 5
SELECT SEQ=YES
\REGROUP
SELECT MOVE=1.2 MONITOR=HIGH
\MOLAX
ATOM FIRST UNTIL LAST FIRST(-1) UNTIL LAST
PLOT
END
\                                  WE NOW HAVE THE STRUCTURE. LABEL IT.
\EDIT
CHANGE Q(5,TYPE) H Q(6,TYPE) H Q(7,TYPE) H
CONT Q(8,TYPE) H Q(9,TYPE) H Q(10,TYPE) H
CONT Q(12,TYPE) H Q(13,TYPE) H
CHANGE C(4,SERIAL) 7 H(10,SERIAL) 22 H(6,SERIAL) 32 H(13,SERIAL) 73
CONT H(9,SERIAL) 11 H(5,SERIAL) 21 H(12,SERIAL) 71 H(8,SERIAL) 31
CONT H(7,SERIAL) 72
sel type ne q
sel type ne qn
sort c h
end
\                                  LOOK AT IT, ESPECIALLY THE CH3
\MOLAX
ATOM FIRST UNTIL LAST FIRST(-1) UNTIL LAST
PLOT
ATOM H(71) UNTIL H(73) C(7)
PLOT
END
\                                  SOME LOOK GRIM, CHECK IN DETAIL
\DISTANCE
OUTPUT LIST=HIGH MON=DIS 
\                                  HIGH LEVEL TO CHECK SYMMETRY OPERATORS
\
\                                  THEY ARE GRIM. REPLACE THEM
\
\
\EDIT
DELETE H(22) H(72)
\HYDROGEN
U[ISO] NEXT 1.2
SERIAL 22
H13 C(2) H(21) C(1) C(3)
AFTER H(21)
SERIAL 72
H13 C(7) H(71) H(73) C(1)
AFTER H(71)
\
\                                 CHECK AGAIN
\DIST
OUTPUT MON=DIST
\
\                                 THIRD JOB ENDS HERE
\                                 THE REST OF THE TASK WOULD BE
\                                 A SERIES OF LITTLE ON-LINE JOBS.
\LIST 12 
FULL FIRST(X'S) UNTIL LAST 
\SFLS 
REFINE
REFINE 
\LIST 12 
\             DO THE REFINEMENT GENTLY. WE KNOW THE 'H' ARE DIFFICULT 
FULL FIRST(X'S) UNTIL LAST C(1,U[ISO]) UNTIL C(7) 
\SFLS 
REFINE
REFINE
REFINE 
\LIST 12 
FULL EXTPARAM 
\SFLS 
REFINE 
\LIST 12 
FULL FIRST(U'S) UNTIL C(7) EXTPARAM 
\SFLS 
REFINE
REFINE
\LIST 12 
FULL FIRST(X'S) UNTIL LAST EXTPARAM C(1,U'S) UNTIL C(7) 
\SFLS 
REFINE 
\ The latest versions of CRYSTALS (Jan 2001) do not apply
\ implicit parameters to H atoms. Therefore the following must
\ be changed, since not refining the H atoms in this case causes
\ problems with restraints later on.
\ rem FULL X Y Z EXTPARAM C(1,U'S) UNTIL C(7) H(11,U[ISO]) UNTIL H(32) 
\ rem EQUIVALENCE H(71,U[ISO]) UNTIL H(73) 
\LIST 12
FULL FIRST(X'S) UNTIL LAST EXTPARAM
CONT C(1,U'S) UNTIL C(7) H(11,U[ISO]) UNTIL H(32)
EQUIVALENCE H(71,U[ISO]) UNTIL H(73) 
\SFLS 
REFINE
REFINE
REFINE 
\ANALYSE 
\          THE DISTRIBUTION AGAINST /FO/ IS VERY POOR. UNIT WEIGHTS 
\          ARE NOT APPLICABLE TO CONSTANT TIME OBSERVATIONS. 
\LIST 4 
SCHEME 14 4
END
ANALYSE 
\          THE DATA GOES DOWN TO ZERO SIGMA. CUT OUT THE WEAK ONES
\LIST 28 
MINIMA /FO/= 6 
END 
\SFLS 
REFINE
REFINE 
END 
\DIST 
E.S.D. YES
SELECT RANGE = COV
OUTPUT MON=ALL PUNCH=PUB 
END 
\TORS 
PUB YES
ATOM C(3) C(2) C(1) C(7) 
ATOM C(3) C(2) C(1) C(3,-1) 
ATOM C(1) C(2) C(3) C(1,-1) 
ATOM C(2,-1) C(3,-1) C(1) C(2) 
END 
\PRINT 11 
\                     SAVE THESE PARAMETERS IN CASE WE WANT TO PUBLISH THEM
\PARAM 
LAYOUT CHOOSE= YES  FLOATING = NO 
END 
\
\                                   TRY SOME RESTRAINTS NOW
\LIST 16
DIST 0.,.005=MEAN C(1) TO C(3), C(1) TO C(2)
CONT              C(2) TO C(3,-1), C(1) TO C(7)
DIST 0.,.05=MEAN C(1) TO H(11),C(2) TO H(21),C(3) TO H(31)
DIST 0.,.05=MEAN C(2) TO H(22),C(3) TO H(32)
DIST 0.,.05=MEAN C(7) TO H(71),C(7) TO H(72), C(7) TO H(73)
ANGLE 0,.2=MEAN C(7) TO C(1) TO C(2), C(7) TO C(1) TO C(3)
ANGLE 0,.2=MEAN C(1) TO C(2) TO C(3,-1), C(1) TO C(3) TO C(2,-1)
ANGLE 0,1.=MEAN C(3) TO C(1) TO H(11),C(2) TO C(1) TO H(11)
ANGLE 0,1.=MEAN C(1) TO C(2) TO H(21), C(1) TO C(3) TO H(31)
CONT            C(3) TO C(2,-1) TO H(21,-1), C(2) TO C(3,-1) TO H(31,-1)
ANGLE 0,1.=MEAN C(1) TO C(2) TO H(22), C(1) TO C(3) TO H(32)
CONT            C(2,-1) TO C(3) TO H(32),C(3,-1) TO C(2) TO H(22)
ANGLE 0,1.=MEAN C(1) TO C(7) TO H(71),C(1) TO C(7) TO H(72)
CONT            C(1) TO C(7) TO H(73)
ANGLE 0,2.=MEAN H(21) TO C(2) TO H(22), H(31) TO C(3) TO H(32)
ANGLE 0,2.=MEAN H(71) TO C(7) TO H(72),H(71) TO C(7) TO H(73)
CONT            H(72) TO C(7) TO H(73)
\CHECK
\LIST 23
MODIFY EXTINCTION=YES
MINIMISE  RESTRAIN=YES
\               AT THIS STAGE WE HOPE ALL PREVIOUS CALCULATIONS ARE VALID
\               AND SO WE CAN PURGE THE DISC FILE TO RETAIN ONLY THE TOP FILES
\               THE PRINT IS JUST TO SHOW HOW FULL THE DISC NOW IS
\DISK
PRINT DISK
\PURGE
\SFLS
REFINE
REFINE
\CHECK
\PARAM
LAYOUT CHOOSE=YES FLOAT=NO
END
\DISK
PRINT
END
\ Uncomment the next line to close down CRYSTALS:
\ FINISH
